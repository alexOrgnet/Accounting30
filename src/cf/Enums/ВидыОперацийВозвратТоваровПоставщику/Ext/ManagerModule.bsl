#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ПолноеИмяОперации(ВидОперации) Экспорт
	
	Если ВидОперации = ПокупкаКомиссия Тогда
		Возврат НСтр("ru = 'Возврат товаров поставщику'");
	ИначеЕсли ВидОперации = ИзПереработки Тогда
		Возврат НСтр("ru = 'Возврат материалов заказчику'");
	ИначеЕсли ВидОперации = Оборудование Тогда
		Возврат НСтр("ru = 'Возврат оборудования поставщику'");
	Иначе
		Возврат НСтр("ru = 'Возврат поставщику'");
	КонецЕсли;
		
КонецФункции	

// Возвращает список значений, доступных по функциональным опциям.
//
// Параметры:
//   Отбор - Массив значений ПеречислениеСсылка.ВидыОперацийВозвратТоваровПоставщику
//         или самое значение - дополнительный отбор
//
// Возвращаемое значение:
//   СписокЗначений - доступные значения
Функция СписокДоступныхЗначений(Отбор = Неопределено) Экспорт
	
	Возврат ПолучитьДоступныеЗначения(Отбор, Неопределено);

КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = ПолучитьДоступныеЗначения(Параметры.Отбор, Параметры.СтрокаПоиска);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьДоступныеЗначения(Отбор, СтрокаПоиска)
	
	Исключения = Новый Массив;
	Если НЕ ПолучитьФункциональнуюОпцию("ВедетсяПроизводственнаяДеятельность") Тогда
		Исключения.Добавить(Перечисления.ВидыОперацийВозвратТоваровПоставщику.ИзПереработки);
	КонецЕсли;
	
	ДоступныеЗначения = Новый СписокЗначений;
	
	Для каждого ЗначениеПеречисления Из Метаданные.Перечисления.ВидыОперацийВозвратТоваровПоставщику.ЗначенияПеречисления Цикл
		
		Если ТипЗнч(СтрокаПоиска) = Тип("Строка")
			И НЕ ПустаяСтрока(СтрокаПоиска)
			И СтрНайти(НРег(ЗначениеПеречисления.Синоним), НРег(СтрокаПоиска)) <> 1 Тогда
			Продолжить;
		КонецЕсли;
		Ссылка = Перечисления.ВидыОперацийВозвратТоваровПоставщику[ЗначениеПеречисления.Имя];
		Если ТипЗнч(Отбор) = Тип("ПеречислениеСсылка.ВидыОперацийВозвратТоваровПоставщику")
			И Отбор <> Ссылка Тогда
			Продолжить;
		ИначеЕсли (ТипЗнч(Отбор) = Тип("ФиксированныйМассив") Или ТипЗнч(Отбор) = Тип("Массив"))
			И Отбор.Найти(Ссылка) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если Исключения.Найти(Ссылка) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ДоступныеЗначения.Добавить(Ссылка, ЗначениеПеречисления.Синоним);
		
	КонецЦикла;
	
	Возврат ДоступныеЗначения;
	
КонецФункции

#КонецОбласти

#КонецЕсли