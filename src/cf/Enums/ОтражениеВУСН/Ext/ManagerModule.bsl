#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Формирует список выбора значений перечисления в документах.
//
// Параметры:
//  ПрименяетсяОсобыйНалоговыйРежим - Булево - признак применения одного из особых налоговых режимов
//                                             для отдельных видов деятельности - ЕНВД или патента.
//  МожноРаспределятьРасход - Булево - признак, что расход по данной операции может распределяться.
//  ДопускаетсяВозвратРасхода - Булево - признак, что данная операция может регистрировать возврат
//                                       совершенного ранее расхода.
//  ЭтоВозвратНалога - Булево - признак, что данная операция является возвратом налога.
//
// Возвращаемое значение:
//   СписокЗначений - список выбора для установки в свойствах поля ввода значения перечисления.
//
Функция СписокВыбора(ПрименяетсяОсобыйНалоговыйРежим, МожноРаспределятьРасход, ДопускаетсяВозвратРасхода = Ложь, ЭтоВозвратНалога = Ложь) Экспорт
	
	СписокВыбора = Новый СписокЗначений;
	
	// Значения, доступные всегда.
	
	Если Не ЭтоВозвратНалога Тогда
		СписокВыбора.Добавить(Принимаются);
	КонецЕсли;
	
	СписокВыбора.Добавить(НеПринимаются);
	
	// Значения, доступные только в определенных случаях.
	
	Если ПрименяетсяОсобыйНалоговыйРежим И МожноРаспределятьРасход Тогда
		СписокВыбора.Добавить(Распределяются);
	КонецЕсли;
	
	Если ДопускаетсяВозвратРасхода Или ЭтоВозвратНалога Тогда
		СписокВыбора.Добавить(ВозвратРасхода);
	КонецЕсли;
	
	Возврат СписокВыбора;
	
КонецФункции

#КонецОбласти

#КонецЕсли