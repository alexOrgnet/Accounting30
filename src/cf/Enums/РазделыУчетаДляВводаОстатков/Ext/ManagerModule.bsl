
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий
	
Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = ДанныеВыбораПоФункциональнымОпциям();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Убирает из полного списка разделов те, фунцкионал ведения учета по которым не включен.
//
// Возвращаемое значение:
//  СписокЗначений - список, куда добавлены все возможные (из используемых) разделы учета.
//
Функция ДанныеВыбораПоФункциональнымОпциям() Экспорт
	
	СписокРазделов = Новый СписокЗначений;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	РазделыУчетаДляВводаОстатков.Ссылка КАК СсылкаЗначение,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(РазделыУчетаДляВводаОстатков.Ссылка) КАК СсылкаПредставление
	|ИЗ
	|	Перечисление.РазделыУчетаДляВводаОстатков КАК РазделыУчетаДляВводаОстатков
	|
	|УПОРЯДОЧИТЬ ПО
	|	РазделыУчетаДляВводаОстатков.Порядок");
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Раздел = Выборка.СсылкаЗначение;
		Если (Раздел = АрендаЛизинг Или Раздел = ОсновныеСредства)
			И Не ПолучитьФункциональнуюОпцию("ВедетсяУчетОсновныхСредств")
		 Или Раздел = КапитальныеВложения
			И Не ПолучитьФункциональнуюОпцию("ВедетсяУчетОсновныхСредств") И Не ПолучитьФункциональнуюОпцию("ВедетсяУчетНМА")
		 Или Раздел = НематериальныеАктивыИНИОКР
			И Не ПолучитьФункциональнуюОпцию("ВедетсяУчетНМА")
		 Или (Раздел = РасчетыСПерсоналомПоОплатеТрудаИДепонентам Или Раздел = РасчетыСПодотчетнымиЛицами)
			И Не УчетЗарплаты.ИспользуетсяПодсистемаУчетаЗарплатыИКадров()
		 Или Раздел = СпецоснасткаВЭксплуатации
			И Не ПолучитьФункциональнуюОпцию("ВедетсяУчетСпецодеждыСпецоснастки") Тогда
			Продолжить;
		КонецЕсли;
		
		СписокРазделов.Добавить(Выборка.СсылкаЗначение, Выборка.СсылкаПредставление);
	
	КонецЦикла;
	
	Возврат СписокРазделов;
	
КонецФункции

#КонецОбласти	
	
#КонецЕсли