#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает идентификатор категории операции АУСН в формате ФНС
//
// Параметры:
//  Категория - ПеречислениеСссылка.КатегорииОперацийАУСН
//
// Возвращаемое значение:
//  Число - числовой идентификатор категории
//
Функция Идентификатор(Категория) Экспорт
	
	Если Не ЗначениеЗаполнено(Категория) Тогда
		Возврат 0;
	КонецЕсли;
	
	ВсеКатегории = ВсеКатегории();
	Идентификатор = ВсеКатегории.Получить(Категория);
	Если Не ЗначениеЗаполнено(Идентификатор) Тогда
		Идентификатор = 0;
	КонецЕсли;
	
	Возврат Идентификатор;
	
КонецФункции

// Возвращает значение перечисления по числовому идентификатору.
//
// Параметры:
//  Идентификатор - Число - идентификатр категории
//
// Возвращаемое значение:
//  ПеречислениеСссылка.КатегорииОперацийАУСН - значение перечисления
//
Функция ЗначениеПоИдентификатору(Идентификатор) Экспорт
	
	Если Не ЗначениеЗаполнено(Идентификатор) Тогда
		Возврат НеЗадана;
	КонецЕсли;
	
	ВсеКатегории = ВсеКатегории();
	Для Каждого Категория Из ВсеКатегории Цикл
		Если Категория.Значение = Идентификатор Тогда
			Возврат Категория.Ключ;
		КонецЕсли;
	КонецЦикла;
	
	Возврат НеЗадана;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ВсеКатегории()
	
	Категории = Новый Соответствие;
	Категории.Вставить(Эквайринг, 1);
	Категории.Вставить(КомиссияЗаЭквайринг, 2);
	Категории.Вставить(УплатаНалога, 3);
	Категории.Вставить(УплатаТорговогоСбора, 4);
	Категории.Вставить(ВыплатаФизическомуЛицу, 5);
	Категории.Вставить(ЭквайрингПриход, 6);
	Категории.Вставить(ЭквайрингВозврат, 7);
	Категории.Вставить(НалоговыеСанкции, 8);
	Категории.Вставить(ЕдиныйНалоговыйПлатеж, 9);
	Категории.Вставить(ДоходОтПродажАгента, 10);
	Категории.Вставить(КомиссияУслугиАгента, 11);
	
	Возврат Категории; 
	
КонецФункции

#КонецОбласти

#КонецЕсли