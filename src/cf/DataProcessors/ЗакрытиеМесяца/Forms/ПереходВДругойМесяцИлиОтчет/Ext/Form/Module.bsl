
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Параметры.ДатаМесяца)
		ИЛИ Не ЗначениеЗаполнено(Параметры.ПериодРегистрации) Тогда
			Отказ = Истина;
			ВызватьИсключение СтрШаблон(НСтр("ru = '%1, недостаточно параметров для открытия формы'"), ИмяФормы);
		Возврат;
	КонецЕсли;
	
	КлючСохраненияПоложенияОкна = Параметры.НазначениеФормы;
	КомандаПерейти = Параметры.НазначениеФормы;
	
	ТекстКнопкиПерейти = "";
	
	Если КомандаПерейти = "ПерейтиВСледующийМесяц" Тогда
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Теперь можно перейти к закрытию %1 г.'"),
			ОбщегоНазначенияБПКлиентСервер.ФормаПадежаМесяца(Параметры.ДатаМесяца, 2));
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 г. закрыт'"),
			ОбщегоНазначенияБПКлиентСервер.ФормаПадежаМесяца(Параметры.ПериодРегистрации, 1));
		ТекстКнопкиПерейти = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Перейти к закрытию %1 г.'"),
			ОбщегоНазначенияБПКлиентСервер.ФормаПадежаМесяца(Параметры.ДатаМесяца, 2));
		Элементы.ГруппаВыборПериодаЗакрытия.Видимость = Ложь;
	ИначеЕсли КомандаПерейти = "ЗакрытьСледующиеМесяцы" Тогда
		
		СледующийМесяц = ДобавитьМесяц(Параметры.ПериодРегистрации, 1);
		
		ТекстСообщения = НСтр("ru = 'Рекомендуем закрыть следующие месяцы.'");
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 г. закрыт'"),
			ОбщегоНазначенияБПКлиентСервер.ФормаПадежаМесяца(Параметры.ПериодРегистрации, 1));
		ТекстКнопкиПерейти = НСтр("ru = 'Выполнить закрытие'");
		Элементы.ГруппаКомандыФормы.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Право;
		
		ПериодПоУмолчанию = ЗакрытиеМесяца.ПериодПоУмолчанию();
		
		// Если осталось закрыть только один месяц, то по умолчанию устанавливаем переключатель
		// на пункте закрытия этого месяца.
		ПериодЗакрытия = ?(СледующийМесяц >= ПериодПоУмолчанию, "ПерейтиИЗакрытьОдинМесяц", "ПерейтиИЗакрытьВсеМесяцы");
		
		ПоследнийМесяц = Макс(ДобавитьМесяц(СледующийМесяц, 1), ПериодПоУмолчанию);
		
		Элементы.ПериодЗакрытия.СписокВыбора[0].Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Закрыть с %1 по %2'"),
			ОбщегоНазначенияБПКлиентСервер.ФормаПадежаМесяца(СледующийМесяц, 2),
			ОбщегоНазначенияБПКлиентСервер.ФормаПадежаМесяца(ПоследнийМесяц, 4));
					
		Элементы.ПериодЗакрытия.СписокВыбора[1].Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Закрыть только %1'"),
			ОбщегоНазначенияБПКлиентСервер.ФормаПадежаМесяца(СледующийМесяц, 4));
			
	ИначеЕсли КомандаПерейти = "СформироватьОтчет" Тогда
		
		ТекстСообщения = НСтр("ru = 'Теперь можно сформировать отчет'");
		
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 г. закрыт'"),
			ОбщегоНазначенияБПКлиентСервер.ФормаПадежаМесяца(Параметры.ПериодРегистрации, 1));
		
		ТекстКнопкиПерейти = НСтр("ru = 'Сформировать отчет'");
		Элементы.ГруппаВыборПериодаЗакрытия.Видимость = Ложь;
		
	Иначе
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Сначала нужно закрыть %1 г.'"),
			ОбщегоНазначенияБПКлиентСервер.ФормаПадежаМесяца(Параметры.ДатаМесяца, 4));
		
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 г. закрывать еще рано'"),
			ОбщегоНазначенияБПКлиентСервер.ФормаПадежаМесяца(Параметры.ПериодРегистрации, 4));
			
		ТекстКнопкиПерейти = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Перейти к закрытию %1 г.'"),
			ОбщегоНазначенияБПКлиентСервер.ФормаПадежаМесяца(Параметры.ДатаМесяца, 2));
			
		Элементы.ГруппаВыборПериодаЗакрытия.Видимость = Ложь;
		Элементы.МесяцЗакрыт.Видимость = Ложь;
		
	КонецЕсли;
	
	Элементы.ПерейтиКЗакрытию.Заголовок = ТекстКнопкиПерейти;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчкиКоманд

&НаКлиенте
Процедура ПерейтиКзакрытию(Команда)
	
	Если КомандаПерейти = "СформироватьОтчет" Тогда
		Закрыть("СформироватьОтчет");
	ИначеЕсли КомандаПерейти = "ЗакрытьСледующиеМесяцы" Тогда
		Закрыть(ПериодЗакрытия);
	Иначе
		Закрыть("Перейти");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НеСейчас(Команда)
	Закрыть("Отмена");
КонецПроцедуры

#КонецОбласти
