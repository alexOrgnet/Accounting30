#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИспользоватьРазделениеПоОбластямДанных = РаботаВМоделиСервиса.РазделениеВключено()
		И РаботаВМоделиСервиса.ДоступноИспользованиеРазделенныхДанных();
	
	Элементы.ГруппаЕслиОсталисьВопросы.Видимость = ИспользоватьРазделениеПоОбластямДанных
		И ИнформационныйЦентрСервер.УстановленаИнтеграцияСоСлужбойПоддержки();
	
	Элементы.ГруппаСообщениеОНенайденном.Видимость = ИспользоватьРазделениеПоОбластямДанных;
	Элементы.ГруппаЗагрузкаДанных.Видимость = ИспользоватьРазделениеПоОбластямДанных
		И ПравоДоступа("Использование", Метаданные.Обработки.ЗагрузкаДанныхИзДругойУчетнойСистемы);
	
	Элементы.ГруппаТехническаяПоддержка.Видимость = НЕ ИспользоватьРазделениеПоОбластямДанных;
	Элементы.ДополнительнаяИнформация.Видимость = НЕ ИспользоватьРазделениеПоОбластямДанных;
	
	Элементы.ГруппаНовостиИОбновления.Видимость = ПолучитьФункциональнуюОпцию("РазрешенаРаботаСНовостями");
	
	Если ОбщегоНазначенияБП.ЭтоБизнесСтарт() Тогда
		Элементы.ГруппаВидеоролики.Видимость = Истина;
		Элементы.ДругиеВидеоролики.Заголовок = СсылкиКаналаБизнесСтарт();
	Иначе
		Элементы.ГруппаВидеоролики.Видимость = Ложь;
	КонецЕсли;
	
	РазместитьЛентуНовостейЧастоЗадаваемыеВопросы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СообщитьОНенайденном(Команда)
	
	Если ПустаяСтрока(ТекстСообщения) Тогда
		ТекстПредупреждения = НСтр("ru='Напишите, что не удалось найти.'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		ТекущийЭлемент = Элементы.ТекстСообщения;
		Возврат;
	КонецЕсли;
	
	ЖурналРегистрацииКлиент.ДобавитьСообщениеДляЖурналаРегистрации(
		СобытиеЖурналаРегистрацииСообщение(),
		,
		ТекстСообщения,
		,
		Истина); // Сообщение сразу записываем.
	ПоказатьОповещениеПользователя(НСтр("ru='Сообщение отправлено'"), , ТекстСообщения);
	
	ТекстСообщения = "";	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КакВключитьОтключитьУчетПоДоговорамОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТекущаяСтраница", "ГруппаУчетПоДоговорам");
	ОткрытьФорму("Обработка.ФункциональностьПрограммы.Форма", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура КакПоказатьСкрытьСчетаУчетаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("ОбщаяФорма.ПерсональныеНастройки");
	
КонецПроцедуры

&НаКлиенте
Процедура КакВключитьПолныйПростойИнтерфейсОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Обработка.ПанельАдминистрированияБП.Форма.Интерфейс");
	
КонецПроцедуры

&НаКлиенте
Процедура КакПосмотретьИзменитьСистемуНалогообложенияОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Организация = ОбщегоНазначенияБПВызовСервера.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	
	ПараметрыФормыНалогиИОтчеты = Новый Структура;
	ПараметрыФормыНалогиИОтчеты.Вставить("Организация", Организация);
	
	ОткрытьФорму("ОбщаяФорма.НалогиИОтчеты", ПараметрыФормыНалогиИОтчеты, , Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбращенияВСлужбуПоддержкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьОбращенияВСлужбуПоддержки" Тогда
		ИнформационныйЦентрКлиент.ОткрытьОбращенияВСлужбуПоддержки();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НовостиИОбновленияОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "Новости" Тогда
		ОткрытьФорму("Справочник.Новости.Форма.ФормаПросмотраНовостей",,, "");
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ЧтоНовогоВПрограмме" Тогда
		ОбщегоНазначенияБПКлиент.ПоказатьОбновления(НСтр("ru='Что нового в программе'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбращениеВТехническуюПоддержкуОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура("ТипСообщения", НавигационнаяСсылкаФорматированнойСтроки);
	ОткрытьФорму("Обработка.ОбращениеВТехническуюПоддержку.Форма.ОбращениеВПоддержку", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаДанныхИзДругойСистемыОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Обработка.ЗагрузкаДанныхИзДругойУчетнойСистемы.Форма");

КонецПроцедуры

&НаКлиенте
Процедура ДругиеВидеороликиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеФункцииИПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СобытиеЖурналаРегистрацииСообщение()

	КодЯзыка = ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка();
	ШаблонТекста = НСтр("ru='%1.Сообщение'", КодЯзыка);
	Возврат СтрШаблон(ШаблонТекста, ГруппаСобытийЖурналаРегистрации());

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ГруппаСобытийЖурналаРегистрации()
	
	КодЯзыка  = ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка();
	Возврат НСтр("ru='Помощь БизнесСтарт'", КодЯзыка);

КонецФункции

&НаСервереБезКонтекста
Функция СсылкиКаналаБизнесСтарт()
	
	Возврат Обработки.ПанелиПростойИнтерфейс.СсылкиКаналаБизнесСтарт();
	
КонецФункции

&НаСервере
Процедура РазместитьЛентуНовостейЧастоЗадаваемыеВопросы()
	
	Если Не ОбщегоНазначенияБП.ЭтоВитринаБизнесСтарта() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыВыполнения = Новый Структура;
	ПараметрыВыполнения.Вставить("АдресХранилища", Новый УникальныйИдентификатор);
	
	ТаблицаСсылок = ОбщегоНазначенияБП.ЛентаНовостейЧастоЗадаваемыеВопросы(
		МаксимальноеКоличествоСсылокЧастоЗадаваемыеВопросы());
	
	Если Не ЗначениеЗаполнено(ТаблицаСсылок) Тогда
		Возврат;
	КонецЕсли;
	
	Родитель = Элементы.Добавить("ЧастоЗадаваемыеВопросы_СсылкиНаРолики",
		Тип("ГруппаФормы"),
		Элементы.ГруппаЧастоЗадаваемыеВопросы);
	
	Родитель.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	Родитель.ОтображатьЗаголовок = Ложь;
	Родитель.Отображение = ОтображениеОбычнойГруппы.ОбычноеВыделение;
	Родитель.ВертикальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Одинарный;
	
	Элементы.Переместить(Родитель,
		Элементы.ГруппаЧастоЗадаваемыеВопросы,
		Элементы.ГруппаЧастоЗадаваемыеВопросы.ПодчиненныеЭлементы[0]);
	
	ШаблонИмениЭлемента = "ЧастоЗадаваемыеВопросы_Ссылка%1";
	НомерСтроки = 1;
	Для Каждого ТекущаяСсылка Из ТаблицаСсылок Цикл
		
		НовыйЭлемент = Элементы.Добавить(СтрШаблон(ШаблонИмениЭлемента, НомерСтроки), Тип("ДекорацияФормы"), Родитель);
		НовыйЭлемент.Вид = ВидДекорацииФормы.Надпись;
		НовыйЭлемент.Заголовок = ТекущаяСсылка.СсылкаНаРолик;
		
		НомерСтроки = НомерСтроки + 1;
		
	КонецЦикла;
	
	НовыйЭлемент = Элементы.Добавить("ЧастоЗадаваемыеВопросы_ВсеВопросы", Тип("ДекорацияФормы"), Родитель);
	НовыйЭлемент.Вид = ВидДекорацииФормы.Надпись;
	НовыйЭлемент.Заголовок = БлокиИнформационнойПанелиКлиентСервер.СсылкаНаПлейлистЧастоЗадаваемыеВопросы();
	
КонецПроцедуры

Функция МаксимальноеКоличествоСсылокЧастоЗадаваемыеВопросы()
	
	// Количество ссылок на ролики, которые выводятся в разделе "Часто задаваемые вопросы"
	Возврат 3;
	
КонецФункции

#КонецОбласти
