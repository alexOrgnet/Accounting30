#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЕстьПравоПросмотрДокументыПоСкладу = ПравоДоступа("Просмотр", Метаданные.ЖурналыДокументов.СкладскиеДокументы);
	ЕстьПравоПросмотрПередачаСотрудникам = ПравоДоступа("Просмотр", Метаданные.ЖурналыДокументов.ПередачаМатериалов);
	ЕстьПравоПросмотрБезвозмезднаяПередача = ПравоДоступа("Просмотр", Метаданные.Документы.ПередачаТоваров);
	ЕстьПравоПросмотрКомплектацияНоменклатуры = ПравоДоступа("Просмотр", Метаданные.Документы.КомплектацияНоменклатуры);
	ЕстьПравоПросмотрИнвентаризацияТоваров = ПравоДоступа("Просмотр", Метаданные.Документы.ИнвентаризацияТоваровНаСкладе);

	Элементы.ДокументыПоСкладу.Видимость = ЕстьПравоПросмотрДокументыПоСкладу; 
	Элементы.ПередачаСотрудникам.Видимость = ЕстьПравоПросмотрПередачаСотрудникам;
	Элементы.БезвозмезднаяПередача.Видимость = ЕстьПравоПросмотрБезвозмезднаяПередача;
	Элементы.КомплектацияНоменклатуры.Видимость = ЕстьПравоПросмотрКомплектацияНоменклатуры;
	Элементы.ИнвентаризацияТоваров.Видимость = ЕстьПравоПросмотрИнвентаризацияТоваров;
	
КонецПроцедуры

#КонецОбласти

#Область КомандыФормы

&НаКлиенте
Процедура ДокументыПоСкладу(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок", НСтр("ru='Документы по складу'"));
	
	ОткрытьФормуВОкнеФормы("ЖурналДокументов.СкладскиеДокументы.ФормаСписка", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередачаСотрудникам(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок", НСтр("ru='Передача материалов сотрудникам'"));
	
	ОткрытьФормуВОкнеФормы("ЖурналДокументов.ПередачаМатериалов.ФормаСписка", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура БезвозмезднаяПередача(Команда)
	
	Отбор = Новый Структура("ВидОперации",
		ПредопределенноеЗначение("Перечисление.ВидыОперацийПередачаТоваров.БезвозмезднаяПередача"));
	ПараметрыФормы = Новый Структура("Отбор", Отбор);
	
	ОткрытьФормуВОкнеФормы("Документ.ПередачаТоваров.Форма.ФормаСпискаБезвозмезднаяПередача", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура КомплектацияНоменклатуры(Команда)
	
	ОткрытьФормуВОкнеФормы("Документ.КомплектацияНоменклатуры.ФормаСписка");
	
КонецПроцедуры

&НаКлиенте
Процедура ИнвентаризацияТоваров(Команда)
	
	ОткрытьФормуВОкнеФормы("Документ.ИнвентаризацияТоваровНаСкладе.ФормаСписка");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОткрытьФормуВОкнеФормы(ИмяФормы, ПараметрыФормы = Неопределено)
	
	ОткрытьФорму(ИмяФормы, ПараметрыФормы, ЭтотОбъект, ИмяФормы, Окно);
	
КонецПроцедуры

#КонецОбласти