#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПолучитьДеревоПомеченныхНаУдалениеВФоне(Параметры, АдресРезультата) Экспорт
	
	КодПиктограммыСправочник = 4;
	КодПиктограммыДокумент = 13;
	
	ТекстЗапроса = "";
	
	РазделениеВключено = ОбщегоНазначения.РазделениеВключено();
	
	Для каждого Справочник Из Метаданные.Справочники Цикл
		Если РазделениеВключено И ЭтоОбщиеДанные(Справочник) Или Не ПравоДоступа("Чтение", Справочник) Тогда
			Продолжить;
		КонецЕсли;
		
		ЕстьГруппы = Справочник.Иерархический
			И Справочник.ВидИерархии = Метаданные.СвойстваОбъектов.ВидИерархии.ИерархияГруппИЭлементов;
		
		Если НЕ ПустаяСтрока(ТекстЗапроса) Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|ОБЪЕДИНИТЬ ВСЕ
			|";
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса + СтрШаблон("ВЫБРАТЬ" + ?(ПустаяСтрока(ТекстЗапроса), " РАЗРЕШЕННЫЕ", "") + "
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(Справочник%1.Ссылка) КАК Представление,
		|	Справочник%1.Ссылка КАК Ссылка,
		|	%2 КАК НомерКартинки,
		|	""%3"" КАК ВидОбъекта
		|ИЗ
		|	Справочник.%1 КАК Справочник%1
		|ГДЕ
		|	Справочник%1.ПометкаУдаления" + ?(ЕстьГруппы, " И НЕ Справочник%1.ЭтоГруппа", ""),
		Справочник.Имя, КодПиктограммыСправочник, СтрЗаменить(Справочник.Представление(), """", """"""));
	КонецЦикла;
	
	Для каждого Документ Из Метаданные.Документы Цикл
		Если РазделениеВключено И ЭтоОбщиеДанные(Документ) Или Не ПравоДоступа("Чтение", Документ) Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ ПустаяСтрока(ТекстЗапроса) Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|ОБЪЕДИНИТЬ ВСЕ
			|";
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса + СтрШаблон("ВЫБРАТЬ ПРЕДСТАВЛЕНИЕССЫЛКИ(Документ%1.Ссылка) КАК Представление,
		|	Документ%1.Ссылка КАК Ссылка,
		|	%2 КАК НомерКартинки,
		|	""%3"" КАК ВидОбъекта
		|ИЗ
		|	Документ.%1 КАК Документ%1
		|ГДЕ
		|	Документ%1.ПометкаУдаления",
		Документ.Имя, КодПиктограммыСправочник, СтрЗаменить(Документ.Представление(), """", """"""));
	КонецЦикла;
	
	ТекстЗапроса = ТекстЗапроса + "
	|ИТОГИ ПО
	|	ВидОбъекта";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Дерево = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	Для каждого Строка Из Дерево.Строки Цикл
		Строка.Представление = СтрШаблон("%1 (%2)", Строка.ВидОбъекта, Строка.Строки.Количество());
	КонецЦикла;
	Дерево.Строки.Сортировать("Представление", Истина);
	
	ПоместитьВоВременноеХранилище(Дерево, АдресРезультата);
	
КонецПроцедуры

Функция СсылкиКаналаБизнесСтарт() Экспорт
	
	ТаблицаСсылок = НоваяТаблицаСсылок();
	ДобавитьСсылку("intro", НСтр("ru = 'Введение в программу 1С:БизнесСтарт'"), ТаблицаСсылок);
	ДобавитьСсылку("depositingip", НСтр("ru = 'Начало работы в программе. Взнос денег на счет ИП'"), ТаблицаСсылок);
	ДобавитьСсылку("goodsandpartners", НСтр("ru = 'Учет товара. Поступление товаров. Информация о контрагентах'"), ТаблицаСсылок);
	ДобавитьСсылку("cashregister", НСтр("ru = 'Онлайн-касса'"), ТаблицаСсылок);
	ДобавитьСсылку("sellingforcashusnd", НСтр("ru = 'Продажа покупателю юридическому лицу за наличный расчет'"), ТаблицаСсылок, Ложь);
	ДобавитьСсылку("retailusnd", НСтр("ru = 'Розничные продажи'"), ТаблицаСсылок);
	ДобавитьСсылку("creditcardsusnd", НСтр("ru = 'Прием оплаты платежными картами'"), ТаблицаСсылок);
	ДобавитьСсылку("suppliers", НСтр("ru = 'Договор с поставщиком. Досье контрагента. Загрузка прайс-листа. Поступление товаров безнал'"), ТаблицаСсылок);
	ДобавитьСсылку("invoicingusnd", НСтр("ru = 'Продажа покупателю за безналичный расчет'"), ТаблицаСсылок);
	ДобавитьСсылку("banking", НСтр("ru = 'Работа с банком'"), ТаблицаСсылок);
	ДобавитьСсылку("outsourcingusnd", НСтр("ru = 'Расходы на аутсорсинг'"), ТаблицаСсылок, Ложь);
	ДобавитьСсылку("businesstrip", НСтр("ru = 'Оформление командировочных расходов'"), ТаблицаСсылок, Ложь);
	ДобавитьСсылку("taxreturnusnd", НСтр("ru = 'Отчетность УСН'"), ТаблицаСсылок, Ложь);
	ДобавитьСсылку("incomebookusnd", НСтр("ru = 'Книга учета доходов и расходов УСН «доходы»'"), ТаблицаСсылок, Ложь);
	ДобавитьСсылку("insurancefee", НСтр("ru = 'Страховые взносы ИП за себя'"), ТаблицаСсылок, Ложь);
	
	МассивПодстрок = Новый Массив;
	
	Для НомерСтроки = 1 По ТаблицаСсылок.Количество() Цикл
		СвойстваСсылки = ТаблицаСсылок[НомерСтроки - 1];
		Если СвойстваСсылки.Используется Тогда
			МассивПодстрок.Добавить(Формат(НомерСтроки, "ЧГ=0"));
			МассивПодстрок.Добавить(". ");
			МассивПодстрок.Добавить(Новый ФорматированнаяСтрока(СвойстваСсылки.Представление, , , , СвойстваСсылки.Ссылка));
			МассивПодстрок.Добавить(Символы.ПС);
			МассивПодстрок.Добавить(" ");
			МассивПодстрок.Добавить(Символы.ПС);
		КонецЕсли;
	КонецЦикла;
	
	МассивПодстрок.Добавить(СсылкаНаКаналБизнесСтарт());
	
	Возврат Новый ФорматированнаяСтрока(МассивПодстрок);
	
КонецФункции

Функция СсылкаНаКаналБизнесСтарт() Экспорт
	
	Возврат СтроковыеФункции.ФорматированнаяСтрока(
		НСтр("ru = 'Посмотрите канал <a href=""http://its.1c.ru/bmk/youtube/bs/channel"">Онлайн-бухгалтерия 1С:БизнесСтарт</a>'"));
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НоваяТаблицаСсылок()
	
	ТаблицаСсылок = Новый ТаблицаЗначений;
	ТаблицаСсылок.Колонки.Добавить("Ссылка", ОбщегоНазначения.ОписаниеТипаСтрока(200));
	ТаблицаСсылок.Колонки.Добавить("Представление", ОбщегоНазначения.ОписаниеТипаСтрока(100));
	ТаблицаСсылок.Колонки.Добавить("Используется", Новый ОписаниеТипов("Булево"));
	
	Возврат ТаблицаСсылок;
	
КонецФункции

Функция ДобавитьСсылку(Идентификатор, Представление, ТаблицаСсылок, Используется = Истина)
	
	ШаблонСсылок = "http://its.1c.ru/bmk/youtube/bs/%1";
	
	СвойстваСсылки = ТаблицаСсылок.Добавить();
	СвойстваСсылки.Ссылка = СтрШаблон(ШаблонСсылок, Идентификатор);
	СвойстваСсылки.Представление = Представление;
	СвойстваСсылки.Используется = Используется;
	
	Возврат СвойстваСсылки;
	
КонецФункции

Функция ЭтоОбщиеДанные(ЭлементМетаданных)
	
	ОбластьДанныхОсновныеДанные = Метаданные.ОбщиеРеквизиты.ОбластьДанныхОсновныеДанные.Состав.Найти(ЭлементМетаданных);
	Если ОбластьДанныхОсновныеДанные <> Неопределено
		И ОбластьДанныхОсновныеДанные.Использование = Метаданные.СвойстваОбъектов.ИспользованиеОбщегоРеквизита.НеИспользовать Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#КонецЕсли