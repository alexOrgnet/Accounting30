#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Имя = Параметры.Имя;
	ЗаголовокПоля = Параметры.Заголовок;
	
	ТипЗначения = КадровыйЭДО.ТипЗначенияШаблонаДокументов(Параметры.ТипЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		ТекстВопроса = НСтр("ru = 'Данные были изменены.
			|Закрыть?'");
		Оповещение = Новый ОписаниеОповещения("ОбработатьВопросОЗакрытииБезСохранения", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЗаголовокПоляПриИзменении(Элемент)
	
	СловаЗаголовка = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивСлов(ЗаголовокПоля);
	Имя = "";
	Для Каждого СловоЗаголовка Из СловаЗаголовка Цикл
		Имя = Имя + ВРег(Лев(СловоЗаголовка, 1));
		Если СтрДлина(СловоЗаголовка) > 1 Тогда
			Имя = Имя + Сред(СловоЗаголовка, 2);
		КонецЕсли;
	КонецЦикла;
	Если Не (СтроковыеФункцииКлиентСервер.ТолькоЛатиницаВСтроке(Лев(Имя, 1))
		Или СтроковыеФункцииКлиентСервер.ТолькоКириллицаВСтроке(Лев(Имя, 1))) Тогда
		
		Имя = "_" + Имя;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяПриИзменении(Элемент)
	
	НормализованноеИмя = "";
	ДобавитьВВерхнемРегистре = Истина;
	Для НомерСимвола = 1 По СтрДлина(Имя) Цикл
		КодСимвола = КодСимвола(Имя, НомерСимвола);
		Если Не СтроковыеФункцииКлиентСервер.ЭтоРазделительСлов(КодСимвола) Тогда
			Если ДобавитьВВерхнемРегистре Тогда
				НормализованноеИмя = НормализованноеИмя + ВРег(Сред(Имя, НомерСимвола, 1));
				ДобавитьВВерхнемРегистре = Ложь;
			Иначе
				НормализованноеИмя = НормализованноеИмя + Сред(Имя, НомерСимвола, 1);
			КонецЕсли;
		Иначе
			Если Сред(Имя, НомерСимвола, 1) = " " Тогда
				ДобавитьВВерхнемРегистре = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Если Не (СтроковыеФункцииКлиентСервер.ТолькоЛатиницаВСтроке(Лев(НормализованноеИмя, 1))
		Или СтроковыеФункцииКлиентСервер.ТолькоКириллицаВСтроке(Лев(НормализованноеИмя, 1))) Тогда
		
		НормализованноеИмя = "_" + НормализованноеИмя;
	КонецЕсли;
	Имя = НормализованноеИмя;
	УстановитьЗаголовок();
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Ок(Команда)
	
	Если Модифицированность Тогда
		Если ПроверитьЗаполнение() Тогда
			ОписаниеПараметра = Новый Структура;
			ОписаниеПараметра.Вставить("Имя", Имя);
			ОписаниеПараметра.Вставить("Заголовок", ЗаголовокПоля);
			Если ТипЗначения = ПредопределенноеЗначение("Перечисление.ТипыРеквизитовШаблоновДокументов.Строка") Тогда
				ОписаниеПараметра.Вставить("ТипЗначения", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(150)));
			ИначеЕсли ТипЗначения = ПредопределенноеЗначение("Перечисление.ТипыРеквизитовШаблоновДокументов.Текст") Тогда
				ОписаниеПараметра.Вставить("ТипЗначения", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки()));
			ИначеЕсли ТипЗначения = ПредопределенноеЗначение("Перечисление.ТипыРеквизитовШаблоновДокументов.ЦелоеЧисло") Тогда
				ОписаниеПараметра.Вставить("ТипЗначения", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 0)));
			ИначеЕсли ТипЗначения = ПредопределенноеЗначение("Перечисление.ТипыРеквизитовШаблоновДокументов.Число2ЗнакаПослеЗапятой") Тогда
				ОписаниеПараметра.Вставить("ТипЗначения", Новый ОписаниеТипов("Число", , Новый КвалификаторыЧисла(15, 2)));
			ИначеЕсли ТипЗначения = ПредопределенноеЗначение("Перечисление.ТипыРеквизитовШаблоновДокументов.Дата") Тогда
				ОписаниеПараметра.Вставить("ТипЗначения", Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.Дата)));
			ИначеЕсли ТипЗначения = ПредопределенноеЗначение("Перечисление.ТипыРеквизитовШаблоновДокументов.Период") Тогда
				ОписаниеПараметра.Вставить("ТипЗначения", Новый ОписаниеТипов("СтандартныйПериод"));
			ИначеЕсли ТипЗначения = ПредопределенноеЗначение("Перечисление.ТипыРеквизитовШаблоновДокументов.Булево") Тогда
				ОписаниеПараметра.Вставить("ТипЗначения", Новый ОписаниеТипов("Булево"));
			КонецЕсли;
			Модифицированность = Ложь;
			Закрыть(ОписаниеПараметра);
		КонецЕсли;
	Иначе
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Процедура УстановитьЗаголовок()
	
	ЗаголовокПоля = Лев(Имя, 1);
	Для НомерСимвола = 2 По СтрДлина(Имя) Цикл
		СимволИмени = Сред(Имя, НомерСимвола, 1);
		Если Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СимволИмени)
			И ВРег(СимволИмени) = СимволИмени Тогда
			
			ЗаголовокПоля = ЗаголовокПоля + " " + НРег(СимволИмени);
		Иначе
			ЗаголовокПоля = ЗаголовокПоля + СимволИмени;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВопросОЗакрытииБезСохранения(Результат, ДополнительныеПараметры) Экспорт
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


