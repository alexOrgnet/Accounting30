
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УпрощеннаяНулеваяОтчетность = Параметры.УпрощеннаяНулеваяОтчетность;
	
	Организация                 = Параметры.Организация;
	ДатаНачалаДеятельности      = КалендарьБухгалтера.ДатаНачалаДеятельности(Организация);
	
	ОтчетныйПериодТеста         = Параметры.ОтчетныйПериод;
	ТекущаяДатаПользователя     = ОбщегоНазначения.ТекущаяДатаПользователя();
	
	Правило                     = Параметры.Правило;
	ПериодичностьОтчетности     = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Правило, "ФинансовыйПериод");
	ПараметрыПравила = Неопределено;
	
	Если УпрощеннаяНулеваяОтчетность Тогда
		Если Параметры.Свойство("Правила")
			И ЗначениеЗаполнено(Параметры.Правила) Тогда
			ПараметрыПравила = ПолучитьИзВременногоХранилища(Параметры.Правила);
			Если ПараметрыПравила <> Неопределено Тогда
				Если ПараметрыПравила.Количество() <> 0 Тогда
					Правила.Загрузить(ПараметрыПравила);
					ПериодичностьОтчетности = Обработки.МониторНалоговИОтчетности.МинимальныйОтчетныйПериод(ПараметрыПравила.ВыгрузитьКолонку("Периодичность"));
					Если ПериодичностьОтчетности <> ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Правило, "ФинансовыйПериод") Тогда
						ОтчетныйПериодТеста = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(ПериодичностьОтчетности, ТекущаяДатаПользователя);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ГосударственныйОрган = Параметры.ГосударственныйОрган;
	
	ТекущийОтчетныйПериод = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(ПериодичностьОтчетности,
		?(ЗначениеЗаполнено(ОтчетныйПериодТеста),
			ОтчетныйПериодТеста,
			ТекущаяДатаПользователя));
	
	ТекущийВопросТеста = ?(ЗначениеЗаполнено(Параметры.НачальныйВопрос), Параметры.НачальныйВопрос, ПервыйВопросТеста());
	ОбщееКоличествоВопросов = КоличествоВопросовТеста(ТекущийВопросТеста);
	
	ВходныеДанные = НоваяТаблицаПериодов();
	
	Если ЭтоАдресВременногоХранилища(Параметры.АдресВходныхДанных) Тогда
		
		ДанныеПараметраФормы = ПолучитьИзВременногоХранилища(Параметры.АдресВходныхДанных);
		
		Для Каждого СтрокаТаблицы Из ДанныеПараметраФормы Цикл
			СтрокаВходныхДанных = ВходныеДанные.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаВходныхДанных, СтрокаТаблицы);
		КонецЦикла;
		
	КонецЕсли;
	
	ЗаполнитьОтчетныеПериоды(ВходныеДанные);
	
	ЗаполнитьгосОрганВДекорациях();
	
	НачатьТест();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	НастроитьОтображениеКнопок();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ТекущийВопросТеста = ИмяВопрос32() И В32_МогуУказатьПериод = "Год" Тогда
		
		ПроверяемыеРеквизиты.Добавить("В32_ПериодОбращенияВФНС");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	// Заполним выбранный ответ в соответствии с загруженным из настроек значением переключателя.
	Если ТекущийВопросТеста = ИмяВопрос1() Тогда
		ИнициализироватьПереключатель(ЭтотОбъект, "В1_Ответ");
	ИначеЕсли ТекущийВопросТеста = ИмяВопрос2() Тогда
		ИнициализироватьПереключатель(ЭтотОбъект, "В2_Ответ");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура НастроитьОтображениеКнопок()
	
	Если РежимОткрытияОкна = РежимОткрытияОкнаФормы.Независимый Тогда
		// В том случае, если форма открывается в независимом режиме, например, по ссылке
		// кнопки должны быть прижаты к вопросам и находится в левом верхнем углу.
		Элементы.ГруппаКнопки.ВертикальноеПоложениеВГруппе = ВертикальноеПоложениеЭлемента.Верх;
		Элементы.ГруппаКнопки.РастягиватьПоГоризонтали = Неопределено;
		Элементы.ГруппаОпрос.РастягиватьПоГоризонтали = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветПереключательПриИзменении(Элемент)
	
	ВыбранныйОтвет = ЭтотОбъект[Элемент.Имя];
	
КонецПроцедуры

&НаКлиенте
Процедура НеМогуУказатьПериодПриИзменении(Элемент)
	
	УстановитьДоступностьПериодаОбращенияВФНС(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура МогуУказатьГодПриИзменении(Элемент)
	
	УстановитьДоступностьПериодаОбращенияВФНС(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КнопкаДалее(Команда)
	
	ПерейтиДалее();
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаНазад(Команда)
	
	ПерейтиНазад();
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаГотово(Команда)
	
	ЗавершитьТест();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция НоваяТаблицаПериодов()
	
	ТаблицаПериодов = Новый ТаблицаЗначений;
	
	ТаблицаПериодов.Колонки.Добавить("Период", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ТаблицаПериодов.Колонки.Добавить("Требуется", Новый ОписаниеТипов("Булево"));
	
	Возврат ТаблицаПериодов;
	
КонецФункции

&НаСервере
Процедура НачатьТест()
	
	// Сначала сбросим видимость всех групп теста и кнопок.
	УстановитьВидимостьПодчиненныхЭлементов("ГруппаТест", Ложь);
	ОтключитьВидимостьКнопок();
	
	НомерТекущегоВопроса = 0;
	
	ШагВперед(ТекущийВопросТеста, "");
	
	// Далее отобразим только группу текущего вопроса и нужные кнопки.
	ОтобразитьВопрос(ТекущийВопросТеста);
	
КонецПроцедуры

&НаСервере
Процедура ПерейтиДалее()
	
	СкрытьВопрос(ТекущийВопросТеста);
	
	ШагВперед(ВыбранныйОтвет, ТекущийВопросТеста);
	
	ОтобразитьВопрос(ТекущийВопросТеста);
	
КонецПроцедуры

&НаСервере
Процедура ПерейтиНазад()
	
	СкрытьВопрос(ТекущийВопросТеста);
	
	ШагНазад(ТекущийВопросТеста);
	
	ОтобразитьВопрос(ТекущийВопросТеста);
	
КонецПроцедуры

&НаСервере
Процедура СкрытьВопрос(ИмяВопроса)
	
	Элементы[ИмяВопроса].Видимость = Ложь;
	ОтключитьВидимостьКнопок();
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьВопрос(ИмяВопроса)
	
	Если НЕ ЗначениеЗаполнено(ИмяВопроса) Тогда
		Возврат;
	КонецЕсли;
	
	Если ИмяВопроса = ИмяВопрос1() Тогда
		ПодготовитьТест1();
	ИначеЕсли ИмяВопроса = ИмяВопрос2() Тогда
		ПодготовитьТест2();
	ИначеЕсли ИмяВопроса = ИмяВопрос31() Тогда
		ПодготовитьТест31();
	ИначеЕсли ИмяВопроса = ИмяВопрос32() Тогда
		ПодготовитьТест32();
	ИначеЕсли ИмяВопроса = ИмяВопрос33() Тогда
		ПодготовитьТест33();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьТест1()
	
	ГруппаТеста = ИмяВопрос1();
	
	Элементы[ГруппаТеста].Видимость = Истина;
	
	ЭтотОбъект.Заголовок = НСтр("ru = 'Шаг 1'");
	
	Элементы.НадписьВопрос1.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Сдали ли Вы отчеты за %1 и ранее?'"),
		ОписаниеПрошлогоОтчетногоПериода(ТекущийОтчетныйПериод, ПериодичностьОтчетности));
	
	ИнициализироватьПереключатель(ЭтотОбъект, "В1_Ответ");
	
	Элементы.КнопкаДалее.Видимость = Истина;
	Элементы.КнопкаДалее.КнопкаПоУмолчанию = Истина;
	Элементы.КнопкаНазад.Видимость = ЕстьПредыдущийВопрос(ГруппаТеста);
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьТест2()
	
	ГруппаТеста = ИмяВопрос2();
	
	ЭтотОбъект.Заголовок = ТекстНомерВопроса(НомерТекущегоВопроса, ОбщееКоличествоВопросов);
	
	Элементы[ГруппаТеста].Видимость = Истина;
	
	ИнициализироватьПереключатель(ЭтотОбъект, "В2_Ответ");
	
	Элементы.КнопкаДалее.Видимость = Истина;
	Элементы.КнопкаДалее.КнопкаПоУмолчанию = Истина;
	Элементы.КнопкаНазад.Видимость = ЕстьПредыдущийВопрос(ГруппаТеста);
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьТест31()
	
	ГруппаТеста = ИмяВопрос31();
	
	ЭтотОбъект.Заголовок = ТекстНомерВопроса(НомерТекущегоВопроса, ОбщееКоличествоВопросов);
	
	Элементы[ГруппаТеста].Видимость = Истина;
	
	Элементы.КнопкаГотово.Видимость         = Истина;
	Элементы.КнопкаГотово.КнопкаПоУмолчанию = Истина;
	Элементы.КнопкаНазад.Видимость = ЕстьПредыдущийВопрос(ГруппаТеста);
	
	// Флаги периодов.
	Для НомерЭлемента = 1 По 4 Цикл
		
		Элемент = Элементы["В31_ПометкаПрошедшийПериод" + НомерЭлемента];
		
		СтрокаПериода = ОтчетныеПериоды.НайтиПоИдентификатору(НомерЭлемента - 1);
		
		Если СтрокаПериода = Неопределено Тогда
			// Отчетного периода нет, скроем флаг.
			Элемент.Видимость = Ложь;
			ЭтотОбъект["В31_ПометкаПрошедшийПериод" + НомерЭлемента] = Ложь;
			ЭтотОбъект["В31_ПрошедшийПериод" + НомерЭлемента]        = 0;
		Иначе
			// Добавим флаг для прошлого периода
			ЗначениеГода = Год(СтрокаПериода.Период);
			
			ЭтотОбъект["В31_ПометкаПрошедшийПериод" + НомерЭлемента] = СтрокаПериода.Требуется;
			ЭтотОбъект["В31_ПрошедшийПериод" + НомерЭлемента]        = ЗначениеГода;
			
			Элемент.Видимость = Истина;
			Если Год(СтрокаПериода.Период) =Год(ТекущийОтчетныйПериод) Тогда
				Элемент.Заголовок = СтрШаблон(НСтр("ru = 'с начала %1'"), Формат(ЗначениеГода, "ЧН=0; ЧГ=0") + " " + НСтр("ru = 'года'"));
			Иначе
				Элемент.Заголовок = Формат(ЗначениеГода, "ЧН=0; ЧГ=0") + " " + НСтр("ru = 'год'");
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьТест32()
	
	ГруппаТеста = ИмяВопрос32();
	
	ЭтотОбъект.Заголовок = ТекстНомерВопроса(НомерТекущегоВопроса, ОбщееКоличествоВопросов);
	
	Элементы[ГруппаТеста].Видимость = Истина;
	
	Элементы.КнопкаГотово.Видимость = Истина;
	Элементы.КнопкаГотово.КнопкаПоУмолчанию = Истина;
	Элементы.КнопкаНазад.Видимость = ЕстьПредыдущийВопрос(ГруппаТеста);
	
	Если НЕ ЗначениеЗаполнено(В32_МогуУказатьПериод) Тогда
		В32_МогуУказатьПериод = "Год";
	КонецЕсли;
	
	ГодПервогоВизита = 2999; // Далекое будущее.
	ТекущийГод       = Год(ТекущаяДатаСеанса());
	
	Элементы.В32_ПериодОбращенияВФНС.СписокВыбора.Очистить();
	Для Каждого СтрокаПериода Из ОтчетныеПериоды Цикл
		
		ОтчетныйГод = Год(СтрокаПериода.Период);
		Если ОтчетныйГод > ТекущийГод Тогда
			Продолжить;
		КонецЕсли;
		
		ГодВизита = ОтчетныйГод;
		
		Элементы.В32_ПериодОбращенияВФНС.СписокВыбора.Добавить(ГодВизита, Формат(ГодВизита, "ЧГ=0"));
		
		Если СтрокаПериода.Требуется И ГодВизита < ГодПервогоВизита Тогда
			ГодПервогоВизита = ОтчетныйГод;
		КонецЕсли;
		
	КонецЦикла;
	
	СписокВыбораПериода = Элементы.В32_ПериодОбращенияВФНС.СписокВыбора;
	Если СписокВыбораПериода.НайтиПоЗначению(ГодПервогоВизита) <> Неопределено Тогда
		В32_ПериодОбращенияВФНС = ГодПервогоВизита;
	Иначе
		// Тест проходится в первый раз, либо результат предыдущего теста устарел из-за смены настроек организации.
		// Задаем текущий год.
		В32_ПериодОбращенияВФНС = СписокВыбораПериода[СписокВыбораПериода.Количество() - 1].Значение;
	КонецЕсли;
	
	ЧастиДополнения = Новый Массив;
	ЧастиДополнения.Добавить(НСтр("ru = 'Ваша отчетность будет дополнена отчетами за периоды, начиная с года последнего обращения в %ГосОрган%.'"));
	ЧастиДополнения.Добавить(" ");
	
	Если ОтчетныеПериоды.Количество() < 4 Тогда
		ЧастиДополнения.Добавить(НСтр("ru = 'Если год обращения неизвестен, будут сформированы отчеты за все периоды с даты регистрации.'"));
	Иначе
		ЧастиДополнения.Добавить(НСтр("ru = 'Если год обращения неизвестен, будут сформированы отчеты за 3 последних года по текущую дату.'"));
	КонецЕсли;
	
	Элементы.НадписьДополнение322.Заголовок = Новый ФорматированнаяСтрока(ЧастиДополнения);
	
	ЗаполнитьгосОрганВДекорациях();
	
	УстановитьДоступностьПериодаОбращенияВФНС(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьТест33()
	
	ГруппаТеста = ИмяВопрос33();
	
	Элементы[ГруппаТеста].Видимость = Истина;
	
	УстановитьЗаголовокИОписаниеОтчета();
	
	РазрешенУчетРегулярнойДеятельности = ТарификацияБПВызовСервераПовтИсп.РазрешенУчетРегулярнойДеятельности();
	Элементы.НадписьЗнакомствоСПрограммой.Видимость = Не РазрешенУчетРегулярнойДеятельности;
	Элементы.НадписьПознакомитьсяСПрограммой.Видимость = Не РазрешенУчетРегулярнойДеятельности;
	
	Элементы.КнопкаГотово.Видимость = Истина;
	Элементы.КнопкаГотово.КнопкаПоУмолчанию = Истина;
	Элементы.КнопкаНазад.Видимость = ЕстьПредыдущийВопрос(ГруппаТеста);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьТест()
	
	Результат = ЗавершитьТестНаСервере();
	
	Если НЕ Результат.Выполнено Тогда
		Возврат;
	КонецЕсли;
	
	Оповестить("ОтчетыПрошлыхПериодов_ПроверкаЗавершена", , Организация);
	
	Если Результат.ДобавленыПрошлыеПериоды Тогда
		
		Если ТарификацияБПВызовСервераПовтИсп.РазрешенУчетРегулярнойДеятельности() Тогда
			ИмяФормыПомощника = "Обработка.ПодготовкаОтчетностиПрошлыхПериодов.Форма.Форма";
		Иначе
			ИмяФормыПомощника = "Обработка.ПодготовкаНулевойОтчетности.Форма.Форма";
		КонецЕсли;
		
		Если ВладелецФормы <> Неопределено
			И ВладелецФормы.ИмяФормы = ИмяФормыПомощника
			И ВладелецФормы.Открыта() Тогда
			
			ВладелецФормы.Активизировать();
			
		Иначе
			
			ПараметрыФормыПомощника = Новый Структура();
			ПараметрыФормыПомощника.Вставить("Организация",          Организация);
			ПараметрыФормыПомощника.Вставить("КонтекстныйВызов",     Истина);
			ПараметрыФормыПомощника.Вставить("ГосударственныйОрган", ГосударственныйОрган);
			
			ОткрытьФорму(ИмяФормыПомощника, ПараметрыФормыПомощника);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

&НаСервере
Функция ЗавершитьТестНаСервере()
	
	ПараметрыЗавершения = Новый Структура;
	ПараметрыЗавершения.Вставить("ДобавленыПрошлыеПериоды", Ложь);
	ПараметрыЗавершения.Вставить("Выполнено", Ложь);
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат ПараметрыЗавершения;
	КонецЕсли;
	
	Если ТекущийВопросТеста = ИмяВопрос31() Тогда
		ЗавершитьТест31();
	ИначеЕсли ТекущийВопросТеста = ИмяВопрос32() Тогда
		ЗавершитьТест32();
	ИначеЕсли ТекущийВопросТеста = ИмяВопрос33() Тогда
		ЗавершитьТест33();
	КонецЕсли;
	
	РезультатТеста = ПолучитьВыходныеДанные();
	
	РегистрыСведений.РезультатыПроверкиНалоговОтчетовПрошлыхПериодов.ЗарегистрироватьИзменения(РезультатТеста);
	ПараметрыЗавершения.Выполнено = Истина;
	ПараметрыЗавершения.ДобавленыПрошлыеПериоды = РезультатТеста.ДобавленыПрошлыеПериоды;
	
	Возврат ПараметрыЗавершения;
	
КонецФункции

&НаСервере
Процедура ЗавершитьТест31()
	
	Для НомерЭлемента = 1 По 4 Цикл
		
		Элемент  = Элементы["В31_ПометкаПрошедшийПериод" + НомерЭлемента];
		Отметка  = ЭтотОбъект["В31_ПометкаПрошедшийПериод" + НомерЭлемента];
		Значение = ЭтотОбъект["В31_ПрошедшийПериод" + НомерЭлемента];
		
		Если Элемент.Видимость Тогда
			ОтметитьОтчетныйПериод(Дата(Значение, 1, 1), Отметка);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗавершитьТест32()
	
	Если В32_МогуУказатьПериод = "Год" Тогда
		
		// Пользователь указал год последнего обращения в ФНС. Чаще всего при этом сдается отчет за прошлый год.
		// Следовательно, отчетность не сдана начиная с года последнего обращения. Его и задаем как минимальный из требуемых.
		Для Каждого СтрокаПериода Из ОтчетныеПериоды Цикл
			СтрокаПериода.Требуется = Год(СтрокаПериода.Период) >= В32_ПериодОбращенияВФНС;
		КонецЦикла;
		
	Иначе
		
		// Пользователь не помнит дату посещения налоговой, отмечаем все периоды нужными.
		Для Каждого СтрокаПериода Из ОтчетныеПериоды Цикл
			СтрокаПериода.Требуется = Истина;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗавершитьТест33()
	
	// Пользователь указал, что у него все сдано, отмечаем все периоды как ненужные.
	Для Каждого СтрокаПериода Из ОтчетныеПериоды Цикл
		СтрокаПериода.Требуется = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьВыходныеДанные()
	
	Результат = РегистрыСведений.РезультатыПроверкиНалоговОтчетовПрошлыхПериодов.НовыйРезультатыТеста();
	Результат.Организация = Организация;
	Результат.Правило     = Правило;
	Результат.Правила     = ?(Правила.Количество() = 0, Неопределено, Правила.Выгрузить());
	
	НачалоОбзора = РегистрыСведений.ПерваяОтчетнаяКампания.ПервыйПериодКампании(Организация);
	
	ДобавленыПрошлыеПериоды = Ложь;
	
	Для Каждого СтрокаПериода Из ОтчетныеПериоды Цикл
		
		Если НЕ СтрокаПериода.Требуется И Результат.ОтчетныеПериоды.Количество() = 0 Тогда
			// Не добавляем ненужные периоды раньше, чем добавили самый ранний выбранный.
			Продолжить;
		КонецЕсли;
		
		Если НЕ УпрощеннаяНулеваяОтчетность Тогда
			НоваяСтрока = Результат.ОтчетныеПериоды.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПериода);
			НоваяСтрока.Правило = ПравилоПоПериоду(Правило, НоваяСтрока.Период);
		Иначе
			Для НомерМесяца = 0 По 11 Цикл //храним помесячно
				Период = ДобавитьМесяц(СтрокаПериода.Период, НомерМесяца);
				Если Период >= НачалоОбзора Тогда
					//Первую отчетную кампанию не учитываем
					Продолжить;
				ИначеЕсли Период >= ТекущийОтчетныйПериод Тогда
					//Текущую отчетную кампанию не учитываем
					Продолжить;
				КонецЕсли;
				НоваяСтрока = Результат.ОтчетныеПериоды.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПериода);
				НоваяСтрока.Период = Период;
				
				НоваяСтрока.Правило = ПравилоПоПериоду(Правило, НоваяСтрока.Период);
			КонецЦикла;
		КонецЕсли;
		
		ДобавленыПрошлыеПериоды = ДобавленыПрошлыеПериоды ИЛИ СтрокаПериода.Требуется;
		
	КонецЦикла;
	
	Результат.ДобавленыПрошлыеПериоды = ДобавленыПрошлыеПериоды;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПравилоПоПериоду(Правило, Период)
	Если Правило.КонецДействия <> Дата(1, 1, 1) Тогда
		НужноИскатьДругоеПравило = Не (Период >= Правило.НачалоДействия И Период <= Правило.КонецДействия);
	Иначе
		// Не сравнивать с концом
		НужноИскатьДругоеПравило = Не (Период >= Правило.НачалоДействия);
	КонецЕсли;
	
	Если НужноИскатьДругоеПравило Тогда
		ПравилоПоСроку = Обработки.ПодготовкаОтчетностиПрошлыхПериодов.ПравилоДляПомощникаПодготовкиОтчетности(
			Организация, КонецГода(Период - 1), КонецГода(Период));
	КонецЕсли;
		
	Если ЗначениеЗаполнено(ПравилоПоСроку) Тогда
		Возврат ПравилоПоСроку;
	Иначе
		Возврат Правило;
	КонецЕсли;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ИнициализироватьПереключатель(Форма, ИмяЭлемента)
	
	Элементы = Форма.Элементы;
	
	Если НЕ ЗначениеЗаполнено(Форма[ИмяЭлемента]) Тогда // Не было сохранено в настройках
		Форма[ИмяЭлемента] = Элементы[ИмяЭлемента].СписокВыбора[0].Значение;
	КонецЕсли;
	
	Форма.ВыбранныйОтвет = Форма[ИмяЭлемента];
	
КонецПроцедуры

&НаСервере
Процедура ШагВперед(НовыйВопрос, ПредыдущийВопрос)
	
	Если НЕ ЗначениеЗаполнено(ПройденныйМаршрут) Тогда
		ПройденныйМаршрут = Новый Структура;
	КонецЕсли;
	
	ПройденныйМаршрут.Вставить(НовыйВопрос, ПредыдущийВопрос);
	
	ТекущийВопросТеста   = НовыйВопрос;
	НомерТекущегоВопроса = НомерТекущегоВопроса + 1;
	
КонецПроцедуры

&НаСервере
Процедура ШагНазад(ТекущийВопрос)
	
	ПредыдущийВопрос = ПредыдущийВопросТеста(ТекущийВопрос);
	
	ПройденныйМаршрут.Удалить(ТекущийВопрос);
	
	ТекущийВопросТеста   = ПредыдущийВопрос;
	НомерТекущегоВопроса = НомерТекущегоВопроса - 1;
	
КонецПроцедуры

&НаСервере
Функция ЕстьПредыдущийВопрос(ТекущийВопрос)
	
	Возврат ЗначениеЗаполнено(ПредыдущийВопросТеста(ТекущийВопрос));
	
КонецФункции

&НаСервере
Функция ПредыдущийВопросТеста(ТекущийВопрос)
	
	ПредыдущийВопрос = Неопределено;
	Если ЗначениеЗаполнено(ПройденныйМаршрут) Тогда
		ПройденныйМаршрут.Свойство(ТекущийВопрос, ПредыдущийВопрос);
	КонецЕсли;
	
	Возврат ПредыдущийВопрос;
	
КонецФункции

&НаСервереБезКонтекста
Функция КоличествоВопросовТеста(НачальныйВопрос)
	
	Возврат ПредельноеКоличествоВопросов() - СмещениеВопроса(НачальныйВопрос);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПредельноеКоличествоВопросов()
	
	Возврат 3;
	
КонецФункции

&НаСервереБезКонтекста
Функция СмещениеВопроса(ВопросТеста)
	
	Смещение = 0;
	
	Если СтрНачинаетсяС(ВопросТеста, "ГруппаТест") Тогда
		
		СтрокаНомерВопроса = Сред(ВопросТеста, СтрДлина("ГруппаТест") + 1, 1);
		
		Если СтрНайти("0123456789", СтрокаНомерВопроса) > 0 Тогда
			Смещение = Число(СтрокаНомерВопроса) - 1;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Смещение;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПервыйВопросТеста()
	
	Возврат ИмяВопрос1();
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьПериодаОбращенияВФНС(Форма)
	
	Элементы = Форма.Элементы;
	
	УказаниеПериодаДоступно = (Форма.В32_МогуУказатьПериод = "Год");
	
	Элементы.В32_ПериодОбращенияВФНС.Доступность = УказаниеПериодаДоступно;
	Элементы.В32_ПериодОбращенияВФНС.АвтоОтметкаНезаполненного = УказаниеПериодаДоступно;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьПодчиненныхЭлементов(ИмяГруппы, Видимость)
	
	Для Каждого ПодчиненныйЭлемент Из Элементы[ИмяГруппы].ПодчиненныеЭлементы Цикл
		ПодчиненныйЭлемент.Видимость = Видимость;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОтключитьВидимостьКнопок()
	
	Элементы.КнопкаНазад.Видимость = Ложь;
	Элементы.КнопкаДалее.Видимость = Ложь;
	Элементы.КнопкаГотово.Видимость = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ОтметитьОтчетныйПериод(ОтчетныйПериод, Требуется)
	
	Отбор = Новый Структура("Период", ОтчетныйПериод);
	СтрокиПериода = ОтчетныеПериоды.НайтиСтроки(Отбор);
	
	Для Каждого СтрокаТаблицы Из СтрокиПериода Цикл
		СтрокаТаблицы.Требуется = Требуется;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОписаниеПрошлогоОтчетногоПериода(Период, Периодичность)
	
	ПрошлыйОтчетныйПериод = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(Периодичность, Период) - 1;
	
	НачалоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(Периодичность, ПрошлыйОтчетныйПериод);
	КонецПериода  = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Периодичность, ПрошлыйОтчетныйПериод);
	
	ПредставлениеПериода = ПредставлениеПериода(НачалоПериода, КонецПериода, "ФП = Истина");
	ПредставлениеПериода = СтрЗаменить(ПредставлениеПериода, "г.", ?(Периодичность = Перечисления.Периодичность.Год, "год", "года"));
	ПредставлениеПериода = НРег(ПредставлениеПериода);
	
	Возврат ПредставлениеПериода;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстНомерВопроса(НомерВопроса, ВсегоВопросов)
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Шаг %1 из %2'"),
			Формат(НомерВопроса, "ЧН=0; ЧГ=0"),
			Формат(ВсегоВопросов, "ЧН=0; ЧГ=0"));
	
КонецФункции

&НаСервере
Процедура УстановитьЗаголовокИОписаниеОтчета()
	
	Если СрокСдачиТекущегоОтчетаЕщеНеПрошел() Тогда
		ЭтотОбъект.Заголовок = НСтр("ru = 'Просроченных отчетов нет'");
		Элементы.НадписьСледующийОтчет.Заголовок = ТекстТекущийОтчет();
	Иначе
		ЭтотОбъект.Заголовок = НСтр("ru = 'Сейчас не требуется сдавать отчетность'");
		Элементы.НадписьСледующийОтчет.Заголовок = ТекстСледующийОтчет();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СрокСдачиТекущегоОтчетаЕщеНеПрошел()
	
	СрокСдачиТекущегоОтчета = ВыполнениеЗадачБухгалтера.СрокВыполненияЗадачи(
		Организация, Правило, ТекущийОтчетныйПериод);
	
	Возврат ЗначениеЗаполнено(СрокСдачиТекущегоОтчета)
		И КонецДня(СрокСдачиТекущегоОтчета) > Обработки.МониторНалоговИОтчетности.ТекущаяДатаМонитора();
	
КонецФункции

&НаСервере
Функция ТекстТекущийОтчет()
	
	ДатаНачалаДеятельности = КалендарьБухгалтера.ДатаНачалаДеятельности(Организация);
	
	ОписаниеРеквизитов = Новый Структура;
	ОписаниеРеквизитов.Вставить("ИдентификаторЗадачи", "Владелец.Код");
	ОписаниеРеквизитов.Вставить("ФинансовыйПериод");
	ОписаниеРеквизитов.Вставить("РасширенныйПервыйНалоговыйПериод");
	
	РеквизитыПравила = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Правило, ОписаниеРеквизитов);
	
	ПериодичностьОтчетностиТекущегоПериода = ?(ГосударственныйОрган = Перечисления.ТипыКонтролирующихОрганов.ПФР,
		Перечисления.Периодичность.Месяц,
		ПериодичностьОтчетности);
		
	КонецТекущегоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(ПериодичностьОтчетностиТекущегоПериода, ТекущийОтчетныйПериод);
	
	ВидНалога = РасчетыСБюджетом.ВидНалогаПоКодуЗадачи(РеквизитыПравила.ИдентификаторЗадачи, Организация);
	
	Срок = Дата(1,1,1);
	НаименованиеОтчета = "";
	Если ЗначениеЗаполнено(ВидНалога) Тогда
	
		Порядок = РегистрыСведений.ЗадачиБухгалтера.ПорядокПредоставленияОтчетаУплатыНалогаЗаПериод(Организация,
			ВидНалога, КонецТекущегоПериода, Перечисления.ВидыДействийКалендаряБухгалтера.Отчет);
			
		ПорядокОтчета = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Порядок, "Отчет", Неопределено);
		Если Не ЗначениеЗаполнено(ПорядокОтчета) Тогда
			Возврат "";
		КонецЕсли;
		
		Срок = ПорядокОтчета.Срок;
		НаименованиеОтчета = ПорядокОтчета.Наименование;
		
	Иначе
		
		ВидыНалогов = РасчетыСБюджетом.ВидыНалоговПоКодуЗадачи(РеквизитыПравила.ИдентификаторЗадачи, Организация);
		
		Для Каждого ВидНалога Из ВидыНалогов Цикл
			Порядок = РегистрыСведений.ЗадачиБухгалтера.ПорядокПредоставленияОтчетаУплатыНалогаЗаПериод(Организация,
				ВидНалога, КонецТекущегоПериода, Перечисления.ВидыДействийКалендаряБухгалтера.Отчет);
			ПорядокОтчета = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Порядок, "Отчет", Неопределено);	
			Если ЗначениеЗаполнено(ПорядокОтчета) Тогда
				Если НЕ ЗначениеЗаполнено(Срок)
					ИЛИ Срок <= Порядок.Отчет.Срок Тогда
					Срок = ПорядокОтчета.Срок;
					НаименованиеОтчета = ПорядокОтчета.Наименование;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Если ПустаяСтрока(НаименованиеОтчета) Тогда
		Возврат "";
	КонецЕсли;
	
	ЧастиОписания = Новый Массив;
	
	ЧастиОписания.Добавить(СтрШаблон(Нстр("ru = 'До %1 требуется сдать текущий отчет'"), Формат(Срок, "ДЛФ=DD")));
	ЧастиОписания.Добавить(СтрШаблон("(%1)", НаименованиеОтчета));
	
	Возврат СтрСоединить(ЧастиОписания, Символы.ПС);
	
КонецФункции

&НаСервере
Функция ТекстСледующийОтчет()
	
	ПериодичностьОтчетностиСледующегоПериода = ?(ГосударственныйОрган = Перечисления.ТипыКонтролирующихОрганов.ПФР,
		Перечисления.Периодичность.Месяц,
		ПериодичностьОтчетности);
		
	СледующийОтчетныйПериод = ИнтерфейсыВзаимодействияБРОКлиентСервер.ДобавитьПериод(
		ТекущийОтчетныйПериод, ПериодичностьОтчетностиСледующегоПериода, 1);
	
	ОписаниеСледующегоОтчета = ПомощникиПоУплатеНалоговИВзносов.ОписаниеОтчета(Организация, Правило, СледующийОтчетныйПериод);
	
	Возврат ОписаниеСледующегоОтчета.ПредставлениеОтчета;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьОтчетныеПериоды(ВходныеДанные)
	
	// Срок давности по налоговым правонарушениям - 3 года со дня совершения.
	// Для не сданных вовремя деклараций считаем днем совершения крайний срок подачи.
	// Обстоятельства, влияющие на увеличение срока (воспрепятствование проверке, предыдущие акты и т.п.) не учитываем,
	// поскольку нет возможности получить о них достоверные сведения.
	// Таким образом, под ответственность с неистекшим сроком давности подпадает 3 отчетных периода, предшествующих текущему.
	
	МинимальныйПериодПоСрокуДавности = ДобавитьМесяц(НачалоГода(ТекущийОтчетныйПериод), -3 * 12);
	
	МинимальныйПериодПоНачалуДеятельности = ДатаНачалаДеятельности;
	
	Если ЗначениеЗаполнено(Правило) Тогда
		ТипРасширенногоПериода = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Правило, "РасширенныйПервыйНалоговыйПериод");
		Если ЗначениеЗаполнено(ТипРасширенногоПериода) Тогда
			ПервыйНалоговыйПериод = ИнтерфейсыВзаимодействияБРО.БлижайшийНалоговыйПериод(Организация,
				ДатаНачалаДеятельности, ТипРасширенногоПериода,, ДатаНачалаДеятельности);
			
			МинимальныйПериодПоНачалуДеятельности = ПервыйНалоговыйПериод.Период;
		КонецЕсли;
	КонецЕсли;
	
	МинимальныйОтчетныйПериод = Макс(МинимальныйПериодПоНачалуДеятельности, МинимальныйПериодПоСрокуДавности);
	Если ГосударственныйОрган = Перечисления.ТипыКонтролирующихОрганов.ФНС
		И ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Организация) Тогда
		// Возможно мы загрузили выписку из ЕГРЮЛ и у нас есть данные о последней отчетности
		ДатаНачалаУчета = ДатаВыпискиИзЕГРЮЛ();
		Если ЗначениеЗаполнено(ДатаНачалаУчета) Тогда
			МинимальныйОтчетныйПериод = Макс(ДатаНачалаУчета, МинимальныйОтчетныйПериод);
		КонецЕсли;
	КонецЕсли;
	
	НачалоМинимальногоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(
		ПериодичностьОтчетности, МинимальныйОтчетныйПериод);
	
	КонецПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(ПериодичностьОтчетности, ТекущийОтчетныйПериод);
	
	Периоды = КалендарьБухгалтера.Периоды(НачалоМинимальногоПериода, КонецПериода, Перечисления.Периодичность.Год);
	
	Для Каждого ОтчетныйПериод Из Периоды Цикл
		
		Если ОтчетныйПериод = ТекущийОтчетныйПериод Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока         = ОтчетныеПериоды.Добавить();
		НоваяСтрока.Период  = ОтчетныйПериод;
		
		// Проверим, есть ли отчетный период во входящем списке.
		Отбор = Новый Структура("Период", ОтчетныйПериод);
		НайденныеСтроки = ВходныеДанные.НайтиСтроки(Отбор);
		
		НоваяСтрока.Требуется = (НайденныеСтроки.Количество() > 0 И НайденныеСтроки[0].Требуется);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ДатаВыпискиИзЕГРЮЛ()

	ДатаНачалаУчета = Обработки.ВводНачальныхОстатков.ПолучитьДатуНачалаУчета(Организация);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",     Организация);
	Запрос.УстановитьПараметр("ДатаНачалаУчета", НачалоДня(ДатаНачалаУчета));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВводНачальныхОстатков.Комментарий КАК ОписаниеДокумента
	|ИЗ
	|	Документ.ВводНачальныхОстатков КАК ВводНачальныхОстатков
	|ГДЕ
	|	НАЧАЛОПЕРИОДА(ВводНачальныхОстатков.Дата, ДЕНЬ) = &ДатаНачалаУчета
	|	И ВводНачальныхОстатков.Организация = &Организация
	|	И ВводНачальныхОстатков.Проведен";
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		ТаблицаРезультата = Результат.Выгрузить();
		ТаблицаРезультата.Свернуть("ОписаниеДокумента");
		Если ТаблицаРезультата.Количество() = 1 Тогда
			Если ЗначениеЗаполнено(ТаблицаРезультата[0].ОписаниеДокумента) Тогда
				Возврат КонецДня(ДатаНачалаУчета)+1;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат "";
		
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяВопрос1()
	
	Возврат "ГруппаТест1";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяВопрос2()
	
	Возврат "ГруппаТест2";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяВопрос31()
	
	Возврат "ГруппаТест31";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяВопрос32()
	
	Возврат "ГруппаТест32";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяВопрос33()
	
	Возврат "ГруппаТест33";
	
КонецФункции

&НаСервере
Процедура ЗаполнитьгосОрганВДекорациях()
	
	Если ГосударственныйОрган = Перечисления.ТипыКонтролирующихОрганов.ФНС Тогда
		НадписьВопрос2       = НСтр("ru = 'налоговой инспекции'");
		НадписьДополнение2   = НСтр("ru = 'налоговая инспекция'");
		НадписьВопрос31      = НСтр("ru = 'налоговая инспекция'");
		НадписьВопрос32      = НСтр("ru = 'налоговую инспекцию'");
		НадписьДополнение322 = НСтр("ru = 'налоговую инспекцию'");
	ИначеЕсли ГосударственныйОрган = Перечисления.ТипыКонтролирующихОрганов.ПФР Тогда
		НадписьВопрос2       = НСтр("ru = 'пенсионного фонда'");
		НадписьДополнение2   = НСтр("ru = 'пенсионный фонд'");
		НадписьВопрос31      = НСтр("ru = 'пенсионный фонд'");
		НадписьВопрос32      = НСтр("ru = 'пенсионный фонд'");
		НадписьДополнение322 = НСтр("ru = 'пенсионный фонд'");
	ИначеЕсли ГосударственныйОрган = Перечисления.ТипыКонтролирующихОрганов.ФСС Тогда
		НадписьВопрос2       = НСтр("ru = 'фонда социального страхования'");
		НадписьДополнение2   = НСтр("ru = 'фонд социального страхования'");
		НадписьВопрос31      = НСтр("ru = 'фонд социального страхования'");
		НадписьВопрос32      = НСтр("ru = 'фонд социального страхования'");
		НадписьДополнение322 = НСтр("ru = 'фонд социального страхования'");
	Иначе
		НадписьВопрос2       = НСтр("ru = 'контролирующего органа'");
		НадписьДополнение2   = НСтр("ru = 'контролирующий орган'");
		НадписьВопрос31      = НСтр("ru = 'контролирующий орган'");
		НадписьВопрос32      = НСтр("ru = 'контролирующий орган'");
		НадписьДополнение322 = НСтр("ru = 'контролирующий орган'");
	КонецЕсли;
	
	Элементы.НадписьВопрос2.Заголовок       = СтрЗаменить(Элементы.НадписьВопрос2.Заголовок,
												"%ГосОрган%", НадписьВопрос2);
	Элементы.НадписьДополнение2.Заголовок   = СтрЗаменить(Элементы.НадписьДополнение2.Заголовок,
												"%ГосОрган%", НадписьДополнение2);
	Элементы.НадписьВопрос31.Заголовок      = СтрЗаменить(Элементы.НадписьВопрос31.Заголовок,
												"%ГосОрган%", НадписьВопрос31);
	Элементы.НадписьВопрос32.Заголовок      = СтрЗаменить(Элементы.НадписьВопрос32.Заголовок,
												"%ГосОрган%", НадписьВопрос32);
	Элементы.НадписьДополнение322.Заголовок = СтрЗаменить(Элементы.НадписьДополнение322.Заголовок,
												"%ГосОрган%", НадписьДополнение322);
КонецПроцедуры

#КонецОбласти