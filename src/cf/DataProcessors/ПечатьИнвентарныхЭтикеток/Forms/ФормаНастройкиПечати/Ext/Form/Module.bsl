#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьСписокРазмеровБумаги();
	ЗаполнитьСписокИнвентарныхЭтикеток();
	
	Настройки = Неопределено;
	ПолучитьОбъектОбработки().ЗагрузитьНастройкиПечати(Настройки);
	Если ТипЗнч(Настройки) = Тип("Соответствие") Тогда
		Объект.РазмерБумаги 	= Настройки["РазмерБумаги"];
		Объект.РазмерЭтикетки 	= Настройки["РазмерЭтикетки"];
		Объект.СразуНаПринтер 	= Настройки["СразуНаПринтер"];
	КонецЕсли;
	Если Параметры.Свойство("ОбъектыПечати") Тогда
		СписокОбъектовПечати.ЗагрузитьЗначения(Параметры.ОбъектыПечати);
	КонецЕсли;
	Если Параметры.Свойство("Организация") Тогда
		Объект.Организация = Параметры.Организация;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	ПередЗакрытиемНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗакрытиемНаСервере()
	
	Если Параметры.Свойство("ОбъектыПечати") Тогда	
		ПолучитьОбъектОбработки().СохранитьНастройкиПечати();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда 
		Возврат;
	КонецЕсли;
	
	ВидЭтикетки = "ИнвентарнаяЭтикеткаОС";
	
	ПараметрыПечати = Новый Структура("ПечатьИзДокумента", Ложь);
	ПараметрыПечати.Вставить("ИмяМакета"		, Объект.РазмерЭтикетки);
	ПараметрыПечати.Вставить("РазмерБумаги"		, Объект.РазмерБумаги);
	ПараметрыПечати.Вставить("СразуНаПринтер"	, Объект.СразуНаПринтер);
	ПараметрыПечати.Вставить("Организация"		, Объект.Организация);
	
	Если ТипЗнч(СписокОбъектовПечати[0].Значение) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") ИЛИ
		 ТипЗнч(СписокОбъектовПечати[0].Значение) = Тип("ДокументСсылка.ПринятиеКУчетуОС") Тогда
			СписокОбъектовНаПечать = СписокОбъектовПечати.ВыгрузитьЗначения();
			ПараметрыПечати.ПечатьИзДокумента = Истина;
			ПараметрыПечати.Организация = ПолучитьОрганизациюДокументаНаСервере(СписокОбъектовПечати[0].Значение);
	Иначе
		СписокОбъектовНаПечать = Новый Массив;
		СписокОбъектовНаПечать.Добавить(ПустойДокумент());
		ПараметрыПечати.Вставить("ДанныеДляПечати", СписокОбъектовПечати.ВыгрузитьЗначения());
	КонецЕсли;
		
	Если Объект.СразуНаПринтер Тогда
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечатиНаПринтер("Обработка.ПечатьИнвентарныхЭтикеток", ВидЭтикетки, СписокОбъектовНаПечать, ПараметрыПечати);
	Иначе
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Обработка.ПечатьИнвентарныхЭтикеток", ВидЭтикетки, СписокОбъектовНаПечать, ЭтотОбъект, ПараметрыПечати);
	КонецЕсли; 
		
	Настройки = Новый Соответствие;
	Настройки.Вставить("РазмерБумаги"	, Объект.РазмерБумаги);
	Настройки.Вставить("РазмерЭтикетки"	, Объект.РазмерЭтикетки);
	Настройки.Вставить("СразуНаПринтер"	, Объект.СразуНаПринтер);
	СохранитьНастройкиПечатиНаСервере(Настройки);
	Закрыть();

КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииОсновнойПрограммы

&НаСервере
Процедура ЗаполнитьСписокРазмеровБумаги()
	
	Элементы.РазмерБумаги.СписокВыбора.Добавить("ЛистФормата_А4", "Лист формата А4");
	Элементы.РазмерБумаги.СписокВыбора.Добавить("ЛентаТермоэтикеток", "Лента термоэтикеток");

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокИнвентарныхЭтикеток()
	
	Макеты = Метаданные.Обработки.ПечатьИнвентарныхЭтикеток.Макеты;
	Для Каждого Макет Из Макеты Цикл
		Элементы.РазмерЭтикетки.СписокВыбора.Добавить(Макет.Имя, Макет.Синоним);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиПечатиНаСервере(Настройки)
	
	ПолучитьОбъектОбработки().СохранитьНастройкиПечати(Настройки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьОбъектОбработки()
	
	Возврат РеквизитФормыВЗначение("Объект");
	
КонецФункции

&НаСервере
Функция ПолучитьОрганизациюДокументаНаСервере(ДокСсылка)
	
	Возврат ДокСсылка.Организация;
	
КонецФункции

&НаСервере
Функция ПустойДокумент()
	
	Возврат Документы.ПоступлениеТоваровУслуг.ПустаяСсылка();
	
КонецФункции


#КонецОбласти



