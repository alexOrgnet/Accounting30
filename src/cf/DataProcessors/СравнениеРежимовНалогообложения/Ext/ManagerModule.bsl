#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает Истина, если включен режим отладки.
//
// Возвращаемое значение:
//  Булево - Истина, если включен режим отладки.
//
Функция РежимОтладки() Экспорт
	
	ПараметрЗапускаПриложения = ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("ПараметрЗапуска");
	Возврат СтрНайти(ПараметрЗапускаПриложения, "СравнениеРежимовНалогообложения") > 0;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ПараметрыДоступныхОрганизаций() Экспорт
	
	Если Не ПравоДоступа("Чтение", Метаданные.РегистрыСведений.НастройкиСистемыНалогообложения) Тогда
		Возврат Новый ТаблицаЗначений;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организации",
		УправлениеДоступомБП.ОрганизацииДанныеКоторыхДоступныПользователю("РегистрБухгалтерии.Хозрасчетный"));
	Запрос.УстановитьПараметр("НаДату", ОбщегоНазначения.ТекущаяДатаПользователя());
	Запрос.УстановитьПараметр("ТекстОСНО", НСтр("ru='ОСНО'"));
	Запрос.УстановитьПараметр("ТекстУСН", НСтр("ru='УСН'"));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫБОР НастройкиСистемыНалогообложенияСрезПоследних.СистемаНалогообложения
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Общая)
	|			ТОГДА &ТекстОСНО
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Упрощенная)
	|			ТОГДА &ТекстУСН
	|	КОНЕЦ КАК ТекущийРежим,
	|	НастройкиСистемыНалогообложенияСрезПоследних.Организация,
	|	НастройкиСистемыНалогообложенияСрезПоследних.СистемаНалогообложения
	|ИЗ
	|	РегистрСведений.НастройкиСистемыНалогообложения.СрезПоследних(
	|			&НаДату,
	|			Организация В (&Организации)
	|				И СистемаНалогообложения <> ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.ОсобыйПорядок)) КАК НастройкиСистемыНалогообложенияСрезПоследних";
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Результат;
	
КонецФункции

Функция ПараметрыНачалаРаботы() Экспорт
	
	Возврат ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		ИмяНастроекСравненияРежимовНалогообложения(),
		ИмяНастройкиПараметрыНачалаРаботы());
	
КонецФункции

Процедура СохранитьПараметрыНачалаРаботы(Параметры) Экспорт
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		ИмяНастроекСравненияРежимовНалогообложения(),
		ИмяНастройкиПараметрыНачалаРаботы(),
		Параметры);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИмяНастроекСравненияРежимовНалогообложения()
	
	Возврат "СравнениеРежимовНалогообложения";
	
КонецФункции

Функция ИмяНастройкиПараметрыНачалаРаботы()
	
	Возврат "ПараметрыНачалаРаботы";
	
КонецФункции

#КонецОбласти

#КонецЕсли

