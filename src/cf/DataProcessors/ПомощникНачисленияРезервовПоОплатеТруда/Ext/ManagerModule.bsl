#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Готовит структуру параметров для формирования комплекта документов
// Вовзращаемое значение:
//     Структура
//
Функция ПараметрыФормированияДокументов()Экспорт
	
	ПараметрыОбработки = Новый Структура();
	ПараметрыОбработки.Вставить("Организация",                 Справочники.Организации.ПустаяСсылка());
	ПараметрыОбработки.Вставить("МесяцНачисления",             Дата(1,1,1));
	ПараметрыОбработки.Вставить("Документы_РезервОтпусков",    Новый ТаблицаЗначений);
	ПараметрыОбработки.Вставить("Документы_РезервПремий",      Новый ТаблицаЗначений);
	ПараметрыОбработки.Вставить("ВедетсяУчетРезервовОтпусков", Ложь);
	ПараметрыОбработки.Вставить("ВедетсяУчетРезервовПремий",   Ложь);
	ПараметрыОбработки.Вставить("ТолькоПросмотр",              Ложь);
	ПараметрыОбработки.Вставить("ПодготовитьДанные",           Ложь);
	
	Возврат ПараметрыОбработки;
	
КонецФункции

// Формирует коплект документов начисления резервов
//
// Параметры:
//  ПараметрыОбработки - Структура, см. ПараметрыФормированияДокументов
//  АдресХранилища     - Строка.
//
Процедура СформироватьДокументы(ПараметрыОбработки, АдресХранилища = Неопределено) Экспорт
	
	Если Не ПараметрыОбработки.ТолькоПросмотр Тогда
		СформироватьДокументыНачисленияРезервов(ПараметрыОбработки);
	КонецЕсли;
	Если ПараметрыОбработки.ПодготовитьДанные И АдресХранилища <> Неопределено Тогда
		ПодготовитьДанные(ПараметрыОбработки, АдресХранилища);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ГенераторКомплектовДокументов

Процедура СформироватьДокументыНачисленияРезервов(ПараметрыОбработки)
	
	Организация                 = ПараметрыОбработки.Организация;
	МесяцНачисления             = ПараметрыОбработки.МесяцНачисления;
	ВедетсяУчетРезервовОтпусков = ПараметрыОбработки.ВедетсяУчетРезервовОтпусков;
	ВедетсяУчетРезервовПремий   = ПараметрыОбработки.ВедетсяУчетРезервовПремий;
	
	Если ВедетсяУчетРезервовОтпусков Тогда
		УчетЗарплаты.ПолучитьДокументНачисленияРезерваОтпусков(МесяцНачисления, Организация);
		Если Месяц(МесяцНачисления) = 12 Тогда
			УчетЗарплаты.ПолучитьДокументИнвентаризацииРезерваОтпусков(МесяцНачисления, Организация);
		КонецЕсли;
	КонецЕсли;
	
	Если ВедетсяУчетРезервовПремий Тогда
		УчетЗарплаты.ПолучитьДокументНачисленияРезерваПремий(МесяцНачисления, Организация);
		Если Месяц(МесяцНачисления) = 12 Тогда
			УчетЗарплаты.ПолучитьДокументИнвентаризацииРезерваПремий(МесяцНачисления, Организация);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПоискКомплектовДокументов

Процедура ПодготовитьДанные(ПараметрыОбработки, АдресХранилища)
	
	Организация                 = ПараметрыОбработки.Организация;
	МесяцНачисления             = ПараметрыОбработки.МесяцНачисления;
	ВедетсяУчетРезервовОтпусков = ПараметрыОбработки.ВедетсяУчетРезервовОтпусков;
	ВедетсяУчетРезервовПремий   = ПараметрыОбработки.ВедетсяУчетРезервовПремий;
	
	Документы_РезервОтпусков = ПараметрыОбработки.Документы_РезервОтпусков;
	Документы_РезервПремий   = ПараметрыОбработки.Документы_РезервПремий;
	
	Документы_РезервОтпусков.Очистить();
	Документы_РезервПремий.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",     Организация);
	Запрос.УстановитьПараметр("МесяцНачисления", МесяцНачисления);
	
	// Сведения о резервах отпуско
	Если ВедетсяУчетРезервовОтпусков Тогда
		
		Запрос.УстановитьПараметр("Резерв", УчетЗарплаты.РезервОтпусков(МесяцНачисления, Организация));
		Запрос.Текст =
		"ВЫБРАТЬ
		|	НачислениеОценочныхОбязательствПоОтпускам.Ссылка КАК Ссылка,
		|	ЛОЖЬ КАК Инвентаризация
		|ИЗ
		|	Документ.НачислениеОценочныхОбязательствПоОтпускам КАК НачислениеОценочныхОбязательствПоОтпускам
		|ГДЕ
		|	НачислениеОценочныхОбязательствПоОтпускам.Проведен
		|	И НачислениеОценочныхОбязательствПоОтпускам.Организация = &Организация
		|	И НачислениеОценочныхОбязательствПоОтпускам.ПериодРегистрации = &МесяцНачисления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РезервыПоОплатеТруда.Ссылка,
		|	РезервыПоОплатеТруда.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Инвентаризация)
		|ИЗ
		|	Документ.РезервыПоОплатеТруда КАК РезервыПоОплатеТруда
		|ГДЕ
		|	РезервыПоОплатеТруда.Проведен
		|	И РезервыПоОплатеТруда.Организация = &Организация
		|	И РезервыПоОплатеТруда.МесяцНачисления = &МесяцНачисления
		|	И РезервыПоОплатеТруда.Резерв = &Резерв";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = Документы_РезервОтпусков.Добавить();
			НоваяСтрока.Документ              = Выборка.Ссылка;
			НоваяСтрока.ДокументПредставление = ПредставлениеДокумента(Выборка.Ссылка);
			НоваяСтрока.Инвентаризация        = Выборка.Инвентаризация;
		КонецЦикла;
	КонецЕсли;
		
	// Сведения о резервах премии
	Если ВедетсяУчетРезервовПремий Тогда
		
		Запрос.УстановитьПараметр("Резерв", УчетЗарплаты.РезервПремий(МесяцНачисления, Организация));
		Запрос.Текст =
		"ВЫБРАТЬ
		|	РезервыПоОплатеТруда.Ссылка КАК Ссылка,
		|	РезервыПоОплатеТруда.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРезервовПоОплатеТруда.Инвентаризация) КАК Инвентаризация
		|ИЗ
		|	Документ.РезервыПоОплатеТруда КАК РезервыПоОплатеТруда
		|ГДЕ
		|	РезервыПоОплатеТруда.Проведен
		|	И РезервыПоОплатеТруда.Организация = &Организация
		|	И РезервыПоОплатеТруда.МесяцНачисления = &МесяцНачисления
		|	И РезервыПоОплатеТруда.Резерв = &Резерв";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = Документы_РезервПремий.Добавить();
			НоваяСтрока.Документ              = Выборка.Ссылка;
			НоваяСтрока.ДокументПредставление = ПредставлениеДокумента(Выборка.Ссылка);
			НоваяСтрока.Инвентаризация        = Выборка.Инвентаризация;
		КонецЦикла;
	КонецЕсли;
	
	РезультатВыполнения = Новый Структура();
	РезультатВыполнения.Вставить("Документы_РезервОтпусков", Документы_РезервОтпусков);
	РезультатВыполнения.Вставить("Документы_РезервПремий", Документы_РезервПремий);
	ПоместитьВоВременноеХранилище(РезультатВыполнения, АдресХранилища);
	
КонецПроцедуры

Функция ПредставлениеДокумента(ДокументСсылка)
	
	ПредставлениеДокумента = ДокументСсылка.Метаданные().Синоним;
	РеквизитыДокумента     = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "Номер, Дата");
	НомерДокумента         = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(РеквизитыДокумента.Номер, Истина, Истина);
	ДатаДокумента          = Формат(РеквизитыДокумента.Дата,"ДЛФ=D");
	
	ШаблонПредставления = НСтр("ru = '%1 %2 от %3'");
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПредставления,
		ПредставлениеДокумента, НомерДокумента, ДатаДокумента);
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли

