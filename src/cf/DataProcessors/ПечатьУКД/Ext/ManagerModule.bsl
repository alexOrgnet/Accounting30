#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// ПРОЦЕДУРЫ И ФУНКЦИИ ФОРМИРОВАНИЯ ПЕЧАТНЫХ ФОРМ

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ЕстьСчетаФактуры = Ложь;
	Для каждого Объект Из МассивОбъектов Цикл
		Если ТипЗнч(Объект) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
			ЕстьСчетаФактуры = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаМакетовУКД = УчетНДС.ТаблицаМакетовУКД();
	
	// Печать УКД в статусе "1"
	Если ЕстьСчетаФактуры Тогда
		Для Каждого МакетУКД ИЗ ТаблицаМакетовУКД Цикл
			Если МакетУКД.Статус <> "1" Тогда
				Продолжить;
			КонецЕсли;
			Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, МакетУКД.ИДМакета) Тогда
				
				ТекстЗапроса = Документы.СчетФактураВыданный.ТекстЗапросаПечатьКорректировочныхСчетовФактур(
					МакетУКД.ВерсияПостановленияНДС1137, Истина, МакетУКД.ПрослеживаемыеТовары);
				
				СтруктураПараметровПечати = УчетНДС.НовыйСтруктураПараметровПечатиУПД_УКД();
				СтруктураПараметровПечати.МассивОбъектов              = МассивОбъектов;
				СтруктураПараметровПечати.ОбъектыПечати               = ОбъектыПечати;
				СтруктураПараметровПечати.ТекстЗапросаДокументам      = ТекстЗапроса;
				СтруктураПараметровПечати.ТолькоПередаточныйДокумент  = Ложь;
				СтруктураПараметровПечати.ТабДокумент                 = Неопределено;
				СтруктураПараметровПечати.ПараметрыПечати             = ПараметрыПечати;
				СтруктураПараметровПечати.КлючПараметровПечати        = МакетУКД.КлючПараметровПечати;
				СтруктураПараметровПечати.ПолныйПутьКМакету           = МакетУКД.ПолныйПутьКМакету;
				СтруктураПараметровПечати.ПараметрыВывода             = ПараметрыВывода;
				
				ТабличныйДокумент = УчетНДС.ПечатьУниверсальныхКорректировочныхДокументов(СтруктураПараметровПечати);
				
				УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
					КоллекцияПечатныхФорм, МакетУКД.ИДМакета, МакетУКД.СинонимМакета, ТабличныйДокумент,,МакетУКД.ПолныйПутьКМакету);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Печать УКД в статусе "2"
	Для Каждого МакетУКД ИЗ ТаблицаМакетовУКД Цикл
		Если МакетУКД.Статус <> "2" Тогда
			Продолжить;
		КонецЕсли;
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, МакетУКД.ИДМакета) Тогда
			ТекстЗапросаПечатьУниверсальныхКорректировочныхДокументов = "";
			Для каждого Объект Из МассивОбъектов Цикл
				Если ТипЗнч(Объект) <> Тип("ДокументСсылка.СчетФактураВыданный") Тогда
					ТекстЗапросаПечатьУниверсальныхКорректировочныхДокументов = 
					Документы[Объект.Метаданные().Имя].ТекстЗапросаПечатьУниверсальныхКорректировочныхДокументов(
						МакетУКД.ВерсияПостановленияНДС1137, МакетУКД.ПрослеживаемыеТовары);
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если НЕ ПустаяСтрока(ТекстЗапросаПечатьУниверсальныхКорректировочныхДокументов) Тогда
				
				СтруктураПараметровПечати = УчетНДС.НовыйСтруктураПараметровПечатиУПД_УКД();
				СтруктураПараметровПечати.МассивОбъектов              = МассивОбъектов;
				СтруктураПараметровПечати.ОбъектыПечати               = ОбъектыПечати;
				СтруктураПараметровПечати.ТекстЗапросаДокументам      = ТекстЗапросаПечатьУниверсальныхКорректировочныхДокументов;
				СтруктураПараметровПечати.ТолькоПередаточныйДокумент  = Истина;
				СтруктураПараметровПечати.ТабДокумент                 = Неопределено;
				СтруктураПараметровПечати.ПараметрыПечати             = ПараметрыПечати;
				СтруктураПараметровПечати.КлючПараметровПечати        = МакетУКД.КлючПараметровПечати;
				СтруктураПараметровПечати.ПолныйПутьКМакету           = МакетУКД.ПолныйПутьКМакету;
				СтруктураПараметровПечати.ПараметрыВывода             = ПараметрыВывода;
				
				ТабличныйДокумент = УчетНДС.ПечатьУниверсальныхКорректировочныхДокументов(СтруктураПараметровПечати);
				
				УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
					КоллекцияПечатныхФорм, МакетУКД.ИДМакета, МакетУКД.СинонимМакета, ТабличныйДокумент,,МакетУКД.ПолныйПутьКМакету);
				
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ОбщегоНазначенияБП.ЗаполнитьДополнительныеПараметрыПечати(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	
КонецПроцедуры

#КонецЕсли