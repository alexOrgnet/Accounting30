#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
	
// Интерфейс для запуска логики обработки
//
// Параметры
// МассивОбъектов - Массив - массив ссылок на объекты печати
// КоллекцияПечатныхФорм - ТаблицаЗначений - таблица содержащая информацию по печатным формам
// ОбъектыПечати - СписокЗначений - ссылки на объекты печати
// ПараметрыВывода - Структура - дополнительные параметры вывода (печать покомплектно и др.)
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	Перем ИмяМакета;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТранспортнаяНакладная") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ТранспортнаяНакладная", "Транспортная накладная",
			СформироватьПечатнуюФормуТранспортнойНакладной(МассивОбъектов, ОбъектыПечати, ИмяМакета),,ИмяМакета);
	КонецЕсли;
	
	ОбщегоНазначенияБП.ЗаполнитьДополнительныеПараметрыПечати(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Функция СформироватьПечатнуюФормуТранспортнойНакладной(МассивОбъектов, ОбъектыПечати, ИмяМакета)
	УстановитьПривилегированныйРежим(Истина);
	
	ИмяМакета = Неопределено;
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	ТабличныйДокумент.АвтоМасштаб          = Истина;
	ТабличныйДокумент.ОриентацияСтраницы   = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ТРАНСПОРТНАЯ_НАКЛАДНАЯ";
	
	ПечатьТорговыхДокументов.УстановитьМинимальныеПоляПечати(ТабличныйДокумент);
	
	ПервыйДокумент = Истина;
	
	Для Каждого СтруктураОбъектов Из ОбщегоНазначенияБП.РазложитьСписокПоТипамОбъектов(МассивОбъектов) Цикл
		ДанныеДляПечати = Документы[СтруктураОбъектов.Ключ.Имя].ПолучитьДанныеДляПечатнойФормыТранспортнаяНакладная(СтруктураОбъектов.Значение);
		
		ЗаполнитьТабличныйДокументТН(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати, ИмяМакета, ПервыйДокумент);
	КонецЦикла;
	
	Если ИмяМакета = Неопределено Тогда
		ИмяМакета = "";
	КонецЕсли; 
	
	Возврат ТабличныйДокумент;
КонецФункции

Процедура ЗаполнитьТабличныйДокументТН(ТабличныйДокумент, ТаблицаДанныхДляПечати, ОбъектыПечати, ИмяМакета, ПервыйДокумент)
	Для Каждого ДанныеПечати Из ТаблицаДанныхДляПечати Цикл
		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		СведенияОКонтрагентах = Новый Структура;
		
		СведенияОКонтрагентах.Вставить("СведенияОГрузополучателе",  БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(ДанныеПечати.Грузополучатель,  ДанныеПечати.Дата));
		СведенияОКонтрагентах.Вставить("СведенияОГрузоотправителе", БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(ДанныеПечати.Грузоотправитель, ДанныеПечати.Дата));
		СведенияОКонтрагентах.Вставить("СведенияОПеревозчике",      БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(ДанныеПечати.Перевозчик,       ДанныеПечати.Дата));
		
		Если ЗначениеЗаполнено(ДанныеПечати.АдресДоставки) Тогда
			СведенияОКонтрагентах.СведенияОГрузополучателе.Вставить("ФактическийАдрес", ДанныеПечати.АдресДоставки);
		КонецЕсли; 
		
		Если ДанныеПечати.Дата < '20120313' Тогда
			ПараметрыМакета = ПараметрыМакетаТН(ДанныеПечати, СведенияОКонтрагентах);
		ИначеЕсли ДанныеПечати.Дата < '20171222' Тогда 
			ПараметрыМакета = ПараметрыМакетаТН1208(ДанныеПечати, СведенияОКонтрагентах);
		ИначеЕсли ДанныеПечати.Дата < '20210101' Тогда
			ПараметрыМакета = ПараметрыМакетаТН1529(ДанныеПечати, СведенияОКонтрагентах);
		ИначеЕсли ДанныеПечати.Дата < '20220301' Тогда
			ПараметрыМакета = ПараметрыМакетаТН2200(ДанныеПечати, СведенияОКонтрагентах);
		Иначе
			ПараметрыМакета = ПараметрыМакетаТН2116(ДанныеПечати, СведенияОКонтрагентах);
		КонецЕсли;
		
		Макет = УправлениеПечатью.МакетПечатнойФормы(ПараметрыМакета.ИмяМакета);
		Если ИмяМакета = Неопределено Тогда
			ИмяМакета = ПараметрыМакета.ИмяМакета;
		ИначеЕсли ИмяМакета <> ПараметрыМакета.ИмяМакета Тогда
			ИмяМакета = "";
		КонецЕсли; 
		
		ОбластьМакета = Макет.ПолучитьОбласть("ГоризонтальнаяЛицеваяСторона");
		ОбластьМакета.Параметры.Заполнить(ПараметрыМакета);
		
		ТабличныйДокумент.Вывести(ОбластьМакета);
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		ОбластьМакетаОборотная = Макет.ПолучитьОбласть("ГоризонтальнаяОборотнаяСторона");
		ОбластьМакетаОборотная.Параметры.Заполнить(ПараметрыМакета);
		
		ТабличныйДокумент.Вывести(ОбластьМакетаОборотная);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);
	КонецЦикла;
КонецПроцедуры

#Область ПараметрыМакета
	
Функция ПараметрыМакетаТН(ДанныеПечати, СведенияОКонтрагентах)
	ПараметрыМакета = Новый Структура;
	ПараметрыМакета.Вставить("ИмяМакета", "Обработка.ПечатьТранспортнойНакладной.ПФ_MXL_ТранспортнаяНакладная");
	
	СведенияОГрузополучателе  = СведенияОКонтрагентах.СведенияОГрузополучателе;
	СведенияОГрузоотправителе = СведенияОКонтрагентах.СведенияОГрузоотправителе;
	СведенияОПеревозчике      = СведенияОКонтрагентах.СведенияОПеревозчике;
	
	Если СведенияОГрузополучателе.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		ПараметрыМакета.Вставить("Пункт2_1", ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОГрузополучателе, 
			"НаименованиеДляПечатныхФорм,ИНН,ФактическийАдрес,Телефоны"));
	Иначе
		ПараметрыМакета.Вставить("Пункт2_2", ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОГрузополучателе, 
			"НаименованиеДляПечатныхФорм,ФактическийАдрес,Телефоны"));
	КонецЕсли;
	
	Если СведенияОГрузоотправителе.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		ПредставлениеГрузоотправителя = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОГрузоотправителе, 
		"НаименованиеДляПечатныхФорм,ИНН,ФактическийАдрес,Телефоны");
		
		ПараметрыМакета.Вставить("Пункт1_1", ПредставлениеГрузоотправителя);
	Иначе
		ПредставлениеГрузоотправителя = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОГрузоотправителе, 
		"НаименованиеДляПечатныхФорм,ФактическийАдрес,Телефоны");
		
		ПараметрыМакета.Вставить("Пункт1_2", ПредставлениеГрузоотправителя);
	КонецЕсли;
	
	ПараметрыМакета.Вставить("Пункт0_2", Формат(ДанныеПечати.Дата, "ДЛФ=D"));
	
	ПараметрыМакета.Вставить("Пункт3_1", ДанныеПечати.КраткоеНаименованиеГруза);
	
	ПараметрыМакета.Вставить("Пункт4_1", ДанныеПечати.СопроводительныеДокументы);
	
	ПараметрыМакета.Вставить("Пункт6_1", СведенияОГрузоотправителе.ФактическийАдрес);
	
	ПараметрыМакета.Вставить("Пункт7_1", ДанныеПечати.АдресДоставки);
	
	Если СведенияОПеревозчике.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		ПредставлениеПеревозчика = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОПеревозчике, 
		"НаименованиеДляПечатныхФорм,ИНН,ФактическийАдрес,Телефоны");
		
		ПараметрыМакета.Вставить("Пункт10_1", ПредставлениеПеревозчика);
	Иначе
		ПредставлениеПеревозчика = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОПеревозчике, 
		"НаименованиеДляПечатныхФорм,ФактическийАдрес,Телефоны");
		
		// До 01.01.2021 ИНН в реквизитах перевозчика  не указывался
		ПараметрыМакета.Вставить("Пункт10_2", ПредставлениеПеревозчика);
	КонецЕсли;
	
	ПараметрыМакета.Вставить("Пункт10_4", ДанныеПечати.Водитель);
	
	ПараметрыМакета.Вставить("Пункт11_1", ДанныеПечати.МаркаАвтомобиля);
	ПараметрыМакета.Вставить("Пункт11_2", ДанныеПечати.РегистрационныйЗнакАвтомобиля);
	
	// Раздел "Стоимость перевозки груза"
	ПараметрыМакета.Вставить("Пункт16_1", ПредставлениеГрузоотправителя);
	ПараметрыМакета.Вставить("Пункт16_2", ПредставлениеПеревозчика);
	
	ПараметрыМакета.Вставить("Пункт16_11", Формат(ДанныеПечати.Дата, "ДЛФ=D"));
	ПараметрыМакета.Вставить("Пункт16_21", Формат(ДанныеПечати.Дата, "ДЛФ=D"));
	
	Возврат ПараметрыМакета;
КонецФункции 

Функция ПараметрыМакетаТН1208(ДанныеПечати, СведенияОКонтрагентах)
	ПараметрыМакета = Новый Структура;
	ПараметрыМакета.Вставить("ИмяМакета", "Обработка.ПечатьТранспортнойНакладной.ПФ_MXL_ТранспортнаяНакладная1208");
	
	СведенияОГрузополучателе  = СведенияОКонтрагентах.СведенияОГрузополучателе;
	СведенияОГрузоотправителе = СведенияОКонтрагентах.СведенияОГрузоотправителе;
	СведенияОПеревозчике      = СведенияОКонтрагентах.СведенияОПеревозчике;
	
	Если СведенияОГрузополучателе.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		ПараметрыМакета.Вставить("Пункт2_1", ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОГрузополучателе, 
			"НаименованиеДляПечатныхФорм,ИНН,ФактическийАдрес,Телефоны"));
	Иначе
		ПараметрыМакета.Вставить("Пункт2_2", ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОГрузополучателе, 
			"НаименованиеДляПечатныхФорм,ФактическийАдрес,Телефоны"));
	КонецЕсли;
	
	Если СведенияОГрузоотправителе.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		ПредставлениеГрузоотправителя = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОГрузоотправителе, 
			"НаименованиеДляПечатныхФорм,ИНН,ФактическийАдрес,Телефоны");
		
		ПараметрыМакета.Вставить("Пункт1_1", ПредставлениеГрузоотправителя);
	Иначе
		ПредставлениеГрузоотправителя = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОГрузоотправителе, 
			"НаименованиеДляПечатныхФорм,ФактическийАдрес,Телефоны");
		
		ПараметрыМакета.Вставить("Пункт1_2", ПредставлениеГрузоотправителя);
	КонецЕсли;
	
	ПараметрыМакета.Вставить("Пункт0_2", Формат(ДанныеПечати.Дата, "ДЛФ=D"));
	
	ПараметрыМакета.Вставить("Пункт3_1", ДанныеПечати.КраткоеНаименованиеГруза);
	
	ПараметрыМакета.Вставить("Пункт4_1", ДанныеПечати.СопроводительныеДокументы);
	
	ПараметрыМакета.Вставить("Пункт6_1", СведенияОГрузоотправителе.ФактическийАдрес);
	
	Если ЗначениеЗаполнено(ДанныеПечати.ОтпускПроизвел) Тогда 
		ДанныеФизЛица = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(ДанныеПечати.Организация, ДанныеПечати.ОтпускПроизвел, ДанныеПечати.Дата);
		ПараметрыМакета.Вставить("Пункт6_6", СокрЛП(СтрШаблон(НСтр("ru = '%1 %2 %3 %4'"), ДанныеФизЛица.Должность, ДанныеФизЛица.Фамилия, ДанныеФизЛица.Имя, ДанныеФизЛица.Отчество)));
	КонецЕсли;
	
	ПараметрыМакета.Вставить("Пункт6_7", ДанныеПечати.Водитель);
	ПараметрыМакета.Вставить("Пункт7_7", ДанныеПечати.Водитель);
	ПараметрыМакета.Вставить("Пункт9_1", Формат(ДанныеПечати.Дата, "ДЛФ=D"));
	
	ПараметрыМакета.Вставить("Пункт7_1", ДанныеПечати.АдресДоставки);
	
	Если СведенияОПеревозчике.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		ПредставлениеПеревозчика = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОПеревозчике, 
			"НаименованиеДляПечатныхФорм,ИНН,ФактическийАдрес,Телефоны");
		
		ПараметрыМакета.Вставить("Пункт10_1", ПредставлениеПеревозчика);
	Иначе
		ПредставлениеПеревозчика = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОПеревозчике, 
			"НаименованиеДляПечатныхФорм,ФактическийАдрес,Телефоны");
		
		// До 01.01.2021 ИНН в реквизитах перевозчика  не указывался
		ПараметрыМакета.Вставить("Пункт10_2", ПредставлениеПеревозчика);
	КонецЕсли;
	
	ПараметрыМакета.Вставить("Пункт10_3", ДанныеПечати.Водитель);
	
	ПараметрыМакета.Вставить("Пункт11_1", ДанныеПечати.МаркаАвтомобиля);
	
	ПараметрыМакета.Вставить("Пункт11_2", ДанныеПечати.РегистрационныйЗнакАвтомобиля);
	
	// Раздел "Дата составления, подписи сторон"
	ПараметрыМакета.Вставить("Пункт16_1", ПредставлениеГрузоотправителя);
	ПараметрыМакета.Вставить("Пункт16_2", ПредставлениеПеревозчика);
	
	ПараметрыМакета.Вставить("Пункт16_11", Формат(ДанныеПечати.Дата, "ДЛФ=D"));
	ПараметрыМакета.Вставить("Пункт16_21", Формат(ДанныеПечати.Дата, "ДЛФ=D"));
	
	Возврат ПараметрыМакета;
КонецФункции 

Функция ПараметрыМакетаТН1529(ДанныеПечати, СведенияОКонтрагентах)
	ПараметрыМакета = Новый Структура;
	ПараметрыМакета.Вставить("ИмяМакета", "Обработка.ПечатьТранспортнойНакладной.ПФ_MXL_ТранспортнаяНакладная1529");
	
	СведенияОГрузополучателе  = СведенияОКонтрагентах.СведенияОГрузополучателе;
	СведенияОГрузоотправителе = СведенияОКонтрагентах.СведенияОГрузоотправителе;
	СведенияОПеревозчике      = СведенияОКонтрагентах.СведенияОПеревозчике;
	
	Если СведенияОГрузополучателе.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		ПараметрыМакета.Вставить("Пункт2_1", ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОГрузополучателе, 
			"НаименованиеДляПечатныхФорм,ИНН,ФактическийАдрес,Телефоны"));
	Иначе
		ПараметрыМакета.Вставить("Пункт2_2", ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОГрузополучателе, 
			"НаименованиеДляПечатныхФорм,ФактическийАдрес,Телефоны"));
	КонецЕсли;
	
	Если СведенияОГрузоотправителе.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		ПредставлениеГрузоотправителя = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОГрузоотправителе, 
			"НаименованиеДляПечатныхФорм,ИНН,ФактическийАдрес,Телефоны");
		
		ПараметрыМакета.Вставить("Пункт1_1", ПредставлениеГрузоотправителя);
	Иначе
		ПредставлениеГрузоотправителя = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОГрузоотправителе, 
		"НаименованиеДляПечатныхФорм,ФактическийАдрес,Телефоны");
		
		ПараметрыМакета.Вставить("Пункт1_2", ПредставлениеГрузоотправителя);
	КонецЕсли;
	
	ПараметрыМакета.Вставить("Пункт0_2", Формат(ДанныеПечати.Дата, "ДЛФ=D"));
	
	ПараметрыМакета.Вставить("Пункт3_1", ДанныеПечати.КраткоеНаименованиеГруза);
	
	ПараметрыМакета.Вставить("Пункт4_1", ДанныеПечати.СопроводительныеДокументы);
	
	ПараметрыМакета.Вставить("Пункт6_1", СведенияОГрузоотправителе.ФактическийАдрес);
	
	ПараметрыМакета.Вставить("Пункт7_1", ДанныеПечати.АдресДоставки);
	
	ПараметрыМакета.Вставить("Пункт6_7", ДанныеПечати.Водитель);
	ПараметрыМакета.Вставить("Пункт7_7", ДанныеПечати.Водитель);
	ПараметрыМакета.Вставить("Пункт9_1", Формат(ДанныеПечати.Дата, "ДЛФ=D"));
	
	Если СведенияОПеревозчике.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		ПредставлениеПеревозчика = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОПеревозчике, 
		"НаименованиеДляПечатныхФорм,ИНН,ФактическийАдрес,Телефоны");
		
		ПараметрыМакета.Вставить("Пункт10_1", ПредставлениеПеревозчика);
	Иначе
		ПредставлениеПеревозчика = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОПеревозчике, 
		"НаименованиеДляПечатныхФорм,ФактическийАдрес,Телефоны");
		
		// До 01.01.2021 ИНН в реквизитах перевозчика  не указывался
		ПараметрыМакета.Вставить("Пункт10_2", ПредставлениеПеревозчика);
	КонецЕсли;
	
	ПараметрыМакета.Вставить("Пункт10_3", ДанныеПечати.Водитель);
	
	ПараметрыМакета.Вставить("Пункт11_1", ДанныеПечати.МаркаАвтомобиля);
	
	ПараметрыМакета.Вставить("Пункт11_2", ДанныеПечати.РегистрационныйЗнакАвтомобиля);
	
	// Раздел "Дата составления, подписи сторон"
	ПараметрыМакета.Вставить("Пункт16_1", ПредставлениеГрузоотправителя);
	ПараметрыМакета.Вставить("Пункт16_2", ПредставлениеПеревозчика);
	
	ПараметрыМакета.Вставить("Пункт16_11", Формат(ДанныеПечати.Дата, "ДЛФ=D"));
	ПараметрыМакета.Вставить("Пункт16_21", Формат(ДанныеПечати.Дата, "ДЛФ=D"));
	
	Возврат ПараметрыМакета;
КонецФункции 

Функция ПараметрыМакетаТН2200(ДанныеПечати, СведенияОКонтрагентах)
	ПараметрыМакета = Новый Структура;
	ПараметрыМакета.Вставить("ИмяМакета", "Обработка.ПечатьТранспортнойНакладной.ПФ_MXL_ТранспортнаяНакладная2200");
	
	СведенияОГрузополучателе  = СведенияОКонтрагентах.СведенияОГрузополучателе;
	СведенияОГрузоотправителе = СведенияОКонтрагентах.СведенияОГрузоотправителе;
	СведенияОПеревозчике      = СведенияОКонтрагентах.СведенияОПеревозчике;
	
	Если СведенияОГрузополучателе.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		ПараметрыМакета.Вставить("Пункт2_1", ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОГрузополучателе, 
			"НаименованиеДляПечатныхФорм,ИНН,ФактическийАдрес,Телефоны"));
	Иначе
		
		ПараметрыМакета.Вставить("Пункт2_2", ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОГрузополучателе, 
			"НаименованиеДляПечатныхФорм,ИНН, ФактическийАдрес,Телефоны"));
	КонецЕсли;
	
	Если СведенияОГрузоотправителе.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		ПредставлениеГрузоотправителя = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОГрузоотправителе, 
			"НаименованиеДляПечатныхФорм,ИНН,ФактическийАдрес,Телефоны");
		
		ПараметрыМакета.Вставить("Пункт1_1", ПредставлениеГрузоотправителя);
	Иначе
		ПредставлениеГрузоотправителя = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОГрузоотправителе, 
			"НаименованиеДляПечатныхФорм,ИНН,ФактическийАдрес,Телефоны");
		
		ПараметрыМакета.Вставить("Пункт1_2", ПредставлениеГрузоотправителя);
	КонецЕсли;
	
	ПараметрыМакета.Вставить("Пункт0_2", Формат(ДанныеПечати.Дата, "ДЛФ=D"));
	
	ПараметрыМакета.Вставить("Пункт3_1", ДанныеПечати.КраткоеНаименованиеГруза);
	
	ПараметрыМакета.Вставить("Пункт4_1", ДанныеПечати.СопроводительныеДокументы);
	
	ПараметрыМакета.Вставить("Пункт6_1", СведенияОГрузоотправителе.ФактическийАдрес);
	
	Если ЗначениеЗаполнено(ДанныеПечати.ОтпускПроизвел) Тогда 
		ДанныеФизЛица = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(ДанныеПечати.Организация, ДанныеПечати.ОтпускПроизвел, ДанныеПечати.Дата);
		ПараметрыМакета.Вставить("Пункт6_6", СокрЛП(СтрШаблон(НСтр("ru = '%1 %2 %3 %4'"), ДанныеФизЛица.Должность, ДанныеФизЛица.Фамилия, ДанныеФизЛица.Имя, ДанныеФизЛица.Отчество)));
	КонецЕсли;
	
	ПараметрыМакета.Вставить("Пункт6_7", ДанныеПечати.Водитель);
	ПараметрыМакета.Вставить("Пункт7_7", ДанныеПечати.Водитель);
	ПараметрыМакета.Вставить("Пункт9_1", Формат(ДанныеПечати.Дата, "ДЛФ=D"));
	
	ПараметрыМакета.Вставить("Пункт7_1", ДанныеПечати.АдресДоставки);
	
	Если СведенияОПеревозчике.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		ПредставлениеПеревозчика = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОПеревозчике, 
		"НаименованиеДляПечатныхФорм,ИНН,ФактическийАдрес,Телефоны");
		
		ПараметрыМакета.Вставить("Пункт10_1", ПредставлениеПеревозчика);
	Иначе
		ПредставлениеПеревозчика = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОПеревозчике, 
		"НаименованиеДляПечатныхФорм, ИНН, ФактическийАдрес,Телефоны");
		// С 01.01.2021 в пункт 10.2 добавлени ИНН юридического лица
		ПараметрыМакета.Вставить("Пункт10_2", ПредставлениеПеревозчика);
	КонецЕсли;
	
	ПараметрыМакета.Вставить("Пункт10_3", ДанныеПечати.Водитель);
	ПараметрыМакета.Вставить("Пункт11_1", ДанныеПечати.МаркаАвтомобиля);
	ПараметрыМакета.Вставить("Пункт11_2", ДанныеПечати.РегистрационныйЗнакАвтомобиля);
	
	// Раздел "Стоимость услуг перевозчика, и порядок расчета провозной платы"
	ПараметрыМакета.Вставить("Пункт16_1", ПредставлениеГрузоотправителя);
	ПараметрыМакета.Вставить("Пункт16_2", ПредставлениеПеревозчика);
	
	ПараметрыМакета.Вставить("Пункт16_11", Формат(ДанныеПечати.Дата, "ДЛФ=D"));
	ПараметрыМакета.Вставить("Пункт16_21", Формат(ДанныеПечати.Дата, "ДЛФ=D"));
	
	Возврат ПараметрыМакета;
КонецФункции

Функция ПараметрыМакетаТН2116(ДанныеПечати, СведенияОКонтрагентах)
	ПараметрыМакета = Новый Структура;
	ПараметрыМакета.Вставить("ИмяМакета", "Обработка.ПечатьТранспортнойНакладной.ПФ_MXL_ТранспортнаяНакладная2116");
	
	СведенияОГрузополучателе  = СведенияОКонтрагентах.СведенияОГрузополучателе;
	СведенияОГрузоотправителе = СведенияОКонтрагентах.СведенияОГрузоотправителе;
	СведенияОПеревозчике      = СведенияОКонтрагентах.СведенияОПеревозчике;
	
	ПараметрыМакета.Вставить("Пункт0_2", Формат(ДанныеПечати.Дата, "ДЛФ=D"));
	
	ПредставлениеГрузоотправителя = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОГрузоотправителе, 
		"НаименованиеДляПечатныхФорм,ИНН,ФактическийАдрес,Телефоны");
	 
	ПараметрыМакета.Вставить("Пункт1_1", ПредставлениеГрузоотправителя);
	
	ПараметрыМакета.Вставить("Пункт2_1", ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОГрузополучателе, 
		"НаименованиеДляПечатныхФорм,ИНН,ФактическийАдрес,Телефоны"));
	
	ПараметрыМакета.Вставить("Пункт3_1", ДанныеПечати.КраткоеНаименованиеГруза);
	
	ПараметрыМакета.Вставить("Пункт4_1", ДанныеПечати.СопроводительныеДокументы);
	
	ПараметрыМакета.Вставить("Пункт8_1", ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОГрузоотправителе, 
		"НаименованиеДляПечатныхФорм,ИНН"));
	
	ПараметрыМакета.Вставить("Пункт8_2", СведенияОГрузоотправителе.ФактическийАдрес);
	
	Если ЗначениеЗаполнено(ДанныеПечати.ОтпускПроизвел) Тогда 
		ДанныеФизЛица = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(ДанныеПечати.Организация, ДанныеПечати.ОтпускПроизвел, ДанныеПечати.Дата);
		ПараметрыМакета.Вставить("Пункт8_0", СокрЛП(СтрШаблон(НСтр("ru = '%1 %2 %3 %4'"), ДанныеФизЛица.Должность, ДанныеФизЛица.Фамилия, ДанныеФизЛица.Имя, ДанныеФизЛица.Отчество)));
	КонецЕсли;
	
	ПараметрыМакета.Вставить("Пункт8_8", ДанныеПечати.Водитель);
	ПараметрыМакета.Вставить("Пункт10_9", ДанныеПечати.Водитель);
	
	ПараметрыМакета.Вставить("Пункт10_1", ДанныеПечати.АдресДоставки);
	
	ПредставлениеПеревозчика = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОПеревозчике, 
		"НаименованиеДляПечатныхФорм,ИНН,ФактическийАдрес,Телефоны");
	
	// Начиная с 01.03.2022 реквизиты перевозчика указываются в одном и том же поле и для юр. и для физлиц
	ПараметрыМакета.Вставить("Пункт6_1", ПредставлениеПеревозчика);
	
	ДанныеПечатиВодитель = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеПечати.Водитель);
	Если ЗначениеЗаполнено(ДанныеПечати.ВодительскоеУдостоверение) Тогда
		ДанныеПечатиВодитель.Добавить(ДанныеПечати.ВодительскоеУдостоверение);
	КонецЕсли; 
	
	ПараметрыМакета.Вставить("Пункт6_2", СтрСоединить(ДанныеПечатиВодитель,", в/у "));
	
	ПараметрыМакета.Вставить("Пункт7_1", ДанныеПечати.МаркаАвтомобиля);
	ПараметрыМакета.Вставить("Пункт7_2", ДанныеПечати.РегистрационныйЗнакАвтомобиля);
	
	// Раздел "Стоимость перевозки груза"
	ПараметрыМакета.Вставить("Пункт12_7", ПредставлениеГрузоотправителя);
	
	ПараметрыМакета.Вставить("Пункт12_6", ПредставлениеПеревозчика);
	
	ПараметрыМакета.Вставить("Пункт16_11", Формат(ДанныеПечати.Дата, "ДЛФ=D"));
	ПараметрыМакета.Вставить("Пункт16_21", Формат(ДанныеПечати.Дата, "ДЛФ=D"));
	
	Возврат ПараметрыМакета;
КонецФункции

#КонецОбласти 

#КонецОбласти 

#КонецЕсли