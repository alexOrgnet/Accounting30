
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВыбраннаяФорма = Параметры.Декларация3НДФЛВыбраннаяФорма;
	
	Если Параметры.Свойство("СтруктураДоходовВычетов") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.СтруктураДоходовВычетов);
	КонецЕсли;
	
	СтруктураДоходовВычетов = Неопределено;
	Параметры.Свойство("СтруктураДоходовВычетов", СтруктураДоходовВычетов);
	
	НастройкиРаспределенияВычетов = ПомощникЗаполнения3НДФЛ.НастройкиРаспределенияВычетов(
		ЭтотОбъект,
		Параметры.ДоступныеДоходы,
		СтруктураДоходовВычетов);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, НастройкиРаспределенияВычетов,
		"ВариантРаспределенияВычета, ВидДохода, СуммаВычета");
	
	РаспределениеПоДоходам.Загрузить(НастройкиРаспределенияВычетов.РаспределениеПоДоходам);
	Элементы.ВидДоходаВычета.СписокВыбора.ЗагрузитьЗначения(НастройкиРаспределенияВычетов.СписокВыбораДохода);
	
	ПересчитатьИРаспределитьСуммуВычета();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СуммаВычетаПриИзменении(Элемент)
	АктуализироватьДанныеОСуммеВычета();
КонецПроцедуры

&НаКлиенте
Процедура ВариантРаспределенияВычетаПриИзменении(Элемент)
	
	ПомощникЗаполнения3НДФЛКлиент.ВариантРаспределенияВычетаПриИзменении(ЭтотОбъект);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидДоходаВычетаПриИзменении(Элемент)
	
	ПомощникЗаполнения3НДФЛКлиент.ВидДоходаВычетаПриИзменении(ВидДохода, СуммаВычета, РаспределениеПоДоходам);
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеПоДоходамПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	ПомощникЗаполнения3НДФЛКлиент.РаспределениеПоДоходамПередОкончаниемРедактирования(
		ЭтотОбъект, ОтменаРедактирования, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеПоДоходамПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	РаспределеноВычетаВсего = РаспределениеПоДоходам.Итог("СуммаВычета");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Если СуммаВычета = 0 И СуммаДохода = 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Заполните хотя бы одно поле'"));
		Возврат;
	КонецЕсли;
	
	Если СуммаВычета > 400000 Тогда
		ТекстСообщения = НСтр("ru = 'Сумма вычета превышает 400 тыс. руб.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "СуммаВычета");
		Возврат;
	КонецЕсли;
	
	СтруктураОтвета = Новый Структура;
	СтруктураОтвета.Вставить("Вид",         ПредопределенноеЗначение("Перечисление.ВычетыФизическихЛиц.Инвестиционный"));
	СтруктураОтвета.Вставить("Информация",  НСтр("ru = 'Инвестиционный вычет'"));
	СтруктураОтвета.Вставить("СуммаДохода", СуммаДохода);
	СтруктураОтвета.Вставить("СуммаВычета", СуммаВычета);
	
	Если ПомощникЗаполнения3НДФЛКлиентСервер.ВычетыРаспределяютсяПоДоходам(ВыбраннаяФорма) Тогда
		СтруктураОтвета.Вставить("ВидДохода",
			ПомощникЗаполнения3НДФЛКлиент.СоответствиеРаспределенияПоДоходам(РаспределениеПоДоходам));
	КонецЕсли;
	
	Закрыть(СтруктураОтвета);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	ПомощникЗаполнения3НДФЛКлиентСервер.УправлениеФормой(Форма);
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьИРаспределитьСуммуВычета()
	
	Если СуммаВычета = РаспределениеПоДоходам.Итог("СуммаВычета") Тогда
		Возврат;
	КонецЕсли;
	
	ПомощникЗаполнения3НДФЛКлиентСервер.РаспределитьСуммуВычета(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура АктуализироватьДанныеОСуммеВычета()
	
	ПересчитатьИРаспределитьСуммуВычета();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти