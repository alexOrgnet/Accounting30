
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ИдентификаторУстройства = Параметры.ИдентификаторУстройства;
	
	ПодготовитьФормуНаСервере();
	
	ДатаНачалаЗагрузки = ТекущаяДата();
КонецПроцедуры

#КонецОбласти

#Область КомандыФормы

&НаКлиенте
Процедура ОК(Команда)
	Результат = Новый Структура;
	Результат.Вставить("Результат", Истина);
	Результат.Вставить("ИдентификаторУстройства", ИдентификаторУстройства);
	Результат.Вставить("ДатаНачалаЗагрузки", ДатаНачалаЗагрузки);
	
	Закрыть(Результат);
КонецПроцедуры 

&НаКлиенте
Процедура Отмена(Команда)
	Результат = Новый Структура;
	Результат.Вставить("Результат", Ложь);
	Результат.Вставить("ИдентификаторУстройства", ИдентификаторУстройства);
	Результат.Вставить("ДатаНачалаЗагрузки",      Неопределено);
	
	Закрыть(Результат);
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()

	ДанныеНастройкиКассы = РегистрыСведений.НастройкиКассОФД.ДанныеНастроекПоКассе(ИдентификаторУстройства);
	ДанныеПодсказки = Новый Массив;
	ДанныеПодсказки.Добавить(НСтр("ru = 'Это первая загрузка по кассе'"));
	ДанныеПодсказки.Добавить(Новый ФорматированнаяСтрока(ДанныеНастройкиКассы.Магазин,Новый Шрифт(,,Истина)));
	ДанныеПодсказки.Добавить(Новый ФорматированнаяСтрока(ДанныеНастройкиКассы.ПредставлениеКассы,Новый Шрифт(,,Истина)));
	ДанныеПодсказки.Добавить(НСтр("ru = 'Укажите дату, начиная с которой будут загружаться данные.'"));
	ДанныеПодсказки.Добавить(НСтр("ru = 'В дальнейшем, дата загрузки будет определяться автоматически.'"));
	
	СтрокаПодсказки = Новый ФорматированнаяСтрока(ДанныеПодсказки[0]);
	Для Инд = 1 По ДанныеПодсказки.Количество()-1 Цикл
		СтрокаПодсказки = Новый ФорматированнаяСтрока(СтрокаПодсказки, Символы.ПС, ДанныеПодсказки[Инд])
	КонецЦикла;
	
	Элементы.ПодсказкаВводПериода.РасширеннаяПодсказка.Заголовок = СтрокаПодсказки;
КонецПроцедуры 

#КонецОбласти



