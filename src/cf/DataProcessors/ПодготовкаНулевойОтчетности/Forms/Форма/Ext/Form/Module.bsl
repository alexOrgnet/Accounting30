&НаКлиенте
Перем ПараметрыОбработчикаОжидания;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект.Период = Обработки.МониторНалоговИОтчетности.ТекущаяДатаМонитора();
	
	Объект.Организация = Параметры.Организация;
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		Объект.Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;
	
	ГосударственныйОрган = Параметры.ГосударственныйОрган;
	
	Заголовок = НСтр("ru='Подготовка нулевой отчетности'");
	Если ЗначениеЗаполнено(ГосударственныйОрган) Тогда
		Если ГосударственныйОрган = Перечисления.ТипыКонтролирующихОрганов.ФНС Тогда
			ОписаниеПолучателя = НСтр("ru='налоговую инспекцию'");
		ИначеЕсли ГосударственныйОрган = Перечисления.ТипыКонтролирующихОрганов.ПФР Тогда
			ОписаниеПолучателя = НСтр("ru='пенсионный фонд'");
		ИначеЕсли ГосударственныйОрган = Перечисления.ТипыКонтролирующихОрганов.ФСС Тогда
			ОписаниеПолучателя = НСтр("ru='фонд социального страхования'");
		Иначе 
			ОписаниеПолучателя = Строка(ГосударственныйОрган);
		КонецЕсли;
		Заголовок = СтрШаблон(НСтр("ru='%1 в %2'"), Заголовок, ОписаниеПолучателя);
		
		// Управление информацией о получателе
		Элементы.ГосударственныйОрганЗаголовок_1.Видимость = Ложь;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.АдресПросроченныхОтчетов) Тогда
		ПросроченныеОтчеты.Загрузить(ПолучитьИзВременногоХранилища(Параметры.АдресПросроченныхОтчетов));
	КонецЕсли;
	
	ОбновитьДанныеПоОрганизации();
	
	СпособСдачиОтчетности = "Интернет";
	
	Элементы.НадписьТребуютсяРеквизиты.Заголовок = ТекстНеЗаполненыРеквизитыДляОтчетности();
	
	РазрешенЭлектронныйДокументооброт = РазрешенЭлектронныйДокументооборот();
	
	ОтчетыЗаполнены    = Ложь;
	ОтчетыСформированы = Ложь;
	
	ОбновитьСостояниеПодготовкиОтчетности();
	
	ПолучитьНачалоРелевантногоПериода();
	НастроитьОтображениеТеста();
	ДополнитьОписаниеОтчета();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗапуститьОбновлениеСпискаОтчетов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если (ИмяСобытия = "Запись_Организации" И Источник = Объект.Организация)
		ИЛИ (ИмяСобытия = "ИзменениеУчетнойПолитики" И Параметр = Объект.Организация) Тогда
	
		ОбновитьДанныеПоОрганизации();
		
		Если ПомощникДоступен Тогда
			ЗапуститьОбновлениеСпискаОтчетов();
		Иначе
			УправлениеФормой(ЭтотОбъект);
		КонецЕсли;
	
	ИначеЕсли ИмяСобытия = "ОтчетыПрошлыхПериодов_ПроверкаЗавершена"
		И Источник = Объект.Организация Тогда
		
		ЗагрузитьРезультатТеста();
		ЗапуститьОбновлениеСпискаОтчетов();
		
	ИначеЕсли ИмяСобытия = "ОплатаСервиса_Изменение" Тогда
		
		РазрешенЭлектронныйДокументооброт = РазрешенЭлектронныйДокументооборот();
		ОтобразитьДействияСдачаОтчетности(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если СтрНайти(НавигационнаяСсылкаФорматированнойСтроки, "НапечататьОтчет") > 0 Тогда
		
		СтандартнаяОбработка = Ложь;
		
		АдресОтчета = АдресОтчета(НавигационнаяСсылкаФорматированнойСтроки);
		
		СформироватьБланкОтчета(АдресОтчета);
		
	ИначеЕсли СтрНайти(НавигационнаяСсылкаФорматированнойСтроки, "ПоказатьОшибкиОтчета") > 0 Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПоказатьОшибкиФормированияОтчета(НавигационнаяСсылкаФорматированнойСтроки);
		
	ИначеЕсли СтрНайти(НавигационнаяСсылкаФорматированнойСтроки, "РеквизитыОрганизацииДляОтчетности") Тогда
		
		СообщениеТребуютсяРеквизитыОбработкаНавигационнойСсылки(Элементы.НадписьТребуютсяРеквизиты,
			НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
		
	ИначеЕсли СтрНайти(НавигационнаяСсылкаФорматированнойСтроки, "ЗапуститьПроверкуПрошлыхПериодов") > 0 Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыТеста = ПараметрыТестаПрошлыхПериодов();
	
		ОткрытьФорму("Обработка.МониторНалоговИОтчетности.Форма.ФормаТестПрошлыхПериодов",
			ПараметрыТеста,
			ЭтотОбъект);
		
	ИначеЕсли СтрНайти(НавигационнаяСсылкаФорматированнойСтроки, "ВвестиДанныеОбУчредителяхИУставномКапитале") > 0 Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Обработчик = Новый ОписаниеОповещения("ЗавершитьВводНачальныхОстатков", ЭтотОбъект);
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("Организация", Объект.Организация);
		ПараметрыФормы.Вставить("ПодготовкаНулевойОтчетности", Истина);
		ОткрытьФорму("Обработка.ВводНачальныхОстатков.Форма",
			ПараметрыФормы,
			ЭтотОбъект,
			УникальныйИдентификатор,
			,
			,
			Обработчик,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СообщениеТребуютсяРеквизитыОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ",                 Объект.Организация);
	ПараметрыФормы.Вставить("Назначение",           "ДляОтчетности");
	ПараметрыФормы.Вставить("ПроверяемыеРеквизиты", ПроверяемыеРеквизитыДляОтчетности());
	
	ОткрытьФорму("Справочник.Организации.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособСдачиОтчетностиПриИзменении(Элемент)
	
	ОтобразитьДействияСдачаОтчетности(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеОрганизацияПриИзменении(Элемент)
	
	Объект.Организация = ПолеОрганизация;
	
	ОбновитьДанныеПоОрганизации();
	
	Если ПомощникДоступен Тогда
		ЗапуститьОбновлениеСпискаОтчетов();
	Иначе
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборТарифаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТарификацияБПКлиент.ОткрытьФормуВыбораТарифа(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛичныйВизитКонтактыГосорганов_АдресНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	УправлениеКонтактнойИнформациейКлиентБП.ПоказатьНаКартеНажатие(ЭтотОбъект, Элемент, ГосударственныйОрган_Адрес);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПомощникНедоступенОбработкаНавигационнойСсылки(Элемент, ТекстСсылки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТекстСсылки = "ОткрытьМониторНалоговИОтчетности" Тогда
		ОбщегоНазначенияБПКлиент.ОткрытьМониторНалоговИОтчетности();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключить1СОтчетность(Команда)
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьФормуМастераЗаявленияНаПодключение(
		Объект.Организация, , , , , , Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Открыть1СОтчетность(Команда)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Раздел", ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Отчеты"));
	ПараметрыФормы.Вставить("Организация", Объект.Организация);
	
	ОткрытьФорму("ОбщаяФорма.РегламентированнаяОтчетность", ПараметрыФормы, , "1С-Отчетность");
	
КонецПроцедуры

&НаКлиенте
Процедура ОписьВложения(Команда)
	
	АдресДанныхОписи = ПодготовитьДанныеОписиВложения();
	
	ДополнительныеПараметрыПечати = Новый Структура("АдресДанныхОписи", АдресДанныхОписи);
	
	ПараметрыПечати = Новый Структура("ЗаголовокФормы, ДополнительныеПараметры",
		"Опись вложения",
		ДополнительныеПараметрыПечати);
	
	ПараметрыКомандыПечати = Новый Массив;
	ПараметрыКомандыПечати.Добавить(Объект.Организация);
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
		"Обработка.ПечатьОписиВложения",
		"ОписьВложения",
		ПараметрыКомандыПечати,
		ЭтотОбъект,
		ПараметрыПечати);
	
КонецПроцедуры

&НаКлиенте
Процедура Конверт(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОбъектыПечати", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ГосударственныйОрган_Контрагент));
	ПараметрыФормы.Вставить("СведенияОПолучателеКонверта", ГосударственныйОрган_СведенияОПолучателеКонверта);
	
	ОткрытьФорму("ОбщаяФорма.НастройкаПечатиКонвертов", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПолучитьНачалоРелевантногоПериода()
	
	РелевантныйОтчетныйПериод = РелевантныйОтчетныйПериод();
	
	НачалоРелевантногоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(
		РелевантныйОтчетныйПериод.Периодичность, РелевантныйОтчетныйПериод.Период);
	
	// Если релевантный отчетный период не определен (например, еще не заполнен список задач),
	// то проверяем начало текущего года.
	Если НЕ ЗначениеЗаполнено(НачалоРелевантногоПериода) Тогда
		НачалоРелевантногоПериода = НачалоГода(Объект.Период);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеПоОрганизации()
	
	НадписьОрганизация = Объект.Организация;
	ПолеОрганизация    = Объект.Организация;
	
	Сведения = Обработки.ПодготовкаНулевойОтчетности.СведенияОбОрганизации(Объект.Организация, Объект.Период, ГосударственныйОрган);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Сведения);
	
	ЗагрузитьРезультатТеста();
	
	НастроитьКонтактныеДанныеГосорганов();
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьРезультатТеста()
	
	ПериодыДобавленныеПроверкой.Очистить();
	
	РезультатТеста = Обработки.МониторНалоговИОтчетности.РезультатПроверкиОтчетностиПрошлыхПериодов(Объект.Организация, ГосударственныйОрган);
	
	ПроверкаПрошлыхПериодовВыполнена = РезультатТеста.ПроверкаВыполнена;
	
	Если ПроверкаПрошлыхПериодовВыполнена
		И ЗначениеЗаполнено(РезультатТеста.ДобавленныеПериоды) Тогда
		
		Для Каждого СтрокаПериода Из РезультатТеста.ДобавленныеПериоды Цикл
		
			НоваяСтрока = ПериодыДобавленныеПроверкой.Добавить();
			
			НоваяСтрока.Периодичность = ?(ЗначениеЗаполнено(СтрокаПериода.Периодичность),
				СтрокаПериода.Периодичность,
				Перечисления.Периодичность.Год);
			
			НоваяСтрока.НачалоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(
				НоваяСтрока.Периодичность, СтрокаПериода.Период);
			НоваяСтрока.КонецПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(
				НоваяСтрока.Периодичность, СтрокаПериода.Период);
			
			НоваяСтрока.Требуется = СтрокаПериода.Требуется;
			
			НоваяСтрока.ПредставлениеПериода = ПредставлениеПериода(
				НоваяСтрока.НачалоПериода,
				КонецДня(НоваяСтрока.КонецПериода),
				"ФП = Истина");
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьОтображениеТеста()
	
	РелевантныйОтчетныйПериод = РелевантныйОтчетныйПериод();
	
	ПоказательПериода = Обработки.МониторНалоговИОтчетности.МинимальныйОтчетныйПериод(
											ТаблицаОтчеты.Выгрузить(,"Периодичность").ВыгрузитьКолонку("Периодичность"));
	
	ЭтоПерваяОтчетнаяКампания = РегистрыСведений.ПерваяОтчетнаяКампания.ЭтоПерваяОтчетнаяКампания(
		Объект.Организация,
		Обработки.МониторНалоговИОтчетности.ТекущаяДатаМонитора(),
		ПоказательПериода);
		
	ЭтоПервыйОтчетныйПериод = ПомощникиПоУплатеНалоговИВзносов.ЭтоПервыйОтчетныйПериод(
		Объект.Организация,
		НачалоРелевантногоПериода,
		РелевантныйОтчетныйПериод.Правило,
		ДатаНачалаДеятельности);
	
	ТребуетсяПроверитьПрошлыеПериоды = НЕ ЭтоПервыйОтчетныйПериод И ЭтоПерваяОтчетнаяКампания;
	
	Элементы.НадписьПроверьтеПрошлыеПериоды.Заголовок = ОписаниеРезультатаТеста();
	Элементы.НадписьПроверьтеПрошлыеПериоды.Видимость = ЭтоПерваяОтчетнаяКампания;
	
КонецПроцедуры

&НаСервере
Процедура ДополнитьОписаниеОтчета()
	
	ДополнитьОписание_ВводДанныхБаланса();
	
КонецПроцедуры

&НаСервере
Функция ОписаниеРезультатаТеста()
	
	// Сначала самые простые варианты
	
	Если НЕ ТребуетсяПроверитьПрошлыеПериоды Тогда // Тест не отображается
		Возврат "";
	КонецЕсли;
	
	МассивСтрок = Новый Массив;
	Если НЕ ПроверкаПрошлыхПериодовВыполнена Тогда
		МассивСтрок.Добавить(
				Новый ФорматированнаяСтрока(НСтр("ru = 'Возможно, надо сдавать отчеты за предыдущие периоды. '")));
		МассивСтрок.Добавить(
				Новый ФорматированнаяСтрока(НСтр("ru = 'Проверить?'"),,,,"ЗапуститьПроверкуПрошлыхПериодов"));
		Возврат Новый ФорматированнаяСтрока(МассивСтрок);
	КонецЕсли;
	
	Если ПериодыДобавленныеПроверкой.Количество() = 0 Тогда
		МассивСтрок.Добавить(
				Новый ФорматированнаяСтрока(НСтр("ru = 'Вы указали, что все отчеты за прошлые периоды сданы. '")));
		МассивСтрок.Добавить(
				Новый ФорматированнаяСтрока(НСтр("ru = 'Проверить еще раз?'"),,,,"ЗапуститьПроверкуПрошлыхПериодов"));
		Возврат Новый ФорматированнаяСтрока(МассивСтрок);
	КонецЕсли;
	
	// Выявляем добавленные тестом периоды и отчеты за эти периоды
	
	МассивПериодов = Новый Массив;
	МассивОтчетов  = Новый Массив;
	
	Для Каждого ДобавленныйПериод Из ПериодыДобавленныеПроверкой Цикл
		
		Если НЕ ДобавленныйПериод.Требуется Тогда
			Продолжить;
		КонецЕсли;
		
		Отбор = Новый Структура("НачалоПериода, КонецПериода, Периодичность",
			НачалоГода(ДобавленныйПериод.НачалоПериода), ДобавленныйПериод.КонецПериода, ДобавленныйПериод.Периодичность);
		
		НайденныеСтроки = ТаблицаОтчеты.НайтиСтроки(Отбор);
		
		Для Каждого НайденныйОтчет Из НайденныеСтроки Цикл
			МассивОтчетов.Добавить(НайденныйОтчет);
			ОтчетныйГод = Формат(Год(НайденныйОтчет.НачалоПериода), "ЧГ=");
			Если МассивПериодов.Найти(ОтчетныйГод) = Неопределено Тогда
				МассивПериодов.Добавить(ОтчетныйГод);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	СтрокаЗаголовок = "";
	СтрокаОписания   = "";
	Если МассивПериодов.Количество() = 1 И МассивОтчетов.Количество() = 1 Тогда
		СтрокаЗаголовок = НСтр("ru = 'По Вашим ответам добавлен отчет за %1 год. '");
		СтрокаОписания = МассивПериодов[0];
	ИначеЕсли МассивПериодов.Количество() = 1 Тогда
		СтрокаЗаголовок = НСтр("ru = 'По Вашим ответам добавлены отчеты за %1 год. '");
		СтрокаОписания = МассивПериодов[0];
	ИначеЕсли МассивПериодов.Количество() > 1 Тогда
		СтрокаЗаголовок = НСтр("ru = 'По Вашим ответам добавлены отчеты за %1 годы. '");
		МинимальныйГод  = Число(МассивПериодов[0]);
		МаксимальныйГод = Число(МассивПериодов[0]);
		Для Каждого ЗначениеМассива ИЗ МассивПериодов Цикл
			МинимальныйГод = Мин(МинимальныйГод, Число(ЗначениеМассива));
			МаксимальныйГод = Макс(МинимальныйГод, Число(ЗначениеМассива));
		КонецЦикла;
		СтрокаОписания = СтрШаблон(НСтр("ru = '%1-%2'"), Формат(МинимальныйГод, "ЧГ="), Формат(МаксимальныйГод, "ЧГ="));
	КонецЕсли;
	
	Если СтрокаЗаголовок = "" Тогда
		Описание = "";
	Иначе
		Описание = СтрШаблон(СтрокаЗаголовок, СтрокаОписания);
	КонецЕсли;
	
	МассивСтрок.Добавить(
			Новый ФорматированнаяСтрока(Описание));
	МассивСтрок.Добавить(
			Новый ФорматированнаяСтрока(НСтр("ru = 'Проверить еще раз?'"),,,,"ЗапуститьПроверкуПрошлыхПериодов"));
	Возврат Новый ФорматированнаяСтрока(МассивСтрок);
	
КонецФункции

&НаСервере
Функция НачалоОбзораЗадач()
	
	МинимальныйПериод = НачалоГода(ДобавитьМесяц(Объект.Период, -36));
	
	Если ПериодыДобавленныеПроверкой.Количество() > 0 Тогда
		
		// Глубина обзора задач "в прошлое" зависит от результата теста.
		ТребующиесяПериоды = ПериодыДобавленныеПроверкой.Выгрузить(Новый Структура("Требуется", Истина), "НачалоПериода");
		ТребующиесяПериоды.Сортировать("НачалоПериода");
		
		Если ТребующиесяПериоды.Количество() > 0 Тогда
			МинимальныйПериод = Мин(МинимальныйПериод, ТребующиесяПериоды[0].НачалоПериода);
		КонецЕсли;
		
	КонецЕсли;
	
	// Учтем дату регистрации - до нее отчетов быть не может.
	НачалоОбзораЗадач = Макс(МинимальныйПериод, ДатаНачалаДеятельности);
	
	Возврат НачалоОбзораЗадач;
	
КонецФункции

&НаКлиенте
Процедура ЗапуститьОбновлениеСпискаОтчетов()
	
	Если ПомощникДоступен Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ОбновитьСписокОтчетов", 0.1, Истина);
	КонецЕсли;
	
КонецПРоцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьСписокОтчетов()
	
	ТаблицаОтчеты.Очистить();
	
	ОтчетыЗаполнены    = Ложь;
	ОтчетыСформированы = Ложь;
	
	УправлениеФормой(ЭтотОбъект);
	
	ДлительнаяОперация = ПолучитьДанныеОбОтчетах();
	
	НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
	
	Обработчик = Новый ОписаниеОповещения("ЗавершитьОбновлениеСпискаОтчетов", ЭтотОбъект);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеОбОтчетах()
	
	ПараметрыЗаполненияОтчетов = ПараметрыЗаполненияОтчетов();
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Заполнение списка отчетов для комплекта нулевой отчетности'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.ПодготовкаНулевойОтчетности.ЗаполнитьОтчеты",
		ПараметрыЗаполненияОтчетов,
		ПараметрыВыполнения);
	
	Возврат ДлительнаяОперация;
	
КонецФункции

&НаКлиенте
Процедура ЗавершитьОбновлениеСпискаОтчетов(ДлительнаяОперация, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		
		ОбработатьРезультатОбновленияСпискаОтчетов(ДлительнаяОперация.АдресРезультата);
		
		ЗапуститьФормированиеОтчетов();
		
	Иначе
		
		ЗаголовокОшибки = НСтр("ru = 'Ошибка при заполнении списка отчетов:'");
		ПоказатьОшибкуПодготовкиОтчетов(ДлительнаяОперация, ЗаголовокОшибки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьРезультатОбновленияСпискаОтчетов(АдресРезультата)
	
	ТаблицыОтчетныхЗадач = ПолучитьИзВременногоХранилища(АдресРезультата);
	ЗадачиПоОтчетам            = ТаблицыОтчетныхЗадач.ОтчетныеЗадачи;
	АдресПравилаПрошлыхПериодов = ТаблицыОтчетныхЗадач.АдресПравилаПрошлыхПериодов;
	
	Для Каждого ЗадачаОтчет Из ЗадачиПоОтчетам Цикл
		НоваяСтрока = ТаблицаОтчеты.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ЗадачаОтчет);
	КонецЦикла;
	
	ОтчетыЗаполнены = Истина;
	
	НастроитьФормированиеОтчетов();
	
	ПроверитьРеквизитыОрганизацииДляОтчетности();
	
	ОбновитьПредставлениеСледующегоОтчета();
	ОбновитьСостояниеПодготовкиОтчетности();
	ПолучитьНачалоРелевантногоПериода();
	НастроитьОтображениеТеста();
	ДополнитьОписаниеОтчета();
	
	ОтобразитьОтчеты();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция ПараметрыЗаполненияОтчетов()
	
	// Необходимо получить не только текущие задачи, для которых уже требуются отчеты,
	// но и ближайшие задачи, срок выполнения которых еще не наступил.
	// При отсутствии текущих задач релевантный период определяется
	// как отчетный период ближайшей (в будущем) по началу выполнения задачи.
	НачалоОбзораЗадач = НачалоОбзораЗадач();
	
	// Для получения ближайших будущих задач строим расписание "с захватом" начала следующего года
	КонецОбзораЗадач = КонецГода(Объект.Период) + 1;
	
	Результат = Новый Структура;
	
	Результат.Вставить("Организация",            Объект.Организация);
	Результат.Вставить("ДатаНачалаДеятельности", ДатаНачалаДеятельности);
	Результат.Вставить("НачалоОбзора",           НачалоОбзораЗадач);
	Результат.Вставить("КонецОбзора",            КонецОбзораЗадач);
	Результат.Вставить("ТекущийДень",            Объект.Период);
	Результат.Вставить("ГосударственныйОрган",   ГосударственныйОрган);
	Результат.Вставить("ПросроченныеОтчеты",     ПросроченныеОтчеты.Выгрузить());
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьОшибкуПодготовкиОтчетов(ДлительнаяОперация, ЗаголовокОшибки)
	
	Предупреждение = Новый Структура;
	
	Текст = ЗаголовокОшибки + Символы.ПС + ДлительнаяОперация.КраткоеПредставлениеОшибки;
	ВызватьИсключение Текст;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОшибкиФормированияОтчета(НавигационнаяСсылкаОтчета)
	
	АдресОтчета = АдресОтчета(НавигационнаяСсылкаОтчета);
	
	Отбор = Новый Структура("ГосорганНомерГруппы, ОтчетНомерГруппы", АдресОтчета.НомерГосоргана, АдресОтчета.НомерОтчета);
	ЗадачаОтчет = ТаблицаОтчеты.НайтиСтроки(Отбор)[0];
	
	Текст = НСтр("ru = 'При формировании отчета обнаружены ошибки:'")
		+ ?(ЗначениеЗаполнено(ЗадачаОтчет.ОшибкиФормирования), СтрСоединить(ЗадачаОтчет.ОшибкиФормирования, Символы.ПС), "");
	
	ВызватьИсключение Текст;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьФормированиеОтчетов()
	
	ОтчетыСформированы = Ложь;
	
	Если НЕ РеквизитыОрганизацииЗаполнены Тогда
		// Рано формировать, будут ошибки.
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперация = СформироватьОтчетыНаСервере();
	
	Если ДлительнаяОперация = Неопределено Тогда
		
		// Отчетов к формированию на сервере нет. Возможно, есть формирующиеся на клиенте?
		СформироватьОтчетыНаКлиенте();
		
	ИначеЕсли ДлительнаяОперация.Статус = "Ошибка" Тогда
		
		ЗаголовокОшибки = НСтр("ru = 'Ошибка при формировании отчетов:'");
		ПоказатьОшибкуПодготовкиОтчетов(ДлительнаяОперация, ЗаголовокОшибки);
		
	Иначе
		
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
		
		Обработчик = Новый ОписаниеОповещения("ПродолжитьФормированиеОтчетов", ЭтотОбъект);
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СформироватьОтчетыНаСервере()
	
	ПараметрыФормированияОтчетов = ПараметрыФормированияОтчетов();
	
	Если НЕ ПараметрыФормированияОтчетов.ФормироватьОтчеты Тогда
		// Нет отчетов для формирования на сервере, незачем запускать задание.
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Формирование комплекта нулевой отчетности'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.ПодготовкаНулевойОтчетности.СформироватьОтчеты",
		ПараметрыФормированияОтчетов,
		ПараметрыВыполнения);
	
	Возврат ДлительнаяОперация;
	
КонецФункции

&НаКлиенте
Процедура ПродолжитьФормированиеОтчетов(ДлительнаяОперация, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		
		ОбработатьРезультатФормированияОтчетовНаСервере(ДлительнаяОперация.АдресРезультата);
		
		СформироватьОтчетыНаКлиенте();
		
	Иначе
		
		ЗаголовокОшибки = НСтр("ru = 'Ошибка при формировании отчетов:'");
		ПоказатьОшибкуПодготовкиОтчетов(ДлительнаяОперация, ЗаголовокОшибки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьРезультатФормированияОтчетовНаСервере(АдресРезультата)
	
	СформированныеОтчеты = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	Для Каждого СформированныйОтчет Из СформированныеОтчеты Цикл
		
		Отбор = Новый Структура("ГосорганНомерГруппы, ОтчетНомерГруппы");
		ЗаполнитьЗначенияСвойств(Отбор, СформированныйОтчет);
		
		СтрокиТаблицы = ТаблицаОтчеты.НайтиСтроки(Отбор);
		
		Если СтрокиТаблицы.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ЗадачаОтчет = СтрокиТаблицы[0];
		
		ЗадачаОтчет.РегламентированныйОтчет = СформированныйОтчет.РегламентированныйОтчет;
		
		Если ЗначениеЗаполнено(СформированныйОтчет.ОшибкиФормирования) Тогда
			ЗадачаОтчет.ОшибкиФормирования = Новый ФиксированныйМассив(СформированныйОтчет.ОшибкиФормирования);
			ЗадачаОтчет.Обновить = Истина;
		Иначе
			ЗадачаОтчет.ОшибкиФормирования = Неопределено;
			ЗадачаОтчет.Обновить = Ложь;
		КонецЕсли;
		
		РазместитьОтчетНаФорме(ЗадачаОтчет);
		
	КонецЦикла;
	
КонецПРоцедуры

&НаКлиенте
Процедура СформироватьОтчетыНаКлиенте()
	
	// Не выполняем поиск, делаем простой обход - тогда вызов сервера может не понадобиться.
	Для Каждого ЗадачаОтчет Из ТаблицаОтчеты Цикл
		
		Если ЗадачаОтчет.ФормироватьНаКлиенте
			И НЕ ЗадачаОтчет.Предстоящая
			И (ЗадачаОтчет.Обновить ИЛИ НЕ ЗначениеЗаполнено(ЗадачаОтчет.РегламентированныйОтчет)) Тогда
			
			Результат = СформироватьОтчетНаКлиенте(ЗадачаОтчет);
			
			ЗадачаОтчет.Обновить = НЕ Результат.ОтчетСформирован;
			ЗадачаОтчет.РегламентированныйОтчет = Результат.СохраненныйОтчет;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ЗавершитьФормированиеОтчетов();
	
КонецПроцедуры

&НаКлиенте
Функция СформироватьОтчетНаКлиенте(ЗадачаОтчет)
	
	Результат = Новый Структура();
	
	Результат.Вставить("ОтчетСформирован", Ложь);
	Результат.Вставить("СохраненныйОтчет", ПредопределенноеЗначение("Документ.РегламентированныйОтчет.ПустаяСсылка"));
	Результат.Вставить("Ошибки", Неопределено);
	
	ФормаОтчета = ФормаРегламентированногоОтчета(ЗадачаОтчет, Истина);
	
	ФормаОтчета.СохранитьНаКлиенте();
	Результат.ОтчетСформирован = Истина;
	Результат.СохраненныйОтчет = ФормаОтчета.СтруктураРеквизитовФормы.мСохраненныйДок;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ЗавершитьФормированиеОтчетов()
	
	ОтчетыСформированы = Истина;
	
	РегистрыСведений.АктуальностьКомплектаНулевойОтчетности.УстановитьАктуальность(Объект.Организация, ОтчетыСформированы);
	
	ОтобразитьОтчеты();
	
	ОбновитьСостояниеПодготовкиОтчетности();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецФункции

&НаСервере
Функция ПараметрыФормированияОтчетов()
	
	ОтборОтчетов = Новый Структура;
	ОтборОтчетов.Вставить("Предстоящая",          Ложь); // Не формируем отчеты по будущим задачам.
	ОтборОтчетов.Вставить("ФормироватьНаКлиенте", Ложь); // Старые формы отчетов не поддерживают формирование на сервере.
	
	ТекущиеОтчеты = ТаблицаОтчеты.Выгрузить(ОтборОтчетов);
	
	ТребуемыеОтчеты = ТекущиеОтчеты.СкопироватьКолонки();
	
	Для Каждого ЗадачаОтчет Из ТекущиеОтчеты Цикл
		
		Если НЕ ЗначениеЗаполнено(ЗадачаОтчет.РегламентированныйОтчет) ИЛИ ЗадачаОтчет.Обновить Тогда
			НоваяСтрока = ТребуемыеОтчеты.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ЗадачаОтчет);
		КонецЕсли;
		
	КонецЦикла;
	
	ПараметрыФормированияОтчетов = Новый Структура;
	
	ПараметрыФормированияОтчетов.Вставить("ФормироватьОтчеты", ТребуемыеОтчеты.Количество() > 0);
	ПараметрыФормированияОтчетов.Вставить("ТребуемыеОтчеты",   ТребуемыеОтчеты);
	ПараметрыФормированияОтчетов.Вставить("Организация",       Объект.Организация);
	ПараметрыФормированияОтчетов.Вставить("Период",            Объект.Период);
	
	Возврат ПараметрыФормированияОтчетов;
	
КонецФункции

&НаСервере
Процедура НастроитьФормированиеОтчетов()
	
	ТребуемыеОтчеты = ТаблицаОтчеты.НайтиСтроки(Новый Структура("Предстоящая", Ложь));
	
	ОтчетностьНеТребуется = ТребуемыеОтчеты.Количество() = 0;
	
	Для Каждого ЗадачаОтчет Из ТребуемыеОтчеты Цикл
	
		Если ЗначениеЗаполнено(ЗадачаОтчет.РегламентированныйОтчет) Тогда
			ЗадачаОтчет.Обновить = НЕ ОтчетыАктуальны;
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	// Доступность функционала
	Элементы.ГруппаОсновная.Видимость           = Форма.ПомощникДоступен;
	Элементы.ГруппаПомощникНедоступен.Видимость = НЕ Форма.ПомощникДоступен И ЗначениеЗаполнено(Объект.Организация);
	
	// Список отчетов
	Элементы.ПодготовкаОтчетностиСледующийОтчет.Видимость  = Форма.ОтчетностьНеТребуется;
	Элементы.ПояснениеРасширенныйНалоговыйПериод.Видимость = Форма.ОтчетностьНеТребуется
		И Форма.ПериодСледующегоОтчетаРасширен;
	
	Элементы.НадписьТребуютсяРеквизиты.Видимость = Форма.ОтчетыЗаполнены И НЕ Форма.РеквизитыОрганизацииЗаполнены;
	
	Элементы.ГруппаПроверка.Видимость = Форма.РеквизитыОрганизацииЗаполнены;
	
	Элементы.ФормированиеОтчетов.Видимость   = Форма.ОтчетыЗаполнены;
	Элементы.БлокиОтчетов.Видимость          = Форма.РеквизитыОрганизацииЗаполнены;
	Элементы.ОжиданиеОтчетов.Видимость       = Не Форма.ОтчетыЗаполнены;
	
	// Тест
	Элементы.ПроверкаПредыдущихПериодов.Видимость = Форма.ТребуетсяПроверитьПрошлыеПериоды;
	
	// Сдача отчетов
	Элементы.СдачаОтчетности.Видимость = Форма.ОтчетыЗаполнены И Форма.ОтчетыСформированы
		И Форма.РеквизитыОрганизацииЗаполнены И НЕ Форма.ОтчетностьНеТребуется;
	
	ОтобразитьДействияСдачаОтчетности(Форма);
	
КонецПРоцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОтобразитьДействияСдачаОтчетности(Форма)
	
	Элементы = форма.Элементы;
	
	Элементы.СтраницыДействияСОтчетами.ТекущаяСтраница = Элементы[Форма.СпособСдачиОтчетности];
	
	Если Форма.СпособСдачиОтчетности = "Интернет" Тогда
		
		Элементы.Открыть1СОтчетность.КнопкаПоУмолчанию    = Форма.РазрешенЭлектронныйДокументооброт И Форма.ЭлектронныйДокументооборотДоступен;
		Элементы.Подключить1СОтчетность.КнопкаПоУмолчанию = Форма.РазрешенЭлектронныйДокументооброт И НЕ Форма.ЭлектронныйДокументооборотДоступен;
		
		Элементы.ЗаголовокПодключить1СОтчетность.Видимость = НЕ Форма.ЭлектронныйДокументооборотДоступен;
		Элементы.ОписаниеПодключить1СОтчетность.Видимость  = Форма.РазрешенЭлектронныйДокументооброт И НЕ Форма.ЭлектронныйДокументооборотДоступен;
		
		Элементы.Подключить1СОтчетность.Видимость = Форма.РазрешенЭлектронныйДокументооброт И НЕ Форма.ЭлектронныйДокументооборотДоступен;
		Элементы.Открыть1СОтчетность.Видимость    = Форма.РазрешенЭлектронныйДокументооброт И Форма.ЭлектронныйДокументооборотДоступен;
		
		Элементы.Описание1СОтчетностьНеПредусмотренаТарифом.Видимость = НЕ Форма.РазрешенЭлектронныйДокументооброт;
		Элементы.ВыборТарифа.Видимость                                = НЕ Форма.РазрешенЭлектронныйДокументооброт;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьКонтактныеДанныеГосорганов()
	
	Если ПустаяСтрока(ГосударственныйОрган_Адрес) И ПустаяСтрока(ГосударственныйОрган_Телефоны) Тогда
		
		// Скроем незаполненные реквизиты госоргана.
		
		Элементы.ПочтаРоссииКонтактыГосорганов_Наименование.Видимость = Ложь;
		Элементы.ПочтаРоссииКонтактыГосорганов_Адрес.Видимость        = Ложь;
		Элементы.Конверт.Видимость = Ложь;
		
		ОписаниеОтправкиПочтаРоссииЗаголовок
			= НСтр("ru = 'Распечатайте все отчеты в 1 экземпляре, подпишите каждый лист и отправьте в %ГосОрган% ценным письмом с описью вложения.'");
		
		Элементы.ЛичныйВизитКонтактыГосорганов.Видимость = Ложь;
		
		ОписаниеОтправкиЛичныйВизитЗаголовок
			= НСтр("ru = 'Распечатайте все отчеты в 2 экземплярах, подпишите каждый лист и отнесите в %ГосОрган%.'");
		
	Иначе
		
		Элементы.ПочтаРоссииКонтактыГосорганов_Наименование.Видимость = Истина;
		Элементы.ПочтаРоссииКонтактыГосорганов_Адрес.Видимость        = Истина;
		Элементы.Конверт.Видимость = Истина;
		
		ОписаниеОтправкиПочтаРоссииЗаголовок
			= НСтр("ru = 'Распечатайте все отчеты в 1 экземпляре, подпишите каждый лист и отправьте ценным письмом с описью вложения по адресу:'");
		
		Элементы.ЛичныйВизитКонтактыГосорганов.Видимость = Истина;
		
		ОписаниеОтправкиЛичныйВизитЗаголовок
			= НСтр("ru = 'Распечатайте все отчеты в 2 экземплярах, подпишите каждый лист и отнесите в %ГосОрган%:'");
		
	КонецЕсли;
	
	Если ГосударственныйОрган = Перечисления.ТипыКонтролирующихОрганов.ФНС Тогда
		тНадписьГосорган = НСтр("ru = 'налоговую инспекцию'");
	ИначеЕсли ГосударственныйОрган = Перечисления.ТипыКонтролирующихОрганов.ПФР Тогда
		тНадписьГосорган = НСтр("ru = 'пенсионный фонд'");
	ИначеЕсли ГосударственныйОрган = Перечисления.ТипыКонтролирующихОрганов.ФСС Тогда
		тНадписьГосорган = НСтр("ru = 'фонда социального страхования'");
	Иначе
		тНадписьГосорган = НСтр("ru = 'контролирующий орган'");
	КонецЕсли;
	
	Элементы.ОписаниеОтправкиЛичныйВизит.Заголовок = СтрЗаменить(ОписаниеОтправкиЛичныйВизитЗаголовок,
												"%ГосОрган%", тНадписьГосорган);
	
	Элементы.ОписаниеОтправкиПочтаРоссии.Заголовок = СтрЗаменить(ОписаниеОтправкиПочтаРоссииЗаголовок,
												"%ГосОрган%", тНадписьГосорган);
	
КонецПроцедуры

&НаСервере
Функция ПодготовитьДанныеОписиВложения()
	
	ОтправляемыеОтчеты = ТаблицаОтчеты.НайтиСтроки(Новый Структура("ГосударственныйОрган", ГосударственныйОрган));
	
	ВложенныеПредметы = Обработки.ПечатьОписиВложения.НоваяТаблицаВложенийОписи();
	
	Для Каждого ОтправляемыйОтчет Из ОтправляемыеОтчеты Цикл
		
		Если НЕ ЗначениеЗаполнено(ОтправляемыйОтчет.РегламентированныйОтчет) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаВложенныеПредметы = ВложенныеПредметы.Добавить();
		СтрокаВложенныеПредметы.НаименованиеПредмета = ОтправляемыйОтчет.Наименование;
		СтрокаВложенныеПредметы.Количество = 1;
		СтрокаВложенныеПредметы.ОбъявленнаяЦенность = 1;
		
	КонецЦикла;
	
	СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Объект.Организация, Объект.Период);
	
	ДанныеОписи = Новый Структура;
	ДанныеОписи.Вставить("Отправитель",       СведенияОбОрганизации.НаименованиеДляПечатныхФорм);
	ДанныеОписи.Вставить("ВложенныеПредметы", ВложенныеПредметы);
	
	Возврат ПоместитьВоВременноеХранилище(ДанныеОписи, УникальныйИдентификатор);
	
КонецФункции

&НаКлиенте
Процедура СформироватьБланкОтчета(АдресОтчета)
	
	Отбор = Новый Структура("ГосорганНомерГруппы, ОтчетНомерГруппы", АдресОтчета.НомерГосоргана, АдресОтчета.НомерОтчета);
	
	ЗадачаОтчет = ТаблицаОтчеты.НайтиСтроки(Отбор)[0];
	
	Если ТипЗнч(ЗадачаОтчет.РегламентированныйОтчет) = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
		
		ОперацииСОтчетом = РегламентированнаяОтчетностьКлиентСервер.ОперацииСРегламентированнымОтчетом(
		ЗадачаОтчет.ИсточникОтчета, ЗадачаОтчет.ВыбраннаяФорма);
		
		Если ОперацииСОтчетом.ПечатьСоШтрихкодомPDF417НаСервере Тогда
			РегламентированнаяОтчетностьКлиент.ВывестиМашиночитаемуюФорму(
				ФормаРегламентированногоОтчета(ЗадачаОтчет, Ложь),
				"ПоказатьСДвухмернымШтрихкодомPDF417");
		Иначе
			ВывестиРегламентированныйОтчет(ЗадачаОтчет.РегламентированныйОтчет);
		КонецЕсли;
	Иначе
		ВывестиДокументыОтчеты(ЗадачаОтчет.РегламентированныйОтчет, ЗадачаОтчет.ИсточникОтчета, ЗадачаОтчет.Наименование);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция МожноПечататьОтчет(ЗадачаОтчет)
	
	Возврат РеквизитыОрганизацииЗаполнены
		И ЗначениеЗаполнено(ЗадачаОтчет.РегламентированныйОтчет)
		И НЕ ЗадачаОтчет.Обновить
		И НЕ ЗначениеЗаполнено(ЗадачаОтчет.ОшибкиФормирования);
	
КонецФункции

&НаКлиенте
Функция ФормаРегламентированногоОтчета(ЗадачаОтчет, ЗаполнитьПриСоздании)
	
	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("мВыбраннаяФорма", ЗадачаОтчет.ВыбраннаяФорма);
	ПараметрыФормы.Вставить("мДатаКонцаПериодаОтчета", ЗадачаОтчет.КонецПериода);
	ПараметрыФормы.Вставить("мДатаНачалаПериодаОтчета", ЗадачаОтчет.НачалоПериода);
	ПараметрыФормы.Вставить("мСкопированаФорма", Неопределено);
	ПараметрыФормы.Вставить("Организация", Объект.Организация);
	ПараметрыФормы.Вставить("ДоступенМеханизмПечатиРеглОтчетностиСДвухмернымШтрихкодомPDF417", Истина);
	ПараметрыФормы.Вставить("СформироватьФормуОтчетаАвтоматически", ЗаполнитьПриСоздании);
	ПараметрыФормы.Вставить("БезОткрытияФормы", Истина);
	
	Если ЗначениеЗаполнено(ЗадачаОтчет.РегламентированныйОтчет) Тогда
		ПараметрыФормы.Вставить("мСохраненныйДок", ЗадачаОтчет.РегламентированныйОтчет);
	КонецЕсли;
	
	Если СтрНайти(ЗадачаОтчет.ВыбраннаяФорма, "Документ")  = 0 Тогда
		ПутьКФормеОтчета = "Отчет." + ЗадачаОтчет.ИсточникОтчета + ".Форма." + ЗадачаОтчет.ВыбраннаяФорма;
	Иначе
		ПутьКФормеОтчета = ЗадачаОтчет.ИсточникОтчета + ".Форма.ФормаДокумента";
	КонецЕсли;
	
	ФормаОтчета = ПолучитьФорму(ПутьКФормеОтчета,
		ПараметрыФормы,
		 ,
		Истина);
		
	Возврат ФормаОтчета;
	
КонецФункции

&НаСервереБезКонтекста
Функция РазрешенЭлектронныйДокументооборот()
	
	Возврат ТарификацияБПВызовСервераПовтИсп.РазрешенЭлектронныйДокументообротСКонтролирующимиОрганами()
		ИЛИ ТарификацияБП.ЕстьВыставленныйСчетНаОплатуСервиса();
	
КонецФункции

#Область ОтображениеЭлементов

&НаСервере
Процедура ОтобразитьОтчеты()
	
	КорневаяГруппа = Элементы.БлокиОтчетов;
	
	СкрытьПодчиненныеГруппы(КорневаяГруппа);
	
	Госорганы = ЗадействованныеГосорганы();
	
	НомераГосорганов = НомераГосорганов(Госорганы); // Постоянные
	НумераторОтчетов = НумераторОтчетов(Госорганы); // Инкрементируемый
	
	РазместитьГосорганыНаФорме(НомераГосорганов);
	ТаблицаОтчеты.Сортировать("ПериодСобытия");
	
	Для Каждого ЗадачаОтчет Из ТаблицаОтчеты Цикл
		
		Если ЗадачаОтчет.Предстоящая Тогда
			// Предстоящие отчеты еще рано формировать.
			Продолжить;
		КонецЕсли;
		
		НомерГосоргана = НомераГосорганов[ЗадачаОтчет.ГосударственныйОрган];
		
		// Инкрементируем счетчик отчетов, представляемых в данный госорган.
		НомерОтчета    = НумераторОтчетов[ЗадачаОтчет.ГосударственныйОрган] + 1;
		НумераторОтчетов[ЗадачаОтчет.ГосударственныйОрган] = НомерОтчета;
		
		Если ЗадачаОтчет.Предстоящая Тогда
			// Предстоящие отчеты еще рано формировать.
			Продолжить;
		КонецЕсли;
		
		// Запишем координаты группы отчета в таблицу
		ЗадачаОтчет.ГосорганНомерГруппы = НомерГосоргана;
		ЗадачаОтчет.ОтчетНомерГруппы    = НомерОтчета;
		
		РазместитьОтчетНаФорме(ЗадачаОтчет);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура РазместитьОтчетНаФорме(ЗадачаОтчет)
	
	СуффиксЭлементовОтчета = СуффиксОтчет(ЗадачаОтчет.ГосорганНомерГруппы, ЗадачаОтчет.ОтчетНомерГруппы);
	
	ГруппаОтчет = ГруппаДляРазмещенияОтчета(ЗадачаОтчет.ГосорганНомерГруппы, ЗадачаОтчет.ОтчетНомерГруппы);
	
	// Заполним свойства элементов из задачи
	
	// Иконка отчета
	ЭлементКартинка = ГруппаОтчет.ПодчиненныеЭлементы[ИмяОтчет() + "Картинка" + СуффиксЭлементовОтчета];
	ЭлементКартинка.Картинка = ИконкаОтчета(ЗадачаОтчет);
	
	// Наименование отчета
	ЭлементНаименование = ГруппаОтчет.ПодчиненныеЭлементы[ИмяОтчет() + "Наименование" + СуффиксЭлементовОтчета];
	ЭлементНаименование.Заголовок = СтрокаНаименованиеОтчета(ЗадачаОтчет);
	
	Если ЗначениеЗаполнено(ЗадачаОтчет.РегламентированныйОтчет) И НЕ ЗадачаОтчет.Обновить
		И НЕ ПустаяСтрока(ЗадачаОтчет.РасширенныйПериодПодсказка) Тогда
		ЭлементНаименование.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
		ЭлементНаименование.Подсказка = ЗадачаОтчет.РасширенныйПериодПодсказка;
	Иначе
		ЭлементНаименование.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	КонецЕсли;
	
	// Статус отчета
	ЭлементСтатус = ГруппаОтчет.ПодчиненныеЭлементы[ИмяОтчет() + "Статус" + СуффиксЭлементовОтчета];
	ЗаданСтатусОтчета = Ложь;
	ЭлементСтатус.Заголовок = СтрокаСтатусОтчета(ЗадачаОтчет, ЗаданСтатусОтчета);
	ЭлементСтатус.Видимость = ЗаданСтатусОтчета;
	
	Если НЕ ЗаданСтатусОтчета Тогда
		// Дополнительные действия
		ЭлементДополнительно = ГруппаОтчет.ПодчиненныеЭлементы[ИмяОтчет() + "Дополнительно" + СуффиксЭлементовОтчета];
		ЭлементДополнительно.Заголовок = ЗадачаОтчет.Дополнительно;
		ЭлементДополнительно.Видимость = ЗначениеЗаполнено(ЭлементДополнительно.Заголовок);
		Если ЗначениеЗаполнено(ЗадачаОтчет.ДополнительноПодсказка) Тогда
			ЭлементДополнительно.Подсказка = ЗадачаОтчет.ДополнительноПодсказка;
		КонецЕсли;
	КонецЕсли;
	
	ГруппаОтчет.Видимость = Истина;
	
КонецПроцедуры

&НаСервере
Функция ГруппаДляРазмещенияОтчета(НомерГосоргана, НомерОтчета)
	
	ГруппаОтчетЭталон = Элементы.Отчет_1_1;
	
	ГруппаГосорган = Элементы[ИмяГосорган() + СуффиксГосорган(НомерГосоргана)];
	
	СуффиксЭлементовОтчета = СуффиксОтчет(НомерГосоргана, НомерОтчета);
	
	ИмяГруппыОтчет = ИмяОтчет() + СуффиксЭлементовОтчета;
	ГруппаОтчет    = ГруппаГосорган.ПодчиненныеЭлементы.Найти(ИмяГруппыОтчет);
	
	Если ГруппаОтчет = Неопределено Тогда
		
		ГруппаОтчет = Элементы.Добавить(ИмяГруппыОтчет, ТипЗнч(ГруппаОтчетЭталон), ГруппаГосорган);
		ЗаполнитьЗначенияСвойств(ГруппаОтчет, ГруппаОтчетЭталон, , "ПутьКДаннымЗаголовка");
		
		// Подчиненные элементы
		Для Каждого ЭлементЭталон Из ГруппаОтчетЭталон.ПодчиненныеЭлементы Цикл
			
			ИмяБезАдреса = ИмяЭлементаБезАдреса(ЭлементЭталон.Имя);
			
			ИмяНовогоЭлемента = ИмяЭлементаСАдресом(ИмяБезАдреса, НомерГосоргана, НомерОтчета);
			
			НовыйЭлемент = Элементы.Добавить(ИмяНовогоЭлемента, ТипЗнч(ЭлементЭталон), ГруппаОтчет);
			ЗаполнитьЗначенияСвойств(НовыйЭлемент, ЭлементЭталон);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ГруппаОтчет;
	
КонецФункции

&НаСервере
Процедура РазместитьГосорганыНаФорме(НомераГосорганов)
	
	КорневаяГруппа           = Элементы.БлокиОтчетов;
	ГруппаГосорганЭталон     = Элементы.ГосударственныйОрган_1;
	ДекорацияЗаголовокЭталон = Элементы.ГосударственныйОрганЗаголовок_1;
	
	Для Каждого ЭлементСоответствия Из НомераГосорганов Цикл
		
		Госорган       = ЭлементСоответствия.Ключ;
		НомерГосоргана = ЭлементСоответствия.Значение;
		
		ГруппаГосорган = Элементы.Найти(ИмяГосорган() + СуффиксГосорган(НомерГосоргана));
		
		Если ГруппаГосорган = Неопределено Тогда
			
			ГруппаГосорган = Элементы.Добавить(ИмяГосорган() + СуффиксГосорган(НомерГосоргана), ТипЗнч(ГруппаГосорганЭталон), КорневаяГруппа);
			ЗаполнитьЗначенияСвойств(ГруппаГосорган, ГруппаГосорганЭталон, , "ПутьКДаннымЗаголовка");
			
		КонецЕсли;
		
		ИмяЭлементаЗаголовокГосоргана = ИмяГосорган() + "Заголовок" + СуффиксГосорган(НомерГосоргана);
		ЭлементЗаголовокГосоргана = ГруппаГосорган.ПодчиненныеЭлементы.Найти(ИмяЭлементаЗаголовокГосоргана);
		
		Если ЭлементЗаголовокГосоргана = Неопределено Тогда
			ЭлементЗаголовокГосоргана = Элементы.Добавить(ИмяЭлементаЗаголовокГосоргана, ТипЗнч(ДекорацияЗаголовокЭталон), ГруппаГосорган);
			ЗаполнитьЗначенияСвойств(ЭлементЗаголовокГосоргана, ДекорацияЗаголовокЭталон);
		КонецЕсли;
		
		ЭлементЗаголовокГосоргана.Заголовок = ПомощникиПоУплатеНалоговИВзносов.ОписаниеГосорганаПолучателяОтчетности(Госорган);
		
		ГруппаГосорган.Видимость = Истина;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СкрытьПодчиненныеГруппы(ГруппаРодитель)
	
	Для Каждого ПодчиненныйЭлемент Из ГруппаРодитель.ПодчиненныеЭлементы Цикл
		
		Если ТипЗнч(ПодчиненныйЭлемент) = Тип("ГруппаФормы") Тогда
			
			ПодчиненныйЭлемент.Видимость = Ложь;
			СкрытьПодчиненныеГруппы(ПодчиненныйЭлемент);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НомераГосорганов(Госорганы)
	
	Номера = Новый Соответствие;
	
	Для ИндексЭлемента = 0 По Госорганы.ВГраница() Цикл
		Номера.Вставить(Госорганы[ИндексЭлемента], ИндексЭлемента + 1);
	КонецЦикла;
	
	Возврат Номера;
	
КонецФункции

&НаСервереБезКонтекста
Функция НумераторОтчетов(Госорганы)
	
	Нумератор = Новый Соответствие;
	
	Для Каждого Госорган Из Госорганы Цикл
		Нумератор.Вставить(Госорган, 0);
	КонецЦикла;
	
	Возврат Нумератор;
	
КонецФункции

&НаСервереБезКонтекста
Функция ИмяЭлементаБезАдреса(ИмяЭлемента)
	
	ЧастиИмени = СтрРазделить(ИмяЭлемента, "_");
	
	Возврат ЧастиИмени[0];
	
КонецФункции

&НаСервереБезКонтекста
Функция ИмяЭлементаСАдресом(ИмяБезАдреса, НомерГосоргана, НомерОтчета)
	
	Возврат ИмяБезАдреса + СуффиксОтчет(НомерГосоргана, НомерОтчета);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция АдресОтчета(ИмяЭлемента)
	
	ЧастиИмени = СтрРазделить(ИмяЭлемента, "_");
	
	Возврат Новый Структура("НомерГосоргана, НомерОтчета", Число(ЧастиИмени[1]), Число(ЧастиИмени[2]));
	
КонецФункции

&НаСервереБезКонтекста
Функция ИмяГосорган()
	
	Возврат "ГосударственныйОрган";
	
КонецФункции

&НаСервереБезКонтекста
Функция ИмяОтчет()
	
	Возврат "Отчет";
	
КонецФункции

&НаСервереБезКонтекста
Функция СуффиксГосорган(НомерГосоргана)
	
	Возврат "_" + НомерГосоргана;
	
КонецФункции

&НаСервереБезКонтекста
Функция СуффиксОтчет(НомерГосоргана, НомерОтчета)
	
	Возврат "_" + НомерГосоргана + "_" + НомерОтчета;
	
КонецФункции

&НаСервере
Функция ИконкаОтчета(ЗадачаОтчет)
	
	Если НЕ РеквизитыОрганизацииЗаполнены
		ИЛИ ЗначениеЗаполнено(ЗадачаОтчет.ОшибкиФормирования) Тогда
		
		Возврат БиблиотекаКартинок.ВниманиеКрасный;
		
	ИначеЕсли ЗначениеЗаполнено(ЗадачаОтчет.РегламентированныйОтчет) И НЕ ЗадачаОтчет.Обновить Тогда
		
		Возврат БиблиотекаКартинок.ФорматPDF;
		
	Иначе
		
		Возврат БиблиотекаКартинок.ДлительнаяОперация16;
		
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ЗадействованныеГосорганы()
	
	Госорганы = ТаблицаОтчеты.Выгрузить();
	Госорганы.Свернуть("ГосударственныйОрган");
	
	Госорганы.Колонки.Добавить("Порядок", ОбщегоНазначения.ОписаниеТипаЧисло(2, 0, ДопустимыйЗнак.Неотрицательный));
	
	Для Каждого СтрокаТаблицы Из Госорганы Цикл
		СтрокаТаблицы.Порядок = Перечисления.ТипыКонтролирующихОрганов.Индекс(СтрокаТаблицы.ГосударственныйОрган);
	КонецЦикла;
	
	Госорганы.Сортировать("Порядок");
	
	Возврат Госорганы.ВыгрузитьКолонку("ГосударственныйОрган");
	
КонецФункции

&НаСервере
Функция СтрокаНаименованиеОтчета(ЗадачаОтчет)
	
	ТекстНавигационнойСсылки = "";
	
	Если МожноПечататьОтчет(ЗадачаОтчет) Тогда
		ТекстНавигационнойСсылки = "НапечататьОтчет"
			+ СуффиксОтчет(ЗадачаОтчет.ГосорганНомерГруппы, ЗадачаОтчет.ОтчетНомерГруппы);
	ИначеЕсли ЗначениеЗаполнено(ЗадачаОтчет.ОшибкиФормирования) Тогда
		ТекстНавигационнойСсылки = "ПоказатьОшибкиОтчета"
			+ СуффиксОтчет(ЗадачаОтчет.ГосорганНомерГруппы, ЗадачаОтчет.ОтчетНомерГруппы);
	КонецЕсли;
	
	Возврат Новый ФорматированнаяСтрока(ЗадачаОтчет.Наименование, , , ,ТекстНавигационнойСсылки);
	
КонецФункции

&НаСервере
Функция ТекстНеЗаполненыРеквизитыДляОтчетности() Экспорт
	
	ТекстДействия = НСтр("ru = 'подготовки отчетности'");
	
	Если ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Объект.Организация) Тогда
		ТекстОписанияСведений = НСтр("ru = 'сведения об организации'");
	Иначе
		ТекстОписанияСведений = НСтр("ru = 'сведения о себе'");
	КонецЕсли;
	
	ГиперссылкаСведенияОСебе = Новый ФорматированнаяСтрока(ТекстОписанияСведений,,,,"РеквизитыОрганизацииДляОтчетности");
	
	ЭлементыСообщенияОбОшибке = Новый Массив;
	ЭлементыСообщенияОбОшибке.Добавить(СтрШаблон(НСтр("ru = 'Для завершения %1 укажите'"), ТекстДействия));
	ЭлементыСообщенияОбОшибке.Добавить(" ");
	ЭлементыСообщенияОбОшибке.Добавить(ГиперссылкаСведенияОСебе);
	
	Возврат Новый ФорматированнаяСтрока(ЭлементыСообщенияОбОшибке);
	
КонецФункции

&НаСервере
Функция ОбновитьСостояниеПодготовкиОтчетности()
	
	Элементы.ПодготовкаОтчетностиСостояние.Заголовок = ТекстСостояниеПодготовкиОтчетности();
	
КонецФункции

&НаСервере
Процедура ОбновитьПредставлениеСледующегоОтчета()
	
	Если ОтчетностьНеТребуется Тогда
		
		Описание = ОписаниеСледующегоОтчета();
		
		ПериодСледующегоОтчетаРасширен = Описание.ОтчетныйПериодРасширен;
		
		Элементы.ПодготовкаОтчетностиСледующийОтчет.Заголовок = Описание.ПредставлениеОтчета;
		Элементы.ПояснениеРасширенныйНалоговыйПериод.Заголовок = Описание.РасширенныйПериодТекстПояснения;
		Элементы.ПояснениеРасширенныйНалоговыйПериодРасширеннаяПодсказка.Заголовок = Описание.РасширенныйПериодТекстПодсказки;
		
	Иначе
		// Если требуется формировать отчеты - элементы для следующего отчета скрыты, настраивать их не нужно.
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ТекстСостояниеПодготовкиОтчетности()
	
	Если НЕ РеквизитыОрганизацииЗаполнены Тогда
		
		Возврат НСтр("ru = 'Отчетность почти готова!'");
		
	ИначеЕсли ОтчетыЗаполнены И ОтчетыСформированы Тогда
		
		Если ОтчетностьНеТребуется Тогда
			Возврат НСтр("ru = 'Пока сдавать отчеты не требуется'");
		Иначе
			Возврат НСтр("ru = 'Отчетность готова!'");
		КонецЕсли;
		
	ИначеЕсли НЕ ОтчетыСформированы Тогда
		
		ЕстьПросроченные = ПериодыДобавленныеПроверкой.НайтиСтроки(Новый Структура("Требуется", Истина)).Количество() > 0;
		
		Если ЕстьПросроченные Тогда
			Возврат НСтр("ru = 'Готовится отчетность за текущую и прошедшие отчетные кампании'");
		Иначе
			
			ОтчетныйПериод = РелевантныйОтчетныйПериод();
			НачалоОтчетногоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(
				ОтчетныйПериод.Периодичность, ОтчетныйПериод.Период);
			КонецОтчетногоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(
				ОтчетныйПериод.Периодичность, ОтчетныйПериод.Период);
			
			ПредставлениеОтчетногоПериода = ПредставлениеПериода(НачалоОтчетногоПериода, КонецОтчетногоПериода, "ФП = Истина");
			
			Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Готовится отчетность за текущую отчетную кампанию - %1'"),
					ПредставлениеОтчетногоПериода);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ОписаниеСледующегоОтчета()
	
	Отбор = Новый Структура("Предстоящая", Истина);
	БлижайшиеЗадачи = ТаблицаОтчеты.Выгрузить(Отбор, "Правило, Периодичность, ПериодСобытия, НачалоВыполнения");
	
	Если БлижайшиеЗадачи.Количество() = 0 Тогда
		Возврат ПомощникиПоУплатеНалоговИВзносов.НовыйОписаниеОтчета();
	КонецЕсли;
	
	БлижайшиеЗадачи.Сортировать("НачалоВыполнения");
	
	БлижайшаяЗадача = БлижайшиеЗадачи[0];
	
	Возврат ПомощникиПоУплатеНалоговИВзносов.ОписаниеОтчета(Объект.Организация, БлижайшаяЗадача.Правило, БлижайшаяЗадача.ПериодСобытия);
	
КонецФункции

&НаСервере
Функция СтрокаСтатусОтчета(ЗадачаОтчет, ЗаданСтатусОтчета)
	
	ТекстНавигационнойСсылки = "";
	
	Статус       = "";
	ШрифтСтатуса = Новый Шрифт( , , , Истина);
	ЦветТекста   = ЦветаСтиля.ЦветШрифтаСеройПодсказки;
	
	Если ЗначениеЗаполнено(ЗадачаОтчет.ОшибкиФормирования) Тогда
		
		ТекстНавигационнойСсылки = "ПоказатьОшибкиОтчета" + СуффиксОтчет(ЗадачаОтчет.ГосорганНомерГруппы, ЗадачаОтчет.ОтчетНомерГруппы);
		
		Статус       = НСтр("ru = 'ошибки при формировании'");
		ШрифтСтатуса = Неопределено;
		ЦветТекста   = ЦветаСтиля.ЦветШрифтаОшибки;
		
	ИначеЕсли НЕ РеквизитыОрганизацииЗаполнены Тогда
		
		Статус = НСтр("ru = 'укажите сведения о себе'");
		
	ИначеЕсли ЗадачаОтчет.Обновить ИЛИ НЕ ЗначениеЗаполнено(ЗадачаОтчет.РегламентированныйОтчет) Тогда
		
		Статус = НСтр("ru = 'формируется...'");
		
	КонецЕсли;
	
	ЗаданСтатусОтчета = ЗначениеЗаполнено(Статус);
	
	Возврат Новый ФорматированнаяСтрока(Статус, ШрифтСтатуса, ЦветТекста, ,ТекстНавигационнойСсылки);
	
КонецФункции

#КонецОбласти

#Область ТестПрошлыхПериодов

&НаСервере
Функция ПараметрыТестаПрошлыхПериодов()
	
	РелевантныйОтчетныйПериод = РелевантныйОтчетныйПериод();
	
	Результат = Новый Структура;
	
	Результат.Вставить("Организация",
		Объект.Организация);
	Результат.Вставить("НачальныйВопрос",
		"ГруппаТест2");
	Результат.Вставить("ОтчетныйПериод",
		РелевантныйОтчетныйПериод.Период);
	Результат.Вставить("Правило",
		РелевантныйОтчетныйПериод.Правило);
	Результат.Вставить("Правила",
		ПоместитьВоВременноеХранилище(РелевантныйОтчетныйПериод.Правила, Новый УникальныйИдентификатор));
	Результат.Вставить("ГосударственныйОрган",
		ГосударственныйОрган);
	Результат.Вставить("УпрощеннаяНулеваяОтчетность",
		Истина);
	
	Если РелевантныйОтчетныйПериод.Правила = Неопределено Тогда
		ДобавленныеОтчетныеПериоды =
			РегистрыСведений.РезультатыПроверкиНалоговОтчетовПрошлыхПериодов.ДобавленныеПериоды(Объект.Организация);
	Иначе
		ДобавленныеОтчетныеПериоды =
			РегистрыСведений.РезультатыПроверкиНалоговОтчетовПрошлыхПериодов.ДобавленныеПериодыГруппыПравил(Объект.Организация, РелевантныйОтчетныйПериод.Правила.ВыгрузитьКолонку("Правило"));
	КонецЕсли;
		
	Результат.Вставить("АдресВходныхДанных", ПоместитьВоВременноеХранилище(ДобавленныеОтчетныеПериоды, УникальныйИдентификатор));
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция РелевантныйОтчетныйПериод()
	
	Если ПустаяСтрока(АдресПравилаПрошлыхПериодов) Тогда
		Расписание = ТаблицаОтчеты.Выгрузить(, "ПериодСобытия, Правило, Периодичность, НачалоВыполнения, Срок");
	Иначе
		Расписание = ПолучитьИзВременногоХранилища(АдресПравилаПрошлыхПериодов);
		Если Расписание = Неопределено Тогда
			Расписание = ТаблицаОтчеты.Выгрузить(, "ПериодСобытия, Правило, Периодичность, НачалоВыполнения, Срок");
		Иначе
			Расписание.Свернуть("ПериодСобытия, Правило, Периодичность, НачалоВыполнения, Срок");
		КонецЕсли;
	КонецЕсли;
	
	Возврат Обработки.ПодготовкаНулевойОтчетности.РелевантныйОтчетныйПериод(Объект.Организация, Объект.Период, Расписание);
	
КонецФункции

#КонецОбласти

#Область ПроверкаРеквизитовДляОтчетности

&НаСервере
Процедура ПроверитьРеквизитыОрганизацииДляОтчетности()
	
	Если ЭтоАдресВременногоХранилища(АдресХранилищаНезаполненныхРеквизитов) Тогда
		УдалитьИзВременногоХранилища(АдресХранилищаНезаполненныхРеквизитов);
		АдресХранилищаНезаполненныхРеквизитов = "";
	КонецЕсли;
	
	НезаполненныеРеквизиты = Неопределено;
	РеквизитыОрганизацииЗаполнены = РеквизитыДляОтчетностиЗаполнены(Объект.Организация, НезаполненныеРеквизиты);
	
	Если НЕ РеквизитыОрганизацииЗаполнены Тогда
		
		АдресХранилищаНезаполненныхРеквизитов = ПоместитьВоВременноеХранилище(НезаполненныеРеквизиты, УникальныйИдентификатор);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПроверяемыеРеквизитыДляОтчетности()
	
	ПроверяемыеРеквизиты = Новый Массив;
	
	ОтборТекущихОтчетов = Новый Структура("Предстоящая", Ложь);
	
	ТребуемыеОтчеты = ТаблицаОтчеты.Выгрузить(ОтборТекущихОтчетов, "ИсточникОтчета, ПериодСобытия");
	
	Для Каждого ТребуемыйОтчет Из ТребуемыеОтчеты Цикл
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			ПроверяемыеРеквизиты,
			РегламентированнаяОтчетностьБП.РеквизитыОбязательныеДляОтчета(ТребуемыйОтчет.ИсточникОтчета,
				Объект.Организация, ТребуемыйОтчет.ПериодСобытия),
			Истина);
		
	КонецЦикла;
	
	Возврат ПроверяемыеРеквизиты;
	
КонецФункции

&НаСервере
Функция РеквизитыДляОтчетностиЗаполнены(Организация, НезаполненныеРеквизиты)
	
	Возврат ОрганизацииФормыДляОтчетности.РеквизитыЗаполнены(
				Организация, ПроверяемыеРеквизитыДляОтчетности(), НезаполненныеРеквизиты);
	
КонецФункции

#КонецОбласти

#Область СведенияБаланса

&НаСервере
Процедура ДополнитьОписание_ВводДанныхБаланса() 
	
	Если ГосударственныйОрган <> Перечисления.ТипыКонтролирующихОрганов.ФНС
		И НЕ ЮридическоеЛицо Тогда
		Возврат;
	КонецЕсли;
	
	НачалоПериода = НачалоРелевантногоПериода;
	
	ТекущаяСтрока = Неопределено;
	Если ТребуетсяПроверитьПрошлыеПериоды Тогда
		
		ТаблицаТекущихПравил = ТаблицаОтчеты.Выгрузить(Новый Структура("Предстоящая", Ложь), "Правило");
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Организация",  Объект.Организация);
		Запрос.УстановитьПараметр("КодЗадачи",    ЗадачиБухгалтераКлиентСервер.КодЗадачиБухгалтерскаяОтчетность());
		Запрос.УстановитьПараметр("МассивПравил", ТаблицаТекущихПравил.ВыгрузитьКолонку("Правило"));
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Расписание.НачалоВыполнения КАК Период,
		|	Расписание.Правило КАК Правило
		|ИЗ
		|	Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК ПравилаПредставленияОтчетовУплатыНалогов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗадачиБухгалтера КАК Расписание
		|		ПО (Расписание.Правило = ПравилаПредставленияОтчетовУплатыНалогов.Ссылка)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗадачиБухгалтера КАК ЗадачиБухгалтера
		|		ПО ПравилаПредставленияОтчетовУплатыНалогов.Владелец = ЗадачиБухгалтера.Ссылка
		|ГДЕ
		|	ЗадачиБухгалтера.Код = &КодЗадачи
		|	И ПравилаПредставленияОтчетовУплатыНалогов.Ссылка В(&МассивПравил)
		|	И Расписание.Организация = &Организация
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	Расписание.ПериодСобытия,
		|	Расписание.Правило
		|ИЗ
		|	Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК ПравилаПредставленияОтчетовУплатыНалогов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РезультатыПроверкиНалоговОтчетовПрошлыхПериодов КАК Расписание
		|		ПО (Расписание.Правило = ПравилаПредставленияОтчетовУплатыНалогов.Ссылка)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗадачиБухгалтера КАК ЗадачиБухгалтера
		|		ПО ПравилаПредставленияОтчетовУплатыНалогов.Владелец = ЗадачиБухгалтера.Ссылка
		|ГДЕ
		|	ЗадачиБухгалтера.Код = &КодЗадачи
		|	И ПравилаПредставленияОтчетовУплатыНалогов.Ссылка В(&МассивПравил)
		|	И Расписание.Организация = &Организация
		|	И Расписание.ТребуетсяНалогОтчет
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период";
		
		Результат = Запрос.Выполнить().Выгрузить();
		ТребуетсяПроверитьДанныеБаланса = Результат.Количество() <> 0;
		Если ТребуетсяПроверитьДанныеБаланса Тогда
			КонецПериода = НачалоДня(КонецГода(Результат[0].Период));
			СтрокиЗадачи = ТаблицаОтчеты.НайтиСтроки(Новый Структура("Правило, Предстоящая, КонецПериода", Результат[0].Правило, Ложь, КонецПериода));
			Если СтрокиЗадачи.Количество() = 0 Тогда
				НачалоВыполнения = НачалоГода(Результат[0].Период);
				СтрокиЗадачи = ТаблицаОтчеты.НайтиСтроки(Новый Структура("Правило, Предстоящая, НачалоВыполнения", Результат[0].Правило, Ложь, НачалоВыполнения));
			КонецЕсли;
			Если СтрокиЗадачи.Количество() <> 0 Тогда
				ТекущаяСтрока = СтрокиЗадачи[0];
			Иначе
				ТребуетсяПроверитьДанныеБаланса = Ложь;
			КонецЕсли;
		КонецЕсли;
	Иначе
		ТребуетсяПроверитьДанныеБаланса = Ложь;
	КонецЕсли;
	
	Описание = "";
	Если ТребуетсяПроверитьДанныеБаланса Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Организация",   Объект.Организация);
		Запрос.УстановитьПараметр("НачалоПериода", КонецДня(КонецПериода) + 1);
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Хозрасчетный.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
		|ГДЕ
		|	Хозрасчетный.Период <= &НачалоПериода
		|	И Хозрасчетный.Организация = &Организация
		|	И Хозрасчетный.Регистратор ССЫЛКА Документ.ВводНачальныхОстатков";
		
		Результат = Запрос.Выполнить();
		
		МассивСтрок = Новый Массив;
		
		Если НЕ Результат.Пустой() Тогда
			ТекстКоманды = Новый ФорматированнаяСтрока(НСтр("ru = '(проверьте '"));
		Иначе
 			ТекстКоманды = Новый ФорматированнаяСтрока(НСтр("ru = '(введите '"));
		КонецЕсли;
		МассивСтрок.Добавить(ТекстКоманды);
		МассивСтрок.Добавить(
				Новый ФорматированнаяСтрока(НСтр("ru = 'начальные остатки'"),,,,"ВвестиДанныеОбУчредителяхИУставномКапитале"));
		МассивСтрок.Добавить(
				Новый ФорматированнаяСтрока(НСтр("ru = ')'")));
		Описание =  Новый ФорматированнаяСтрока(МассивСтрок);
		
		ТекущаяСтрока.Дополнительно = Описание;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиРегламентированныйОтчет(РегламентированныйОтчет) Экспорт
	
	МассивДокументов = ПодготовитьРегламентированныйОтчетНаСервере(РегламентированныйОтчет);
	
	#Если ВебКлиент Тогда
		МассивПередаваемыхФайлов = Новый Массив;
		Для Каждого ДокументОтчет ИЗ МассивДокументов Цикл
			ПередаваемыйФайл = Новый ОписаниеПередаваемогоФайла;
			ПередаваемыйФайл.Имя      = ДокументОтчет.ИмяФайла;
			ПередаваемыйФайл.Хранение = ПоместитьВоВременноеХранилище(ДокументОтчет.ДанныеФайла, Новый УникальныйИдентификатор);
			МассивПередаваемыхФайлов.Добавить(ПередаваемыйФайл);
		КонецЦикла;
		ВыбратьИСохранитьФайлыНаКлиенте(МассивПередаваемыхФайлов);
	#Иначе
		Для Каждого ДокументОтчет ИЗ МассивДокументов Цикл
			ЗапуститьПриложение(ДокументОтчет.ПолноеИмяФайла);
		КонецЦикла;
	#КонецЕсли	
	
КонецПроцедуры

&НаСервере
Функция ПодготовитьРегламентированныйОтчетНаСервере(РегламентированныйОтчет)
	
	МассивДокументов = Новый Массив;
	
	ИмяВременнойПапки = ПолучитьИмяВременногоФайла();
	СоздатьКаталог(ИмяВременнойПапки);
	
	СведенияОФайле = ИнтерфейсыВзаимодействияБРО.ПечатныйБланкРегламентированногоОтчета(РегламентированныйОтчет);
	ПечатнаяФорма = ПолучитьИзВременногоХранилища(СведенияОФайле.АдресПечатногоБланка);
	
	ТипФайла   = ТипФайлаТабличногоДокумента.PDF;
	ПолноеИмяФайла = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(ИмяВременнойПапки) + СведенияОФайле.ИмяФайлаПечатногоБланка;
	
	ПечатнаяФорма.Записать(ПолноеИмяФайла);
	
	МассивДокументов.Добавить(Новый Структура("ИмяФайла, ПолноеИмяФайла, ДанныеФайла", СведенияОФайле.ИмяФайлаПечатногоБланка, ПолноеИмяФайла, Новый ДвоичныеДанные(ПолноеИмяФайла)));
	
	Возврат МассивДокументов;
	
КонецФункции

&НаКлиенте
Процедура ВывестиДокументыОтчеты(Ссылка, ИсточникОтчета, ИмяОтчета) Экспорт
	
	МассивДокументов = ПодготовитьДокументыОтчетыНаСервере(Ссылка, ИсточникОтчета, ИмяОтчета);
	
	#Если ВебКлиент Тогда
		МассивПередаваемыхФайлов = Новый Массив;
		Для Каждого ДокументОтчет ИЗ МассивДокументов Цикл
			ПередаваемыйФайл = Новый ОписаниеПередаваемогоФайла;
			ПередаваемыйФайл.Имя      = ДокументОтчет.ИмяФайла;
			ПередаваемыйФайл.Хранение = ПоместитьВоВременноеХранилище(ДокументОтчет.ДанныеФайла, Новый УникальныйИдентификатор);
			МассивПередаваемыхФайлов.Добавить(ПередаваемыйФайл);
		КонецЦикла;
		ВыбратьИСохранитьФайлыНаКлиенте(МассивПередаваемыхФайлов);
	#Иначе
		Для Каждого ДокументОтчет ИЗ МассивДокументов Цикл
			ЗапуститьПриложение(ДокументОтчет.ПолноеИмяФайла);
		КонецЦикла;
	#КонецЕсли	
	
КонецПроцедуры

&НаСервере
Функция ПодготовитьДокументыОтчетыНаСервере(Ссылка, ИсточникОтчета, ИмяОтчета)
	
	МассивДокументов = Новый Массив;
	
	ИмяИсточникаОтчета = СтрЗаменить(ИсточникОтчета, "Документ.", "");
	
	Если ИмяИсточникаОтчета = "СведенияОЗастрахованныхЛицахСЗВ_М"
		ИЛИ ИмяИсточникаОтчета = "СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ" 
		ИЛИ ИмяИсточникаОтчета = "СправкиНДФЛДляПередачиВНалоговыйОрган" Тогда
		
		Если ИмяИсточникаОтчета = "СведенияОЗастрахованныхЛицахСЗВ_М"
			ИЛИ ИмяИсточникаОтчета = "СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ" Тогда
			ОтчетныйПериод = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ОтчетныйПериод");
		Иначе
			ОтчетныйПериод = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "НалоговыйПериодДата");
		КонецЕсли;
		
		ИменаМакетов = Документы[ИмяИсточникаОтчета].ИменаМакетов(ОтчетныйПериод);
	Иначе
		
		Возврат МассивДокументов;
		
	КонецЕсли;
	
	ПечатныеФормы = УправлениеПечатью.СформироватьПечатныеФормы(ИсточникОтчета, ИменаМакетов,
						ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Ссылка), Новый Структура());

	ИмяВременнойПапки = ПолучитьИмяВременногоФайла();
	СоздатьКаталог(ИмяВременнойПапки);
	
	ПрефиксИмени = ИмяОтчета;
	ПрефиксИмени = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(ПрефиксИмени);

	ПолноеИмяФайла = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(ИмяВременнойПапки);
	
	Для Каждого СтрокаПечатныхФорм Из ПечатныеФормы.КоллекцияПечатныхФорм Цикл
		
		ПечатнаяФорма = СтрокаПечатныхФорм.ТабличныйДокумент;
		
		ИмяФайла = ПрефиксИмени + "(" + СтрокаПечатныхФорм.СинонимМакета + ")";
		ИмяФайла = ИмяФайла + "." + "pdf";
		ИмяФайла = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(ИмяФайла);
		
		ПолноеИмяФайла = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(ИмяВременнойПапки) + ИмяФайла;
		ПечатнаяФорма.Записать(ПолноеИмяФайла, ТипФайлаТабличногоДокумента.PDF);
		
		МассивДокументов.Добавить(Новый Структура("ИмяФайла, ПолноеИмяФайла, ДанныеФайла", ИмяФайла, ПолноеИмяФайла, Новый ДвоичныеДанные(ПолноеИмяФайла)));
		
	КонецЦикла;
	
	Возврат МассивДокументов;
	
КонецФункции

&НаКлиенте
Процедура ЗавершитьВводНачальныхОстатков(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат <> КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	
	ПерезаполнитьОтчеты();
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьОтчеты()
	
	МассивСтрок = Новый Массив;
	Для Каждого СтрокаТаблицы ИЗ ТаблицаОтчеты Цикл
		
		Если СтрокаТаблицы.ИсточникОтчета = "РегламентированныйОтчетБухОтчетностьМП"
			И НЕ СтрокаТаблицы.Предстоящая
			И СтрокаТаблицы.СостояниеСдачиОтчета = Перечисления.СостояниеСдачиОтчетности.ДокументооборотНеНачат
			И СтрокаТаблицы.Статус <> ИнтерфейсыВзаимодействияБРОКлиентСервер.СтатусСданоСтрокой() Тогда
			
			МассивСтрок.Добавить(СтрокаТаблицы);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если МассивСтрок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаДляОбновления = ТаблицаОтчеты.Выгрузить(МассивСтрок);
	ТаблицаДляОбновления.ЗаполнитьЗначения(Неопределено, "РегламентированныйОтчет");
	АдресОбновленныхДанных = ПоместитьВоВременноеХранилище(ТаблицаДляОбновления, Новый УникальныйИдентификатор);
	
	ПараметрыОбновления = Новый Структура();
	ПараметрыОбновления.Вставить("Организация",     Объект.Организация);
	ПараметрыОбновления.Вставить("Период",          Объект.Период);
	ПараметрыОбновления.Вставить("ТребуемыеОтчеты", ТаблицаДляОбновления);
	Обработки.ПодготовкаНулевойОтчетности.СформироватьОтчеты(ПараметрыОбновления, АдресОбновленныхДанных);
	
	ТаблицаОбновленная = ПолучитьИзВременногоХранилища(АдресОбновленныхДанных);
	
	ДокументыКУдалению = Новый Массив;
	Для Каждого СтрокаТаблицы ИЗ ТаблицаОбновленная Цикл
		МассивСтрок = ТаблицаОтчеты.НайтиСтроки(Новый Структура("Наименование", СтрокаТаблицы.Наименование));
		Если МассивСтрок.Количество() = 1 Тогда
			ДокументыКУдалению.Добавить(МассивСтрок[0].РегламентированныйОтчет);
			МассивСтрок[0].РегламентированныйОтчет = СтрокаТаблицы.РегламентированныйОтчет;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Документ ИЗ ДокументыКУдалению Цикл
		ДокументОбъект = Документ.ПолучитьОбъект();
		ДокументОбъект.УстановитьПометкуУдаления(Истина);
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти

#Область ЗаписьФайлов

&НаКлиенте
Процедура ВыбратьИСохранитьФайлыНаКлиенте(Знач ПолучаемыеФайлы, Знач ПараметрыДиалога = Неопределено) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПолучаемыеФайлы", ПолучаемыеФайлы);
	
	ОповещениеПодключенияРасширенияРаботыСФайлами = Новый ОписаниеОповещения(
		"ВыбратьИСохранитьФайлыНаКлиентеПослеПодключенияРасширенияРаботыСФайлами",
		ЭтотОбъект, ДополнительныеПараметры);
	
	НачатьПодключениеРасширенияРаботыСФайлами(ОповещениеПодключенияРасширенияРаботыСФайлами);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИСохранитьФайлыНаКлиентеПослеПодключенияРасширенияРаботыСФайлами(Подключено, ДополнительныеПараметры) Экспорт
	
	ДиалогВыбораКаталога = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ДиалогВыбораКаталога.Заголовок = НСтр("ru = 'Укажите каталог'");
	
	Если НЕ Подключено Тогда
		
		Попытка
			Для Каждого ПолучаемыйФайл ИЗ ДополнительныеПараметры.ПолучаемыеФайлы Цикл
				ПолучитьФайл(ПолучаемыйФайл.Хранение, ПолучаемыйФайл.Имя, Истина);
			КонецЦикла;
			
			ТекстСообщения = НСтр("ru = 'Файлы успешно выгружены'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			
		Исключение
			ШаблонСообщения = НСтр("ru = 'При записи файлов возникла ошибка
			|%1'");
			
			ТекстСообщения = СтрШаблон(ШаблонСообщения, КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			
		КонецПопытки;
		
		Возврат;
		
	КонецЕсли;
	
	ОписаниеОповещенияПолученияФайлов = Новый ОписаниеОповещения(
		"ВыбратьИСохранитьФайлНаКлиентеПослеПолученияФайлов",
		ЭтотОбъект, ДополнительныеПараметры);
	
	НачатьПолучениеФайлов(ОписаниеОповещенияПолученияФайлов, ДополнительныеПараметры.ПолучаемыеФайлы, ДиалогВыбораКаталога, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИСохранитьФайлНаКлиентеПослеПолученияФайлов(ПолученныеФайлы, ДополнительныеПараметры) Экспорт
	
	ТекстСообщения = НСтр("ru = 'Файлы успешно выгружены'");
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
