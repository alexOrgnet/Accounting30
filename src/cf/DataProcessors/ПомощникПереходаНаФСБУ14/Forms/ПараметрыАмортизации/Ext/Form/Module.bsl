
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИзменениеНесколькихСтрок = Параметры.ИзменениеНесколькихСтрок;

	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.ПараметрыАмортизации);
	// Тумблер реквизита типа Булево слева отображает значение Ложь, что соответствует значению "Неопределенный срок использования".
	// Чтобы слева отображалось значение, соответствующее "Срок использования можно определить", используем реквизит типа Булево
	// с инвертированным значением.
	СрокПолезногоИспользованияМожноОпределить = Не НеопределенныйСрокПолезногоИспользования;
	
	Если СрокПолезногоИспользованияМожноОпределить
		И Не ЗначениеЗаполнено(СпособНачисленияАмортизации)
		И Не ТолькоПросмотр
		И КлючНазначенияИспользования = "ЭлементыАмортизации" Тогда
		СпособНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.Линейный;
		Модифицированность = Истина;
	КонецЕсли;
	
	ПараметрыАмортизации = Параметры.ПараметрыАмортизации;
	
	НастроитьФорму();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	Если Не ПеренестиВДокумент Тогда
		Возврат;
	КонецЕсли;
	
	РезультатЗакрытия = Новый Структура;
	РезультатЗакрытия.Вставить("ИзменениеНесколькихСтрок", ИзменениеНесколькихСтрок);
	РезультатЗакрытия.Вставить("ПараметрыАмортизации", ПараметрыАмортизации);
	ОповеститьОВыборе(РезультатЗакрытия);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СпособНачисленияАмортизацииПриИзменении(Элемент)
	УстановитьВидимостьЭлементовАмортизации(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура СрокПолезногоИспользованияМожноОпределитьПриИзменении(Элемент)
	НеопределенныйСрокПолезногоИспользования = Не СрокПолезногоИспользованияМожноОпределить;
	УстановитьВидимостьЭлементовАмортизации(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	Если Модифицированность Или ИзменениеНесколькихСтрок Тогда
		ПеренестиВДокумент = Истина;
		ЗаполнитьЗначенияСвойств(ПараметрыАмортизации, ЭтотОбъект);
	КонецЕсли;
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьФорму()
	
	Если КлючНазначенияИспользования = "ЭлементыАмортизации" Тогда
		Заголовок = НСтр("ru = 'Элементы амортизации'");
		УстановитьВидимостьЭлементовАмортизации(ЭтотОбъект);
		Элементы.ПараметрыНачисленияАмортизации.Видимость = Ложь;
	Иначе
		Заголовок = НСтр("ru = 'Счета амортизации'");
		Элементы.ЭлементыАмортизации.Видимость = Ложь;
		НастроитьВыборСчетовАмортизации();
	КонецЕсли;
	
	УстановитьВидимостьКоманд();

КонецПроцедуры

&НаСервере
Процедура НастроитьВыборСчетовАмортизации()

	СчетаДляВыбора = Новый ФиксированныйМассив(Обработки.ПомощникПереходаНаФСБУ14.СчетаУчетаНМА().СчетаАмортизации);

	НовыйМассивПараметров = Новый Массив;
	НовыйМассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", СчетаДляВыбора));

	Элементы.СчетУчетаАмортизации.ПараметрыВыбора = Новый ФиксированныйМассив(НовыйМассивПараметров);
	
	Если Не ТолькоПросмотр И Не ЗначениеЗаполнено(СчетАмортизации) И СчетаДляВыбора.Количество() > 0 Тогда
		СчетАмортизации = СчетаДляВыбора[0];
		Модифицированность = Истина;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьКоманд()

	Элементы.ФормаОК.Видимость = Не ТолькоПросмотр;
	Элементы.ФормаОтмена.Видимость = Не ТолькоПросмотр;
	Элементы.ФормаЗакрыть.Видимость = ТолькоПросмотр;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьЭлементовАмортизации(Форма)

	Элементы = Форма.Элементы;
	Элементы.СпособНачисленияАмортизации.Видимость = Не Форма.НеопределенныйСрокПолезногоИспользования;
	Элементы.ГруппаЛиквидационнаяСтоимость.Видимость     = Не Форма.НеопределенныйСрокПолезногоИспользования;
	
	Если Форма.НеопределенныйСрокПолезногоИспользования Тогда
		
		Элементы.ГруппаСрокИспользования.Видимость  = Ложь;
		Элементы.Коэффициент.Видимость                 = Ложь;
		Элементы.ГруппаОбъемПродукции.Видимость         = Ложь;
			
	Иначе

		ЛинейныйСпособ = (Форма.СпособНачисленияАмортизации = ПредопределенноеЗначение(
		"Перечисление.СпособыНачисленияАмортизацииНМА.Линейный"));
		ПоОбъемуПродукции = (Форма.СпособНачисленияАмортизации = ПредопределенноеЗначение(
		"Перечисление.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции"));
		УменьшаемыйОстаток = (Форма.СпособНачисленияАмортизации = ПредопределенноеЗначение(
		"Перечисление.СпособыНачисленияАмортизацииНМА.УменьшаемогоОстатка"));

		Элементы.ГруппаСрокИспользования.Видимость = ЛинейныйСпособ Или УменьшаемыйОстаток;
		Элементы.Коэффициент.Видимость = УменьшаемыйОстаток;
		Элементы.ГруппаОбъемПродукции.Видимость = ПоОбъемуПродукции;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти