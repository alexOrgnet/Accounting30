#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Организация = Параметры.Организация;
	ОтчетныйПериод = Параметры.ОтчетныйПериод;

	МаксимальнаяСуммаВзноса = Цел(Параметры.МаксимальнаяСуммаВзноса);
	УменьшеноРанее = Параметры.УменьшеноРанее;
	СуммаВзносаДляУменьшенияПСН = Параметры.ВыбраннаяСуммаВзноса;
	ИспользоватьВсюСумму = Параметры.СуммаВзносаСкорректирована;
	
	ВзносПоЕдиномуТарифу = Параметры.ВзносПоЕдиномуТарифу;
	ВзносСДоходов = Параметры.ВзносСдоходов;
	ДоляДоходовПоПатенту = Параметры.ДоляДоходовПоПатенту;
	ВзносыИспользованныеДляУСН = Параметры.ВзносыИспользованныеДляУСН;
	
	Элементы.СуммаВзносаДляУменьшенияПСН.Доступность = ИспользоватьВсюСумму;
	
	Элементы.ДоляДоходовПоПатенту.Видимость =
		Не УчетнаяПолитика.ПрименяетсяТолькоУСНПатент(Организация, ОтчетныйПериод);
	
	УстановитьТекстПодсказки();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВзносПоЕдиномуТарифуНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОписаниеДействия = ОписаниеДействияОплатаВзносаИП(Организация, КонецГода(ОтчетныйПериод));
	Если ОписаниеДействия <> Неопределено Тогда
		ВыполнениеЗадачБухгалтераКлиент.ВыполнитьДействие(ОписаниеДействия);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВзносСДоходовНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Год(ОтчетныйПериод) = Год(НастройкиУчетаКлиентСервер.ДатаПереходаНаЕдиныйНалоговыйПлатеж()) Тогда
		// Переходный период: взнос с доходов в 2023 г. включаем в базу в 2023 г.
		НалоговыйПериод = КонецГода(ОтчетныйПериод);
		ПериодСобытияРасчетаВзноса = ОтчетныйПериод;
	Иначе
		// Взнос с доходов включается в налоговую базу года, в котором он уплачивается
		НалоговыйПериод = ОтчетныйПериод;
		ПериодСобытияРасчетаВзноса = Неопределено;
	КонецЕсли;
	
	ОписаниеДействия = ОписаниеДействияОплатаВзносаИП(Организация,
		НалоговыйПериод,
		Истина,
		ПериодСобытияРасчетаВзноса);
	
	Если ОписаниеДействия <> Неопределено Тогда
		ВыполнениеЗадачБухгалтераКлиент.ВыполнитьДействие(ОписаниеДействия);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьВсюСуммуПриИзменении(Элемент)
	
	Элементы.СуммаВзносаДляУменьшенияПСН.Доступность = ИспользоватьВсюСумму;
	Если Не ИспользоватьВсюСумму Тогда
		СуммаВзносаДляУменьшенияПСН = МаксимальнаяСуммаВзноса;
		Элементы.ДекорацияОшибка.Видимость = Ложь;
	КонецЕсли;
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ПроверитьВведеннуюСумму();
	Если УказанаНеПравильнаяСумма Тогда
		ЭтотОбъект.ТекущийЭлемент = Элементы.СуммаВзносаДляУменьшенияПСН;
		Возврат;
	КонецЕсли;
	
	ЗаписатьИЗакрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПроверитьВведеннуюСумму()

	Если СуммаВзносаДляУменьшенияПСН > МаксимальнаяСуммаВзноса Тогда
		Элементы.ДекорацияОшибка.Заголовок = НСтр("ru ='Сумма не может быть больше суммы взносов.'");
		УказанаНеПравильнаяСумма = Истина;
	ИначеЕсли СуммаВзносаДляУменьшенияПСН < УменьшеноРанее Тогда
		Элементы.ДекорацияОшибка.Заголовок = НСтр("ru ='Сумма не может быть меньше, чем уменьшено ранее.'");
		УказанаНеПравильнаяСумма = Истина;
	Иначе
		УказанаНеПравильнаяСумма = Ложь;
	КонецЕсли;
	Элементы.ДекорацияОшибка.Видимость = УказанаНеПравильнаяСумма;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТекстПодсказки()
	
	СуммаВзносаРасчетная = Цел((ВзносПоЕдиномуТарифу + ВзносСДоходов) * ДоляДоходовПоПатенту / 100);
	ФорматЧиселВПояснении = НСтр("ru = 'ЧДЦ=0; ЧН=0; ЧГ=3,0'");
	
	Если СуммаВзносаРасчетная > МаксимальнаяСуммаВзноса И ВзносыИспользованныеДляУСН <> 0 Тогда
		ОсновнойТекст = НСтр("ru = 'Всего = По единому тарифу + С доходов - Использовано для УСН'");
		ДополнительныйТекст = СтрШаблон("%1 + %2 - %3",
			Формат(ВзносПоЕдиномуТарифу, ФорматЧиселВПояснении),
			Формат(ВзносСДоходов, ФорматЧиселВПояснении),
			Формат(ВзносыИспользованныеДляУСН, ФорматЧиселВПояснении));
	Иначе
		Если ДоляДоходовПоПатенту < 100 Тогда
			ОсновнойТекст = НСтр("ru = 'Всего = (По единому тарифу + С доходов) * Доля доходов по патенту'");
			ДополнительныйТекст = СтрШаблон("(%1 + %2) * %3%%",
				Формат(ВзносПоЕдиномуТарифу, ФорматЧиселВПояснении),
				Формат(ВзносСДоходов, ФорматЧиселВПояснении),
				Формат(ДоляДоходовПоПатенту, НСтр("ru = 'ЧДЦ=2; ЧН=0; ЧГ=3,0'")));
		Иначе
			ОсновнойТекст = НСтр("ru = 'Всего = По единому тарифу + С доходов'");
			ДополнительныйТекст = СтрШаблон("%1 + %2",
				Формат(ВзносПоЕдиномуТарифу, ФорматЧиселВПояснении),
				Формат(ВзносСДоходов, ФорматЧиселВПояснении));
		КонецЕсли;
	КонецЕсли;
		
	ПодсказкаМаксимум = Новый Массив;
	ПодсказкаМаксимум.Добавить(ОсновнойТекст);
	ПодсказкаМаксимум.Добавить(Символы.ПС);
	ПодсказкаМаксимум.Добавить(ДополнительныйТекст);
	
	Элементы.ПодсказкаРасчетДоли.Заголовок = Новый ФорматированнаяСтрока(ПодсказкаМаксимум);

КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть()

	Если ЗаписатьДанные() Тогда
		
		Если Модифицированность Тогда
			ПараметрыОповещения = Новый Структура;
			ПараметрыОповещения.Вставить("Организация", Организация);
			ПараметрыОповещения.Вставить("Период", ОтчетныйПериод);
				
			Оповестить("Запись_УменьшениеПСННаФиксированныеВзносы", ПараметрыОповещения, ЭтотОбъект);
		КонецЕсли;

		Модифицированность = Ложь;
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗаписатьДанные()
	
	Если Не Модифицированность Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если МаксимальнаяСуммаВзноса <> СуммаВзносаДляУменьшенияПСН Тогда
		МенеджерЗаписи = РегистрыСведений.УменьшениеНалогаНаВзносыПодлежащиеУплате.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Период = НачалоКвартала(КонецГода(ОтчетныйПериод));
		МенеджерЗаписи.Организация = Организация;
		МенеджерЗаписи.Сумма = СуммаВзносаДляУменьшенияПСН;
		МенеджерЗаписи.ДеятельностьНаПатенте = Истина;
		МенеджерЗаписи.Записать(Истина);
	ИначеЕсли Не ИспользоватьВсюСумму Тогда
		ОчиститьЗаписиРегистра();
	Иначе
		Модифицированность = Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервереБезКонтекста
Функция ОписаниеДействияОплатаВзносаИП(Организация, НалоговыйПериод, ВзносСДоходов = Ложь, ПериодСобытия = Неопределено)
	
	ПорядокУплаты = УчетСтраховыхВзносовИППовтИсп.ПорядокУплатыВзноса(Организация, НалоговыйПериод, ВзносСДоходов);
	Если ПорядокУплаты = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ОписаниеКоманды = ВыполнениеЗадачБухгалтераКлиентСервер.НовыеПараметрыКомандЗадачи();
	ЗаполнитьЗначенияСвойств(ОписаниеКоманды, ПорядокУплаты);
	
	Если ВзносСДоходов И ЗначениеЗаполнено(ПериодСобытия) Тогда
		// В 2023 г . взнос с доходов можно заплатить в текущем налоговом периоде.
		// Аванс рассчитывается и оплачивается до 25 числа месяца, следующего за отчетным кварталом,
		// поэтому исключаем ситуацию, когда при расчете взноса с доходов попадут доходы следующего квартала
		ОписаниеКоманды.ПериодСобытия = ПериодСобытия;
		ОписаниеКоманды.Периодичность = Перечисления.Периодичность.Квартал;
		ОписаниеКоманды.Наименование = СтрШаблон(НСтр("ru ='Взносы за себя с доходов за %1'"),
			СтроковыеФункции.ПредставлениеПериодаВТексте(
				НачалоГода(НалоговыйПериод),
				ПериодСобытия));
	КонецЕсли;
	
	Возврат ВыполнениеЗадачБухгалтера.ОписаниеДействия(ОписаниеКоманды);
	
КонецФункции

&НаСервере
Процедура ОчиститьЗаписиРегистра()
	
	СуммаВзносаДляУменьшенияПСН = МаксимальнаяСуммаВзноса;
	
	МенеджерЗаписи = РегистрыСведений.УменьшениеНалогаНаВзносыПодлежащиеУплате.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Период = НачалоКвартала(КонецГода(ОтчетныйПериод));
	МенеджерЗаписи.Организация = Организация;
	МенеджерЗаписи.ДеятельностьНаПатенте = Истина;
	МенеджерЗаписи.Удалить();
	
КонецПроцедуры

#КонецОбласти