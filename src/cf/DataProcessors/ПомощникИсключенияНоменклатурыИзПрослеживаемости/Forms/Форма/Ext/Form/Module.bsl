#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОткрытиеПриЗапускеПрограммы = Параметры.ОткрытиеПриЗапускеПрограммы;
	
	ПростойИнтерфейс = ПолучитьФункциональнуюОпцию("ИнтерфейсТаксиПростой");
	
	Элементы.ПутьКОбработкеПростойИнтерфейс.Видимость = ОткрытиеПриЗапускеПрограммы И ПростойИнтерфейс;
	Элементы.ПутьКОбработке.Видимость = ОткрытиеПриЗапускеПрограммы И НЕ ПростойИнтерфейс;
	
	Если ОткрытиеПриЗапускеПрограммы Тогда
		ХранилищеОбщихНастроек.Сохранить(
		"ПомощникИсключенияНоменклатурыИзПрослеживаемости",
		"ОбработкаУжеВыводилась",
		Истина);
	КонецЕсли;
	
	// Исключаются из прослеживаемости белорусские холодильники по постановлению 345 от 04.03.2023
	// Исключение с 06.03.2023
	// Заполним параметры по умолчанию
	ИсключаемаяСтранаПроисхождения   = Справочники.СтраныМира.НайтиПоКоду("112"); // Беларусь
	ДатаИсключенияИзПрослеживаемости = '20230306';
	
	ТаблицаКодовТНВЭД = Обработки.ПомощникИсключенияНоменклатурыИзПрослеживаемости.ПолучитьСписокКодовТНВЭД();
	СписокКодовТНВЭД.ЗагрузитьЗначения(ТаблицаКодовТНВЭД.ВыгрузитьКолонку("Код"));
	
	Элементы.ГруппаОшибки.Видимость = Ложь;
	ТекПользователь = Пользователи.ТекущийПользователь();
	ОтображатьПодсказки = Истина;
	ВалютаРегУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	Если ИсточникВыбора.ИмяФормы = "Обработка.ПодборНоменклатуры.Форма.Форма" Тогда
		ОбработкаВыбораПодборНаСервереНоменклатура(ВыбранноеЗначение, ИсточникВыбора.ИмяТаблицы);
	ИначеЕсли ИсточникВыбора.ИмяФормы = "Обработка.ПодборОсновныхСредств.Форма.Форма" Тогда
		ОбработкаВыбораПодборНаСервереОС(ВыбранноеЗначение);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНоменклатура

&НаКлиенте
Процедура ТаблицаТоварыНоменклатураПриИзменении(Элемент)
	
	Элементы.Номенклатура.ЗакончитьРедактированиеСтроки(Ложь);
	ТекущиеДанные = Элементы.Номенклатура.ТекущиеДанные;
	ДанныеСтроки = Новый Структура("Номенклатура, КодТНВЭД, СтранаПроисхождения, ЕстьОстатки, ОтмеченКакПрослеживаемый");
	ЗаполнитьЗначенияСвойств(ДанныеСтроки, ТекущиеДанные);
	
	НоменклатураПриИзмененииНаСервере(ДанныеСтроки);
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ДанныеСтроки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОС

&НаКлиенте
Процедура ТаблицаОСОСПриИзменении(Элемент)
	
	Элементы.ОС.ЗакончитьРедактированиеСтроки(Ложь);
	ТекущиеДанные = Элементы.ОС.ТекущиеДанные;
	ДанныеСтроки = Новый Структура("ОсновноеСредство, КодТНВЭД, ГрупповойОбъект, ОтмеченКакПрослеживаемый");
	ЗаполнитьЗначенияСвойств(ДанныеСтроки, ТекущиеДанные);
	
	ОсновноеСредствоПриИзмененииНаСервере(ДанныеСтроки);
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ДанныеСтроки);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗакрытьПанельПодсказок(Команда)
	
	Элементы.ИзменитьВидимостьПодсказки.Картинка = БиблиотекаКартинок.СтрелкаВлево;
	ОтображатьПодсказки = Ложь;
	Элементы.ГруппаПодсказки.Видимость = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВидимостьПодсказки(Команда)
	
	Если ОтображатьПодсказки Тогда
		Элементы.ИзменитьВидимостьПодсказки.Картинка = БиблиотекаКартинок.СтрелкаВлево;
		ОтображатьПодсказки = Ложь;
		Элементы.ГруппаПодсказки.Видимость = Ложь;
	Иначе
		Элементы.ИзменитьВидимостьПодсказки.Картинка = БиблиотекаКартинок.СтрелкаВправо;
		ОтображатьПодсказки = Истина;
		Элементы.ГруппаПодсказки.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНоменклатуру(Команда)
	
	Если Объект.Номенклатура.Количество() > 0 Тогда
		ТекстВопроса = НСтр("ru = 'Перед заполнением таблица ""Номенклатура"" будет очищена. Заполнить?'");
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗаполнениемНоменклатурыЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
	Иначе
		ЗаполнитьНоменклатуруНаСервере(СтруктураПараметровДляЗапроса());
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьОС(Команда)
	
	Если Объект.ОС.Количество() > 0 Тогда
		ТекстВопроса = НСтр("ru = 'Перед заполнением таблица ""Основные средства"" будет очищена. Заполнить?'");
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗаполнениемОСЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
	Иначе
		ЗаполнитьОСНаСервере(СтруктураПараметровДляЗапроса());
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПодборНоменклатуры(Команда)
	
	ПараметрыПодбора = ПолучитьПараметрыПодбора("Номенклатура");
	Если ПараметрыПодбора <> Неопределено Тогда
		ОткрытьФорму("Обработка.ПодборНоменклатуры.Форма.Форма", ПараметрыПодбора,
			ЭтаФорма, УникальныйИдентификатор);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПодборОС(Команда)
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПоказыватьГрупповыеОС", Истина);
	
	ОткрытьФорму("Обработка.ПодборОсновныхСредств.Форма.Форма", ПараметрыФормы, ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ИсключитьТоварыУдалитьОстатки(Команда)
	
	ТекстСообщения = "";
	ВыполнитьНаСервере();
	
	Если ТекстСообщения = "" Тогда
		ТекстСообщения = НСтр("ru='Отсутствуют объекты для исключения из прослеживаемости.'") + Символы.ПС +
			НСтр("ru='Нет выбывших из прослеживаемости объектов, по которым требуется списание остатков по РНПТ'");
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Сообщение", ТекстСообщения);
	ПараметрыФормы.Вставить("Заголовок", НСтр("ru = 'Результат выполнения'"));
	ОткрытьФорму("ОбщаяФорма.ФормаСообщение", ПараметрыФормы);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НоменклатураПриИзмененииНаСервере(ДанныеСтроки)
	
	ДанныеОНоменклатуре = ПолучитьДанныеОНоменклатуре(ДанныеСтроки.Номенклатура);
	Отказ = Ложь;
	Если ДанныеОНоменклатуре.Количество() > 0 Тогда
		СтрокаДанных = ДанныеОНоменклатуре[0];
		Если Не СтрокаДанных.ПрослеживаемыйТовар И Не СтрокаДанных.ПрослеживаемыйКомплект Тогда
			ОбщегоНазначения.СообщитьПользователю(
			Нстр("ru='В качестве номенклатуры может быть указана только прослеживаемая номенклатура.'"));
			Отказ = Истина;
		ИначеЕсли СтрокаДанных.ПрослеживаемыйТовар И СтрокаДанных.КоличествоСтранНаОстатках > 1 Тогда
			ОбщегоНазначения.СообщитьПользователю(
			Нстр("ru='По выбранной номенклатуре имеются остатки по разным странам происхождения. В этом случае номенклатуру нельзя исключить из прослеживаемости.'"));
			Отказ = Истина;
		ИначеЕсли СтрокаДанных.ПрослеживаемыйКомплект Тогда 
			ОбщегоНазначения.СообщитьПользователю(
			Нстр("ru='По комплектам исключение из прослеживаемости будет произведено автоматически при выполнении обработки.'"));
			Отказ = Истина;
		КонецЕсли;
		Если Отказ Тогда
			ДанныеСтроки.Номенклатура = ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка");
			ДанныеСтроки.СтранаПроисхождения = ПредопределенноеЗначение("Справочник.СтраныМира.ПустаяСсылка");
			ДанныеСтроки.КодТНВЭД = ПредопределенноеЗначение("Справочник.КлассификаторТНВЭД.ПустаяСсылка");
			ДанныеСтроки.ЕстьОстатки = Ложь;
			ДанныеСтроки.ОтмеченКакПрослеживаемый = Ложь;
			
		Иначе
			ЗаполнитьЗначенияСвойств(ДанныеСтроки, СтрокаДанных);
			ДанныеСтроки.ОтмеченКакПрослеживаемый = СтрокаДанных.ПрослеживаемыйТовар;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОсновноеСредствоПриИзмененииНаСервере(ДанныеСтроки)
	
	ДанныеОбОС = ПолучитьДанныеОбОС(ДанныеСтроки.ОсновноеСредство);
	Отказ = Ложь;
	Если ДанныеОбОС.Количество() > 0 Тогда
		СтрокаДанных = ДанныеОбОС[0];
		Если Не СтрокаДанных.ПрослеживаемыйТовар Тогда
			ОбщегоНазначения.СообщитьПользователю(
			Нстр("ru='В качестве основного средства может быть выбрано только прослеживаемое основное средство.'"));
			Отказ = Истина;
		ИначеЕсли СтрокаДанных.ПрослеживаемыйТовар И СтрокаДанных.КоличествоСтранНаОстатках > 1 Тогда
			ОбщегоНазначения.СообщитьПользователю(
			Нстр("ru='По выбранному основному средству имеются остатки по разным странам происхождения. В этом случае основное средство нельзя исключить из прослеживаемости.'"));
			Отказ = Истина;
		ИначеЕсли СтрокаДанных.СоставноеОС Тогда
			ОбщегоНазначения.СообщитьПользователю(
			Нстр("ru='По составным основным средствам исключение из прослеживаемости будет произведено автоматически при выполнении обработки.'"));
			Отказ = Истина;
		КонецЕсли;
		Если Отказ Тогда
			ДанныеСтроки.ОсновноеСредство = ПредопределенноеЗначение("Справочник.ОсновныеСредства.ПустаяСсылка");
			ДанныеСтроки.КодТНВЭД = ПредопределенноеЗначение("Справочник.КлассификаторТНВЭД.ПустаяСсылка");
			ДанныеСтроки.ГрупповойОбъект = Ложь;
			ДанныеСтроки.ОтмеченКакПрослеживаемый = Ложь;
			
		Иначе
			ЗаполнитьЗначенияСвойств(ДанныеСтроки, СтрокаДанных);
			ДанныеСтроки.ОтмеченКакПрослеживаемый = СтрокаДанных.ПрослеживаемыйТовар;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция СтруктураПараметровДляЗапроса()
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("Период",              Новый Граница(ДатаИсключенияИзПрослеживаемости -1, ВидГраницы.Включая));
	СтруктураПараметров.Вставить("КодыТНВЭД",           СписокКодовТНВЭД);
	СтруктураПараметров.Вставить("СтранаПроисхождения", ИсключаемаяСтранаПроисхождения);
	СтруктураПараметров.Вставить("ПериодИсключенияИзПрослеживаемости", ДатаИсключенияИзПрослеживаемости);
	
	Возврат СтруктураПараметров;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьНоменклатуруНаСервере(СтруктураПараметров)
	
	Если СтруктураПараметров.СтранаПроисхождения = Справочники.СтраныМира.ПустаяСсылка() Тогда
		Возврат;
	КонецЕсли;
	
	Результат = ПолучитьДанныеДляЗаполненияНоменклатуры(СтруктураПараметров);
	
	// Список номенклатуры, которую необходимо исключить из прослеживаемости
	ТаблицаТоваровДляИсключения = Результат[10].Выгрузить();
	// Список номенклатуры, которая подходит по ТН ВЭД, но не известна страна происхождения
	ТаблицаТоваровБезСтраны = Результат[11].Выгрузить();
	// Список номенклатуры, по которой не получится отключить прослеживаемость
	// Имеются остатки по разным страна происхождения
	ТаблицаНоменклатурыСОшибками = Результат[12].Выгрузить();
	
	Объект.Номенклатура.Загрузить(ТаблицаТоваровБезСтраны);
	Для Каждого Строка Из ТаблицаТоваровДляИсключения Цикл
		НоваяСтрока = Объект.Номенклатура.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
	КонецЦикла;
	
	ЗаполнитьОшибки(ТаблицаНоменклатурыСОшибками);
	
	Элементы.ГруппаОшибки.Видимость = Объект.Ошибки.Количество() > 0;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеДляЗаполненияНоменклатуры(СтруктураПараметров)
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	|	КлассификаторТНВЭД.Ссылка КАК КодТНВЭД
	|ПОМЕСТИТЬ СписокКодовТНВЭД
	|ИЗ
	|	Справочник.КлассификаторТНВЭД КАК КлассификаторТНВЭД
	|ГДЕ
	|	КлассификаторТНВЭД.Код В(&КодыТНВЭД)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Номенклатура,
	|	Номенклатура.ПрослеживаемыйТовар КАК ПрослеживаемыйТовар,
	|	Номенклатура.КодТНВЭД КАК КодТНВЭД,
	|	Номенклатура.СтранаПроисхождения КАК СтранаПроисхождения
	|ПОМЕСТИТЬ СписокНоменклатуры
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.ПометкаУдаления = ЛОЖЬ
	|	И Номенклатура.ПрослеживаемыйТовар = ИСТИНА
	|	И Номенклатура.КодТНВЭД В
	|			(ВЫБРАТЬ
	|				СписокКодовТНВЭД.КодТНВЭД
	|			ИЗ
	|				СписокКодовТНВЭД)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ПрослеживаемыеТоварыОстатки.Комплектующие = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяССылка)
	|			ТОГДА ПрослеживаемыеТоварыОстатки.Номенклатура
	|		ИНАЧЕ ПрослеживаемыеТоварыОстатки.Комплектующие
	|	КОНЕЦ КАК Номенклатура,
	|	ПрослеживаемыеТоварыОстатки.СтранаПроисхождения КАК СтранаПроисхождения
	|ПОМЕСТИТЬ СписокНоменклатурыПоОстаткам
	|ИЗ
	|	РегистрНакопления.ПрослеживаемыеТовары.Остатки(
	|			&Период,
	|			Номенклатура В
	|					(ВЫБРАТЬ
	|						СписокНоменклатуры.Номенклатура
	|					ИЗ
	|						СписокНоменклатуры)
	|				ИЛИ Комплектующие В
	|					(ВЫБРАТЬ
	|						СписокНоменклатуры.Номенклатура
	|					ИЗ
	|						СписокНоменклатуры)) КАК ПрослеживаемыеТоварыОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПрослеживаемыеОсновныеСредстваОстатки.Комплектующие,
	|	ПрослеживаемыеОсновныеСредстваОстатки.СтранаПроисхождения
	|ИЗ
	|	РегистрНакопления.ПрослеживаемыеОсновныеСредства.Остатки(
	|			&Период,
	|			Комплектующие В
	|				(ВЫБРАТЬ
	|					СписокНоменклатуры.Номенклатура
	|				ИЗ
	|					СписокНоменклатуры)) КАК ПрослеживаемыеОсновныеСредстваОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокНоменклатурыПоОстаткам.Номенклатура КАК Номенклатура,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СписокНоменклатурыПоОстаткам.СтранаПроисхождения) КАК СтранаПроисхождения,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА СписокНоменклатурыПоОстаткам.СтранаПроисхождения = &СтранаПроисхождения
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьОстаткиПоСтранеИсключения
	|ПОМЕСТИТЬ ОстаткиПоКоличествуСтран
	|ИЗ
	|	СписокНоменклатурыПоОстаткам КАК СписокНоменклатурыПоОстаткам
	|
	|СГРУППИРОВАТЬ ПО
	|	СписокНоменклатурыПоОстаткам.Номенклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	СписокНоменклатуры.КодТНВЭД КАК КодТНВЭД,
	|	СписокНоменклатуры.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ВЫБОР
	|		КОГДА ОстаткиПоКоличествуСтран.Номенклатура ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьОстатки
	|ПОМЕСТИТЬ СписокНоменклатурыСНаличиемОстаков
	|ИЗ
	|	СписокНоменклатуры КАК СписокНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиПоКоличествуСтран КАК ОстаткиПоКоличествуСтран
	|		ПО СписокНоменклатуры.Номенклатура = ОстаткиПоКоличествуСтран.Номенклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокНоменклатурыПоОстаткам.Номенклатура КАК Номенклатура,
	|	СписокНоменклатурыПоОстаткам.СтранаПроисхождения КАК СтранаПроисхождения,
	|	СписокНоменклатуры.КодТНВЭД КАК КодТНВЭД,
	|	ИСТИНА КАК ЕстьОстатки
	|ПОМЕСТИТЬ НоменклатураДляИсключения
	|ИЗ
	|	СписокНоменклатурыПоОстаткам КАК СписокНоменклатурыПоОстаткам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОстаткиПоКоличествуСтран КАК ОстаткиПоКоличествуСтран
	|		ПО СписокНоменклатурыПоОстаткам.Номенклатура = ОстаткиПоКоличествуСтран.Номенклатура
	|			И (ОстаткиПоКоличествуСтран.СтранаПроисхождения = 1)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокНоменклатуры КАК СписокНоменклатуры
	|		ПО СписокНоменклатурыПоОстаткам.Номенклатура = СписокНоменклатуры.Номенклатура
	|ГДЕ
	|	СписокНоменклатурыПоОстаткам.СтранаПроисхождения = &СтранаПроисхождения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СписокНоменклатурыСНаличиемОстаков.Номенклатура,
	|	СписокНоменклатурыСНаличиемОстаков.СтранаПроисхождения,
	|	СписокНоменклатурыСНаличиемОстаков.КодТНВЭД,
	|	СписокНоменклатурыСНаличиемОстаков.ЕстьОстатки
	|ИЗ
	|	СписокНоменклатурыСНаличиемОстаков КАК СписокНоменклатурыСНаличиемОстаков
	|ГДЕ
	|	СписокНоменклатурыСНаличиемОстаков.ЕстьОстатки = ЛОЖЬ
	|	И СписокНоменклатурыСНаличиемОстаков.СтранаПроисхождения = &СтранаПроисхождения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураИсторияПрослеживаемогоТовара.Ссылка КАК Ссылка,
	|	НоменклатураИсторияПрослеживаемогоТовара.ПрослеживаемыйКомплект КАК ПрослеживаемыйКомплект
	|ПОМЕСТИТЬ Комплекты
	|ИЗ
	|	Справочник.Номенклатура.ИсторияПрослеживаемогоТовара КАК НоменклатураИсторияПрослеживаемогоТовара
	|ГДЕ
	|	НоменклатураИсторияПрослеживаемогоТовара.ПрослеживаемыйКомплект = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Номенклатура.КодТНВЭД КАК КодТНВЭД,
	|	Номенклатура.СтранаПроисхождения КАК СтранаПроисхождения,
	|	НоменклатураИсторияПрослеживаемогоТовара.Ссылка КАК Ссылка,
	|	НоменклатураИсторияПрослеживаемогоТовара.ПрослеживаемыйТовар КАК ПрослеживаемыйТовар,
	|	ЕСТЬNULL(Комплекты.ПрослеживаемыйКомплект, ЛОЖЬ) КАК Комплект
	|ПОМЕСТИТЬ ИсключеннаяНоменклатура
	|ИЗ
	|	Справочник.Номенклатура.ИсторияПрослеживаемогоТовара КАК НоменклатураИсторияПрослеживаемогоТовара
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
	|			ЛЕВОЕ СОЕДИНЕНИЕ Комплекты КАК Комплекты
	|			ПО Номенклатура.Ссылка = Комплекты.Ссылка
	|		ПО НоменклатураИсторияПрослеживаемогоТовара.Ссылка = Номенклатура.Ссылка
	|ГДЕ
	|	Номенклатура.ПометкаУдаления = ЛОЖЬ
	|	И НоменклатураИсторияПрослеживаемогоТовара.Период = &ПериодИсключенияИзПрослеживаемости
	|	И НоменклатураИсторияПрослеживаемогоТовара.ПрослеживаемыйТовар = ЛОЖЬ
	|	И НоменклатураИсторияПрослеживаемогоТовара.ПрослеживаемыйКомплект = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрослеживаемыеТоварыОстатки.Номенклатура КАК Номенклатура,
	|	ПрослеживаемыеТоварыОстатки.СтранаПроисхождения КАК СтранаПроисхождения
	|ПОМЕСТИТЬ ОстаткиПоИсключеннойНоменклатуре
	|ИЗ
	|	РегистрНакопления.ПрослеживаемыеТовары.Остатки(
	|			&Период,
	|			Номенклатура В
	|				(ВЫБРАТЬ
	|					ИсключеннаяНоменклатура.Ссылка
	|				ИЗ
	|					ИсключеннаяНоменклатура
	|				ГДЕ
	|					ИсключеннаяНоменклатура.Комплект = ЛОЖЬ)) КАК ПрослеживаемыеТоварыОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПрослеживаемыеТоварыОстатки.Номенклатура,
	|	ПрослеживаемыеТоварыОстатки.СтранаПроисхождения
	|ИЗ
	|	РегистрНакопления.ПрослеживаемыеТовары.Остатки(
	|			&Период,
	|			Комплектующие В
	|				(ВЫБРАТЬ
	|					ИсключеннаяНоменклатура.Ссылка
	|				ИЗ
	|					ИсключеннаяНоменклатура
	|				ГДЕ
	|					ИсключеннаяНоменклатура.Комплект = ЛОЖЬ)) КАК ПрослеживаемыеТоварыОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиПоИсключеннойНоменклатуре.Номенклатура КАК Номенклатура,
	|	МАКСИМУМ(ОстаткиПоИсключеннойНоменклатуре.СтранаПроисхождения) КАК СтранаПроисхождения
	|ПОМЕСТИТЬ ОстаткиПоИсключеннойНоменклатуреСводно
	|ИЗ
	|	ОстаткиПоИсключеннойНоменклатуре КАК ОстаткиПоИсключеннойНоменклатуре
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиПоИсключеннойНоменклатуре.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокНоменклатурыСНаличиемОстаков.Номенклатура КАК Номенклатура,
	|	СписокНоменклатурыСНаличиемОстаков.КодТНВЭД КАК КодТНВЭД,
	|	СписокНоменклатурыСНаличиемОстаков.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ИСТИНА КАК ОтмеченКакПрослеживаемый
	|ИЗ
	|	СписокНоменклатурыСНаличиемОстаков КАК СписокНоменклатурыСНаличиемОстаков
	|ГДЕ
	|	СписокНоменклатурыСНаличиемОстаков.ЕстьОстатки = ЛОЖЬ
	|	И СписокНоменклатурыСНаличиемОстаков.СтранаПроисхождения = ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НоменклатураДляИсключения.Номенклатура КАК Номенклатура,
	|	НоменклатураДляИсключения.СтранаПроисхождения КАК СтранаПроисхождения,
	|	НоменклатураДляИсключения.КодТНВЭД КАК КодТНВЭД,
	|	ИСТИНА КАК ОтмеченКакПрослеживаемый,
	|	МАКСИМУМ(НоменклатураДляИсключения.ЕстьОстатки) КАК ЕстьОстатки
	|ИЗ
	|	НоменклатураДляИсключения КАК НоменклатураДляИсключения
	|
	|СГРУППИРОВАТЬ ПО
	|	НоменклатураДляИсключения.Номенклатура,
	|	НоменклатураДляИсключения.СтранаПроисхождения,
	|	НоменклатураДляИсключения.КодТНВЭД
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИсключеннаяНоменклатура.Ссылка,
	|	ЕСТЬNULL(ОстаткиПоИсключеннойНоменклатуреСводно.СтранаПроисхождения, ИсключеннаяНоменклатура.СтранаПроисхождения),
	|	ИсключеннаяНоменклатура.КодТНВЭД,
	|	ЛОЖЬ,
	|	ВЫБОР
	|		КОГДА ОстаткиПоИсключеннойНоменклатуреСводно.Номенклатура ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|ИЗ
	|	ИсключеннаяНоменклатура КАК ИсключеннаяНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиПоИсключеннойНоменклатуреСводно КАК ОстаткиПоИсключеннойНоменклатуреСводно
	|		ПО ИсключеннаяНоменклатура.Ссылка = ОстаткиПоИсключеннойНоменклатуреСводно.Номенклатура
	|ГДЕ
	|	ИсключеннаяНоменклатура.Комплект = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиПоКоличествуСтран.Номенклатура КАК Номенклатура,
	|	СписокНоменклатуры.КодТНВЭД КАК КодТНВЭД
	|ИЗ
	|	ОстаткиПоКоличествуСтран КАК ОстаткиПоКоличествуСтран
	|		ЛЕВОЕ СОЕДИНЕНИЕ СписокНоменклатуры КАК СписокНоменклатуры
	|		ПО ОстаткиПоКоличествуСтран.Номенклатура = СписокНоменклатуры.Номенклатура
	|ГДЕ
	|	ОстаткиПоКоличествуСтран.ЕстьОстаткиПоСтранеИсключения = ИСТИНА
	|	И ОстаткиПоКоличествуСтран.СтранаПроисхождения > 1";
	
	ОписаниеПараметровЗапроса = Запрос.НайтиПараметры();
	Для каждого Параметр Из ОписаниеПараметровЗапроса Цикл
		Запрос.УстановитьПараметр(Параметр.Имя, СтруктураПараметров[Параметр.Имя]);
	КонецЦикла;
	
	Результат = Запрос.ВыполнитьПакет();
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьОСНаСервере(СтруктураПараметров)
	
	Если СтруктураПараметров.СтранаПроисхождения = Справочники.СтраныМира.ПустаяСсылка() Тогда
		Возврат;
	КонецЕсли;

	Результат = ПолучитьДанныеДляЗаполненияОС(СтруктураПараметров);
	
	// Список ОС, которую необходимо исключить из прослеживаемости
	ТаблицаОСДляИсключения = Результат[10].Выгрузить();
	// Список групповых ОС, по которой не получится отключить прослеживаемость
	// Имеются остатки по разным страна происхождения
	ТаблицаОССОшибками = Результат[11].Выгрузить();
	
	Объект.ОС.Загрузить(ТаблицаОСДляИсключения);
	ЗаполнитьОшибки(ТаблицаОССОшибками);
	
	Элементы.ГруппаОшибки.Видимость = Объект.Ошибки.Количество() > 0;

КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеДляЗаполненияОС(СтруктураПараметров)
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	|	КлассификаторТНВЭД.Ссылка КАК КодТНВЭД
	|ПОМЕСТИТЬ СписокКодовТНВЭД
	|ИЗ
	|	Справочник.КлассификаторТНВЭД КАК КлассификаторТНВЭД
	|ГДЕ
	|	КлассификаторТНВЭД.Код В(&КодыТНВЭД)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОсновныеСредства.Ссылка КАК ОсновноеСредство,
	|	ОсновныеСредства.КодТНВЭД КАК КодТНВЭД,
	|	ВЫБОР
	|		КОГДА ОсновныеСредства.ЕдиницаУчета = ЗНАЧЕНИЕ(Перечисление.ЕдиницыУчетаОС.ГрупповойОбъект)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ГрупповойОбъект
	|ПОМЕСТИТЬ СписокОСПоТНВЭД
	|ИЗ
	|	Справочник.ОсновныеСредства КАК ОсновныеСредства
	|ГДЕ
	|	ОсновныеСредства.ПометкаУдаления = ЛОЖЬ
	|	И ОсновныеСредства.ПрослеживаемыйТовар = ИСТИНА
	|	И ОсновныеСредства.КодТНВЭД В
	|			(ВЫБРАТЬ
	|				СписокКодовТНВЭД.КодТНВЭД
	|			ИЗ
	|				СписокКодовТНВЭД)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрослеживаемыеОсновныеСредстваОстатки.ОсновноеСредство КАК ОсновноеСредство,
	|	ПрослеживаемыеОсновныеСредстваОстатки.СтранаПроисхождения КАК СтранаПроисхождения
	|ПОМЕСТИТЬ СписокОСПоОстаткамПоСтранам
	|ИЗ
	|	РегистрНакопления.ПрослеживаемыеОсновныеСредства.Остатки(
	|			&Период,
	|			ОсновноеСредство В
	|					(ВЫБРАТЬ
	|						СписокОСПоТНВЭД.ОсновноеСредство
	|					ИЗ
	|						СписокОСПоТНВЭД)
	|				И Комплектующие = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК ПрослеживаемыеОсновныеСредстваОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокОСПоОстаткамПоСтранам.ОсновноеСредство КАК ОсновноеСредство,
	|	СписокОСПоОстаткамПоСтранам.СтранаПроисхождения КАК СтранаПроисхождения,
	|	СписокОСПоТНВЭД.КодТНВЭД КАК КодТНВЭД,
	|	СписокОСПоТНВЭД.ГрупповойОбъект КАК ГрупповойОбъект
	|ПОМЕСТИТЬ СписокОССовсемиРеквизитами
	|ИЗ
	|	СписокОСПоОстаткамПоСтранам КАК СписокОСПоОстаткамПоСтранам
	|		ЛЕВОЕ СОЕДИНЕНИЕ СписокОСПоТНВЭД КАК СписокОСПоТНВЭД
	|		ПО СписокОСПоОстаткамПоСтранам.ОсновноеСредство = СписокОСПоТНВЭД.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокОССовсемиРеквизитами.ОсновноеСредство КАК ОсновноеСредство,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СписокОССовсемиРеквизитами.СтранаПроисхождения) КАК СтранаПроисхождения,
	|	СписокОССовсемиРеквизитами.КодТНВЭД КАК КодТНВЭД,
	|	СписокОССовсемиРеквизитами.ГрупповойОбъект КАК ГрупповойОбъект
	|ПОМЕСТИТЬ ГрупповыеОСПоКоличествуСтран
	|ИЗ
	|	СписокОССовсемиРеквизитами КАК СписокОССовсемиРеквизитами
	|ГДЕ
	|	СписокОССовсемиРеквизитами.ГрупповойОбъект = ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	СписокОССовсемиРеквизитами.ОсновноеСредство,
	|	СписокОССовсемиРеквизитами.КодТНВЭД,
	|	СписокОССовсемиРеквизитами.ГрупповойОбъект
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрупповыеОСПоКоличествуСтран.ОсновноеСредство КАК ОсновноеСредство,
	|	ГрупповыеОСПоКоличествуСтран.КодТНВЭД КАК КодТНВЭД,
	|	ГрупповыеОСПоКоличествуСтран.ГрупповойОбъект КАК ГрупповойОбъект,
	|	СписокОССовсемиРеквизитами.СтранаПроисхождения КАК СтранаПроисхождения
	|ПОМЕСТИТЬ ГрупповыеОбъектыПоКоторымМожноОтключитьПрослеживаемость
	|ИЗ
	|	ГрупповыеОСПоКоличествуСтран КАК ГрупповыеОСПоКоличествуСтран
	|		ЛЕВОЕ СОЕДИНЕНИЕ СписокОССовсемиРеквизитами КАК СписокОССовсемиРеквизитами
	|		ПО ГрупповыеОСПоКоличествуСтран.ОсновноеСредство = СписокОССовсемиРеквизитами.ОсновноеСредство
	|ГДЕ
	|	ГрупповыеОСПоКоличествуСтран.СтранаПроисхождения < 2
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрупповыеОСПоКоличествуСтран.ОсновноеСредство КАК ОсновноеСредство,
	|	ГрупповыеОСПоКоличествуСтран.КодТНВЭД КАК КодТНВЭД,
	|	ГрупповыеОСПоКоличествуСтран.ГрупповойОбъект КАК ГрупповойОбъект,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА СписокОССовсемиРеквизитами.СтранаПроисхождения = &СтранаПроисхождения
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьИсключаемаяСтрана
	|ПОМЕСТИТЬ ГрупповыеОСПоРазнымСтранамСПроверкойПоИсключаемой
	|ИЗ
	|	ГрупповыеОСПоКоличествуСтран КАК ГрупповыеОСПоКоличествуСтран
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокОССовсемиРеквизитами КАК СписокОССовсемиРеквизитами
	|		ПО ГрупповыеОСПоКоличествуСтран.ОсновноеСредство = СписокОССовсемиРеквизитами.ОсновноеСредство
	|ГДЕ
	|	ГрупповыеОСПоКоличествуСтран.СтранаПроисхождения > 1
	|
	|СГРУППИРОВАТЬ ПО
	|	ГрупповыеОСПоКоличествуСтран.ОсновноеСредство,
	|	ГрупповыеОСПоКоличествуСтран.КодТНВЭД,
	|	ГрупповыеОСПоКоличествуСтран.ГрупповойОбъект
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОсновныеСредстваИсторияПрослеживаемогоТовара.Ссылка КАК Ссылка,
	|	ОсновныеСредства.КодТНВЭД КАК КодТНВЭД,
	|	ВЫБОР
	|		КОГДА ОсновныеСредства.ЕдиницаУчета = ЗНАЧЕНИЕ(Перечисление.ЕдиницыУчетаОС.ГрупповойОбъект)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ГрупповойОбъект
	|ПОМЕСТИТЬ СписокВыбывшихОС
	|ИЗ
	|	Справочник.ОсновныеСредства.ИсторияПрослеживаемогоТовара КАК ОсновныеСредстваИсторияПрослеживаемогоТовара
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОсновныеСредства КАК ОсновныеСредства
	|		ПО ОсновныеСредстваИсторияПрослеживаемогоТовара.Ссылка = ОсновныеСредства.Ссылка
	|ГДЕ
	|	ОсновныеСредстваИсторияПрослеживаемогоТовара.Период = &ПериодИсключенияИзПрослеживаемости
	|	И ОсновныеСредстваИсторияПрослеживаемогоТовара.ПрослеживаемыйТовар = ЛОЖЬ
	|	И ОсновныеСредства.ПометкаУдаления = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрослеживаемыеОсновныеСредстваОбороты.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ СписокВыбывшихСоставныхОС
	|ИЗ
	|	РегистрНакопления.ПрослеживаемыеОсновныеСредства.Обороты(
	|			,
	|			,
	|			Период,
	|			ОсновноеСредство В
	|					(ВЫБРАТЬ
	|						СписокВыбывшихОС.Ссылка
	|					ИЗ
	|						СписокВыбывшихОС
	|					ГДЕ
	|						СписокВыбывшихОС.ГрупповойОбъект = ЛОЖЬ)
	|				И Комплектующие <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК ПрослеживаемыеОсновныеСредстваОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокВыбывшихОС.Ссылка КАК Ссылка,
	|	СписокВыбывшихОС.КодТНВЭД КАК КодТНВЭД,
	|	СписокВыбывшихОС.ГрупповойОбъект КАК ГрупповойОбъект,
	|	ВЫБОР
	|		КОГДА СписокВыбывшихСоставныхОС.ОсновноеСредство ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК СоставноеОС
	|ПОМЕСТИТЬ ВыбывшиеОССПризнакомСоставноеОС
	|ИЗ
	|	СписокВыбывшихОС КАК СписокВыбывшихОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ СписокВыбывшихСоставныхОС КАК СписокВыбывшихСоставныхОС
	|		ПО СписокВыбывшихОС.Ссылка = СписокВыбывшихСоставныхОС.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокОССовсемиРеквизитами.ОсновноеСредство КАК ОсновноеСредство,
	|	СписокОССовсемиРеквизитами.КодТНВЭД КАК КодТНВЭД,
	|	СписокОССовсемиРеквизитами.ГрупповойОбъект КАК ГрупповойОбъект,
	|	ИСТИНА КАК ОтмеченКакПрослеживаемый
	|ИЗ
	|	СписокОССовсемиРеквизитами КАК СписокОССовсемиРеквизитами
	|ГДЕ
	|	СписокОССовсемиРеквизитами.СтранаПроисхождения = &СтранаПроисхождения
	|	И СписокОССовсемиРеквизитами.ГрупповойОбъект = ЛОЖЬ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ГрупповыеОбъектыПоКоторымМожноОтключитьПрослеживаемость.ОсновноеСредство,
	|	ГрупповыеОбъектыПоКоторымМожноОтключитьПрослеживаемость.КодТНВЭД,
	|	ГрупповыеОбъектыПоКоторымМожноОтключитьПрослеживаемость.ГрупповойОбъект,
	|	ИСТИНА
	|ИЗ
	|	ГрупповыеОбъектыПоКоторымМожноОтключитьПрослеживаемость КАК ГрупповыеОбъектыПоКоторымМожноОтключитьПрослеживаемость
	|ГДЕ
	|	ГрупповыеОбъектыПоКоторымМожноОтключитьПрослеживаемость.СтранаПроисхождения = &СтранаПроисхождения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВыбывшиеОССПризнакомСоставноеОС.Ссылка,
	|	ВыбывшиеОССПризнакомСоставноеОС.КодТНВЭД,
	|	ВыбывшиеОССПризнакомСоставноеОС.ГрупповойОбъект,
	|	ЛОЖЬ
	|ИЗ
	|	ВыбывшиеОССПризнакомСоставноеОС КАК ВыбывшиеОССПризнакомСоставноеОС
	|ГДЕ
	|	ВыбывшиеОССПризнакомСоставноеОС.СоставноеОС = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрупповыеОСПоРазнымСтранамСПроверкойПоИсключаемой.ОсновноеСредство КАК Номенклатура,
	|	ГрупповыеОСПоРазнымСтранамСПроверкойПоИсключаемой.КодТНВЭД КАК КодТНВЭД,
	|	ГрупповыеОСПоРазнымСтранамСПроверкойПоИсключаемой.ГрупповойОбъект КАК ГрупповойОбъект
	|ИЗ
	|	ГрупповыеОСПоРазнымСтранамСПроверкойПоИсключаемой КАК ГрупповыеОСПоРазнымСтранамСПроверкойПоИсключаемой
	|ГДЕ
	|	ГрупповыеОСПоРазнымСтранамСПроверкойПоИсключаемой.ЕстьИсключаемаяСтрана = ИСТИНА";
	
	ОписаниеПараметровЗапроса = Запрос.НайтиПараметры();
	Для каждого Параметр Из ОписаниеПараметровЗапроса Цикл
		Запрос.УстановитьПараметр(Параметр.Имя, СтруктураПараметров[Параметр.Имя]);
	КонецЦикла;
	
	Результат = Запрос.ВыполнитьПакет();
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ВопросПередЗаполнениемНоменклатурыЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		
		Объект.Номенклатура.Очистить();
		ЗаполнитьНоменклатуруНаСервере(СтруктураПараметровДляЗапроса());
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗаполнениемОСЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		
		Объект.ОС.Очистить();
		ЗаполнитьОСНаСервере(СтруктураПараметровДляЗапроса());
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ДанныеДляИсключенияНоменклатурыИУдаленияОстатков()
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаНоменклатуры.Номенклатура КАК Номенклатура,
	|	ТаблицаНоменклатуры.ОтмеченКакПрослеживаемый КАК ОтмеченКакПрослеживаемый
	|ПОМЕСТИТЬ СписокНоменклатуры
	|ИЗ
	|	&ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|ГДЕ
	|	ТаблицаНоменклатуры.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокНоменклатуры.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ СписокНоменклатурыСУжеОтключеннойИсторией
	|ИЗ
	|	СписокНоменклатуры КАК СписокНоменклатуры
	|ГДЕ
	|	СписокНоменклатуры.ОтмеченКакПрослеживаемый = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрослеживаемыеТоварыОстатки.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ СписокКомплектов
	|ИЗ
	|	РегистрНакопления.ПрослеживаемыеТовары.Остатки(
	|			&Период,
	|			Комплектующие В
	|				(ВЫБРАТЬ
	|					СписокНоменклатуры.Номенклатура
	|				ИЗ
	|					СписокНоменклатуры)) КАК ПрослеживаемыеТоварыОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрослеживаемыеТоварыОстатки.Номенклатура КАК Номенклатура,
	|	МИНИМУМ(ВЫБОР
	|			КОГДА СписокНоменклатуры.Номенклатура = ПрослеживаемыеТоварыОстатки.Комплектующие
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК КомплектующийИсключается
	|ПОМЕСТИТЬ СписокКомплкектовПоКомплектующим
	|ИЗ
	|	РегистрНакопления.ПрослеживаемыеТовары.Остатки(
	|			&Период,
	|			Номенклатура В
	|				(ВЫБРАТЬ
	|					СписокКомплектов.Номенклатура
	|				ИЗ
	|					СписокКомплектов)) КАК ПрослеживаемыеТоварыОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ СписокНоменклатуры КАК СписокНоменклатуры
	|		ПО ПрослеживаемыеТоварыОстатки.Комплектующие = СписокНоменклатуры.Номенклатура
	|
	|СГРУППИРОВАТЬ ПО
	|	ПрослеживаемыеТоварыОстатки.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрослеживаемыеОсновныеСредстваОстатки.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ СписокСоставныхОС
	|ИЗ
	|	РегистрНакопления.ПрослеживаемыеОсновныеСредства.Остатки(
	|			&Период,
	|			Комплектующие В
	|				(ВЫБРАТЬ
	|					СписокНоменклатуры.Номенклатура
	|				ИЗ
	|					СписокНоменклатуры)) КАК ПрослеживаемыеОсновныеСредстваОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрослеживаемыеОсновныеСредстваОстатки.ОсновноеСредство КАК ОсновноеСредство,
	|	МИНИМУМ(ВЫБОР
	|			КОГДА ПрослеживаемыеОсновныеСредстваОстатки.Комплектующие = СписокНоменклатуры.Номенклатура
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК КомплектующийИсключается
	|ПОМЕСТИТЬ СписокСоставныхОСПоКомплектующим
	|ИЗ
	|	РегистрНакопления.ПрослеживаемыеОсновныеСредства.Остатки(
	|			&Период,
	|			ОсновноеСредство В
	|				(ВЫБРАТЬ
	|					СписокСоставныхОС.ОсновноеСредство
	|				ИЗ
	|					СписокСоставныхОС)) КАК ПрослеживаемыеОсновныеСредстваОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ СписокНоменклатуры КАК СписокНоменклатуры
	|		ПО ПрослеживаемыеОсновныеСредстваОстатки.Комплектующие = СписокНоменклатуры.Номенклатура
	|
	|СГРУППИРОВАТЬ ПО
	|	ПрослеживаемыеОсновныеСредстваОстатки.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	СправочникНоменклатура.ПрослеживаемыйТовар КАК ПрослеживаемыйТовар
	|ПОМЕСТИТЬ СписокНоменклатурыСПроверкойПризнакаПрослеживаемости
	|ИЗ
	|	СписокНоменклатуры КАК СписокНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО СписокНоменклатуры.Номенклатура = СправочникНоменклатура.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокНоменклатурыСПроверкойПризнакаПрослеживаемости.Номенклатура КАК Номенклатура
	|ИЗ
	|	СписокНоменклатурыСПроверкойПризнакаПрослеживаемости КАК СписокНоменклатурыСПроверкойПризнакаПрослеживаемости
	|ГДЕ
	|	СписокНоменклатурыСПроверкойПризнакаПрослеживаемости.ПрослеживаемыйТовар = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрослеживаемыеТоварыОстатки.Организация КАК Организация,
	|	ПрослеживаемыеТоварыОстатки.Номенклатура КАК Номенклатура,
	|	ПрослеживаемыеТоварыОстатки.ВидЗапасов КАК ВидЗапасов,
	|	ПрослеживаемыеТоварыОстатки.Комиссионер КАК Комиссионер,
	|	ПрослеживаемыеТоварыОстатки.Комитент КАК Комитент,
	|	ПрослеживаемыеТоварыОстатки.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ПрослеживаемыеТоварыОстатки.РНПТ КАК РНПТ,
	|	ПрослеживаемыеТоварыОстатки.Комплектующие КАК Комплектующие,
	|	ПрослеживаемыеТоварыОстатки.КоличествоОстаток КАК Количество,
	|	ПрослеживаемыеТоварыОстатки.КоличествоПрослеживаемостиОстаток КАК КоличествоПрослеживаемости
	|ИЗ
	|	РегистрНакопления.ПрослеживаемыеТовары.Остатки(
	|			&Период,
	|			Номенклатура В
	|					(ВЫБРАТЬ
	|						СписокНоменклатуры.Номенклатура
	|					ИЗ
	|						СписокНоменклатуры)
	|				ИЛИ Комплектующие В
	|					(ВЫБРАТЬ
	|						СписокНоменклатуры.Номенклатура
	|					ИЗ
	|						СписокНоменклатуры)) КАК ПрослеживаемыеТоварыОстатки
	|ИТОГИ ПО
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрослеживаемыеОсновныеСредстваОстатки.Организация КАК Организация,
	|	ПрослеживаемыеОсновныеСредстваОстатки.РНПТ КАК РНПТ,
	|	ПрослеживаемыеОсновныеСредстваОстатки.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ПрослеживаемыеОсновныеСредстваОстатки.ОсновноеСредство КАК ОсновноеСредство,
	|	ПрослеживаемыеОсновныеСредстваОстатки.Комплектующие КАК Комплектующие,
	|	ПрослеживаемыеОсновныеСредстваОстатки.КоличествоОстаток КАК Количество,
	|	ПрослеживаемыеОсновныеСредстваОстатки.КоличествоПрослеживаемостиОстаток КАК КоличествоПрослеживаемости
	|ИЗ
	|	РегистрНакопления.ПрослеживаемыеОсновныеСредства.Остатки(
	|			&Период,
	|			Комплектующие В
	|				(ВЫБРАТЬ
	|					СписокНоменклатуры.Номенклатура
	|				ИЗ
	|					СписокНоменклатуры)) КАК ПрослеживаемыеОсновныеСредстваОстатки
	|ИТОГИ ПО
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокКомплкектовПоКомплектующим.Номенклатура КАК Номенклатура
	|ИЗ
	|	СписокКомплкектовПоКомплектующим КАК СписокКомплкектовПоКомплектующим
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО СписокКомплкектовПоКомплектующим.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	СписокКомплкектовПоКомплектующим.КомплектующийИсключается = ИСТИНА
	|	И СписокКомплкектовПоКомплектующим.Номенклатура.ПрослеживаемыйКомплект = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокСоставныхОСПоКомплектующим.ОсновноеСредство КАК ОсновноеСредство
	|ИЗ
	|	СписокСоставныхОСПоКомплектующим КАК СписокСоставныхОСПоКомплектующим
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОсновныеСредства КАК ОсновныеСредства
	|		ПО СписокСоставныхОСПоКомплектующим.ОсновноеСредство = ОсновныеСредства.Ссылка
	|ГДЕ
	|	СписокСоставныхОСПоКомплектующим.КомплектующийИсключается = ИСТИНА
	|	И ОсновныеСредства.ПрослеживаемыйТовар = ИСТИНА";
	
	Запрос.УстановитьПараметр("ТаблицаНоменклатуры", Объект.Номенклатура.Выгрузить());
	Запрос.УстановитьПараметр("Период", Новый Граница(ДатаИсключенияИзПрослеживаемости - 1, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодОтключенияПрослеживаемости", ДатаИсключенияИзПрослеживаемости);
	
	МассивРезультата = Запрос.ВыполнитьПакет();
	
	СтруктураТаблиц = Новый Структура;
	СтруктураТаблиц.Вставить("СписокНоменклатуры",          МассивРезультата[7].Выгрузить());
	СтруктураТаблиц.Вставить("ОстаткиПрослеживаемыеТовары", МассивРезультата[8].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам));
	СтруктураТаблиц.Вставить("ОстаткиПрослеживаемыеОС",     МассивРезультата[9].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам));
	СтруктураТаблиц.Вставить("СписокИсключаемыхКомплектов", МассивРезультата[10].Выгрузить());
	СтруктураТаблиц.Вставить("СписокИсключаемыхОС",         МассивРезультата[11].Выгрузить());
	
	Возврат СтруктураТаблиц;

КонецФункции

&НаСервере
Процедура ВыполнитьНаСервере()
	
	// Сначала обработаем номенклатуре, комплекты, составные ОС
	СтруктураТаблицПоНоменклатуре = ДанныеДляИсключенияНоменклатурыИУдаленияОстатков();
	
	СтруктураПараметровДляОперацииБух = Новый Структура;
	СтруктураПараметровДляОперацииБух.Вставить("Содержание",
	НСтр("ru='Удаление остатков по РНПТ для исключенных из прослеживаемости белорусских холодильников'"));
	СтруктураПараметровДляОперацииБух.Вставить("Комментарий",
	НСтр("ru='#Документ создан автоматически при исключении белорусских холодильников из прослеживаемости'"));
		
	// 1. Сначала исключим из прослеживаемости номенклатуру - которая является прослеживаемыми товарами
	ИсключитьНоменклатуруИзПрослеживаемости(СтруктураТаблицПоНоменклатуре.СписокНоменклатуры);
	
	// 2. После того как исключили из прослеживаемости некоторые товары, возможны некоторые комплекты перестали быть прослеживаемыми
	ИсключитьНоменклатуруИзПрослеживаемости(СтруктураТаблицПоНоменклатуре.СписокИсключаемыхКомплектов, Истина, Ложь);
	
	// 3. Необходимо список остатки по РНПТ по товарам и комплектам, выбывшим из прослеживаемости
	УдалитьОстаткиПоРНПТ(СтруктураТаблицПоНоменклатуре.ОстаткиПрослеживаемыеТовары, СтруктураПараметровДляОперацииБух);
	
	// 4. Могут быть составные ОС, и прослеживаемые товары, входящие в состав, выбыли из прослеживаемости
	ИсключитьОСИзПрослеживаемости(СтруктураТаблицПоНоменклатуре.СписокИсключаемыхОС, Ложь);
	
	// 5. Если составные ОС выбывают из прослеживаемости или какие то составляющие из составных ОС выбывают из прослеживаемости
	// необходимо списать остатки по РНПТ 
	УдалитьОстаткиПоРНПТ(СтруктураТаблицПоНоменклатуре.ОстаткиПрослеживаемыеОС, СтруктураПараметровДляОперацииБух, Истина, Ложь);
	
	// Обработаем простые и групповые ОС
	СтруктураТаблицДанныхПоОС = ДанныеДляИсключенияОСИУдаленияОстатков();
	
	// 6. Исключаем основные средства из прослеживаемости
	ИсключитьОСИзПрослеживаемости(СтруктураТаблицДанныхПоОС.СписокОС, Истина); 
	
	// 7. Списываем остатки по РНПТ по исключенным из прослеживаемости ОС
	УдалитьОстаткиПоРНПТ(СтруктураТаблицДанныхПоОС.ОстаткиПрослеживаемыеОС, СтруктураПараметровДляОперацииБух, Истина, Истина);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОшибки(СписокОбъектов)
	
	Если Объект.Ошибки.Количество() = 0 Тогда
		Объект.Ошибки.Загрузить(СписокОбъектов);
	Иначе
		Для Каждого Строка Из СписокОбъектов Цикл
			НайденнаяСтрока = Объект.Ошибки.НайтиСтроки(Новый Структура("Номенклатура", Строка.Номенклатура));
			Если НайденнаяСтрока.Количество() > 0 Тогда
				Продолжить;
			Иначе
				НоваяСтрока = Объект.Ошибки.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИсключитьОСИзПрослеживаемости(СписокОС, ЗаписыватьЛог = Истина)
	
	Если СписокОС = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ОСИсключеноИзПрослеживаемости = Ложь;
	Для Каждого Строка Из СписокОС Цикл
		Если ТипЗнч(Строка.ОсновноеСредство) = Тип("СправочникСсылка.ОбъектыСтроительства") Тогда
			Продолжить;
		КонецЕсли;
		ОбъектОС = Строка.ОсновноеСредство.ПолучитьОбъект();
		ОбъектОС.ПрослеживаемыйТовар = Ложь;
		Если ОбъектОС.ИсторияПрослеживаемогоТовара.Количество() = 0 Тогда
			НачальнаяСтрока = ОбъектОС.ИсторияПрослеживаемогоТовара.Добавить();
			НачальнаяСтрока.Период = '00010101';
			НачальнаяСтрока.ПрослеживаемыйТовар = Ложь;
			
			НоваяСтрока = ОбъектОС.ИсторияПрослеживаемогоТовара.Добавить();
			НоваяСтрока.Период = '20210701';
			НоваяСтрока.ПрослеживаемыйТовар = Истина;
		КонецЕсли;
		НоваяСтрока = ОбъектОС.ИсторияПрослеживаемогоТовара.Добавить();
		НоваяСтрока.Период = ДатаИсключенияИзПрослеживаемости;
		НоваяСтрока.ПрослеживаемыйТовар = Ложь;
		
		Попытка
			ОбъектОС.Записать();
			ОСИсключеноИзПрослеживаемости = Истина;
		Исключение
			ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки());
			
		КонецПопытки;
			
	КонецЦикла;
	
	Если ЗаписыватьЛог И ОСИсключеноИзПрослеживаемости Тогда
		
		ТекстСообщения = ТекстСообщения + НСтр("ru='Основные средства исключены из прослеживаемости.'") + Символы.ПС;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИсключитьНоменклатуруИзПрослеживаемости(СписокНоменклатуры, Комплекты = Ложь, ЗаписыватьЛог = Истина)
	
	Если СписокНоменклатуры = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НоменклатураИсключенаИзПрослеживаемости = Ложь;
	Для Каждого Строка Из СписокНоменклатуры Цикл
		ОбъектНоменклатура = Строка.Номенклатура.ПолучитьОбъект();
		ОбъектНоменклатура.ПрослеживаемыйТовар = Ложь;
		ОбъектНоменклатура.ПрослеживаемыйКомплект = Ложь;
		Если ОбъектНоменклатура.ИсторияПрослеживаемогоТовара.Количество() = 0 Тогда
			НачальнаяСтрока = ОбъектНоменклатура.ИсторияПрослеживаемогоТовара.Добавить();
			НачальнаяСтрока.Период = '00010101';
			НачальнаяСтрока.ПрослеживаемыйТовар = Ложь;
			НачальнаяСтрока.ПрослеживаемыйКомплект = Ложь;
			
			НоваяСтрока = ОбъектНоменклатура.ИсторияПрослеживаемогоТовара.Добавить();
			НоваяСтрока.Период = '20210701';
			НоваяСтрока.ПрослеживаемыйТовар = Не Комплекты;
			НоваяСтрока.ПрослеживаемыйКомплект = Комплекты;
		КонецЕсли;
		
		НоваяСтрока = ОбъектНоменклатура.ИсторияПрослеживаемогоТовара.Добавить();
		НоваяСтрока.Период = ДатаИсключенияИзПрослеживаемости;
		НоваяСтрока.ПрослеживаемыйТовар = Ложь;
		НоваяСтрока.ПрослеживаемыйКомплект = Ложь;
		Попытка
			ОбъектНоменклатура.Записать();
			НоменклатураИсключенаИзПрослеживаемости = Истина;
		Исключение
			ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки());
		КонецПопытки;
	КонецЦикла;
	
	Если ЗаписыватьЛог И НоменклатураИсключенаИзПрослеживаемости Тогда
		
		ТекстСообщения = ТекстСообщения + НСтр("ru='Номенклатура исключена из прослеживаемости.'") + Символы.ПС;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ДанныеДляИсключенияОСИУдаленияОстатков()
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СписокОС.ГрупповойОбъект КАК ГрупповойОбъект,
	|	СписокОС.КодТНВЭД КАК КодТНВЭД,
	|	СписокОС.ОтмеченКакПрослеживаемый КАК ОтмеченКакПрослеживаемый
	|ПОМЕСТИТЬ ТаблицаОС
	|ИЗ
	|	&СписокОС КАК СписокОС
	|ГДЕ
	|	СписокОС.ОсновноеСредство <> ЗНАЧЕНИЕ(Справочник.ОсновныеСредства.ПустаяСсылка)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ОсновныеСредства.ПрослеживаемыйТовар КАК ПрослеживаемыйТовар
	|ПОМЕСТИТЬ СписокОССПроверкойПрослеживаемости
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОсновныеСредства КАК ОсновныеСредства
	|		ПО ТаблицаОС.ОсновноеСредство = ОсновныеСредства.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокОССПроверкойПрослеживаемости.ОсновноеСредство КАК ОсновноеСредство
	|ИЗ
	|	СписокОССПроверкойПрослеживаемости КАК СписокОССПроверкойПрослеживаемости
	|ГДЕ
	|	СписокОССПроверкойПрослеживаемости.ПрослеживаемыйТовар = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрослеживаемыеОсновныеСредстваОстатки.Организация КАК Организация,
	|	ПрослеживаемыеОсновныеСредстваОстатки.РНПТ КАК РНПТ,
	|	ПрослеживаемыеОсновныеСредстваОстатки.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ПрослеживаемыеОсновныеСредстваОстатки.ОсновноеСредство КАК ОсновноеСредство,
	|	ПрослеживаемыеОсновныеСредстваОстатки.Комплектующие КАК Комплектующие,
	|	ПрослеживаемыеОсновныеСредстваОстатки.КоличествоОстаток КАК Количество,
	|	ПрослеживаемыеОсновныеСредстваОстатки.КоличествоПрослеживаемостиОстаток КАК КоличествоПрослеживаемости
	|ИЗ
	|	РегистрНакопления.ПрослеживаемыеОсновныеСредства.Остатки(
	|			&Период,
	|			ОсновноеСредство В
	|				(ВЫБРАТЬ
	|					ТаблицаОС.ОсновноеСредство
	|				ИЗ
	|					ТаблицаОС)) КАК ПрослеживаемыеОсновныеСредстваОстатки
	|ИТОГИ ПО
	|	Организация";
	
	Запрос.УстановитьПараметр("СписокОС", Объект.ОС.Выгрузить());
	Запрос.УстановитьПараметр("Период", Новый Граница(ДатаИсключенияИзПрослеживаемости -1, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодИсключенияИзПрослеживаемости", ДатаИсключенияИзПрослеживаемости);
	
	МассивРезультата = Запрос.ВыполнитьПакет();
	
	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("СписокОС",                МассивРезультата[2].Выгрузить());
	СтруктураРезультата.Вставить("ОстаткиПрослеживаемыеОС", МассивРезультата[3].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам));
	
	Возврат СтруктураРезультата;
	
КонецФункции

&НаСервере
Процедура УдалитьОстаткиПоРНПТ(ТаблицаОстатков, СтруктураПараметровДляОперацииБух, ОС = Ложь, ЗаписыватьЛог = Истина)
	
	Если ТаблицаОстатков = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОстаткиУдалены = Ложь;
	Пока ТаблицаОстатков.Следующий() Цикл
		НачатьТранзакцию();
		Попытка
			ОперацияОбъект               = Документы.ОперацияБух.СоздатьДокумент();
			ОперацияОбъект.Дата          = ДатаИсключенияИзПрослеживаемости - 1;
			ОперацияОбъект.Организация   = ТаблицаОстатков.Организация;
			ОперацияОбъект.Ответственный = ТекПользователь;
			ОперацияОбъект.Содержание    = СтруктураПараметровДляОперацииБух.Содержание;
			ОперацияОбъект.Комментарий   = СтруктураПараметровДляОперацииБух.Комментарий;
			ОперацияОбъект.Записать();
		Исключение
			ВызватьИсключение ОписаниеОшибки();
		КонецПопытки;
		Если ОС Тогда
			Движение = ОперацияОбъект.Движения.ПрослеживаемыеОсновныеСредства;
		Иначе
			Движение = ОперацияОбъект.Движения.ПрослеживаемыеТовары; 
		КонецЕсли;
		Движение.Записывать = Истина;
		
		ВыборкаДетальныеЗаписи = ТаблицаОстатков.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Запись = Движение.Добавить();
			Запись.Период      = ОперацияОбъект.Дата;
			Запись.ВидДвижения = ВидДвиженияНакопления.Расход;
			ЗаполнитьЗначенияСвойств(Запись, ВыборкаДетальныеЗаписи);
		КонецЦикла;
		
		Попытка
			
			ОперацияОбъект.Движения.Записать();
			ОстаткиУдалены = Истина
		Исключение
			ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки());
			
		КонецПопытки;
		
		ЗафиксироватьТранзакцию();
	КонецЦикла;
	
	Если ЗаписыватьЛог И ОстаткиУдалены Тогда
		Если ОС Тогда
			ТекстСообщения = ТекстСообщения +НСтр("ru='Списаны остатки по РНПТ по исключенным из прослеживаемости основным средствам.'") + Символы.ПС;
		Иначе
			ТекстСообщения = ТекстСообщения +НСтр("ru='Списаны остатки по РНПТ по исключенной из прослеживаемости номенклатуре.'") + Символы.ПС;
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Функция ПолучитьПараметрыПодбора(ИмяТаблицы)

	ЗаголовокПодбора = НСтр("ru = 'Подбор номенклатуры для исключения из прослеживаемости'");
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПоказыватьЦены"         , Ложь);
	ПараметрыФормы.Вставить("ТипЦен"                 , Неопределено);
	ПараметрыФормы.Вставить("ЕстьЦена"               , Ложь);
	ПараметрыФормы.Вставить("ЕстьКоличество"         , Ложь);
	ПараметрыФормы.Вставить("ДатаРасчетов"           , ДатаИсключенияИзПрослеживаемости);
	ПараметрыФормы.Вставить("Валюта"                 , ВалютаРегУчета);
	ПараметрыФормы.Вставить("Склад"                  , Неопределено);
	ПараметрыФормы.Вставить("Заголовок"              , ЗаголовокПодбора);
	ПараметрыФормы.Вставить("ВидПодбора"             , "");
	ПараметрыФормы.Вставить("ИмяТаблицы"             , ИмяТаблицы);
	ПараметрыФормы.Вставить("Организация"            , Неопределено);
	ПараметрыФормы.Вставить("Подразделение"          , Неопределено);
	ПараметрыФормы.Вставить("Услуги"                 , Ложь);
	ПараметрыФормы.Вставить("ПоказыватьОстатки"      , Ложь);
	ПараметрыФормы.Вставить("ЗаполнятьЦеныПоПокупке" , Истина);
	ПараметрыФормы.Вставить("СуммаВключаетНДС"		 , Ложь);
	ПараметрыФормы.Вставить("Реализация"			 , Ложь);
	ПараметрыФормы.Вставить("СтавкаНДС"				 , ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС"));
	
	Возврат ПараметрыФормы;

КонецФункции

&НаСервере
Процедура ОбработкаВыбораПодборНаСервереОС(ВыбранноеЗначение)
	
	ТаблицаОС = ПолучитьИзВременногоХранилища(ВыбранноеЗначение.АдресОСВХранилище);
	ДанныеОбОС = ПолучитьДанныеОбОС(ТаблицаОС.ВыгрузитьКолонку("ОсновноеСредство"));
	Для Каждого СтрокаОС Из ДанныеОбОС Цикл
		СтруктураОтбора = Новый Структура("ОсновноеСредство", СтрокаОС.ОсновноеСредство);
		СтрокаТабличнойЧасти = Объект.ОС.НайтиСтроки(СтруктураОтбора);
		
		Если СтрокаТабличнойЧасти.Количество() > 0 Тогда
			Продолжить;
		Иначе
			Если Не СтрокаОС.ПрослеживаемыйТовар Тогда
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Основное средство ""%1"" не является прослеживаемым.'"), СтрокаОС.ОсновноеСредство);
				
				ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
				
			ИначеЕсли СтрокаОС.ПрослеживаемыйТовар И СтрокаОС.КоличествоСтранНаОстатках > 1 Тогда
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'По основному средству ""%1"" имеются остатки по разным странам происхождения. В этом случае основное средство нельзя исключить из прослеживаемости.'"),
				СтрокаОС.ОсновноеСредство);
				ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
				
			ИначеЕсли СтрокаОС.СоставноеОС Тогда
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Основное средство ""%1"" является составным. По составным основным средствам исключение из прослеживаемости будет произведено автоматически при выполнении обработки.'"),
				СтрокаОС.ОсновноеСредство);
				ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
				
			Иначе
				НоваяСтрока = Объект.ОС.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаОС);
				НоваяСтрока.ОтмеченКакПрослеживаемый = СтрокаОС.ПрослеживаемыйТовар;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ОбработкаВыбораПодборНаСервереНоменклатура(ВыбранноеЗначение, ИмяТаблицы)
	
	ТаблицаТоваров = ПолучитьИзВременногоХранилища(ВыбранноеЗначение.АдресПодобраннойНоменклатурыВХранилище);
	ДанныеОТоварах = ПолучитьДанныеОНоменклатуре(ТаблицаТоваров.ВыгрузитьКолонку("Номенклатура"));
	
	Для Каждого СтрокаТовара Из ДанныеОТоварах Цикл
		
		СтруктураОтбора = Новый Структура("Номенклатура", СтрокаТовара.Номенклатура);
		СтрокаТабличнойЧасти = Объект[ИмяТаблицы].НайтиСтроки(СтруктураОтбора);
		
		Если СтрокаТабличнойЧасти.Количество() > 0 Тогда
			Продолжить;
		Иначе
			
			Если Не СтрокаТовара.ПрослеживаемыйТовар И Не СтрокаТовара.ПрослеживаемыйКомплект Тогда
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Номенклатура ""%1"" не является прослеживаемой.'"), СтрокаТовара.Номенклатура);
				
				ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
				
			ИначеЕсли СтрокаТовара.ПрослеживаемыйТовар И СтрокаТовара.КоличествоСтранНаОстатках > 1 Тогда
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'По номенклатуре ""%1"" имеются остатки по разным странам происхождения. В этом случае номенклатуру нельзя исключить из прослеживаемости.'"),
				СтрокаТовара.Номенклатура);
				
				ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
				
			ИначеЕсли СтрокаТовара.ПрослеживаемыйКомплект Тогда 
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Номенклатура ""%1"" является комплектом. По комплектам исключение из прослеживаемости будет произведено автоматически при выполнении обработки.'"), СтрокаТовара.Номенклатура);
				
				ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			Иначе
				
				СтрокаТабличнойЧасти = Объект[ИмяТаблицы].Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, СтрокаТовара);
				СтрокаТабличнойЧасти.ОтмеченКакПрослеживаемый = СтрокаТовара.ПрослеживаемыйТовар;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеОНоменклатуре(Номенклатура)
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Номенклатура.ПрослеживаемыйТовар КАК ПрослеживаемыйТовар,
	|	Номенклатура.ПрослеживаемыйКомплект КАК ПрослеживаемыйКомплект,
	|	Номенклатура.КодТНВЭД КАК КодТНВЭД,
	|	Номенклатура.СтранаПроисхождения КАК СтранаПроисхождения,
	|	Номенклатура.Ссылка КАК Номенклатура
	|ПОМЕСТИТЬ СписокНоменклатуры
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка В(&Номенклатура)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПрослеживаемыеТоварыОстатки.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ВЫБОР
	|		КОГДА ПрослеживаемыеТоварыОстатки.Комплектующие = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ПрослеживаемыеТоварыОстатки.Номенклатура
	|		ИНАЧЕ ПрослеживаемыеТоварыОстатки.Комплектующие
	|	КОНЕЦ КАК Номенклатура
	|ПОМЕСТИТЬ Остатки
	|ИЗ
	|	РегистрНакопления.ПрослеживаемыеТовары.Остатки(
	|			&Период,
	|			Номенклатура В (&Номенклатура)
	|				ИЛИ Комплектующие В (&Номенклатура)) КАК ПрослеживаемыеТоварыОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Остатки.СтранаПроисхождения) КАК КоличествоСтранНаОстатках,
	|	Остатки.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ ОстаткиСКоличествомСтранНаОстатках
	|ИЗ
	|	Остатки КАК Остатки
	|
	|СГРУППИРОВАТЬ ПО
	|	Остатки.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	СписокНоменклатуры.КодТНВЭД КАК КодТНВЭД,
	|	СписокНоменклатуры.ПрослеживаемыйТовар КАК ПрослеживаемыйТовар,
	|	СписокНоменклатуры.ПрослеживаемыйКомплект КАК ПрослеживаемыйКомплект,
	|	ВЫБОР
	|		КОГДА ОстаткиСКоличествомСтранНаОстатках.КоличествоСтранНаОстатках = 1
	|			ТОГДА Остатки.СтранаПроисхождения
	|		ИНАЧЕ СписокНоменклатуры.СтранаПроисхождения
	|	КОНЕЦ КАК СтранаПроисхождения,
	|	ЕСТЬNULL(ОстаткиСКоличествомСтранНаОстатках.КоличествоСтранНаОстатках, 0) КАК КоличествоСтранНаОстатках,
	|	ВЫБОР
	|		КОГДА Остатки.Номенклатура ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьОстатки
	|ИЗ
	|	СписокНоменклатуры КАК СписокНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ Остатки КАК Остатки
	|		ПО СписокНоменклатуры.Номенклатура = Остатки.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиСКоличествомСтранНаОстатках КАК ОстаткиСКоличествомСтранНаОстатках
	|		ПО СписокНоменклатуры.Номенклатура = ОстаткиСКоличествомСтранНаОстатках.Номенклатура";
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Период", Новый Граница(ДатаИсключенияИзПрослеживаемости -1, ВидГраницы.Включая));
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПолучитьДанныеОбОС(ОсновноеСредство)
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	|	ОсновныеСредства.Ссылка КАК Ссылка,
	|	ОсновныеСредства.КодТНВЭД КАК КодТНВЭД,
	|	ОсновныеСредства.ПрослеживаемыйТовар КАК ПрослеживаемыйТовар,
	|	ВЫБОР
	|		КОГДА ОсновныеСредства.ЕдиницаУчета = ЗНАЧЕНИЕ(Перечисление.ЕдиницыУчетаОС.ГрупповойОбъект)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ГрупповоеОС
	|ПОМЕСТИТЬ СписокОС
	|ИЗ
	|	Справочник.ОсновныеСредства КАК ОсновныеСредства
	|ГДЕ
	|	ОсновныеСредства.Ссылка В (&ОС)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрослеживаемыеОсновныеСредстваОстатки.ОсновноеСредство КАК ОсновноеСредство,
	|	ВЫБОР
	|		КОГДА ПрослеживаемыеОсновныеСредстваОстатки.Комплектующие = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК СоставноеОС,
	|	ПрослеживаемыеОсновныеСредстваОстатки.СтранаПроисхождения КАК СтранаПроисхождения
	|ПОМЕСТИТЬ ОстаткиПоОС
	|ИЗ
	|	РегистрНакопления.ПрослеживаемыеОсновныеСредства.Остатки(&Период, ОсновноеСредство В (&ОС)) КАК ПрослеживаемыеОсновныеСредстваОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиПоОС.ОсновноеСредство КАК ОсновноеСредство,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ОстаткиПоОС.СтранаПроисхождения) КАК КоличествоСтранПроисхождения
	|ПОМЕСТИТЬ ГрупповыеОСПоКоличествуСтран
	|ИЗ
	|	СписокОС КАК СписокОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОстаткиПоОС КАК ОстаткиПоОС
	|		ПО СписокОС.Ссылка = ОстаткиПоОС.ОсновноеСредство
	|ГДЕ
	|	СписокОС.ГрупповоеОС = ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиПоОС.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокОС.Ссылка КАК ОсновноеСредство,
	|	СписокОС.КодТНВЭД КАК КодТНВЭД,
	|	СписокОС.ПрослеживаемыйТовар КАК ПрослеживаемыйТовар,
	|	СписокОС.ГрупповоеОС КАК ГрупповойОбъект,
	|	ЕСТЬNULL(ГрупповыеОСПоКоличествуСтран.КоличествоСтранПроисхождения, 0) КАК КоличествоСтранНаОстатках,
	|	ЕСТЬNULL(ОстаткиПоОС.СоставноеОС, ЛОЖЬ) КАК СоставноеОС
	|ИЗ
	|	СписокОС КАК СписокОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиПоОС КАК ОстаткиПоОС
	|		ПО СписокОС.Ссылка = ОстаткиПоОС.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ ГрупповыеОСПоКоличествуСтран КАК ГрупповыеОСПоКоличествуСтран
	|		ПО СписокОС.Ссылка = ГрупповыеОСПоКоличествуСтран.ОсновноеСредство";
	
	Запрос.УстановитьПараметр("ОС", ОсновноеСредство);
	Запрос.УстановитьПараметр("Период", Новый Граница(ДатаИсключенияИзПрослеживаемости -1, ВидГраницы.Включая));
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ИмяСобытияЖурналаРегистрации()

	Возврат НСтр("ru = 'Исключение белорусских холодильников из прослеживаемости'", ОбщегоНазначения.КодОсновногоЯзыка());

КонецФункции

#КонецОбласти
