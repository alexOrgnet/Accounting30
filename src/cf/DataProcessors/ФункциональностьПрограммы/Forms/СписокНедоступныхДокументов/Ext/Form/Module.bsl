
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОсновнаяТаблица = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ОсновнаяТаблица", "");
	
	Если Не ЗначениеЗаполнено(ОсновнаяТаблица) Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Заголовок = Параметры.Заголовок;
	ОбращениеКТабличнойЧасти = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		Параметры, "ОбращениеКТабличнойЧасти", Ложь);
	
	Если Не ОбращениеКТабличнойЧасти Тогда
		НастроитьСписокДляОсновнойТаблицыДокумента();
	Иначе
		НастроитьСписокДляТабличнойЧастиДокумента();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПометитьНаУдаление(Команда)
	
	ПометитьНаУдалениеНаСервере();
	
	Закрыть(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокДокументов

&НаКлиенте
Процедура СписокДокументовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ТипЗнч(Элемент.ТекущаяСтрока) = Тип("Число") Тогда
		СтандартнаяОбработка = Ложь;
		ПараметрыОткрываемойФормы = Новый Структура;
		ПараметрыОткрываемойФормы.Вставить("ТолькоПросмотр", Истина);
		ПараметрыОткрываемойФормы.Вставить("Ключ", Элемент.ТекущиеДанные.Ссылка);
		ОткрытьФорму(СтрШаблон("%1.ФормаОбъекта", ОсновнаяТаблица), ПараметрыОткрываемойФормы);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПометитьНаУдалениеНаСервере()
	
	Схема = Элементы.СписокДокументов.ПолучитьИсполняемуюСхемуКомпоновкиДанных();
	Настройки = Элементы.СписокДокументов.ПолучитьИсполняемыеНастройкиКомпоновкиДанных();
	
	Компоновщик = Новый КомпоновщикМакетаКомпоновкиДанных;
	Макет = Компоновщик.Выполнить(Схема, Настройки, , , Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(Макет);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ОбъектыНаУдаление = Новый ТаблицаЗначений;
	ПроцессорВывода.УстановитьОбъект(ОбъектыНаУдаление);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	Обработки.ФункциональностьПрограммы.ПометитьОбъектыНаУдаление(ОбъектыНаУдаление.ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

&НаСервере
Процедура НастроитьСписокДляОсновнойТаблицыДокумента()
	
	СписокДокументов.ОсновнаяТаблица = ОсновнаяТаблица;
	
	СхемаЗапроса = Новый СхемаЗапроса;
	ОператорЗапроса = СхемаЗапроса.ПакетЗапросов[0].Операторы[0];
	ОператорЗапроса.Источники.Добавить(ОсновнаяТаблица);
	ОператорЗапроса.ВыбираемыеПоля.Добавить("Дата");
	ОператорЗапроса.ВыбираемыеПоля.Добавить("Номер");
	
	СоставныеЧастиИмени = СтрРазделить(ОсновнаяТаблица, ".");
	ИмяДокумента = СоставныеЧастиИмени[1];
	
	МетаданныеДокумента = Метаданные.Документы[ИмяДокумента];
	РеквизитыДокумента = МетаданныеДокумента.Реквизиты;
	
	ДобавляемыеПоля = Новый Массив;
	ДобавляемыеПоля.Добавить("СуммаДокумента");
	ДобавляемыеПоля.Добавить("Контрагент");
	
	ОтборСпискаДокументов = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "Отбор", Неопределено);
	Если ТипЗнч(ОтборСпискаДокументов) = Тип("ОтборКомпоновкиДанных") Тогда
		КомпоновкаДанныхКлиентСервер.СкопироватьЭлементы(СписокДокументов.Отбор, ОтборСпискаДокументов, Ложь);
		Для Каждого ЭлементОтбора Из ОтборСпискаДокументов.Элементы Цикл
			ЧастиПути = СтрРазделить(Строка(ЭлементОтбора.ЛевоеЗначение), ".");
			ДобавляемыеПоля.Добавить(ЧастиПути[0]);
		КонецЦикла;
	КонецЕсли;
	
	НеНайденныеПоля = Новый Массив;
	
	Для Каждого ИмяПоля Из ДобавляемыеПоля Цикл
		Если РеквизитыДокумента.Найти(ИмяПоля) <> Неопределено Тогда
			ОператорЗапроса.ВыбираемыеПоля.Добавить(ИмяПоля);
		Иначе
			НеНайденныеПоля.Добавить(ИмяПоля);
		КонецЕсли;
	КонецЦикла;
	
	СписокДокументов.ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
	
	ДобавляемыеПоля = ОбщегоНазначенияКлиентСервер.РазностьМассивов(ДобавляемыеПоля, НеНайденныеПоля);
	
	ЭлементыСписка = Элементы.СписокДокументов;
	Для каждого ИмяПоля Из ДобавляемыеПоля Цикл
		КолонкаСписка = Элементы.Добавить(СтрШаблон("СписокДокументов%1", ИмяПоля), Тип("ПолеФормы"), ЭлементыСписка);
		КолонкаСписка.ПутьКДанным = СтрШаблон("СписокДокументов.%1", ИмяПоля);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьСписокДляТабличнойЧастиДокумента()
	
	СписокДокументов.ОсновнаяТаблица = "";
	ПараметрыЗапроса = Новый Структура;
	
	СхемаЗапроса = Новый СхемаЗапроса;
	ИменаТаблиц = Параметры.ИменаТабличныхЧастей;
	МаксимальныйИндекс = ИменаТаблиц.ВГраница();
	ПакетЗапросов = СхемаЗапроса.ПакетЗапросов[0];
	
	СоставныеЧастиИмени = СтрРазделить(ОсновнаяТаблица, ".");
	ИмяДокумента = СоставныеЧастиИмени[1];
	
	МетаданныеДокумента = Метаданные.Документы[ИмяДокумента];
	РеквизитыДокумента = МетаданныеДокумента.Реквизиты;
	
	ДобавляемыеПоля = Новый Массив;
	ДобавляемыеПоля.Добавить("СуммаДокумента");
	ДобавляемыеПоля.Добавить("Контрагент");
	
	НеНайденныеПоля = Новый Массив;
	
	Для Каждого ИмяПоля Из ДобавляемыеПоля Цикл
		Если РеквизитыДокумента.Найти(ИмяПоля) = Неопределено Тогда
			НеНайденныеПоля.Добавить(ИмяПоля);
		КонецЕсли;
	КонецЦикла;
	
	ДобавляемыеПоля = ОбщегоНазначенияКлиентСервер.РазностьМассивов(ДобавляемыеПоля, НеНайденныеПоля);
	ОтборыПоТабличнымЧастям = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		Параметры, "ОтборыТабличныхЧастей", Неопределено);
	Если ОтборыПоТабличнымЧастям = Неопределено Тогда
		ОтборыПоТабличнымЧастям = Новый Структура;
	КонецЕсли;
	
	Для Индекс = 0 По МаксимальныйИндекс Цикл
		Если Индекс = 0 Тогда
			ОператорЗапроса = ПакетЗапросов.Операторы[0];
		Иначе
			ОператорЗапроса = ПакетЗапросов.Операторы.Добавить();
		КонецЕсли;
		ИмяТаблицы = ИменаТаблиц[Индекс];
		ОператорЗапроса.Источники.Добавить(СтрШаблон("%1.%2", ОсновнаяТаблица, ИмяТаблицы));
		ОператорЗапроса.ТипОбъединения = ТипОбъединенияСхемыЗапроса.Объединить;
		ОператорЗапроса.ВыбираемыеПоля.Добавить("Ссылка");
		ОператорЗапроса.ВыбираемыеПоля.Добавить("Ссылка.Дата");
		ОператорЗапроса.ВыбираемыеПоля.Добавить("Ссылка.Номер");
		Для Каждого ИмяПоля Из ДобавляемыеПоля Цикл
			ОператорЗапроса.ВыбираемыеПоля.Добавить(СтрШаблон("Ссылка.%1", ИмяПоля));
		КонецЦикла;
		ОтборПоТабличнойЧасти = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			ОтборыПоТабличнымЧастям, ИмяТаблицы, Неопределено);
		Если ОтборПоТабличнойЧасти <> Неопределено Тогда
			ИмяПараметра = СтрШаблон("Параметр%1", Формат(Индекс, "ЧН=0; ЧГ="));
			ОператорЗапроса.Отбор.Добавить(СтрШаблон("%1 В (&%2)", ОтборПоТабличнойЧасти.Путь, ИмяПараметра));
			ПараметрыЗапроса.Вставить(ИмяПараметра, ОтборПоТабличнойЧасти.Значение);
		КонецЕсли;
	КонецЦикла;
	
	СписокДокументов.ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
	Для Каждого ЭлементПараметр Из ПараметрыЗапроса Цикл
		СписокДокументов.Параметры.УстановитьЗначениеПараметра(ЭлементПараметр.Ключ, ЭлементПараметр.Значение);
	КонецЦикла;
	
	ЭлементыСписка = Элементы.СписокДокументов;
	Для каждого ИмяПоля Из ДобавляемыеПоля Цикл
		КолонкаСписка = Элементы.Добавить(СтрШаблон("СписокДокументов%1", ИмяПоля), Тип("ПолеФормы"), ЭлементыСписка);
		КолонкаСписка.ПутьКДанным = СтрШаблон("СписокДокументов.%1", ИмяПоля);
	КонецЦикла;
	
	
КонецПроцедуры

#КонецОбласти