// @strict-types
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Обработка заполнения.
// 
// Параметры:
//  ДанныеЗаполнения - СтрокаТаблицыЗначений: см. Справочники.Реклама.НовыйТаблицаЭлементыРекламы
//  ТекстЗаполнения - Строка, Неопределено - Текст заполнения
//  СтандартнаяОбработка - Булево
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ДанныеЗаполнения = Неопределено 
		Или ТипЗнч(ДанныеЗаполнения) <> Тип("СтрокаТаблицыЗначений") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения, , "Изображение");
	
	ДатаЗагрузки = ОбщегоНазначения.ТекущаяДатаПользователя();
	
	Если Не ПустаяСтрока(ДанныеЗаполнения.Изображение) Тогда
		Изображение = Новый ХранилищеЗначения(ДанныеЗаполнения.Изображение, Новый СжатиеДанных(9));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеЗаполнения.ЭкранныеФормыДляПоказа) Тогда
		
		ЭкранныеФормы.Очистить();
		
		Для Каждого ЭкраннаяФорма Из ДанныеЗаполнения.ЭкранныеФормыДляПоказа Цикл
			
			СтрокаЭкраннойФормы = ЭкранныеФормы.Добавить();
			СтрокаЭкраннойФормы.ИмяФормы = ЭкраннаяФорма;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
	//@skip-warning
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли