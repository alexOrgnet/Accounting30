#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ПроверитьСоответствиеВидаДеятельностиВидуРасходов(Отказ);
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ЭтоНУ = НЕ ЭтоГруппа
		И (ВидДеятельностиДляНалоговогоУчетаЗатрат = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения
			ИЛИ ВидДеятельностиДляНалоговогоУчетаЗатрат = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты);
			
	
	Если НЕ ЭтоНУ Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("ВидРасходовНУ");
        
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользуетсяЕНВД")
		И Не ПолучитьФункциональнуюОпцию("ИспользуетсяУСНПатент") Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ВидДеятельностиДляНалоговогоУчетаЗатрат");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если Не ЭтоГруппа И Не ЗначениеЗаполнено(ВидДеятельностиДляНалоговогоУчетаЗатрат) Тогда
		ВидДеятельностиДляНалоговогоУчетаЗатрат = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения;
	КонецЕсли;
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ТребуетсяЗаписьРемонтОСВРегистрМетодыОпределенияПрямыхРасходовНУ() Тогда
		ДополнительныеСвойства.Вставить("ОбновитьМетодыОпределенияПрямыхРасходовНУРемонтОС", Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	РегистрыСведений.МетодыОпределенияПрямыхРасходовПроизводстваВНУ.ДополнитьПриЗаписиСтатьиРемонтОС(ЭтотОбъект);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьСоответствиеВидаДеятельностиВидуРасходов(Отказ)
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользуетсяЕНВД") И Не ПолучитьФункциональнуюОпцию("ИспользуетсяУСНПатент") Тогда
		Возврат;
	КонецЕсли;
	
	// Не все виды расходов могут относиться к деятельности, облагаемой ЕНВД или в рамках других специальных режимов
	ВидыРасходовТолькоОсновнаяСистемаНалогообложения = Справочники.СтатьиЗатрат.ВидыРасходовТолькоОсновнаяСистемаНалогообложения();
	
	Если ВидыРасходовТолькоОсновнаяСистемаНалогообложения.Найти(ВидРасходовНУ) <> Неопределено
		И (ВидДеятельностиДляНалоговогоУчетаЗатрат = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения
		Или ВидДеятельностиДляНалоговогоУчетаЗатрат = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты) Тогда
		
		ТекстСообщения = НСтр("ru = 'Расходы с видом ""%1"" не могут относиться к деятельности с особым порядком налогообложения.
		|Измените вид расходов или установите назначение статьи ""По деятельности с основной системой налогообложения""'");
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ВидРасходовНУ);
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ВидРасходовНУ", "Объект", Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ТребуетсяЗаписьРемонтОСВРегистрМетодыОпределенияПрямыхРасходовНУ()
	Возврат ЭтоНовый() И ИмяПредопределенныхДанных = "РемонтОС";
КонецФункции

#КонецОбласти

#КонецЕсли
