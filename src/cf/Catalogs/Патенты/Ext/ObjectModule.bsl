#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	Если ДанныеЗаполнения = Неопределено Тогда
		ДанныеЗаполнения = Новый Структура;
	КонецЕсли;
	
	Если Не ДанныеЗаполнения.Свойство("Владелец") Тогда
		Владелец = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;

КонецПроцедуры

// Очищаем ссылки на правило из регистра сведений "ЗадачиБухгалтераНалоговыеПлатежи"
Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Правило", Ссылка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗадачиБухгалтераНалоговыеПлатежи.Организация,
	|	ЗадачиБухгалтераНалоговыеПлатежи.ПлатежноеПоручение
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтераНалоговыеПлатежи КАК ЗадачиБухгалтераНалоговыеПлатежи
	|ГДЕ
	|	ЗадачиБухгалтераНалоговыеПлатежи.Правило = &Правило";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		МенеджерЗаписи = РегистрыСведений.ЗадачиБухгалтераНалоговыеПлатежи.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
		МенеджерЗаписи.Удалить();
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	// Очистим неиспользуемые реквизиты
	НеиспользуемыеРеквизиты = МассивНеиспользуемыхРеквизитов();
	
	Для Каждого ИмяРеквизита Из НеиспользуемыеРеквизиты Цикл
		ЭтотОбъект[ИмяРеквизита] = Неопределено;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция МассивНеиспользуемыхРеквизитов()
	
	МассивРеквизитов = Новый Массив;
	МассивРеквизитов.Добавить("УдалитьДатаНачала");
	МассивРеквизитов.Добавить("УдалитьДатаОкончания");
	МассивРеквизитов.Добавить("УдалитьПотенциальноВозможныйГодовойДоход");
	МассивРеквизитов.Добавить("УдалитьКБК");
	МассивРеквизитов.Добавить("УдалитьСуммаПервогоПлатежа");
	МассивРеквизитов.Добавить("УдалитьДатаПервогоПлатежа");
	МассивРеквизитов.Добавить("УдалитьСуммаВторогоПлатежа");
	МассивРеквизитов.Добавить("УдалитьДатаВторогоПлатежа");
	МассивРеквизитов.Добавить("УдалитьНалоговыйОрган");
	МассивРеквизитов.Добавить("УдалитьПостановкаНаУчетВНалоговомОргане");
	МассивРеквизитов.Добавить("УдалитьКодПоОКТМО");
	МассивРеквизитов.Добавить("УдалитьНомерПатента");
	МассивРеквизитов.Добавить("УдалитьДатаВыдачи");
	МассивРеквизитов.Добавить("УдалитьРасходыУменьшающиеНалог");
	МассивРеквизитов.Добавить("УдалитьСуммаОсвобожденияОтНалога");
	
	Возврат МассивРеквизитов;
	
КонецФункции

#КонецОбласти

#КонецЕсли
