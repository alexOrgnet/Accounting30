
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда
		ВызватьИсключение НСтр("ru = 'В форме может открываться только ранее записанное уведомление'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ТолькоПросмотр = Истина;
	
	Заголовок = ТекущийОбъект.Наименование;
	
	ТекстВФорматеMarkdown.УстановитьФорматированнуюСтроку(
		СтроковыеФункции.ФорматированнаяСтрока(ТекущийОбъект.ТекстВФорматеMarkdown));
	
	СписокФайлов = Новый Массив;
	РаботаСФайлами.ЗаполнитьПрисоединенныеФайлыКОбъекту(Объект.Ссылка, СписокФайлов);
	Если СписокФайлов.Количество() = 0 Тогда
		Элементы.ПриложенныеФайлы.Видимость = Ложь;
	Иначе
		ЗагрузитьОписанияФайлов(СписокФайлов);
	КонецЕсли;
	
	Элементы.ДатаОбновления.Видимость = (ТекущийОбъект.ДатаСоздания <> ТекущийОбъект.ДатаОбновления);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьФайл(Команда)
	
	ТекДанные = Элементы.ПриложенныеФайлы.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РаботаСФайламиКлиент.ОткрытьФайл(
		РаботаСФайламиКлиент.ДанныеФайла(ТекДанные.Значение, УникальныйИдентификатор));
	
КонецПроцедуры
	
&НаКлиенте
Процедура СохранитьФайл(Команда)
	
	ТекДанные = Элементы.ПриложенныеФайлы.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РаботаСФайламиКлиент.СохранитьФайлКак(
		РаботаСФайламиКлиент.ДанныеФайла(ТекДанные.Значение, УникальныйИдентификатор));
	
КонецПроцедуры	

#КонецОбласти

#Область ОбработчикиТаблицыФормыПриложенныеФайлы

&НаКлиенте
Процедура ПриложенныеФайлыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	РаботаСФайламиКлиент.ОткрытьФайл(
		РаботаСФайламиКлиент.ДанныеФайла(Элемент.ТекущиеДанные.Значение, УникальныйИдентификатор));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗагрузитьОписанияФайлов(СписокФайлов)
	
	ПриложенныеФайлы.ЗагрузитьЗначения(СписокФайлов);
	
	ПараметрыФайла = РаботаСФайламиКлиентСервер.ПараметрыДанныхФайла();
	ПараметрыФайла.ИдентификаторФормы             = УникальныйИдентификатор;
	ПараметрыФайла.ПолучатьСсылкуНаДвоичныеДанные = Ложь;
	ПараметрыФайла.ВызыватьИсключение             = Ложь;
	
	Для Каждого ПриложенныйФайл Из ПриложенныеФайлы Цикл
		
		ОписаниеФайла = РаботаСФайлами.ДанныеФайла(ПриложенныйФайл.Значение, ПараметрыФайла);
		Если ОписаниеФайла = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ТипСодержимого = УведомленияОтФНСАУСН.ТипСодержимогоПоРасширению(ОписаниеФайла.Расширение);
		ПодходящаяКартинка = УведомленияОтФНСАУСН.КартинкаПоТипуСодержимого(ТипСодержимого);
		Если ПодходящаяКартинка = Неопределено Тогда // неизвестный  тип
			
			ПриложенныйФайл.Представление = ОписаниеФайла.ИмяФайла;
			ПриложенныйФайл.Картинка = БиблиотекаКартинок.ФорматПустой;
			
		Иначе
			
			ПриложенныйФайл.Представление = ОписаниеФайла.Наименование;
			ПриложенныйФайл.Картинка = ПодходящаяКартинка;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
