#Область ДлительныеОперации

// Функция возвращающая таблицу значений 
// для помощника подбора классификации оперцаций НДС 0% по номенклатуре
// данная функция вызывается как фоновое задание

// Параметры:
//   Параметры - Структура
//		- Поля:
//			ТаблицаКодовОпераций - ТаблицаЗначений
//									- Колонки:
//										КодОперации - Строка
//										Операции СправочникСсылка.Операции0
//										ПредставлениеОпераций - Строка
//			НачалоПериода - Дата
//			КонецПериода - Дата
//			Организация - СправочникСсылка.Организации
//			Номенклатура - СправочникСсылка.Номенклатура
//			ЭтоГруппа - Булево

// Возвращаемое значение:
//  - ТаблицаЗначений
//		- Колонки:
//			Номенклатура - СправочникСсылка.Номенклатура
//			ПредставлениеТекущихОпераций - Строка
//			Операции - СправочникСсылка.Операции0
//			ПредставлениеОпераций - Строка
//			Порядок - Число
//			Порядок - Число
//			КлючСтроки - Строка
//			РазныеОперации - Булево

Функция ПодобратьОперации0(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Параметры.Вставить("ТаблицаКодовОпераций", Параметры.ТаблицаКодовОпераций);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаКодовОпераций.КодОперации КАК КодОперации,
	|	ТаблицаКодовОпераций.Операции КАК Операции,
	|	ТаблицаКодовОпераций.ПредставлениеОпераций КАК ПредставлениеОпераций
	|ПОМЕСТИТЬ КодыОпераций
	|ИЗ
	|	&ТаблицаКодовОпераций КАК ТаблицаКодовОпераций
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КодОперации,
	|	Операции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	РеализацияТоваровУслугТовары.Ссылка КАК Ссылка,
	|	РеализацияТоваровУслугТовары.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ Реализации
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|ГДЕ
	|	НЕ РеализацияТоваровУслугТовары.Ссылка.ПометкаУдаления
	|	И РеализацияТоваровУслугТовары.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|	И &ТоварыОрганизация
	|	И &ТоварыПериод
	|	И &ТоварыНоменклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ЕСТЬNULL(ТаможеннаяДекларацияЭкспортДокументыОснования.Ссылка.КодОперации, """") КАК КодОперации,
	|	РеализацияТоваровУслугТовары.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ РеализацииСКодами
	|ИЗ
	|	Реализации КАК РеализацияТоваровУслугТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ТаможеннаяДекларацияЭкспорт.ДокументыОснования КАК ТаможеннаяДекларацияЭкспортДокументыОснования
	|		ПО РеализацияТоваровУслугТовары.Ссылка = ТаможеннаяДекларацияЭкспортДокументыОснования.ДокументОснование
	|			И (ТаможеннаяДекларацияЭкспортДокументыОснования.Ссылка.КодОперации <> """")
	|			И (НЕ ТаможеннаяДекларацияЭкспортДокументыОснования.Ссылка.ПометкаУдаления)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КодОперации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Реализации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	РеализацииСКодами.Номенклатура КАК Номенклатура,
	|	РеализацииСКодами.Номенклатура.Операции0 КАК ТекущиеОперации,
	|	ЕСТЬNULL(КодыОпераций.Операции, ЗНАЧЕНИЕ(Справочник.Операции0.ПустаяСсылка)) КАК Операции,
	|	КодыОпераций.ПредставлениеОпераций КАК ПредставлениеОпераций
	|ПОМЕСТИТЬ Номенклатуры
	|ИЗ
	|	РеализацииСКодами КАК РеализацииСКодами
	|		ЛЕВОЕ СОЕДИНЕНИЕ КодыОпераций КАК КодыОпераций
	|		ПО РеализацииСКодами.КодОперации = КодыОпераций.КодОперации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеализацияТоваровУслугУслуги.Номенклатура,
	|	РеализацияТоваровУслугУслуги.Номенклатура.Операции0,
	|	ЗНАЧЕНИЕ(Справочник.Операции0.ПустаяСсылка),
	|	""""
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Услуги КАК РеализацияТоваровУслугУслуги
	|ГДЕ
	|	НЕ РеализацияТоваровУслугУслуги.Ссылка.ПометкаУдаления
	|	И РеализацияТоваровУслугУслуги.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|	И &УслугиОрганизация
	|	И &УслугиПериод
	|	И &УслугиНоменклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Номенклатуры.Номенклатура КАК Номенклатура,
	|	ЕСТЬNULL(КодыОпераций.ПредставлениеОпераций, """") КАК ПредставлениеТекущихОпераций,
	|	Номенклатуры.Операции КАК Операции,
	|	Номенклатуры.ПредставлениеОпераций КАК ПредставлениеОпераций
	|ПОМЕСТИТЬ НоменклатурыПредставлениеОпераций
	|ИЗ
	|	Номенклатуры КАК Номенклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ КодыОпераций КАК КодыОпераций
	|		ПО Номенклатуры.ТекущиеОперации = КодыОпераций.Операции
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ РеализацииСКодами
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ КодыОпераций
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Номенклатуры.Номенклатура КАК Номенклатура,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Номенклатуры.Операции) КАК КолвоОпераций,
	|	0 КАК КолвоПустыхОпераций
	|ПОМЕСТИТЬ НоменклатураКолвоОперацийДляСвертки
	|ИЗ
	|	Номенклатуры КАК Номенклатуры
	|
	|СГРУППИРОВАТЬ ПО
	|	Номенклатуры.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Номенклатуры.Номенклатура,
	|	0,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Номенклатуры.Операции)
	|ИЗ
	|	Номенклатуры КАК Номенклатуры
	|ГДЕ
	|	Номенклатуры.Операции = ЗНАЧЕНИЕ(Справочник.Операции0.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	Номенклатуры.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Номенклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураКолвоОперацийДляСвертки.Номенклатура КАК Номенклатура,
	|	СУММА(НоменклатураКолвоОперацийДляСвертки.КолвоОпераций) КАК КолвоОпераций,
	|	СУММА(НоменклатураКолвоОперацийДляСвертки.КолвоПустыхОпераций) КАК КолвоПустыхОпераций
	|ПОМЕСТИТЬ НоменклатураКолвоОпераций
	|ИЗ
	|	НоменклатураКолвоОперацийДляСвертки КАК НоменклатураКолвоОперацийДляСвертки
	|
	|СГРУППИРОВАТЬ ПО
	|	НоменклатураКолвоОперацийДляСвертки.Номенклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НоменклатураКолвоОперацийДляСвертки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Номенклатуры.Номенклатура КАК Номенклатура,
	|	Номенклатуры.ПредставлениеТекущихОпераций КАК ПредставлениеТекущихОпераций,
	|	Номенклатуры.Операции КАК Операции,
	|	Номенклатуры.ПредставлениеОпераций КАК ПредставлениеОпераций,
	|	ВЫБОР
	|		КОГДА НоменклатураКолвоОпераций.КолвоОпераций = НоменклатураКолвоОпераций.КолвоПустыхОпераций
	|			ТОГДА 2
	|		КОГДА НоменклатураКолвоОпераций.КолвоОпераций = 1
	|					И Номенклатуры.Операции <> ЗНАЧЕНИЕ(Справочник.Операции0.ПустаяСсылка)
	|				ИЛИ НоменклатураКолвоОпераций.КолвоОпераций - НоменклатураКолвоОпераций.КолвоПустыхОпераций = 1
	|			ТОГДА 1
	|		ИНАЧЕ 3
	|	КОНЕЦ КАК Порядок,
	|	ВЫБОР
	|		КОГДА НоменклатураКолвоОпераций.КолвоОпераций - НоменклатураКолвоОпераций.КолвоПустыхОпераций > 1
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК РазныеОперации
	|ИЗ
	|	НоменклатурыПредставлениеОпераций КАК Номенклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ НоменклатураКолвоОпераций КАК НоменклатураКолвоОпераций
	|		ПО Номенклатуры.Номенклатура = НоменклатураКолвоОпераций.Номенклатура
	|ГДЕ
	|	(НоменклатураКолвоОпераций.КолвоОпераций = 1
	|			ИЛИ НоменклатураКолвоОпераций.КолвоОпераций > 1
	|				И Номенклатуры.Операции <> ЗНАЧЕНИЕ(Справочник.Операции0.ПустаяСсылка))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок УБЫВ,
	|	Номенклатура,
	|	ПредставлениеТекущихОпераций
	|АВТОУПОРЯДОЧИВАНИЕ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НоменклатурыПредставлениеОпераций
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НоменклатураКолвоОпераций";
	
	Если ЗначениеЗаполнено(Параметры.Организация) Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&ТоварыОрганизация","РеализацияТоваровУслугТовары.Ссылка.Организация = &Организация");
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&УслугиОрганизация","РеализацияТоваровУслугУслуги.Ссылка.Организация = &Организация");
		
		Запрос.Параметры.Вставить("Организация", Параметры.Организация);
		
	Иначе
		
		Запрос.Параметры.Вставить("ТоварыОрганизация", Истина);
		Запрос.Параметры.Вставить("УслугиОрганизация", Истина);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.НачалоПериода)
		И ЗначениеЗаполнено(Параметры.КонецПериода) Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&ТоварыПериод","РеализацияТоваровУслугТовары.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода");
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&УслугиПериод","РеализацияТоваровУслугУслуги.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода");
		
		Запрос.Параметры.Вставить("НачалоПериода", Параметры.НачалоПериода);
		Запрос.Параметры.Вставить("КонецПериода", Параметры.КонецПериода);
		
	ИначеЕсли ЗначениеЗаполнено(Параметры.НачалоПериода)
		И НЕ ЗначениеЗаполнено(Параметры.КонецПериода) Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&ТоварыПериод","РеализацияТоваровУслугТовары.Ссылка.Дата >= &НачалоПериода");
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&УслугиПериод","РеализацияТоваровУслугУслуги.Ссылка.Дата >= &НачалоПериода");
		
		Запрос.Параметры.Вставить("НачалоПериода", Параметры.НачалоПериода);
		
	ИначеЕсли НЕ ЗначениеЗаполнено(Параметры.НачалоПериода)
		И ЗначениеЗаполнено(Параметры.КонецПериода) Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&ТоварыПериод","РеализацияТоваровУслугТовары.Ссылка.Дата <= &КонецПериода");
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&УслугиПериод","РеализацияТоваровУслугУслуги.Ссылка.Дата <= &КонецПериода");
		
		Запрос.Параметры.Вставить("КонецПериода", Параметры.КонецПериода);
		
	Иначе
		
		Запрос.Параметры.Вставить("ТоварыПериод", Истина);
		Запрос.Параметры.Вставить("УслугиПериод", Истина);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Номенклатура) Тогда
		
		Если Параметры.ЭтоГруппа Тогда
			
			Запрос.Текст = СтрЗаменить(Запрос.Текст,
				"&ТоварыНоменклатура","РеализацияТоваровУслугТовары.Номенклатура В ИЕРАРХИИ(&Номенклатура)");
			Запрос.Текст = СтрЗаменить(Запрос.Текст,
				"&УслугиНоменклатура","РеализацияТоваровУслугУслуги.Номенклатура В ИЕРАРХИИ(&Номенклатура)");
			
		Иначе
			
			Запрос.Текст = СтрЗаменить(Запрос.Текст,
				"&ТоварыНоменклатура","РеализацияТоваровУслугТовары.Номенклатура = &Номенклатура");
			Запрос.Текст = СтрЗаменить(Запрос.Текст,
				"&УслугиНоменклатура","РеализацияТоваровУслугУслуги.Номенклатура = &Номенклатура");
			
		КонецЕсли;
		
		Запрос.Параметры.Вставить("Номенклатура", Параметры.Номенклатура);
		
	Иначе
		
		Запрос.Параметры.Вставить("ТоварыНоменклатура", Истина);
		Запрос.Параметры.Вставить("УслугиНоменклатура", Истина);
		
	КонецЕсли;
	
	Таблица = Запрос.Выполнить().Выгрузить();
	
	КС36 = Новый КвалификаторыСтроки(36);
	Массив = Новый Массив;
	Массив.Добавить(Тип("Строка"));
	ОписаниеТиповС36 = Новый ОписаниеТипов(Массив, , КС36);

	Таблица.Колонки.Добавить("КлючСтроки",ОписаниеТиповС36);

	Возврат Таблица;

КонецФункции

#КонецОбласти