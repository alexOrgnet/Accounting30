
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.ЭтоРНПТ Тогда
		ПроверкаРНПТ.ПриСозданииНаСервереРНПТ(ЭтотОбъект);
	КонецЕсли;
	ПодготовитьФормуНаСервере();
	// Очистим настройки формы.
	КлючСохраненияПоложенияОкна = Новый УникальныйИдентификатор();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// При интерактивной записи для РНПТ получаем свойства из сервиса ФНС.
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ИнтерактивнаяЗапись", Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Установим размер формы в зависимости от содержания.
	Если ПроверкаРНПТВключена И Объект.ЭтоРНПТ И ЗначениеЗаполнено(Объект.Ссылка) И ЗначениеЗаполнено(ДатаПолученияСведений) Тогда
		Если Ошибка Тогда 
			ЭтаФорма.Высота = 12;
		Иначе
			ЭтаФорма.Высота = 23;
		КонецЕсли;
	Иначе
		ЭтаФорма.Высота = 7;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КодПриИзменении(Элемент)
	
	ТекущийТекстНомераДекларации = Объект.Код;
	
	КодПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КодИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	ТекущийТекстНомераДекларации = Текст;
	ПодключитьОбработчикОжидания("Подключаемый_ВывестиИнформациюОбОшибкахВНомере", 0.1, Истина);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	ВедетсяУчетПрослеживаемыхТоваров = ПолучитьФункциональнуюОпцию("ВестиУчетПрослеживаемыхТоваров");
	КорректныйПериод = ОбщегоНазначенияБПСобытия.КорректныйПериодВводаДокументов();
	
	НачалоКорректногоПериода = КорректныйПериод.НачалоКорректногоПериода;
	КонецКорректногоПериода  = КорректныйПериод.КонецКорректногоПериода;
	
	ТекущийТекстНомераДекларации = Объект.Код;
	КодОшибки = УчетНДСКлиентСервер.НаличиеОшибокВНомереДекларации(
		ТекущийТекстНомераДекларации, НачалоКорректногоПериода, КонецКорректногоПериода);
		
	ПроверкаРНПТВключена = ПроверкаРНПТ.ПроверкаРНПТВключена();
	Если Параметры.Ключ.Пустая() И (Объект.ЭтоРНПТ ИЛИ Объект.ЭтоНомерТД)Тогда
		ЭтоКонтекстноеСоздание = Истина;
	КонецЕсли;
	
	ЦветОшибка    = ЦветаСтиля.ПоясняющийОшибкуТекст;
	ЦветНетОшибки = ЦветаСтиля.ПоясняющийТекст;
	
	УстановитьЗаголовокФормы();
	
	УправлениеФормой(ЭтотОбъект);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Если Форма.КодОшибки <> 0 Тогда
		Элементы.ОшибочныйНомерДекларации.Видимость = Истина;
		Элементы.ОшибочныйНомерДекларации.Заголовок = УчетНДСКлиентСервер.ПояснениеКОшибкеВНомереДекларации(Форма.КодОшибки, Ложь, Объект.ЭтоРНПТ);
	Иначе
		Элементы.ОшибочныйНомерДекларации.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.ГруппаСведения.Видимость = Форма.ПроверкаРНПТВключена И Объект.ЭтоРНПТ И ЗначениеЗаполнено(Объект.Ссылка) И ЗначениеЗаполнено(Форма.ДатаПолученияСведений);
	
	Элементы.ОписаниеОшибки.Видимость = Форма.Ошибка;
	Элементы.ГруппаПолученныеСведения.Видимость = НЕ Форма.Ошибка;
	
КонецПроцедуры

&НаСервере
Процедура КодПриИзмененииНаСервере()
	
	Реквизиты = Справочники.НомераГТД.ПараметрыТаможеннойДекларации(ТекущийТекстНомераДекларации);
	
	Если ВедетсяУчетПрослеживаемыхТоваров И НЕ ЭтоКонтекстноеСоздание Тогда
		ЗаполнитьЗначенияСвойств(Объект, Реквизиты, "РегистрационныйНомер,СтранаВвозаНеРФ,ЭтоРНПТ,ЭтоНомерТД");
	Иначе
		ЗаполнитьЗначенияСвойств(Объект, Реквизиты, "РегистрационныйНомер,СтранаВвозаНеРФ");
		Если НЕ ЭтоКонтекстноеСоздание Тогда
			Объект.ЭтоРНПТ    = Ложь;
			Объект.ЭтоНомерТД = Истина;
		КонецЕсли;
	КонецЕсли;
	КодОшибки = УчетНДСКлиентСервер.НаличиеОшибокВНомереДекларации(
		ТекущийТекстНомераДекларации, НачалоКорректногоПериода, КонецКорректногоПериода, Объект.ЭтоРНПТ);
		
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовокФормы()
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 (создание)'"),
			?(ПолучитьФункциональнуюОпцию("ВестиУчетПрослеживаемыхТоваров") И Объект.ЭтоРНПТ, НСтр("ru = 'РНПТ'"), НСтр("ru = 'Номер ТД'")));
	Иначе
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 (%2)'"),
			Объект.Код,
			?(ПолучитьФункциональнуюОпцию("ВестиУчетПрослеживаемыхТоваров") И Объект.ЭтоРНПТ, НСтр("ru = 'РНПТ'"), НСтр("ru = 'номер ТД'")));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВывестиИнформациюОбОшибкахВНомере() Экспорт

	КодОшибки = УчетНДСКлиентСервер.НаличиеОшибокВНомереДекларации(
		ТекущийТекстНомераДекларации, НачалоКорректногоПериода, КонецКорректногоПериода);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти