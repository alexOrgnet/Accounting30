
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПоказыватьТолькоОстатки = Истина;
	ИмяТаблицы = Параметры.ИмяТаблицы;
	Список.Параметры.УстановитьЗначениеПараметра("Период",
		Новый Граница(КонецДня(Параметры.Период), ВидГраницы.Включая));
		
	Список.Параметры.УстановитьЗначениеПараметра("Номенклатура", Параметры.Номенклатура);
	Список.Параметры.УстановитьЗначениеПараметра("Организация",  Параметры.Организация);
	
	ВидыСубконто = Новый Массив;
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НомераГТД);
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтраныПроисхождения);

	Список.Параметры.УстановитьЗначениеПараметра("ВидыСубконто", ВидыСубконто);
	
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Номера таможенных деклараций: %1'"),
		Параметры.Номенклатура);
		
	УстановитьЗначениеПараметраПоказыватьТолькоОстатки(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	
	ОбновитьИнформационнуюНадписьПараметрыВыбора(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыбратьЗначение();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыбрать(Команда)
	
	ВыбратьЗначение();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьТолькоОстаткиПриИзменении(Элемент)
	
	УстановитьЗначениеПараметраПоказыватьТолькоОстатки(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьИнформационнуюНадписьПараметрыВыбора(Форма)

	ЭлементыОтбора = Форма.Список.Отбор.Элементы;

	ТекстНадписи = "";
	Для Каждого Элемент Из ЭлементыОтбора Цикл
		Если ЗначениеЗаполнено(Элемент.ПравоеЗначение) и Элемент.Использование Тогда
			ТекстНадписи = ТекстНадписи + Элемент.ПравоеЗначение + "; ";
		КонецЕсли;
	КонецЦикла;

	ТекстНадписи = Лев(ТекстНадписи, СтрДлина(ТекстНадписи) - 2);

	Форма.ИнформационнаяНадписьПараметровВыбора = ТекстНадписи;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗначениеПараметраПоказыватьТолькоОстатки(Форма)

	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(
		Форма.Список, 
		"КоличествоОстаток", 
		0, 
		Форма.ПоказыватьТолькоОстатки, 
		ВидСравненияКомпоновкиДанных.Больше
		);

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьЗначение()
	
	СтрокаПодбора = Элементы.Список.ТекущиеДанные;
	
	Если СтрокаПодбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеВыбора = Новый Структура;
	ДанныеВыбора.Вставить("НомерГТД", СтрокаПодбора.Ссылка);
	ДанныеВыбора.Вставить("СтранаПроисхождения", СтрокаПодбора.СтранаПроисхождения);
	
	ОповеститьОВыборе(ДанныеВыбора);
	
КонецПроцедуры

#КонецОбласти