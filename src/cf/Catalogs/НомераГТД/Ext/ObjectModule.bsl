
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЭтоГруппа Тогда
		Реквизиты = Справочники.НомераГТД.ПараметрыТаможеннойДекларации(Код);
		Если НЕ ЭтоРНПТ И НЕ ЭтоНомерТД Тогда
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, Реквизиты, "РегистрационныйНомер,СтранаВвозаНеРФ, ЭтоРНПТ, ЭтоНомерТД");
		Иначе
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, Реквизиты, "РегистрационныйНомер,СтранаВвозаНеРФ");
		КонецЕсли;
		Код = СокрЛП(Код);
	КонецЕсли;
	
	ИнтерактивнаяЗапись = ДополнительныеСвойства.Свойство("ИнтерактивнаяЗапись")
		И ДополнительныеСвойства.ИнтерактивнаяЗапись;
		
	// Актуализируем сведения в регистре "Сведения о РНПТ из ФНС" только при интерактивной перезаписи РНПТ.
	ДополнительныеСвойства.Вставить("НужноЗаписыватьВРегистр",
		Не ЭтоГруппа 
		И ПолучитьФункциональнуюОпцию("ВестиУчетПрослеживаемыхТоваров")
		И ЭтоРНПТ
		И НЕ ЭтоНовый()
		И ИнтерактивнаяЗапись);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ДополнительныеСвойства.Свойство("НужноЗаписыватьВРегистр") 
		И ДополнительныеСвойства.НужноЗаписыватьВРегистр Тогда
		СведенияОРНПТ = ПроверкаРНПТ.НовыйСведенияОРНПТ();
		СведенияОРНПТ.КодРНПТ               = Код;
		ПолучитьСведенияИзСервиса = Ложь;
		Если Не ПроверкаРНПТПереопределяемый.РНПТСоответствуетФормату(Код) Тогда
			СведенияОРНПТ.ОшибкаПроверкиРНПТ    = Истина;
			СведенияОРНПТ.ОписаниеОшибки        = ПроверкаРНПТ.ТекстРНПТНеНайден();
			СведенияОРНПТ.ДатаПолученияСведений = ТекущаяДатаСеанса();
		Иначе
			ПолучитьСведенияИзСервиса = Истина
		КонецЕсли;
		ПроверкаРНПТ.ЗаписатьСведенияОРНПТИзФНС(Ссылка, СведенияОРНПТ, Ложь, ПолучитьСведенияИзСервиса, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли


