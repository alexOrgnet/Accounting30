
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Отбор.Свойство("Налог") И ЗначениеЗаполнено(Параметры.Отбор.Налог) Тогда
		Налог = Параметры.Отбор.Налог;
	КонецЕсли;
	
	Элементы.ГруппаСоздать.Видимость = Не ЗначениеЗаполнено(Налог);
	Элементы.Создать.Видимость = ЗначениеЗаполнено(Налог);
	Элементы.Налог.Видимость = Не ЗначениеЗаполнено(Налог);
	
	АвтоЗаголовок = Ложь;
	
	Если Налог = Перечисления.ВидыИмущественныхНалогов.НалогНаИмущество Тогда
		Заголовок = Нстр("ru='Основания льгот по налогу на имущество'");
	ИначеЕсли Налог = Перечисления.ВидыИмущественныхНалогов.ТранспортныйНалог Тогда
		Заголовок = Нстр("ru='Основания льгот по транспортному налогу'");
	ИначеЕсли Налог = Перечисления.ВидыИмущественныхНалогов.ЗемельныйНалог Тогда
		Заголовок = Нстр("ru='Основания льгот по земельному налогу'");
	Иначе
		Заголовок = Нстр("ru='Основания льгот по имущественным налогам'");
	КонецЕсли;
	
	ПомеченныеНаУдалениеСервер.СкрытьПомеченныеНаУдаление(ЭтотОбъект);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа, Параметр)
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Если Копирование Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Налог) Тогда
		ОтборПоНалогу = ОтборыСписковКлиентСервер.ЭлементОтбораСпискаПоИмени(Список, "Налог");
		Если ТипЗнч(ОтборПоНалогу) = Тип("ЭлементОтбораКомпоновкиДанных")
			 И ОтборПоНалогу.Использование
			 И ОтборПоНалогу.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда
			Налог = ОтборПоНалогу.ПравоеЗначение;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Налог) Тогда
		ВызватьИсключение НСтр("ru='Не установлен отбор по налогу. Ввод новых элементов недоступен.'")
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Элементы.Список.Обновить();
	Элементы.Список.ТекущаяСтрока = ВыбранноеЗначение;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
    ПомеченныеНаУдалениеКлиент.ПриИзмененииСписка(ЭтотОбъект, Элемент);		
КонецПроцедуры

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки, ИспользуютсяСтандартныеНастройки)
    ПомеченныеНаУдалениеСервер.УдалитьОтборПометкаУдаления(Настройки);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьЛьготуНалогНаИмущество(Команда)
	СоздатьЛьготу(ПредопределенноеЗначение("Перечисление.ВидыИмущественныхНалогов.НалогНаИмущество"));
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЛьготуЗемельныйНалог(Команда)
	СоздатьЛьготу(ПредопределенноеЗначение("Перечисление.ВидыИмущественныхНалогов.ЗемельныйНалог"));
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СоздатьЛьготу(Налог)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", Новый Структура("Налог", Налог));
	ОткрытьФорму("Справочник.ОснованияЛьготПоИмущественнымНалогам.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект)

КонецПроцедуры

#КонецОбласти