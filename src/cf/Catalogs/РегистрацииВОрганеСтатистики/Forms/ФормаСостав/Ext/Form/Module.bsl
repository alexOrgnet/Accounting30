#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.ДатаАктуальности) Тогда
		ДатаАктуальности = Параметры.ДатаАктуальности;
	Иначе
		ДатаАктуальности = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Регистрация = Параметры.Регистрация;
	Заголовок = СтрШаблон(НСтр("ru = '%1 (Состав регистрации)'"), Строка(Регистрация));
	
	ЗаполнитьИспользование(ДатаАктуальности);
	
	Если МестаИспользования.ПолучитьЭлементы().Количество() Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаМестаИспользования;
		Элементы.МестаИспользования.Подсказка = СтрШаблон(НСтр("ru = 'Данные на %1'"), Формат(ДатаАктуальности, "ДФ=dd.MM.yyyy"));
		Элементы.МестаИспользования.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
	Иначе
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаНетИспользования;
		Элементы.ДекорацияНеИспользуется.Заголовок = СтрШаблон(НСтр("ru = 'Регистрация не используется на %1'"), Формат(ДатаАктуальности, "ДФ=dd.MM.yyyy"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура МестаИспользованияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элемент.ТекущиеДанные.Ссылка <> Неопределено Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(, Элемент.ТекущиеДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьИспользование(ДатаАктуальности)
	
	ИспользованиеРегистрации = РегистрацииВОрганеСтатистикиВызовСервера.МестаИспользованияРегистрацииВОрганеСтатистики(Регистрация, ДатаАктуальности);
	
	ЭлементыДерева = МестаИспользования.ПолучитьЭлементы();
	
	Для Каждого Элемент Из ИспользованиеРегистрации Цикл
		Группа = ЭлементыДерева.Добавить();
		Группа.Наименование = Элемент.Ключ;
		ЭлементыГруппы = Группа.ПолучитьЭлементы();
		Для Каждого Ссылка Из Элемент.Значение Цикл
			Элемент = ЭлементыГруппы.Добавить();
			Элемент.Ссылка = Ссылка;
			Элемент.Наименование = Строка(Ссылка);
		КонецЦикла;
	КонецЦикла;
	
	Элементы.МестаИспользования.НачальноеОтображениеДерева = НачальноеОтображениеДерева.РаскрыватьВсеУровни;
	
КонецПроцедуры

#КонецОбласти