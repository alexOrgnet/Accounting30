#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает событие ОС для переденного вида события, если оно единственное
//
// Параметры:
//    ВидСобытия - ПеречислениеСсылка.ВидыСобытийОС - Вид события основного средства
//
// Возвращаемое значение:
//    СправочникСсылка.СобытияОС - Подходящее событие
//
Функция СобытиеОСПоВиду(Знач ВидСобытия) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидСобытия", ВидСобытия);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 2
	|	СобытияОС.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СобытияОС КАК СобытияОС
	|ГДЕ
	|	СобытияОС.ВидСобытияОС = &ВидСобытия
	|	И НЕ СобытияОС.ПометкаУдаления";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Справочники.СобытияОС.ПустаяСсылка();
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Количество() > 1 Тогда
		// Не можем достоверно выбрать 1 элемент, поэтому не вернем ничего
		Возврат Справочники.СобытияОС.ПустаяСсылка();
	КонецЕсли;
	
	Выборка.Следующий();
	Возврат Выборка.Ссылка;
		
КонецФункции 

#Область ОбработчикиОбновления

// Процедура - обработчик обновления - создает новый элемент справочника События ОС
//
// Параметры:
//  Параметры - Структура - параметры обработчика обновления.
//
Процедура СоздатьПересмотрЭлементовАмортизации(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СобытияОС.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СобытияОС КАК СобытияОС
	|ГДЕ
	|	СобытияОС.ВидСобытияОС = ЗНАЧЕНИЕ(Перечисление.ВидыСобытийОС.ПересмотрЭлементовАмортизации)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Ложь;
	
	НаименованиеСтатьиПересмотрЭлементовАмортизации = НСтр(
		"ru='Пересмотр элементов амортизации'", 
		ОбщегоНазначения.КодОсновногоЯзыка());
	СобытиеОСОбъект = Справочники.СобытияОС.СоздатьЭлемент();
	СобытиеОСОбъект.ВидСобытияОС = Перечисления.ВидыСобытийОС.ПересмотрЭлементовАмортизации;
	СобытиеОСОбъект.Наименование = НаименованиеСтатьиПересмотрЭлементовАмортизации;
	
	Попытка
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(СобытиеОСОбъект);
		
	Исключение
		
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Справочники.СобытияОС,
			, // Данные неизвестны
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ОписаниеОшибки = Новый Массив;
		
		ОписаниеОшибки.Добавить(СтрШаблон(НСтр("ru = 'Не создано Событие ОС ""%1"".'"), НаименованиеСтатьиПересмотрЭлементовАмортизации));
		ОписаниеОшибки.Добавить(НСтр(
			"ru = 'Создайте Событие ОС.
			|В поле ""Вид события"" укажите ""Пересмотр элементов амортизации"".'"));
		ОписаниеОшибки.Добавить(НСтр("ru = 'Подробная информация записана в журнал регистрации.'"));
		
		ВызватьИсключение СтрСоединить(ОписаниеОшибки, Символы.ПС);
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли