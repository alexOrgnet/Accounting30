// Ширина формы установлена исходя из длины заголовка

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РедактированиеНастроек = Справочники.НастройкиУчетаЗатрат.ПрименитьПараметрыФормыНастроек(ЭтотОбъект);
	
	ЗаполнитьСодержимоеФормы(РедактированиеНастроек);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаOK(Команда)
	
	РезультатФормы = Неопределено;
	
	Если Модифицированность Тогда
		РезультатФормы = УстановитьВыполненныеНастройки(АдресРедактированиеНастроек, ИспользуетсяПлановаяСебестоимость);
	КонецЕсли;
	
	Закрыть(РезультатФормы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПлановаяСебестоимостьНедоступнаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ДокументыВыпускаБезПлановыхЦен" Тогда
		НастройкиУчетаКлиент.ПоказатьСписокДокументовБезПлановойСебестоимости(Период, Организация, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСодержимоеФормы(РедактированиеНастроек)
	
	ИспользуетсяПлановаяСебестоимость = (РедактированиеНастроек.Настройки.ВариантУчетаСтоимостиВыпуска =
		Перечисления.ВариантыУчетаСтоимостиВыпуска.ПредварительнаяСтоимость);
		
	НастроитьПлановаяСебестоимостьНедоступна();
	
	КлючСохраненияПоложенияОкна = Элементы.ПлановаяСебестоимостьНедоступна.Видимость;
	
КонецПроцедуры

// Плановая себестоимость недоступна, если уже введены документы без плановой себестоимости.
// Процедура настраивает форму, чтобы она отражала эту проблему.
//
&НаСервере
Процедура НастроитьПлановаяСебестоимостьНедоступна()
	
	Если ИспользуетсяПлановаяСебестоимость Или ТолькоПросмотр Тогда
		Элементы.ПлановаяСебестоимостьНедоступна.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	ДоступноВключениеПлановойСебестоимости = РегистрыСведений.УчетнаяПолитика.ДоступноВключениеПлановойСебестоимости(
		Организация,
		Период);
		
	Если ДоступноВключениеПлановойСебестоимости Тогда
		Элементы.ПлановаяСебестоимостьНедоступна.Видимость     = Ложь;
	Иначе
		ТолькоПросмотр = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция УстановитьВыполненныеНастройки(Знач АдресРедактированиеНастроек, Знач ИспользуетсяПлановаяСебестоимость)
	
	РедактированиеНастроек = Справочники.НастройкиУчетаЗатрат.НачатьУстановкуВыполненныхНастроек(АдресРедактированиеНастроек);
	
	ВариантУчета = ?(
		ИспользуетсяПлановаяСебестоимость,
		Перечисления.ВариантыУчетаСтоимостиВыпуска.ПредварительнаяСтоимость,
		Перечисления.ВариантыУчетаСтоимостиВыпуска.ФактическаяСтоимость);
		
	РедактированиеНастроек.Настройки.ВариантУчетаСтоимостиВыпуска = ВариантУчета;
	
	Возврат Справочники.НастройкиУчетаЗатрат.ЗавершитьУстановкуВыполненныхНастроек(
		АдресРедактированиеНастроек,
		РедактированиеНастроек);
	
КонецФункции

#КонецОбласти
