#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РедактированиеНастроек = Справочники.НастройкиУчетаЗатрат.ПрименитьПараметрыФормыНастроек(ЭтотОбъект, Счет);
	
	ЗаполнитьСодержимоеФормы(РедактированиеНастроек);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура АналитикаПодразделенияПриИзменении(Элемент)
	ПроверитьОграничениеКоличестваСубконто(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура АналитикаСтатьиЗатратПриИзменении(Элемент)
	ПроверитьОграничениеКоличестваСубконто(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура АналитикаНоменклатурныеГруппыПриИзменении(Элемент)
	ПроверитьОграничениеКоличестваСубконто(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура АналитикаПродукцияПриИзменении(Элемент)
	ПроверитьОграничениеКоличестваСубконто(ЭтотОбъект);
	УстановитьВидимостьПереходНЗПДетализацияПоПродукции();
КонецПроцедуры

&НаКлиенте
Процедура АналитикаШаблонПриИзменении(Элемент)
	ПроверитьОграничениеКоличестваСубконто(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаOK(Команда)
	
	РезультатФормы = Неопределено;
	
	Если Модифицированность Тогда
		
		Если Не ВыполненоОграничениеКоличестваСубконто(ЭтотОбъект) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				НСтр("ru = 'Отключите часть разрезов аналитики счета.
                     |Рассмотрите возможность использовать несколько счетов с разной аналитикой.'"),
				, // КлючДанных
				Элементы.ПревышеноКоличествоСубконто.Имя);
			Возврат;
		КонецЕсли;
		
		РедактируемыеЗначения = Новый Структура;
		
		Для Каждого ОписаниеРеквизита Из РедактируемыеРеквизиты Цикл
			АналитикаВключена = ЭтотОбъект[ОписаниеРеквизита.Значение];
			РедактируемыеЗначения.Вставить(ОписаниеРеквизита.Значение, АналитикаВключена);
		КонецЦикла;
		
		РезультатФормы = УстановитьВыполненныеНастройки(
			АдресРедактированиеНастроек,
			Счет,
			РедактируемыеЗначения,
			АдресРеквизитыАналитикаСчета,
			НуженПереходНЗПДетализацияПоПродукции);
		
	КонецЕсли;
	
	Закрыть(РезультатФормы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСодержимоеФормы(РедактированиеНастроек)
	
	УстановитьЗаголовокАналитикаСчета(Элементы.АналитикаСчетаЗаголовок, Счет);
	
	УстановитьЗаголовокИнформация(Элементы.АналитикаСчетаФлажки);
	
	УстановитьФлажкиАналитикаСчета(РедактированиеНастроек);
	
	УстановитьПодсказкиАналитикиСчета();
	
	ПроверитьОграничениеКоличестваСубконто(ЭтотОбъект);
	
	УстановитьВидимостьПереходНЗПДетализацияПоПродукции(Ложь);
	
	СтандартныеПодсистемыСервер.СброситьРазмерыИПоложениеОкна(ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьЗаголовокАналитикаСчета(НастраиваемоеПоле, Счет)
	
	НастраиваемоеПоле.Заголовок = СтрШаблон(
		НСтр("ru = 'Аналитика счета %1:'"),
		ПланыСчетов.Хозрасчетный.ПолноеПредставлениеСчета(Счет));
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьЗаголовокИнформация(НастраиваемоеПоле)
	
	ДокументыЗатрат = Новый Массив;
	ДокументыЗатрат.Добавить(Метаданные.Документы.ПоступлениеТоваровУслуг);
	ДокументыЗатрат.Добавить(Метаданные.Документы.ТребованиеНакладная);
	
	ПредставленияДокументов = Новый Массив;
	Для Каждого Документ Из ДокументыЗатрат Цикл
		ПредставленияДокументов.Добавить(Документ.Синоним);
	КонецЦикла;
	
	ШаблонЗаголовка = СтрШаблон(
		НСтр("ru = 'Значения аналитики указываются в документах учета затрат: %1 и других.
              |Добавить другие разрезы учета можно в <a href = %2>Плане счетов</a>'"),
		СтрСоединить(ПредставленияДокументов, НСтр("ru = ', '")),
		"e1cib/command/ОбщаяКоманда.ПланСчетов");
		
	НастраиваемоеПоле.РасширеннаяПодсказка.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(ШаблонЗаголовка);
	
КонецПроцедуры

&НаСервере
Функция УстановитьФлажкиАналитикаСчета(РедактированиеНастроек)
	
	АналитикаСчета = РедактированиеНастроек.Настройки.ОтображаемаяАналитика[Счет];
	
	РеквизитыАналитики = ПредопределенныеРеквизитыАналитики();
	
	ОтображаемыеРеквизиты  = Новый Массив;
	РеквизитыДляДобавления = Новый Массив;
	
	Для Каждого РазрезАналитики Из АналитикаСчета Цикл
		
		Реквизит = РеквизитыАналитики[РазрезАналитики];
		Если Реквизит = Неопределено Тогда
			Реквизит = "Аналитика" + XMLСтрока(РеквизитыДляДобавления.Количество());
			РеквизитыАналитики.Вставить(РазрезАналитики, Реквизит);
			РеквизитыДляДобавления.Добавить(Реквизит);
		КонецЕсли;
		
		ОтображаемыеРеквизиты.Добавить(Реквизит);
		
	КонецЦикла;
	
	Для Каждого ОписаниеАналитики Из НастраиваемаяАналитика(Счет) Цикл
		
		Реквизит = РеквизитыАналитики[ОписаниеАналитики.Ключ];
		
		Если ОтображаемыеРеквизиты.Найти(Реквизит) = Неопределено Тогда
			ОтображаемыеРеквизиты.Добавить(Реквизит);
		КонецЕсли;
		
		Если ОписаниеАналитики.Значение Тогда
			ЭтоСубконто = (ТипЗнч(ОписаниеАналитики.Ключ) = Тип("ПланВидовХарактеристикСсылка.ВидыСубконтоХозрасчетные"));
			РедактируемыеРеквизиты.Добавить(Реквизит, , ЭтоСубконто);
		КонецЕсли;
		
	КонецЦикла;
	
	ДобавитьРеквизитыАналитики(РеквизитыДляДобавления, РеквизитыАналитики);
	НастроитьВидимостьРеквизитовАналитики(РеквизитыАналитики, ОтображаемыеРеквизиты);
	
	Для Каждого РазрезАналитики Из АналитикаСчета Цикл
		Реквизит = РеквизитыАналитики[РазрезАналитики];
		ЭтотОбъект[Реквизит] = Истина;
	КонецЦикла;
	
	УстановитьОграничениеКоличестваАналитики(РедактированиеНастроек.Настройки.Аналитика[Счет]);
	
	АдресРеквизитыАналитикаСчета = ПоместитьВоВременноеХранилище(РеквизитыАналитики, УникальныйИдентификатор);
	
	Возврат ОтображаемыеРеквизиты;
	
КонецФункции

&НаСервере
Процедура ДобавитьРеквизитыАналитики(ИменаРеквизитов, РеквизитыАналитики)
	
	Если Не ЗначениеЗаполнено(ИменаРеквизитов) Тогда
		Возврат;
	КонецЕсли;
	
	// Реквизиты формы
	АналитикаРеквизитов = Новый Соответствие;
	Для Каждого ОписаниеАналитики Из РеквизитыАналитики Цикл
		АналитикаРеквизитов.Вставить(ОписаниеАналитики.Значение, ОписаниеАналитики.Ключ);
	КонецЦикла;
	
	ДобавляемыеРеквизиты = Новый Массив;
	Для Каждого ИмяРеквизита Из ИменаРеквизитов Цикл
		
		КлючАналитики = АналитикаРеквизитов[ИмяРеквизита];
		
		РеквизитФормы = Новый РеквизитФормы(ИмяРеквизита, Новый ОписаниеТипов("Булево"), , КлючАналитики, Истина);
		
		ДобавляемыеРеквизиты.Добавить(РеквизитФормы);
		
	КонецЦикла;
	
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	// Поля формы
	Для Каждого ИмяРеквизита Из ИменаРеквизитов Цикл
		
		НовыйЭлемент = Элементы.Вставить(
			ИмяРеквизита,
			ТипЗнч(Элементы.АналитикаШаблон),
			Элементы.АналитикаШаблон.Родитель,
			Элементы.АналитикаШаблон);
		
		ЗаполнитьЗначенияСвойств(НовыйЭлемент, Элементы.АналитикаШаблон, "Вид, ПоложениеЗаголовка, ТолькоПросмотр");
		
		НовыйЭлемент.ПутьКДанным = ИмяРеквизита;
		НовыйЭлемент.Видимость   = Истина;
		
		НовыйЭлемент.УстановитьДействие("ПриИзменении", Элементы.АналитикаШаблон.ПолучитьДействие("ПриИзменении"));
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьВидимостьРеквизитовАналитики(РеквизитыАналитики, ОтображаемыеРеквизиты)
	
	Для Каждого ОписаниеРеквизита Из РеквизитыАналитики Цикл
		
		ИмяПоля = ОписаниеРеквизита.Значение;
		
		Видимость = (ОтображаемыеРеквизиты.Найти(ИмяПоля) <> Неопределено);
		
		Элементы[ИмяПоля].Видимость = Видимость;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОграничениеКоличестваАналитики(АналитикаСчета)
	
	НастраиваемаяАналитика = НастраиваемаяАналитика(Счет);
	
	ОграничениеКоличестваСубконто = БухгалтерскийУчет.МаксимальноеКоличествоСубконто();
	Для Каждого КлючАналитики Из АналитикаСчета Цикл
		
		Если ТипЗнч(КлючАналитики) <> Тип("ПланВидовХарактеристикСсылка.ВидыСубконтоХозрасчетные") Тогда
			// Не ограничено числом субконто
			Продолжить;
		КонецЕсли;
		
		Если НастраиваемаяАналитика[КлючАналитики] <> Неопределено Тогда
			// Пользователь может уложиться в ограничение, отключив аналитику
			Продолжить;
		КонецЕсли;
		
		ОграничениеКоличестваСубконто = ОграничениеКоличестваСубконто - 1;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьПереходНЗПДетализацияПоПродукции(Видимость = Неопределено)
	
	Если Видимость <> Неопределено Тогда
		Элементы.ГруппаПереходНЗПДетализацияПоПродукции.Видимость = Видимость;
		Возврат;
	КонецЕсли;
	
	РедактированиеНастроек = ПолучитьИзВременногоХранилища(АдресРедактированиеНастроек);
	Закрытие = РедактированиеНастроек.Настройки.Закрытие[Счет];
	Если Закрытие.НезавершенноеПроизводство = Перечисления.ВариантыУчетаНезавершенногоПроизводства.ВедетсяБезИнвентаризации
		И ПереходНЗПДетализацияПоПродукции.ВозможенПереходНЗПДетализацияПоПродукции(РедактированиеНастроек.Настройки, Счет, Закрытие.НезавершенноеПроизводство, АналитикаПродукция) Тогда
		НуженПереходНЗПДетализацияПоПродукции = АналитикаПродукция;
	Иначе
		НуженПереходНЗПДетализацияПоПродукции = Ложь;
	КонецЕсли;
	
	Элементы.ГруппаПереходНЗПДетализацияПоПродукции.Видимость = НуженПереходНЗПДетализацияПоПродукции;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПредопределенныеРеквизитыАналитики()
	
	// Описывает возможности формы по отрисовке предопределенных разрезов (наличие флажков на форме)
	
	Реквизиты = Новый Соответствие;
	
	Реквизиты.Вставить("Подразделение",                                                       "АналитикаПодразделения");
	Реквизиты.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат,         "АналитикаСтатьиЗатрат");
	Реквизиты.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы, "АналитикаНоменклатурныеГруппы");
	Реквизиты.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Продукция,            "АналитикаПродукция");
	
	Возврат Реквизиты;
	
КонецФункции

&НаСервереБезКонтекста
Функция НастраиваемаяАналитика(Счет)
	
	Аналитика = Новый Соответствие; // Ключ - аналитика; Значение - Булево: Истина, если редактируется непосредственно в этой форме
	Аналитика.Вставить("Подразделение", Ложь); // См. УстановитьПодсказкиАналитикиСчета
	
	НастраиваемаяАналитика = Справочники.НастройкиУчетаЗатрат.НастраиваемаяАналитика();
	НастраиваемаяАналитикаСчета = НастраиваемаяАналитика[Счет];
	
	Если ТипЗнч(НастраиваемаяАналитикаСчета) <> Тип("Массив") Тогда
		Возврат Аналитика;
	КонецЕсли;
	
	Для Каждого РазрезАналитики Из НастраиваемаяАналитикаСчета Цикл
		Аналитика.Вставить(РазрезАналитики, Истина);
	КонецЦикла;
	
	Возврат Аналитика;
	
КонецФункции

&НаСервере
Процедура УстановитьПодсказкиАналитикиСчета()
	
	Если ПолучитьФункциональнуюОпцию("ВестиУчетПоПодразделениям") Тогда
		Текст   = НСтр("ru = 'В <a href = %1>Функциональности программы</a> включен учет операций обособленных подразделений, поэтому учет затрат также ведется в разрезе подразделений'");
		Команда = "e1cib/command/ОбщаяКоманда.Функциональность";
	Иначе
		Текст   = "Детализацию учета затрат по подразделениям можно настроить в <a href = %1>Плане счетов</a>";
		Команда = "e1cib/command/ОбщаяКоманда.ПланСчетов";
	КонецЕсли;
	
	Элементы.АналитикаПодразделения.РасширеннаяПодсказка.Заголовок =
		СтроковыеФункции.ФорматированнаяСтрока(СтрШаблон(Текст, Команда));
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПроверитьОграничениеКоличестваСубконто(Форма)
	
	Форма.Элементы.ПревышеноКоличествоСубконто.Видимость = Не ВыполненоОграничениеКоличестваСубконто(Форма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ВыполненоОграничениеКоличестваСубконто(Форма)
	
	ОстатокОграничения = Форма.ОграничениеКоличестваСубконто;
	
	Для Каждого ОписаниеРеквизита Из Форма.РедактируемыеРеквизиты Цикл
		
		АналитикаВключена = Форма[ОписаниеРеквизита.Значение];
		
		ЭтоСубконто = ОписаниеРеквизита.Пометка;
		Если АналитикаВключена И ЭтоСубконто Тогда
			ОстатокОграничения = ОстатокОграничения - 1;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ОстатокОграничения >= 0;
	
КонецФункции

&НаСервереБезКонтекста
Функция УстановитьВыполненныеНастройки(Знач АдресРедактированиеНастроек, Знач Счет, Знач РедактируемыеЗначения, Знач АдресРеквизитыАналитикаСчета, ИзменитьНЗП = Ложь)
	
	РедактированиеНастроек = Справочники.НастройкиУчетаЗатрат.НачатьУстановкуВыполненныхНастроек(АдресРедактированиеНастроек);
	
	НастройкиСчета = Новый Массив;
	НастройкиСчета.Добавить(РедактированиеНастроек.Настройки.Аналитика[Счет]);
	НастройкиСчета.Добавить(РедактированиеНастроек.Настройки.ОтображаемаяАналитика[Счет]);
	
	РеквизитыАналитики = ПолучитьИзВременногоХранилища(АдресРеквизитыАналитикаСчета);
	Для Каждого ОписаниеРеквизита Из РеквизитыАналитики Цикл
		
		ИмяРеквизита = ОписаниеРеквизита.Значение;
		Если Не РедактируемыеЗначения.Свойство(ИмяРеквизита) Тогда
			Продолжить;
		КонецЕсли;
		
		КлючАналитики = ОписаниеРеквизита.Ключ;
		
		Для Каждого Настройка Из НастройкиСчета Цикл
			Если РедактируемыеЗначения[ИмяРеквизита] Тогда
				Если Настройка.Найти(КлючАналитики) = Неопределено Тогда
					Настройка.Добавить(КлючАналитики);
				КонецЕсли;
			Иначе
				ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(Настройка, КлючАналитики);
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;

	НезавершенноеПроизводство = РедактированиеНастроек.Настройки.Закрытие[Счет].НезавершенноеПроизводство;
	Если ИзменитьНЗП Тогда
		НезавершенноеПроизводство = Перечисления.ВариантыУчетаНезавершенногоПроизводства.НеВедется;
		ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(
			РедактированиеНастроек.Настройки.КомбинированныеНастройкиНезавершенноеПроизводство,
			Счет);
	КонецЕсли;
	
	Если НезавершенноеПроизводство = Перечисления.ВариантыУчетаНезавершенногоПроизводства.ВедетсяПоРезультатамИнвентаризации Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(
			РедактированиеНастроек.Настройки.АналитикаНезавершенногоПроизводства[Счет],
			ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Продукция);
	КонецЕсли;
	
	Возврат Справочники.НастройкиУчетаЗатрат.ЗавершитьУстановкуВыполненныхНастроек(
		АдресРедактированиеНастроек,
		РедактированиеНастроек);
	
КонецФункции

#КонецОбласти
