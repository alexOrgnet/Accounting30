#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РедактированиеНастроек = Справочники.НастройкиУчетаЗатрат.ПрименитьПараметрыФормыНастроек(ЭтотОбъект);
	
	ЗаполнитьСодержимоеФормы(РедактированиеНастроек);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаOK(Команда)
	
	РезультатФормы = Неопределено;
	
	Если Модифицированность Тогда
		РезультатФормы = УстановитьВыполненныеНастройки(АдресРедактированиеНастроек, ВариантУчета);
	КонецЕсли;
	
	Закрыть(РезультатФормы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСодержимоеФормы(РедактированиеНастроек)
	
	Если РедактированиеНастроек.Настройки.ВариантУчетаСтоимостиВыпуска = Перечисления.ВариантыУчетаСтоимостиВыпуска.ПлановаяСтоимость Тогда
		ВариантУчета = РедактированиеНастроек.Настройки.ВариантУчетаСтоимостиВыпуска;
	Иначе
		ВариантУчета = Перечисления.ВариантыУчетаСтоимостиВыпуска.ОтклоненияСтоимости;
	КонецЕсли;
	
	ИмяВариантУчета = ОбщегоНазначения.ИмяЗначенияПеречисления(ВариантУчета);
	ТекущийЭлемент = Элементы[ИмяВариантУчета];
	
	ИспользуетсяСубконтоПродукция = Перечисления.ВариантыУчетаСтоимостиВыпуска.ИспользоватьСубконтоПродукция(
		Перечисления.ВариантыУчетаСтоимостиВыпуска.ОтклоненияСтоимости);
		
	Элементы.СубконтоПродукция.Видимость = Не ИспользуетсяСубконтоПродукция;
	
	КлючСохраненияПоложенияОкна = XMLСтрока(ИспользуетсяСубконтоПродукция);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция УстановитьВыполненныеНастройки(Знач АдресРедактированиеНастроек, Знач ВариантУчета)
	
	РедактированиеНастроек = Справочники.НастройкиУчетаЗатрат.НачатьУстановкуВыполненныхНастроек(АдресРедактированиеНастроек);
	
	РедактированиеНастроек.Настройки.ВариантУчетаСтоимостиВыпуска = ВариантУчета;
	
	Возврат Справочники.НастройкиУчетаЗатрат.ЗавершитьУстановкуВыполненныхНастроек(
		АдресРедактированиеНастроек,
		РедактированиеНастроек);
	
КонецФункции

#КонецОбласти
