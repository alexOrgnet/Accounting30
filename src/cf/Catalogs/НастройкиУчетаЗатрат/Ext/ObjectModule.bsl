#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Устанавливает наименование элемента справочника из контекста его применения.
// Следует применять, если каждой записи учетной политики соответствует отдельный элемент справочника.
//
// Параметры:
//  ЗаписьУчетнойПолитики - РегистрСведенийМенеджерЗаписи.УчетнаяПолитика - учетная политика, частью которой является настройка
//
Процедура УстановитьНаименованиеПоКонтексту(ЗаписьУчетнойПолитики) Экспорт
	
	Если Не ЗначениеЗаполнено(ЗаписьУчетнойПолитики.Организация) И Не ЗначениеЗаполнено(ЗаписьУчетнойПолитики.Период) Тогда
		Возврат;
	КонецЕсли;
	
	НаименованиеПоКонтексту = СтрШаблон(
		НСтр("ru = '%1, %2'", ОбщегоНазначения.КодОсновногоЯзыка()),
		Формат(ЗаписьУчетнойПолитики.Период, "ДЛФ=D"),
		ЗаписьУчетнойПолитики.Организация);
		
	Если НаименованиеПоКонтексту = Наименование Тогда
		Возврат;
	КонецЕсли;
	
	Наименование = НаименованиеПоКонтексту;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	НастройкиЗаполнения = Неопределено;
	
	Если ДанныеЗаполнения = Неопределено Тогда
		НастройкиЗаполнения = Справочники.НастройкиУчетаЗатрат.НовыйБазовыеНастройкиУчетаЗатрат();
		Справочники.НастройкиУчетаЗатрат.ЗаполнитьПоУмолчанию(НастройкиЗаполнения);
	ИначеЕсли Справочники.НастройкиУчетаЗатрат.ЭтоНастройкиУчетаЗатрат(ДанныеЗаполнения) Тогда
		НастройкиЗаполнения = ДанныеЗаполнения;
	КонецЕсли;
	
	Если НастройкиЗаполнения <> Неопределено Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ЗаполнитьЗначенияСвойств(
			ЭтотОбъект,
			НастройкиЗаполнения,
			"УстановленыВручную, ОсновнойСчетУчета, ВариантУчетаСтоимостиВыпуска");
		
		ПланСчетов.Очистить();
		
		Для Каждого Счет Из НастройкиЗаполнения.СчетаУчета Цикл
			
			ПланСчетов.Добавить().Счет = Счет;
			
		КонецЦикла;
		
		Справочники.НастройкиУчетаЗатрат.Сохранить(ЭтотОбъект, НастройкиЗаполнения);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ИспользоватьПлановуюСебестоимость = Перечисления.ВариантыУчетаСтоимостиВыпуска.ИспользоватьПлановуюСебестоимость(
		ВариантУчетаСтоимостиВыпуска);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
