
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.КоманднаяПанельФормы;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ИсключаемыеИменаОтборов = Новый Массив;
	ПрименитьПараметрыФормы(ИсключаемыеИменаОтборов);
	
	ИсключаемыеИмена = СтрСоединить(ИсключаемыеИменаОтборов, ",");
	ЗарплатаКадры.ПриСозданииНаСервереФормыСДинамическимСписком(ЭтотОбъект, "Список", , , , ИсключаемыеИмена);
	
	ПомеченныеНаУдалениеСервер.СкрытьПомеченныеНаУдаление(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки)
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, Настройки);
	
	ПомеченныеНаУдалениеСервер.УдалитьОтборПометкаУдаления(Настройки);
	
КонецПроцедуры

&НаСервере
Процедура СписокПриОбновленииСоставаПользовательскихНастроекНаСервере(СтандартнаяОбработка)
	
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, , СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	
	ПомеченныеНаУдалениеКлиент.ПриИзмененииСписка(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаСервере
Процедура ПрименитьПараметрыФормы(ИсключаемыеИменаОтборов)
	// Свойства формы.
	Если УстановитьСвойствоФормыПоПараметру("Заголовок") Тогда
		АвтоЗаголовок = Ложь;
	КонецЕсли;
	Если Параметры.РежимВыбора Тогда
		РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	КонецЕсли;
	
	// Свойства списка.
	УстановитьСвойствоСпискаПоПараметруФормы("РежимВыбора");
	УстановитьСвойствоСпискаПоПараметруФормы("МножественныйВыбор");
	УстановитьСвойствоСпискаПоПараметруФормы("ТекущаяСтрока");
	
	Фильтры = Параметры.Отбор;
	Если Фильтры.Свойство("ГоловнаяОрганизация") Тогда
		Если ТипЗнч(Фильтры.ГоловнаяОрганизация) = Тип("СправочникСсылка.Организации")
			И ЗначениеЗаполнено(Фильтры.ГоловнаяОрганизация) Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				Список,
				"Организация.ГоловнаяОрганизация",
				Фильтры.ГоловнаяОрганизация,
				,
				,
				,
				РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный);
			ИсключаемыеИменаОтборов.Добавить("Организация");
		КонецЕсли;
		Фильтры.Удалить("ГоловнаяОрганизация");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойствоСпискаПоПараметруФормы(ИмяПараметра)
	Если Параметры.Свойство(ИмяПараметра) И ЗначениеЗаполнено(Параметры[ИмяПараметра]) Тогда
		Элементы.Список[ИмяПараметра] = Параметры[ИмяПараметра];
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция УстановитьСвойствоФормыПоПараметру(ИмяПараметра)
	Если Параметры.Свойство(ИмяПараметра) И ЗначениеЗаполнено(Параметры[ИмяПараметра]) Тогда
		ЭтотОбъект[ИмяПараметра] = Параметры[ИмяПараметра];
		Возврат Истина;
	КонецЕсли;
	Возврат Ложь;
КонецФункции

#КонецОбласти
