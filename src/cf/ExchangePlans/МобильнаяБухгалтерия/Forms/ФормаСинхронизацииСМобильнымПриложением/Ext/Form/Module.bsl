#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЕстьОграничения = МобильнаяБухгалтерия.ИспользованиеМобильнойБухгалтерииОграниченоТарифом();
	
	Элементы.ГруппаБаннерТарифа.Видимость = ЕстьОграничения;
	Элементы.ГруппаУстановка.Видимость = НЕ ЕстьОграничения;
	
	Если ЕстьОграничения Тогда
		ВывестиБаннерТарифа();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ФормаОплатыТарифа" Тогда
		СтандартнаяОбработка = Ложь;
		ТарификацияБПКлиент.ОткрытьФормуОплатыСервиса(УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура СтраницаМобильноеПриложениеAndroid(Команда)
	
	АдресСтраницы = "https://play.google.com/store/apps/details?id=com.e1c.MobileAccounting";
		
	ПерейтиПоНавигационнойСсылке(АдресСтраницы);
	
КонецПроцедуры

&НаКлиенте
Процедура СтраницаМобильноеПриложениеiOS(Команда)
	
	АдресСтраницы = "https://apps.apple.com/ru/app/id1210606612";
		
	ПерейтиПоНавигационнойСсылке(АдресСтраницы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура ВывестиБаннерТарифа()
	
	РодительскийЭлемент = Элементы.ГруппаБаннерТарифа;
	
	ИмяБаннера = "ГруппаБаннерТарифаМобильноеПриложение";
	ЭлементБаннера = Элементы.Добавить(ИмяБаннера, Тип("ГруппаФормы"), РодительскийЭлемент);
	ЭлементБаннера.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ЭлементБаннера.ОтображатьЗаголовок = Ложь;
	ЭлементБаннера.Отображение = ОтображениеОбычнойГруппы.СлабоеВыделение;
	ЭлементБаннера.ЦветТекстаЗаголовка = ЦветаСтиля.ЦветТекстаФормы;
	ЭлементБаннера.ЦветФона = ФункциональностьПрограммы.ЦветБаннераНедоступенТариф();
	
	ЭлементОписанияБаннера = Элементы.Добавить(ИмяБаннера + "Описание", Тип("ДекорацияФормы"), ЭлементБаннера);
	ЭлементОписанияБаннера.АвтоМаксимальнаяШирина = Ложь;
	ЭлементОписанияБаннера.Ширина = 50;
	ЭлементОписанияБаннера.МаксимальнаяШирина = 50;
	
	ОписаниеФункциональностиТарифа = Новый Массив;
	ОписаниеФункциональностиТарифа.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='На текущем тарифе мобильное приложение недоступно.'")));
	ОписаниеФункциональностиТарифа.Добавить(Символы.ПС);
	ОписаниеФункциональностиТарифа.Добавить(Новый ФорматированнаяСтрока(
		НСтр("ru='Перейдите на другой тариф'"), , , , "ФормаОплатыТарифа"));
	ОписаниеФункциональностиТарифа.Добавить(Новый ФорматированнаяСтрока(НСтр("ru=', чтобы использовать больше возможностей'")));
	
	ЭлементОписанияБаннера.Заголовок = Новый ФорматированнаяСтрока(ОписаниеФункциональностиТарифа);
	
КонецПроцедуры

#КонецОбласти
