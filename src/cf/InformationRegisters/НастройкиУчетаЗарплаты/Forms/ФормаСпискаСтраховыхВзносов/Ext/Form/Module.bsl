
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Отбор.Свойство("Организация", Организация) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			ЭтаФорма.СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев, "Организация", Организация, ВидСравненияКомпоновкиДанных.Равно, , Истина,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	КонецЕсли;
	Если Параметры.Отбор.Свойство("ГоловнаяОрганизация", ГоловнаяОрганизация) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			ЭтаФорма.ПрименяемыеТарифыСтраховыхВзносов, "ГоловнаяОрганизация", ГоловнаяОрганизация, ВидСравненияКомпоновкиДанных.Равно, , Истина,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	КонецЕсли;
	
	Если Организация = ГоловнаяОрганизация Тогда
		Элементы.ПрименяемыеТарифыСтраховыхВзносовГоловнаяОрганизация.Видимость = Ложь;
	Иначе
		Элементы.ПрименяемыеТарифыСтраховыхВзносов.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		Если УчетнаяПолитика.ПрименяетсяАУСН(Организация, ТекущаяДатаСеанса()) Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы,
				"СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев",
				"ИзменятьСоставСтрок",
				Ложь);
		Иначе
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы,
				"СтраницаРазмерФиксированныхВзносовОтНесчастныхСлучаев",
				"Видимость",
				Ложь);		
		КонецЕсли;
	КонецЕсли; 	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеУчетнойПолитики" Тогда
		Элементы.ПрименяемыеТарифыСтраховыхВзносов.Обновить();
		Элементы.СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
