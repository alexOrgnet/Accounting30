
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

// Возвращает признак того, что элементы балансовой стоимости нематериальных активов при переходе на ФСБУ 14
//  определяются остатками на счетах регистра бухгалтерии и вручную не корректируются.
// 
// Параметры:
//  Организация - СправочникСсылка.Организации
// 
// Возвращаемое значение:
//  Булево - Стоимость определяется по данным учета
//
Функция СтоимостьОпределяетсяПоДаннымУчета(Организация) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СпособПереходаНаФСБУ14.СтоимостьОпределяетсяПоДаннымУчета КАК СтоимостьОпределяетсяПоДаннымУчета
	|ИЗ
	|	РегистрСведений.СпособПереходаНаФСБУ14 КАК СпособПереходаНаФСБУ14
	|ГДЕ
	|	СпособПереходаНаФСБУ14.Организация = &Организация";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Выборка.СтоимостьОпределяетсяПоДаннымУчета;
	
КонецФункции

// Сохраняет способ определения стоимости НМА при переходе на ФСБУ 14.
// 
// Параметры:
//  Организация - СправочникСсылка.Организации
//  СтоимостьОпределяетсяПоДаннымУчета - Булево
//
Процедура ЗаписатьСпособОпределенияСтоимости(Организация, СтоимостьОпределяетсяПоДаннымУчета) Экспорт
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Организация.Установить(Организация);
	Запись = НаборЗаписей.Добавить();
	Запись.Организация = Организация;
	Запись.СтоимостьОпределяетсяПоДаннымУчета = СтоимостьОпределяетсяПоДаннымУчета;
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
