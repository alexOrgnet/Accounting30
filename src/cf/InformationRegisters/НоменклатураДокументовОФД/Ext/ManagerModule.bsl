
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция СопоставлениеНоменклатурыПоДокументу(СписокДокументов) Экспорт
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("СписокДокументов", СписокДокументов);
	
	ТекстыЗапросов = Новый Массив;
	ТекстыЗапросов.Добавить(
	"ВЫБРАТЬ
	|	ОтчетОРозничныхПродажах.Организация КАК Организация,
	|	ОтчетОРозничныхПродажах.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_ДокументыЗагрузки
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах
	|ГДЕ
	|	ОтчетОРозничныхПродажах.Ссылка В(&СписокДокументов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураДокументовОФД.ДокументЗагрузки КАК ДокументЗагрузки,
	|	НоменклатураДокументовОФД.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	НоменклатураДокументовОФД.ТабличнаяЧасть КАК ТабличнаяЧасть,
	|	НоменклатураДокументовОФД.НоменклатураНаименование КАК НоменклатураНаименование,
	|	НоменклатураДокументовОФД.Штрихкод КАК Штрихкод
	|ПОМЕСТИТЬ ВТ_НоменклатураЧековДокументов
	|ИЗ
	|	ВТ_ДокументыЗагрузки КАК ВТ_ДокументыЗагрузки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураДокументовОФД КАК НоменклатураДокументовОФД
	|		ПО ВТ_ДокументыЗагрузки.Организация = НоменклатураДокументовОФД.Организация
	|			И ВТ_ДокументыЗагрузки.Ссылка = НоменклатураДокументовОФД.ДокументЗагрузки
	|ГДЕ
	|	НЕ НоменклатураДокументовОФД.ДокументЗагрузки ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_НоменклатураЧековДокументов.НоменклатураНаименование КАК Наименование,
	|	ВТ_НоменклатураЧековДокументов.Штрихкод КАК Штрихкод,
	|	ВТ_НоменклатураЧековДокументов.ИдентификаторСтроки КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_НоменклатураЧеков
	|ИЗ
	|	ВТ_НоменклатураЧековДокументов КАК ВТ_НоменклатураЧековДокументов");
	ТекстыЗапросов.Добавить(РегистрыСведений.СопоставлениеНоменклатурыОФД.ТекстЗапросаПоискПодходящего());
	ТекстыЗапросов.Добавить(
	"ВЫБРАТЬ
	|	ВТ_НоменклатураЧековДокументов.ДокументЗагрузки КАК ДокументЗагрузки,
	|	ВТ_НоменклатураЧековДокументов.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ВТ_НоменклатураЧековДокументов.ТабличнаяЧасть КАК ИмяТабличнойЧасти,
	|	ЕСТЬNULL(ВТ_СопоставленнаяНоменклатура.НоменклатураСсылка, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК НоменклатураСсылка,
	|	ВТ_НоменклатураЧековДокументов.НоменклатураНаименование КАК НоменклатураНаименование,
	|	НЕ ВТ_СопоставленнаяНоменклатура.НоменклатураСсылка ЕСТЬ NULL КАК НоменклатураСопоставлена
	|ИЗ
	|	ВТ_НоменклатураЧековДокументов КАК ВТ_НоменклатураЧековДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СопоставленнаяНоменклатура КАК ВТ_СопоставленнаяНоменклатура
	|		ПО ВТ_НоменклатураЧековДокументов.ИдентификаторСтроки = ВТ_СопоставленнаяНоменклатура.ИдентификаторСтроки");
	
	Запрос.Текст = СтрСоединить(ТекстыЗапросов, ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета());
	
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли

