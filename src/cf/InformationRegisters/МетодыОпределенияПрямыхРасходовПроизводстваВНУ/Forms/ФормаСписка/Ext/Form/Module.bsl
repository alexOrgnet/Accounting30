
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Отбор.Свойство("Организация") И Параметры.Отбор.Свойство("ПериодДействия") Тогда
		ОтборОрганизация = Параметры.Отбор.Организация;
		ОтборПериодДействия = Параметры.Отбор.ПериодДействия;
	Иначе
		Элементы.ОтборОрганизация.Видимость = Ложь;
		Элементы.ОтборПериодДействия.Видимость = Ложь;
		Элементы.ЗаполнитьПоУмолчанию.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.ОтборОрганизация.Видимость = Справочники.Организации.ИспользуетсяНесколькоОрганизаций();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПослеУдаления(Элемент)
	Оповестить("ИзмененыЗаписиРегистраМетодовОпределенияПрямыхРасходовНУ");
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьПоУмолчанию(Команда)
	
	СостояниеЗаполненияРегистра = ПроверитьСостояниеРегистра(ОтборОрганизация, ОтборПериодДействия);
	
	Если СостояниеЗаполненияРегистра = "Заполнен" Тогда
		Оповещение = Новый ОписаниеОповещения("ЗаполнитьПоУмолчаниюЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Существующие записи будут замещены значениями, соответствующими рекомендациям ст. 318 НК РФ.
		|
		|Продолжить?'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	ИначеЕсли СостояниеЗаполненияРегистра = "НеЗаполнен" Тогда
		ЗаполнитьПоУмолчаниюНаСервере();
		Оповестить("ИзмененыЗаписиРегистраМетодовОпределенияПрямыхРасходовНУ");
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнитьПоУмолчаниюЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаполнитьПоУмолчаниюНаСервере();
		Оповестить("ИзмененыЗаписиРегистраМетодовОпределенияПрямыхРасходовНУ");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоУмолчаниюНаСервере()
	
	РегистрыСведений.МетодыОпределенияПрямыхРасходовПроизводстваВНУ.Настроить(ОтборОрганизация, ОтборПериодДействия);
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПроверитьСостояниеРегистра(Организация, Период)
	
	Возврат РегистрыСведений.МетодыОпределенияПрямыхРасходовПроизводстваВНУ.СостояниеЗаполнения(Организация, Период);
	
КонецФункции

#КонецОбласти

