#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НастройкиУчетаФормаСписка.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	Если Параметры.Отбор.Свойство("ОсновноеСредство") И ЗначениеЗаполнено(Параметры.Отбор.ОсновноеСредство) Тогда
		Элементы.ОсновноеСредство.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	НастройкиУчетаФормаСпискаКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПослеУдаления(Элемент)
	
	ОбновитьИнтерфейс();
	
	Оповестить("ИзмененаИнформацияОС");
	
КонецПроцедуры

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки)
	
	НастройкиУчетаФормаСписка.СписокПередЗагрузкойПользовательскихНастроекНаСервере(ЭтотОбъект, Элемент, Настройки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	Для каждого Строка Из Строки Цикл
	
		ДанныеСтроки = Строка.Значение.Данные;
		
		ПорядокНалогообложенияСтрокой = "";
		
		Если ДанныеСтроки.НеПодлежитНалогообложению Тогда
			ПорядокНалогообложенияСтрокой = Нстр("ru='Не подлежит налогообложению'");
		ИначеЕсли ДанныеСтроки.ВидЛьготы = Перечисления.ВидыЛьготПоИмущественнымНалогам.Освобождение Тогда 				
			ПорядокНалогообложенияСтрокой = Нстр("ru='Освобождается от налогообложения'");
		ИначеЕсли ДанныеСтроки.ВидЛьготы = Перечисления.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиНаПроцент 
			 ИЛИ ДанныеСтроки.ВидЛьготы = Перечисления.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиДоУказанной Тогда 				
			ПорядокНалогообложенияСтрокой = СтрШаблон(Нстр("ru='Облагается по сниженной ставке %1%%'"), ДанныеСтроки.СниженнаяНалоговаяСтавка);
		ИначеЕсли ДанныеСтроки.ВидЛьготы = Перечисления.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаПроцент Тогда 				
			ПорядокНалогообложенияСтрокой = СтрШаблон(Нстр("ru='Сумма налога снижена на %1%%'"), ДанныеСтроки.ПроцентУменьшения);
		ИначеЕсли ДанныеСтроки.ВидЛьготы = Перечисления.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаСумму Тогда 				
			ПорядокНалогообложенияСтрокой = СтрШаблон(Нстр("ru='Сумма налога снижена на %1 руб.'"), Формат(ДанныеСтроки.СуммаУменьшения, "ЧН=;"));
		ИначеЕсли ДанныеСтроки.ВидЛьготы = Перечисления.ВидыЛьготПоИмущественнымНалогам.СнижениеНалоговойБазыНаСумму Тогда 				
			ПорядокНалогообложенияСтрокой = СтрШаблон(Нстр("ru='Необлагаемая кадастровая стоимость %1 руб.'"), Формат(ДанныеСтроки.НеоблагаемаяКадастроваяСтоимость, "ЧН=;"));
		Иначе
			ПорядокНалогообложенияСтрокой = Нстр("ru='Особые льготы не установлены'");
		КонецЕсли;	
		
		ТекстПорядокНалогообложения = Строка.Значение.Оформление.Получить("ПорядокНалогообложения").НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Текст"));
		ТекстПорядокНалогообложения.Значение = ПорядокНалогообложенияСтрокой;
		
	КонецЦикла; 

КонецПроцедуры

#КонецОбласти
