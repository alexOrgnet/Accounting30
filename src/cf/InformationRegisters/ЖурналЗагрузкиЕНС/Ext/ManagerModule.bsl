#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

Процедура ПолучитьЗапись(ПараметрыЗаписи) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписи = РегистрыСведений.ЖурналЗагрузкиЕНС.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Организация    = ПараметрыЗаписи.Организация;
	МенеджерЗаписи.ДатаВыполнения = ПараметрыЗаписи.ДатаВыполнения;
	МенеджерЗаписи.Прочитать();
	
	ДанныеИзХранилища = МенеджерЗаписи.Содержание.Получить();
	ПоместитьВоВременноеХранилище(ДанныеИзХранилища, ПараметрыЗаписи.АдресХранилища);
	
	ПараметрыЗаписи.ИмяФайла = СтрШаблон(ШаблонИмениФайлаЗаписи(),
		СтрЗаменить(Формат(ПараметрыЗаписи.ДатаВыполнения, "ДЛФ=D"), ".", ""),
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыЗаписи.Организация, "ИНН"));
	
КонецПроцедуры

Функция ПараметрыПолученияЗаписи() Экспорт
	
	ПараметрыЗаписи = Новый Структура();
	ПараметрыЗаписи.Вставить("Организация");
	ПараметрыЗаписи.Вставить("ДатаВыполнения");
	ПараметрыЗаписи.Вставить("АдресХранилища");
	ПараметрыЗаписи.Вставить("ИмяФайла");
	
	Возврат ПараметрыЗаписи;
	
КонецФункции

Функция ШаблонИмениФайлаЗаписи() Экспорт
	
	Шаблон = "reglog_d%1_i%2.zip";
	
	Возврат Шаблон;
	
КонецФункции

#КонецОбласти

#КонецЕсли