#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает данные о налоге АУСН за указанный период, начисленном ФНС.
//
// Параметры:
//  Организация - ОпределяемыйТип.Организация - организация, для которой нужны данные.
//  Период      - Дата - начало месяца, за который нужны данные.
// 
// Возвращаемое значение:
//  Структура - См. НовыйДанныеРасчета.
//
Функция ДанныеЗаПериод(Организация, Период) Экспорт
	
	ДанныеРасчета = НовыйДанныеРасчета();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период",      НалоговыйПериод(Период));
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Расчет.Сумма КАК Сумма,
	|	Расчет.НалоговыйПериод КАК Период,
	|	Расчет.ДатаЗапроса КАК ДатаРасчета,
	|	Расчет.ПредварительныйРасчет КАК ПредварительныйРасчет,
	|	Расчет.ОбъектНалогообложения КАК ОбъектНалогообложения,
	|	Расчет.СтавкаНалога КАК СтавкаНалога,
	|	Расчет.Доходы КАК Доходы,
	|	Расчет.Расходы КАК Расходы,
	|	Расчет.ИдентификаторНачисления КАК Источник,
	|	Расчет.Ошибки КАК Ошибки
	|ИЗ
	|	РегистрСведений.РасчетНалогаАУСН КАК Расчет
	|ГДЕ
	|	Расчет.Организация = &Организация
	|	И Расчет.НалоговыйПериод = &Период";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ДанныеРасчета, Выборка);
	КонецЕсли;
	
	Возврат ДанныеРасчета;

КонецФункции

// Определяет допустимые значения поля НалоговыйПериод
//
// Параметры:
//  Период - Дата - обозначает налоговый период (месяц)
// 
// Возвращаемое значение:
//  Дата
//
Функция НалоговыйПериод(Период) Экспорт
	Возврат НачалоМесяца(Период);
КонецФункции

// Выполняет обновление данных регистра из загруженных данных сервиса АУСН
//
// Параметры:
//  Расчет - см. РегистрыСведений.НалоговыеНачисленияАУСН.НовыйРасчетНалога - записываемые данные
//  НалоговыйПериод - Дата
//  Организация  - СправочникСсылка.Организации
//  ИдентификаторНачисления - ОпределяемыйТип.ИдентификаторНачисленияАУСН - источник записываемых данных
//  ДатаЗапроса - Дата - дата UTC, на которую расчет актуален
//
Процедура ЗаписатьРасчет(Расчет, НалоговыйПериод, Организация, ИдентификаторНачисления, ДатаЗапроса) Экспорт
	
	Запись = СоздатьМенеджерЗаписи();
	
	ЗаполнитьЗначенияСвойств(Запись, Расчет);
	
	Запись.НалоговыйПериод = НалоговыйПериод;
	Запись.Организация = Организация;
	Запись.ИдентификаторНачисления = ИдентификаторНачисления;
	Запись.ДатаЗапроса = ДатаЗапроса;
	
	Запись.Записать(Истина);
	
КонецПроцедуры

// Записывает ошибку, полученную из сервиса АУСН
//
// Параметры:
//  Организация - СправочникСсылка.Организации
//  НалоговыйПериод - Дата
//  Ошибки - Строка - содержание ошибки
//
Процедура ЗаписатьОшибку(Организация, НалоговыйПериод, Ошибки) Экспорт
	
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.Организация.Установить(Организация);
	Набор.Отбор.НалоговыйПериод.Установить(НалоговыйПериод);
	Запись = Набор.Добавить();
	Запись.Организация = Организация;
	Запись.НалоговыйПериод = НалоговыйПериод;
	Запись.Ошибки = Ошибки;
	
	Набор.Записать(Истина);
	
КонецПроцедуры

// Определяет детальные данные расчета налога.
//
// Параметры:
//  Период - Дата - определяет налоговый период (месяц), за который выполняется расчет
//  Организация - СправочникСсылка.Организации - налогоплательщик
//  ИдентификаторНачисления - ОпределяемыйТип.ИдентификаторНачисленияАУСН - источник детальных данных в информационных системах ФНС
// 
// Возвращаемое значение:
//  Структура - см. НовыйРасчетНалога
//
Функция ДетальныеДанныеРасчета(Период, Организация, ИдентификаторНачисления) Экспорт
	
	Возврат РегистрыСведений.НалоговыеНачисленияАУСН.НайтиДанныеРасчетаНалога(
		Период,
		Организация,
		ИдентификаторНачисления);
	
КонецФункции

#Область ПодключениеПодсистем
// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//  Ограничение - См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом
#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Конструктор данных расчета
//
// Возвращаемое значение:
//  Структура
//
Функция НовыйДанныеРасчета()
	
	Результат = Новый Структура;
	
	Результат.Вставить("Сумма",                 0);
	Результат.Вставить("Период",                '00010101');
	Результат.Вставить("ДатаРасчета",           '00010101');
	Результат.Вставить("ДатаЗапроса",           '00010101');
	Результат.Вставить("ПредварительныйРасчет", Ложь);
	Результат.Вставить("ОбъектНалогообложения", Перечисления.ОбъектыНалогообложенияПоУСН.ПустаяСсылка());
	Результат.Вставить("СтавкаНалога",          0);
	Результат.Вставить("Доходы",                0);
	Результат.Вставить("Расходы",               0);
	Результат.Вставить("Источник",              0);
	Результат.Вставить("Ошибки",                "");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли

