
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет местонахождение основных средств.
// В отличие от метода СрезПоследних менеджера регистра, работает с отбором по списку объектов.
//
// Параметры:
//  Период           - Дата, МоментВремени - период, на начало которого выполняется расчет
//  Организация      - СправочникСсылка.Организация
//  ОсновныеСредства - Массив из СправочникСсылка.ОсновныеСредства - отбор, перечень объектов
// 
// Возвращаемое значение:
//  Соответствие - содержит местонахождение основных средств:
//         * Ключ - СправочникСсылка.ОсновныеСредства - объект
//         * Значение - СправочникСсылка.ПодразделенияОрганизаций - местонахождение
//
Функция АктуальныеСведения(Период, Организация, ОсновныеСредства) Экспорт
	
	Местонахождение = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период",           Новый Граница(Период, ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("Организация",      Организация);
	Запрос.УстановитьПараметр("ОсновныеСредства", ОсновныеСредства);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеРегистра.ОсновноеСредство КАК ОсновноеСредство,
	|	ДанныеРегистра.Местонахождение КАК Местонахождение
	|ИЗ
	|	РегистрСведений.МестонахождениеОСБухгалтерскийУчет.СрезПоследних(
	|			&Период,
	|			Организация = &Организация
	|				И ОсновноеСредство В (&ОсновныеСредства)) КАК ДанныеРегистра";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Местонахождение.Вставить(Выборка.ОсновноеСредство, Выборка.Местонахождение);
	КонецЦикла;
	
	Возврат Местонахождение;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли
