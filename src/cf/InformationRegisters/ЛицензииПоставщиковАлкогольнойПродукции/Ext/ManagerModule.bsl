#Если Сервер Или ТолстыйКлиентОбычноеПриложение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Обработчик обновления.
//
Процедура ЗаполнитьВидыДеятельности() Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	*
	|ИЗ
	|	РегистрСведений.ЛицензииПоставщиковАлкогольнойПродукции КАК ЛицензииПоставщиковАлкогольнойПродукции
	|ГДЕ
	|	ЛицензииПоставщиковАлкогольнойПродукции.ВидДеятельности = """"");
	Выборка = Запрос.Выполнить().Выбрать();
	
	Лицензии = РегистрыСведений.ЛицензииПоставщиковАлкогольнойПродукции.СоздатьНаборЗаписей();
	Отбор = Лицензии.Отбор;
	Пока Выборка.Следующий() Цикл
		
		Отбор.Период.Установить(Выборка.Период);
		Отбор.Поставщик.Установить(Выборка.Поставщик);
		Отбор.КодВидаПродукции.Установить(Выборка.КодВидаПродукции);
		Отбор.ВидЛицензии.Установить(Выборка.ВидЛицензии);
		
		Лицензия = Лицензии.Добавить();
		ЗаполнитьЗначенияСвойств(Лицензия, Выборка, , "ВидДеятельности");
		Лицензия.ВидДеятельности = ?(ПустаяСтрока(Выборка.КодВидаПродукции),
			НСтр("ru = 'Закупка, хранение и поставки алкогольной и спиртосодержащей продукции'"),
			НСтр("ru = 'Производство, хранение и поставки произведенной алкогольной и спиртосодержащей пищевой продукции'"));
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(Лицензии);
		
		Лицензии.Очистить();
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#КонецЕсли