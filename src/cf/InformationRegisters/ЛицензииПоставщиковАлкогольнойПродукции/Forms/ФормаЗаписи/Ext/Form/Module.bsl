
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СписокВыбораВидаПродукции = Элементы.КодВидаПродукции.СписокВыбора;
	РегистрыСведений.СведенияОбАлкогольнойПродукции.СписокВидовПродукции(СписокВыбораВидаПродукции);
	СписокВыбораВидаПродукции[0].Представление = НСтр("ru = 'Все'");
	Если ПустаяСтрока(Запись.КодВидаПродукции) Тогда
		
		НаименованиеВидаПродукции = НСтр("ru = 'Ограничение не установлено'");
		
	Иначе
		
		ЭлементСписка = СписокВыбораВидаПродукции.НайтиПоЗначению(Запись.КодВидаПродукции);
		Если ЭлементСписка <> Неопределено Тогда
			НаименованиеВидаПродукции = СтрЗаменить(ЭлементСписка.Представление, Запись.КодВидаПродукции + " - ", "");
		КонецЕсли;
		
	КонецЕсли;
			
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидДеятельностиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ЭталонныйВариант = Элемент.СписокВыбора.НайтиПоЗначению(ВыбранноеЗначение);
	Если ЭталонныйВариант <> Неопределено Тогда

		Запись.ВидДеятельности = ЭталонныйВариант.Представление;
		
		Если СтрНайти(ВРег(Запись.ВидДеятельности), "ПРОИЗВОДСТВО") = 0 Тогда
			// Указание кода вида продукции скорее всего не обязательно.
			ОчисткаВидаПродукции();

		КонецЕсли;

		Если Не ЗначениеЗаполнено(Запись.ВидЛицензии) Тогда
			Запись.ВидЛицензии = ВидЛицензииПоКодуПродукции(Запись.КодВидаПродукции);
		КонецЕсли;
		
		СтандартнаяОбработка = Ложь;

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КодВидаПродукцииПриИзменении(Элемент)
	
	Если ПустаяСтрока(Запись.КодВидаПродукции) Тогда
		ОчисткаВидаПродукции();
		Возврат;
	КонецЕсли;

	ЭлементСписка = Элемент.СписокВыбора.НайтиПоЗначению(Запись.КодВидаПродукции);
	Если ЭлементСписка <> Неопределено Тогда
		
		НаименованиеВидаПродукции = СтрЗаменить(ЭлементСписка.Представление, Запись.КодВидаПродукции + " - ", "");
		Запись.ВидЛицензии = ВидЛицензииПоКодуПродукции(Запись.КодВидаПродукции);
		
	Иначе
		
		НаименованиеВидаПродукции = НСтр("ru = 'Ограничение не установлено'");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КодВидаПродукцииОчистка(Элемент, СтандартнаяОбработка)
	
	ОчисткаВидаПродукции();
	
КонецПроцедуры

&НаКлиенте
Процедура ОчисткаВидаПродукции()
	
	Запись.КодВидаПродукции = Элементы.КодВидаПродукции.СписокВыбора[0].Значение;
	НаименованиеВидаПродукции = НСтр("ru = 'Ограничение не установлено'");
	
	Если Не ЗначениеЗаполнено(Запись.ВидЛицензии) Тогда
		Запись.ВидЛицензии = ВидЛицензииПоКодуПродукции(Запись.КодВидаПродукции);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьПроизвольныйПериод(Команда)
	
	Диалог = Новый ДиалогРедактированияСтандартногоПериода;
	Диалог.Период.ДатаНачала    = Запись.Период;
	Диалог.Период.ДатаОкончания = Запись.ДатаОкончания;
	
	ДополнительныеПараметры = Новый Структура("Диалог", Диалог);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПроизвольныйПериодЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	Диалог.Показать(ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыбратьПроизвольныйПериодЗавершение(Период, ДополнительныеПараметры) Экспорт
	
	Если Период = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	Диалог = ДополнительныеПараметры.Диалог;
	Запись.Период = Диалог.Период.ДатаНачала;
	Запись.ДатаОкончания  = Диалог.Период.ДатаОкончания;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ВидЛицензииПоКодуПродукции(Знач КодВидаПродукции)

	ВидЛицензии = Перечисления.ВидыЛицензийНаПродажуАлкоголя.АлкогольнаяПродукция;
	
	Если ПустаяСтрока(КодВидаПродукции) Тогда
		Возврат ВидЛицензии;
	КонецЕсли;
	
	КодыПива = РегламентированнаяОтчетностьАЛКО.МассивКодовИзМакетаСписки(
		"ВидыПродукции", ТекущаяДатаСеанса(), "Отчет.РегламентированныйОтчетАлкоПриложение26.Форма.ФормаОтчета");
	Если КодыПива.Найти(КодВидаПродукции) <> Неопределено Тогда
		ВидЛицензии = Перечисления.ВидыЛицензийНаПродажуАлкоголя.Пиво;
	КонецЕсли;
	
	Возврат ВидЛицензии;
	
КонецФункции

#КонецОбласти