#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Добавляем в набор записей по правилу новое уведомление, если добавление не нужно, возвращаем ЛОЖЬ
Функция ДобавитьЗаписьПравила(Организация, Уведомление, ПравилоУведомления, ПериодСобытия, РегистрацияВНалоговомОргане = Неопределено) Экспорт
	
	ДанныеЗаполнения = НовыйСтруктураРегистра();
	СтруктураЗаписи = НовыйСтруктураРегистра();
	
	НаборЗаписей = РегистрыСведений.ЗадачиБухгалтераУведомления.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Уведомление.Установить(Уведомление);
	
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() > 0 Тогда
	
		ЗаписьПравила = НаборЗаписей[0];
		
	Иначе
		
		ЗаписьПравила = НаборЗаписей.Добавить();
	
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(
		СтруктураЗаписи,
		ОбщегоНазначения.СтруктураПоМенеджеруЗаписи(ЗаписьПравила, Метаданные.РегистрыСведений.ЗадачиБухгалтераУведомления));
	
	ДанныеЗаполнения.Вставить("Организация",                 Организация);
	ДанныеЗаполнения.Вставить("Уведомление",                 Уведомление);
	ДанныеЗаполнения.Вставить("Правило",                     ПравилоУведомления);
	ДанныеЗаполнения.Вставить("ПериодСобытия",               ПериодСобытия);
	ДанныеЗаполнения.Вставить("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);
	
	ЗаписьИзменена = Ложь;
	
	Если НЕ ОбщегоНазначения.ДанныеСовпадают(СтруктураЗаписи, ДанныеЗаполнения) Тогда
		
		ЗаполнитьЗначенияСвойств(ЗаписьПравила, ДанныеЗаполнения);
		
		НаборЗаписей.Записать();
		
		ЗаписьИзменена = Истина;
	
	КонецЕсли;
	
	Возврат ЗаписьИзменена;
	
КонецФункции

// Удаляет из регистра запись по об исполнении задачи указанным документом.
//
// Параметры:
//  Организация         - СправочникСсылка.Организации - организация
//  Уведомление  - ДокументСсылка - документ, по которому удаляется запись
//                       (допустимые типы - см. тип значения измерения Уведомление)
//
Процедура УдалитьЗапись(Организация, Уведомление) Экспорт
	
	ВнешняяТранзакция = ТранзакцияАктивна();
	
	Если НЕ ВнешняяТранзакция Тогда
		НачатьТранзакцию();
	КонецЕсли;
	
	// Установим управляемые блокировки
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ЗадачиБухгалтераУведомления");
	ЭлементБлокировки.УстановитьЗначение("Организация", Организация);
	ЭлементБлокировки.УстановитьЗначение("Уведомление", Уведомление);
	Блокировка.Заблокировать();
	
	НаборЗаписей = РегистрыСведений.ЗадачиБухгалтераУведомления.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Организация.Установить(Организация);
	НаборЗаписей.Отбор.Уведомление.Установить(Уведомление);
	
	НаборЗаписей.Записать();
	
	Если НЕ ВнешняяТранзакция Тогда
		ЗафиксироватьТранзакцию();
	КонецЕсли;
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти 

#Область СлужебныйПрограммныйИнтерфейс

Функция ДанныеЗадачиПоУведомлению(Уведомление) Экспорт
	
	Результат = НовыйСтруктураРегистра();
	Если Не ЗначениеЗаполнено(Уведомление) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Уведомление", Уведомление);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗадачиБухгалтераУведомления.Организация КАК Организация,
	|	ЗадачиБухгалтераУведомления.Уведомление КАК Уведомление,
	|	ЗадачиБухгалтераУведомления.Правило КАК Правило,
	|	ЗадачиБухгалтераУведомления.ПериодСобытия КАК ПериодСобытия,
	|	ЗадачиБухгалтераУведомления.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтераУведомления КАК ЗадачиБухгалтераУведомления
	|ГДЕ
	|	ЗадачиБухгалтераУведомления.Уведомление = &Уведомление";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыйСтруктураРегистра()

	Возврат Новый Структура("Организация, Уведомление, Правило, ПериодСобытия, РегистрацияВНалоговомОргане");

КонецФункции 

#КонецОбласти

#КонецЕсли