#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ВариантОтбора") Тогда
		ВариантОтбораДокументов = Параметры.ВариантОтбора;
	Иначе
		ВариантОтбораДокументов = "Все";
	КонецЕсли;
	Если Параметры.Свойство("Организация") И ЗначениеЗаполнено(Параметры.Организация) Тогда
		ОтборОрганизация = Параметры.Организация;
		ОтборОрганизацияИспользование = Истина;
		ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
	КонецЕсли;
	Если Параметры.Свойство("Банк") И ЗначениеЗаполнено(Параметры.Банк) Тогда
		ОтборБанк = Параметры.Банк;
		ОтборБанкИспользование = Истина;
		ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Банк");
	КонецЕсли;
	УстановитьВариантОтбораДокументов(ЭтотОбъект);
	ПодключенныеБанки = ИнтеграцияАУСН.ПодключенныеБанки();
	АдресХранилищаПодключенныеБанки = ПоместитьВоВременноеХранилище(
		ПодключенныеБанки.ВыгрузитьКолонку("Банк"), УникальныйИдентификатор);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = ИнтеграцияАУСНКлиент.ИмяСобытияОбменССервисом() Тогда
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВариантОтбораДокументовПриИзменении(Элемент)
	
	УстановитьВариантОтбораДокументов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияИспользованиеПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	ТекущаяСтрока = Элемент.ТекущиеДанные;
	Элементы.СписокПрочитатьПовторно.Доступность = ТекущаяСтрока <> Неопределено
		И ТекущаяСтрока.Статус = ПредопределенноеЗначение("Перечисление.СтатусыДокументовАУСН.ОшибкаОбработкиПользователем");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборБанкИспользованиеПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Банк"); 
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	
	ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборБанкПриИзменении(Элемент)
	
	ОтборБанкИспользование = ЗначениеЗаполнено(ОтборБанк);
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Банк");
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПрочитатьПовторно(Команда)
	
	ВыделенныеСтроки = Элементы.Список.ВыделенныеСтроки;
	ДокументыАУСНДляИзменения = Новый Массив;
	Для Каждого Строка Из ВыделенныеСтроки Цикл 
		ДанныеСтроки = Элементы.Список.ДанныеСтроки(Строка);
		Если ДанныеСтроки.Статус = ПредопределенноеЗначение(
			"Перечисление.СтатусыДокументовАУСН.ОшибкаОбработкиПользователем") Тогда
			ДокументыАУСНДляИзменения.Добавить(ДанныеСтроки.Идентификатор);
		КонецЕсли;
	КонецЦикла;
	ПодготовитьДокументыКПовторномуЧтениюНаСервере(ДокументыАУСНДляИзменения);
	ИнтеграцияАУСНКлиент.ВыполнитьОбменССервисом(ЭтотОбъект, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВариантОтбораДокументов(Форма)
	
	Настройки = Форма.Список.КомпоновщикНастроек.Настройки;
	
	ИмяПоля = "Статус";
	
	Если Форма.ВариантОтбораДокументов = ВариантОтбораДокументыСОшибками() Тогда
		
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(
			Настройки.Отбор,
			ИмяПоля);
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			Настройки.Отбор,
			ИмяПоля,
			ПредопределенноеЗначение("Перечисление.СтатусыДокументовАУСН.ОшибкаОбработкиПользователем"),
			ВидСравненияКомпоновкиДанных.Равно,
			,
			Истина);
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(
			Настройки.Отбор,
			ИмяПоля);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ВариантОтбораДокументыСОшибками()
	
	Возврат "СОшибками";
	
КонецФункции 

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Если Форма.ОтборОрганизацияИспользование Тогда
		Организация = Форма.ОтборОрганизация;
	Иначе
		Организация = Неопределено;
	КонецЕсли;
	Если Форма.ОтборБанкИспользование Тогда
		Банк = Форма.ОтборБанк;
	Иначе
		Банк = Неопределено;
	КонецЕсли;
	Элементы.ГруппаВариантОтбораДокументов.Видимость = ЕстьДокументыСОшибками(Организация, Банк);
	Если Не Элементы.ГруппаВариантОтбораДокументов.Видимость Тогда
		Форма.ВариантОтбораДокументов = "Все";
		УстановитьВариантОтбораДокументов(Форма);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЕстьДокументыСОшибками(Организация, Банк)
	
	Возврат РегистрыСведений.ДокументыАУСН.ЕстьДокументыСОшибками(Организация, Банк);
	
КонецФункции

&НаСервереБезКонтекста
Процедура ПодготовитьДокументыКПовторномуЧтениюНаСервере(ДокументыКПовторномуЧтению)
	
	РегистрыСведений.ДокументыАУСН.ПодготовитьКПовторномуЧтению(ДокументыКПовторномуЧтению);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборБанкНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыОткрываемойФормы = Новый Структура;
	ОтборОкрываемойФормы = Новый Структура;
	ПодключенныеБанки = ПолучитьИзВременногоХранилища(АдресХранилищаПодключенныеБанки);
	ОтборОкрываемойФормы.Вставить("Ссылка", ПодключенныеБанки);
	ПараметрыОткрываемойФормы.Вставить("Отбор", ОтборОкрываемойФормы);
	ПараметрыОткрываемойФормы.Вставить("ОтображениеТаблицы", ОтображениеТаблицы.Список);
	ОткрытьФорму(
		"Справочник.КлассификаторБанков.ФормаВыбора",
		ПараметрыОткрываемойФормы,
		Элемент,
		УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти