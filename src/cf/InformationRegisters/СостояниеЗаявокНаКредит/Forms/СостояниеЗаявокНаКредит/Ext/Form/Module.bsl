
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("СервисОбменаСБанками", СервисОбменаСБанками);
	Если НЕ ЗначениеЗаполнено(СервисОбменаСБанками) Тогда
		// По умолчанию открываем заявки на кредит.
		СервисОбменаСБанками = Перечисления.СервисыОбменаСБанками.ЗаявкиНаКредит;
	КонецЕсли;


	// Заявки создаются всегда по головной организации вместе со всеми ее обособленными подразделениями.
	ОсновнаяОрганизация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	ОсновнаяГоловнаяОрганизация = ОбщегоНазначенияБПВызовСервераПовтИсп.ГоловнаяОрганизация(ОсновнаяОрганизация);

	ОбщегоНазначенияБПВызовСервера.УстановитьОтборПоОсновнойОрганизации(ЭтотОбъект, , , ОсновнаяГоловнаяОрганизация);
	
	ОтборыПоОрганизации = ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(
		Список.КомпоновщикНастроек.Настройки.Отбор, "Организация");
	Если ОтборыПоОрганизации.Количество() > 0 И ОтборыПоОрганизации[0].Использование Тогда
		ОтборОрганизация = ОтборыПоОрганизации[0].ПравоеЗначение;
	Иначе	
		ОтборОрганизация = ОсновнаяГоловнаяОрганизация;
	КонецЕсли;
	
	ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");

	ЕстьПравоИзмененияЗаявок = ПравоДоступа("Изменение", Метаданные.Документы.ЗаявкаНаКредит);
	
	Элементы.Создать.Доступность                      = ЕстьПравоИзмененияЗаявок;
	Элементы.СписокКонтекстноеМенюСоздать.Доступность = ЕстьПравоИзмененияЗаявок;
	Элементы.ОбновитьСостояниеЗаявок.Доступность      = ЕстьПравоИзмененияЗаявок;
	
	// Настроим элементы формы в зависимости от вида заявок.
	Это1СКредит = СервисОбменаСБанками = Перечисления.СервисыОбменаСБанками.ЗаявкиНаКредит;

	// Отбор по виду сервиса изменить нельзя.
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		Список.КомпоновщикНастроек.Настройки.Отбор,
		"СервисОбменаСБанками",
		СервисОбменаСБанками,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		Истина,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	
	Если Это1СКредит Тогда
		Заголовок                    = НСтр("ru = 'Заявки 1С:Кредит'");
		Элементы.Ставка.Видимость    = Истина;
		Элементы.СуммаМакс.Заголовок = НСтр("ru = 'Сумма (макс)'");
		Элементы.СрокМакс.Заголовок  = НСтр("ru = 'Срок (макс)'");
	Иначе
		Заголовок                    = НСтр("ru = 'Заявки 1С:Лизинг'");
		Элементы.Ставка.Видимость    = Ложь; // Для лизинга ставка не используется
		Элементы.СуммаМакс.Заголовок = НСтр("ru = 'Сумма (одобрено)'");
		Элементы.СрокМакс.Заголовок  = НСтр("ru = 'Срок (одобрено)'");
	КонецЕсли;

	// На текущий момент ролики опубликованы только для 1С:Кредита.
	Элементы.ГруппаВидеоролики.Видимость = Это1СКредит
		И ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(ВРег("ЗаявкиНаКредит"), 
			ВРег("Видеоролики"), 
			Истина);
	
	УстановитьУсловноеОформление();
	
	УстановитьБаннерПолученыОдобренияОтБанков();
	
	ПомеченныеНаУдалениеСервер.СкрытьПомеченныеНаУдаление(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновленоСостояниеЗаявкиНаКредит" Тогда
		
		Если ТипЗнч(Параметр) = Тип("Структура") Тогда
			
			ОтборОрганизация = Параметр.Организация;
			ОтборОрганизацияПриИзменении(Неопределено);
			
			Элементы.Список.ТекущаяСтрока = ЗаявкиНаКредитВызовСервера.КлючЗаписиСостоянияЗаявки(Параметр);
		   
		Иначе
			
			Элементы.Список.Обновить();
			
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "СервисПоКредитам_НовыеСообщенияОтБанков" Тогда
		
		УстановитьБаннерПолученыОдобренияОтБанков();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьБаннер", 1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ПерсонализированныеПредложенияСервисовКлиент.ПерейтиПоСсылкеБаннера(
		НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка, Баннер, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборОрганизацияИспользованиеПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьБаннер", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	
	ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьБаннер", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Создать(Команда)
	
	ПриДобавленииСтрокиСписка();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьБаннерНажатие(Элемент)
	
	ОтключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер");
	
	ПерсонализированныеПредложенияСервисовКлиент.ЗакрытьБаннер(ЭтотОбъект, ОтборОрганизация);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредыдущийБаннерНажатие(Элемент)
	
	ОтключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер");
	
	ПерсонализированныеПредложенияСервисовКлиент.УстановитьРежимОжиданияНаБаннере(ЭтотОбъект);
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьПредыдущийБаннер", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СледующийБаннерНажатие(Элемент)
	
	ОтключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер");
	
	ПерсонализированныеПредложенияСервисовКлиент.УстановитьРежимОжиданияНаБаннере(ЭтотОбъект);
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер", 0.1, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки, ИспользуютсяСтандартныеНастройки)
	
	ПомеченныеНаУдалениеСервер.УдалитьОтборПометкаУдаления(Настройки);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	ПриВыбореСтрокиСписка();

КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;

	Если Элементы.Создать.Доступность И Не Копирование Тогда
		ПриДобавленииСтрокиСписка();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)

	Отказ = Истина;
	ПриВыбореСтрокиСписка(); 

КонецПроцедуры

&НаКлиенте
Процедура СписокПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные.Состояние = ПредопределенноеЗначение("Перечисление.СостоянияЗаявокНаКредит.Черновик") Тогда
	
		ПредставлениеЗаявки = СтрШаблон(НСтр("ru = 'Заявка %1 %2 на %3 от %4'"), 
			СервисОбменаСБанками,
			ЗаявкиНаКредитКлиентСервер.ПредставлениеСуммыКредита(ТекущиеДанные.СуммаЗаявки),
			ЗаявкиНаКредитКлиентСервер.ПредставлениеСрокаКредита(ТекущиеДанные.СрокЗаявки),
			Формат(ТекущиеДанные.ДатаИзменения, "ДЛФ=D"));
	
		Если ТекущиеДанные.ПометкаУдаления Тогда
			ТекстВопроса = СтрШаблон(НСтр("ru = 'Снять с ""%1"" пометку на удаление?'"), ПредставлениеЗаявки);
		Иначе
			ТекстВопроса = СтрШаблон(НСтр("ru = 'Пометить ""%1"" на удаление?'"), ПредставлениеЗаявки);
		КонецЕсли; 
		
		ОписаниеОповещения = Новый ОписаниеОповещения("СписокПередУдалениемЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Возврат;
	
	Иначе
		// Заявка уже была ранее отправлена хотя бы в какой-то банк, ее не даем удалить.
		ТекстСообщения = НСтр("ru = 'Заявка уже отправлена.
									|Удалить ее невозможно.'");
		ПоказатьПредупреждение(, ТекстСообщения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередУдалениемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	УстановитьПометкуУдаления(ТекущиеДанные.ЗаявкаНаКредит, НЕ ТекущиеДанные.ПометкаУдаления);
	
	// Обновляем форму списка.
	Элементы.Список.Обновить();
	
	ПомеченныеНаУдалениеКлиент.ПриИзмененииСписка(ЭтотОбъект, Элементы.Список);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	// Отбираем заявки, которые находятся в состоянии Черновик и для которых не указан конкретный банк получатель,
	// чтобы для них показывать количество банков.
	ЗаявкиЧерновики = Новый Массив;

	Для каждого СтрокаЗаявки Из Строки Цикл
		
		ТекущиеДанные = СтрокаЗаявки.Значение.Данные;
		
		Если ТекущиеДанные.Состояние = Перечисления.СостоянияЗаявокНаКредит.Черновик
			И НЕ ЗначениеЗаполнено(ТекущиеДанные.Банк) Тогда
			ЗаявкиЧерновики.Добавить(ТекущиеДанные.ЗаявкаНаКредит);
		КонецЕсли;
				
		ОписаниеСуммы = ЗаявкиНаКредитКлиентСервер.ПредставлениеСуммыКредита(ТекущиеДанные.СуммаЗаявки);
		СтрокаЗаявки.Значение.Оформление["СуммаЗаявки"].УстановитьЗначениеПараметра("Текст", ОписаниеСуммы);
		
		ОписаниеСрока = ЗаявкиНаКредитКлиентСервер.ПредставлениеСрокаКредита(ТекущиеДанные.СрокЗаявки);
		СтрокаЗаявки.Значение.Оформление["СрокЗаявки"].УстановитьЗначениеПараметра("Текст", ОписаниеСрока);
		
		Если ЗначениеЗаполнено(ТекущиеДанные.СуммаМакс) Тогда
			ОписаниеСуммы = ЗаявкиНаКредитКлиентСервер.ПредставлениеСуммыКредита(ТекущиеДанные.СуммаМакс);
			СтрокаЗаявки.Значение.Оформление["СуммаМакс"].УстановитьЗначениеПараметра("Текст", ОписаниеСуммы);
		КонецЕсли;	
			
		Если ЗначениеЗаполнено(ТекущиеДанные.СрокМакс) Тогда
			ОписаниеСрока = ЗаявкиНаКредитКлиентСервер.ПредставлениеСрокаКредита(ТекущиеДанные.СрокМакс);
			СтрокаЗаявки.Значение.Оформление["СрокМакс"].УстановитьЗначениеПараметра("Текст", ОписаниеСрока);
		КонецЕсли;
		
	КонецЦикла; 

	Если НЕ ЗначениеЗаполнено(ЗаявкиЧерновики) Тогда
		// Нет заявок в состоянии Черновик, поэтому больше обновлять нечего.
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ЗаявкиЧерновики", ЗаявкиЧерновики);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТЧБанки.Ссылка,
	|	КОЛИЧЕСТВО(*) КАК КоличествоБанков
	|ИЗ
	|	Документ.ЗаявкаНаКредит.Банки КАК ТЧБанки
	|ГДЕ
	|	ТЧБанки.Ссылка В (&ЗаявкиЧерновики)
	|СГРУППИРОВАТЬ ПО
	|	ТЧБанки.Ссылка";
	
	УстановитьПривилегированныйРежим(Истина);
	БанкиПолучатели = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	
	БанкиПолучатели.Индексы.Добавить("Ссылка");
	
	Для каждого СтрокаЗаявки Из Строки Цикл
		
		ТекущиеДанные = СтрокаЗаявки.Значение.Данные;
		
		Если ТекущиеДанные.Состояние <> Перечисления.СостоянияЗаявокНаКредит.Черновик Тогда
			Продолжить;
		КонецЕсли;
		
		СведенияОЗаявке = БанкиПолучатели.Найти(ТекущиеДанные.ЗаявкаНаКредит, "Ссылка");
		Если СведенияОЗаявке = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		Если СведенияОЗаявке.КоличествоБанков > 1 Тогда
			СтрокаЗаявки.Значение.Оформление["Банк"].УстановитьЗначениеПараметра("Текст", СтрокаСЧислом(
				Нстр("ru = '; %1 получатель; ; %1 получателя; %1 получателей; %1 получателей'"),
				СведенияОЗаявке.КоличествоБанков,
				ВидЧисловогоЗначения.Количественное,
				"L=ru"));
		КонецЕсли;		
		
	КонецЦикла;

	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриИзменении(Элемент)

	ПомеченныеНаУдалениеКлиент.ПриИзмененииСписка(ЭтотОбъект, Элемент);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьСостояниеЗаявок(Команда)
	
	ДлительнаяОперация = ЗапуститьУниверсальныйОбменСБанкамиПоСервису();
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбновитьСостояниеЗаявокЗавершение", ЭтотОбъект);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьСообщения(Команда)
	
	ПараметрыРасшифровки = УниверсальныйОбменСБанкамиКлиент.ПараметрыРасшифроватьНерасшифрованныеТранзакции();
	ПараметрыРасшифровки.Сервис = СервисОбменаСБанками;
	ПараметрыРасшифровки.ТранзакцииДляРасшифровки = НерасшифрованныеТранзакции.ВыгрузитьЗначения();
	Если ОтборОрганизацияИспользование Тогда
		ПараметрыРасшифровки.Организация = ОтборОрганизация;
	КонецЕсли;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("РасшифроватьСообщенияЗавершение", ЭтотОбъект);
	УниверсальныйОбменСБанкамиКлиент.РасшифроватьНерасшифрованныеТранзакции(ОповещениеОЗавершении, ПараметрыРасшифровки);
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьВидеороликиНажатие(Элемент)
	
	Элементы.ГруппаВидеоролики.Видимость = Ложь;
	СохранитьОтключениеВидимостиВидеороликов();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПриДобавленииСтрокиСписка()
	
	Если СервисОбменаСБанками = ПредопределенноеЗначение("Перечисление.СервисыОбменаСБанками.ЗаявкиНаКредит") Тогда
		КлючеваяОперация = "СозданиеФормыЗаявкаНаКредит";
	Иначе
		КлючеваяОперация = "СозданиеФормыЗаявкаНаЛизинг";
	КонецЕсли;
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);

	ЗначенияЗаполнения = ОбщегоНазначенияБПВызовСервера.ЗначенияЗаполненияДинамическогоСписка(Список.КомпоновщикНастроек);
	
	ПараметрыСоздания = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Документ.ЗаявкаНаКредит.ФормаОбъекта", ПараметрыСоздания, Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриВыбореСтрокиСписка()
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	ВыбраннаяСтрока = Элементы.Список.ТекущаяСтрока;
	
	Если ТекущиеДанные.Состояние = ПредопределенноеЗначение("Перечисление.СостоянияЗаявокНаКредит.Черновик") Тогда

		Если СервисОбменаСБанками = ПредопределенноеЗначение("Перечисление.СервисыОбменаСБанками.ЗаявкиНаКредит") Тогда
			КлючеваяОперация = "ОткрытиеФормыЗаявкаНаКредит";
		Иначе
			КлючеваяОперация = "ОткрытиеФормыЗаявкаНаЛизинг";
		КонецЕсли;
		ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
	
		ОткрытьФорму("Документ.ЗаявкаНаКредит.ФормаОбъекта", Новый Структура("Ключ", ТекущиеДанные.ЗаявкаНаКредит));

	ИначеЕсли ТекущиеДанные.ТребуетсяРасшифровка Тогда
		Если ЕстьПравоИзмененияЗаявок Тогда
			ОбработкаОкончанияРасшифровки = Новый ОписаниеОповещения(
				"ПослеРасшифровкиСообщения", 
				ЗаявкиНаКредитКлиент, 
				Новый Структура("КлючЗаписи, Банк", ВыбраннаяСтрока, ТекущиеДанные.Банк));

			УниверсальныйОбменСБанкамиКлиент.РасшифроватьДанныеТранзакции(ОбработкаОкончанияРасшифровки, ТекущиеДанные.Транзакция);
		Иначе
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Это сообщение от %1 требуется сначала расшифровать, обратитесь к администратору.'"),
				ТекущиеДанные.Банк);
			ПоказатьПредупреждение(, ТекстСообщения);
		КонецЕсли;
	Иначе	
		ОткрытьФорму("РегистрСведений.СостояниеЗаявокНаКредит.Форма.ИнформацияПоЗаявкеНаКредит", Новый Структура("Ключ", ВыбраннаяСтрока));
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
		
	// ЦВЕТА СОСТОЯНИЙ
		
	// "Одобрено", "КредитЗапрошен", "Готово" - зеленый
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Состояние");
		
	СписокСтатусовОдобрено = Новый СписокЗначений;
	СписокСтатусовОдобрено.Добавить(Перечисления.СостоянияЗаявокНаКредит.Одобрено);
	СписокСтатусовОдобрено.Добавить(Перечисления.СостоянияЗаявокНаКредит.КредитЗапрошен);
	СписокСтатусовОдобрено.Добавить(Перечисления.СостоянияЗаявокНаКредит.Готово);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.Состояние", ВидСравненияКомпоновкиДанных.ВСписке, СписокСтатусовОдобрено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЗаявкиНаКредит.ЦветСостояния(Перечисления.СостоянияЗаявокНаКредит.Одобрено));
	
	// "Ошибка" - красный
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Состояние");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.ЕстьОшибки", ВидСравненияКомпоновкиДанных.Равно, Истина);
		
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветОшибкиОтправкиБРО);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='Ошибка'"));
	
	// "Истек срок" - вся строка серая
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Список");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Состояние");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.ИстекСрок", ВидСравненияКомпоновкиДанных.Равно, Истина);
		
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветНеподдерживаемойОтправкиБРО);
	
	// НЕ РАСШИФРОВАННЫЕ СООБЩЕНИЯ
	
	// Выделяем всю строку жирным
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Список");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Состояние");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.Новое", ВидСравненияКомпоновкиДанных.Равно, Истина);
		
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(Элементы.Список.Шрифт,,, Истина));
		
	// Если одобренные условия зашифрованы, то вместо текста выводим знаки "*"
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Ставка");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СуммаМин");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СуммаМакс");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СрокМин");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СрокМакс");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СрокДействия");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.ТребуетсяРасшифровка", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.Состояние", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СостоянияЗаявокНаКредит.Одобрено);
		
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", "* * * * *");
	
КонецПроцедуры

&НаСервере
Функция ЗапуститьУниверсальныйОбменСБанкамиПоСервису()
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Запуск универсального обмена с банками'");
	
	ИмяПроцедуры = "ЗаявкиНаКредит.ЗапуститьУниверсальныйОбменСБанкамиПоСервисуВФоне";
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("СервисОбменаСБанками", СервисОбменаСБанками);
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(ИмяПроцедуры, ПараметрыПроцедуры, ПараметрыВыполнения);
	
КонецФункции

&НаКлиенте
Процедура ОбновитьСостояниеЗаявокЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") 
		ИЛИ НЕ Результат.Свойство("Статус") Тогда
		Возврат;
	КонецЕсли;

	Если Результат.Статус <> "Выполнено" Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Список.Обновить();
	
	УстановитьБаннерПолученыОдобренияОтБанков();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьОтключениеВидимостиВидеороликов()
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(ВРег("ЗаявкиНаКредит"), ВРег("Видеоролики"), Ложь);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьПометкуУдаления(Знач ЗаявкаНаКредит, Знач ПометкаУдаления)

	Объект = ЗаявкаНаКредит.ПолучитьОбъект();
	Если Объект = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.УстановитьПометкуУдаления(ПометкаУдаления);

КонецПроцедуры

#Область БаннерПолученыОдобренияБанков

&НаСервере
Процедура УстановитьБаннерПолученыОдобренияОтБанков()
	
	Если НЕ ЕстьПравоИзмененияЗаявок Тогда
		// При отсутствии прав на редактирование не показываем кнопку для расшифровки.
		Возврат;
	КонецЕсли;

	// Нерасшифрованные транзакции.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СервисОбменаСБанками", СервисОбменаСБанками);
	Запрос.УстановитьПараметр("УсловиеПоОрганизация", Истина);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	СостояниеЗаявокНаКредит.Транзакция КАК Транзакция
	|ИЗ
	|	РегистрСведений.СостояниеЗаявокНаКредит КАК СостояниеЗаявокНаКредит
	|ГДЕ
	|	СостояниеЗаявокНаКредит.ТребуетсяРасшифровка = ИСТИНА
	|	И СостояниеЗаявокНаКредит.СервисОбменаСБанками = &СервисОбменаСБанками
	|	И &УсловиеПоОрганизация";
	
	Если Параметры.Свойство("Организация")
		И ТипЗнч(Параметры.Организация) = Тип("СправочникСсылка.Организации") Тогда
		Запрос.УстановитьПараметр("Организация", Параметры.Организация);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоОрганизация", "СостояниеЗаявокНаКредит.Организация = &Организация");
	КонецЕсли;
	
	НерасшифрованныеТранзакции.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Транзакция"));
	
	// Количество банков, одобрения которых не расшифрованы.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СервисОбменаСБанками", СервисОбменаСБанками);
	Запрос.УстановитьПараметр("УсловиеПоОрганизация", Истина);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	СостояниеЗаявокНаКредит.Банк КАК Банк
	|ИЗ
	|	РегистрСведений.СостояниеЗаявокНаКредит КАК СостояниеЗаявокНаКредит
	|ГДЕ
	|	СостояниеЗаявокНаКредит.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияЗаявокНаКредит.Одобрено)
	|	И СостояниеЗаявокНаКредит.ТребуетсяРасшифровка = ИСТИНА
	|	И СостояниеЗаявокНаКредит.СервисОбменаСБанками = &СервисОбменаСБанками
	|	И &УсловиеПоОрганизация";
	
	Если Параметры.Свойство("Организация")
		И ТипЗнч(Параметры.Организация) = Тип("СправочникСсылка.Организации") Тогда
		Запрос.УстановитьПараметр("Организация", Параметры.Организация);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоОрганизация", "СостояниеЗаявокНаКредит.Организация = &Организация");
	КонецЕсли;
	
	КоличествоБанковСНерасшифрованнымиОдобрениями = Запрос.Выполнить().Выбрать().Количество();
	
	// Управление видимостью баннера.
	Элементы.ГруппаПолученыСообщенияОтБанков.Видимость = КоличествоБанковСНерасшифрованнымиОдобрениями <> 0;
	
	Шаблон = НСтр("ru = 'Получено одобрение от %1. Чтобы увидеть подробности, расшифруйте сообщения.'");
	Подстрока = СтрокаСЧислом(НСтр("ru = ';%1 партнера сервиса;; %1 партнеров сервиса;%1 партнеров сервиса;%1 партнеров сервиса'"),
			КоличествоБанковСНерасшифрованнымиОдобрениями,
			ВидЧисловогоЗначения.Количественное, "L=ru");
	Элементы.ПолученыСообщенияОтБанковТекст.Заголовок = СтрШаблон(Шаблон, Подстрока);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьСообщенияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	УстановитьБаннерПолученыОдобренияОтБанков();
	
КонецПроцедуры

#КонецОбласти

#Область Баннер

&НаКлиенте
Процедура Подключаемый_УстановитьБаннер()
	
	УстановитьБаннер();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УстановитьСледующийБаннер()
	
	// Поскольку обработчик может вызываться не только интерактивно пользователем,
	// но и автоматически по таймеру, меняем баннер при условии, что форма находится в фокусе.
	Если НЕ ВводДоступен() Тогда
		ПодключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер",
			ПерсонализированныеПредложенияСервисовКлиент.ИнтервалПереключенияБаннеров(), Истина);
		Возврат;
	КонецЕсли;
	
	УстановитьБаннер();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УстановитьПредыдущийБаннер()
	
	УстановитьБаннер(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьБаннер(ПоказатьПредыдущий = Ложь)
	
	ДлительнаяОперация = ПолучитьБаннерНаСервере(ПоказатьПредыдущий);
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус <> "Ошибка" Тогда
		
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
		
		Обработчик = Новый ОписаниеОповещения("ПослеПолученияБаннераВФоне", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияБаннераВФоне(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		УстановитьБаннерНаФорме(ДлительнаяОперация.АдресРезультата);
		ПодключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер",
			ПерсонализированныеПредложенияСервисовКлиент.ИнтервалПереключенияБаннеров(), Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьБаннерНаСервере(ПоказатьПредыдущий)
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение баннера в фоне'");
	НастройкиЗапуска.ЗапуститьВФоне = Истина;
	НастройкиЗапуска.ОжидатьЗавершение = 0;
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("Организация", ?(ОтборОрганизацияИспользование, ОтборОрганизация, Неопределено));
	СтруктураПараметров.Вставить("Размещение", ПерсонализированныеПредложенияСервисов.ИмяРазмещенияЗаявкиНаКредит());
	СтруктураПараметров.Вставить("ПоказатьПредыдущий", ПоказатьПредыдущий);
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ПерсонализированныеПредложенияСервисов.ПолучитьБаннер",
		СтруктураПараметров,
		НастройкиЗапуска);
	
КонецФункции

&НаСервере
Процедура УстановитьБаннерНаФорме(АдресРезультата)
	
	ПерсонализированныеПредложенияСервисов.УстановитьБаннерНаФорме(ЭтотОбъект, АдресРезультата);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
