#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ЭтоПерваяОтчетнаяКампания(Организация, ДатаОпроса, Периодичность) Экспорт
	
	ЭтоПерваяОтчетнаяКампания = Истина;
	
	МенеджерЗаписи = РегистрыСведений.ПерваяОтчетнаяКампания.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Организация = Организация;
	МенеджерЗаписи.Прочитать();
	Если МенеджерЗаписи.Выбран() Тогда
		
		Если Периодичность = Перечисления.Периодичность.Месяц Тогда
			ДатаОкончанияКампании = КонецМесяца(МенеджерЗаписи.ПериодКампании);
		ИначеЕсли Периодичность = Перечисления.Периодичность.Квартал Тогда
			ДатаОкончанияКампании = КонецКвартала(МенеджерЗаписи.ПериодКампании);
		Иначе
			ДатаОкончанияКампании = КонецГода(МенеджерЗаписи.ПериодКампании);
		КонецЕсли;
			
		ЭтоПерваяОтчетнаяКампания = ДатаОкончанияКампании > ДатаОпроса;
		
	Иначе
		ЗарегистрироватьПервуюОтчетнуюКампанию(Организация, ДатаОпроса);
	КонецЕсли;
	
	Возврат ЭтоПерваяОтчетнаяКампания;
	
КонецФункции

Функция ПервыйПериодКампании(Организация) Экспорт
	
	МенеджерЗаписи = РегистрыСведений.ПерваяОтчетнаяКампания.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Организация = Организация;
	МенеджерЗаписи.Прочитать();
	Возврат МенеджерЗаписи.ПериодКампании;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗарегистрироватьПервуюОтчетнуюКампанию(Организация, ДатаКампании)
	
	МенеджерЗаписи = РегистрыСведений.ПерваяОтчетнаяКампания.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Организация    = Организация;
	МенеджерЗаписи.ПериодКампании = НачалоМесяца(ДатаКампании);
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли