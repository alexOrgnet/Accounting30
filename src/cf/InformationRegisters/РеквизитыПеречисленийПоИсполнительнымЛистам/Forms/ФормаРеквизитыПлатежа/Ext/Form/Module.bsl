#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Получим данные объекта
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.Реквизиты);
	Ссылка          = Параметры.Ссылка;
	ПериодДокумента = Параметры.СвойстваКонтекста.Период;
	СчетПолучателя  = Параметры.СвойстваКонтекста.СчетПолучателя;
	ЗаполнитьСписокОчередностьПлатежа();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если НЕ Модифицированность Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьВопрос(
		Новый ОписаниеОповещения("ВопросПеренестиЗначенияВДокументЗавершение", ЭтотОбъект),
		НСтр("ru = 'Перенести указанные значения в документ?'"),
		РежимДиалогаВопрос.ДаНетОтмена,
		, // Заголовок
		КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПеренестиЗначенияВДокументЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		ПеренестиВДокумент();
		
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		
		Модифицированность = Ложь;
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОчередностьПлатежаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчередностьПлатежаПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ИдентификаторПлатежаПриИзменении(Элемент)
	
	ВывестиСообщениеОбОшибкахУИН();
	
КонецПроцедуры

&НаКлиенте
Процедура ИдентификаторПлатежаОчистка(Элемент, СтандартнаяОбработка)
	
	УправлениеВидимостьюПредупрежденийОбОшибкахУИН();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ПеренестиВДокумент();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПеренестиВДокумент()
	
	ЗначениеМодифицированности = Модифицированность;
	Модифицированность = Ложь;
	
	Закрыть(РезультатРедактирования(ЭтотОбъект, ЗначениеМодифицированности));
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция РезультатРедактирования(Форма, ЗначениеМодифицированности = Ложь)
	
	Результат = ПлатежиВБюджетКлиентСервер.НовыйРеквизитыПлатежаВБюджет(Форма);
	Результат.Вставить("Модифицированность", ЗначениеМодифицированности);
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	ПлатежноеПоручениеФормыКлиентСервер.ОбновитьПодсказкуОчередностьПлатежа(Форма, Форма.ОчередностьПлатежа);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокОчередностьПлатежа()
	
	СписокДанных = УчетДенежныхСредствКлиентСервер.ПолучитьСписокОчередностьПлатежа(ПериодДокумента);
	Элемент      = Элементы.ОчередностьПлатежа;
	
	Элемент.СписокВыбора.Очистить();
	Для каждого ЭлементДанных Из СписокДанных Цикл
		Элемент.СписокВыбора.Добавить(ЭлементДанных.Значение, ЭлементДанных.Представление);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиСообщениеОбОшибкахУИН()
	
	СформироватьСтрокуПроверкиУИН();
	УправлениеВидимостьюПредупрежденийОбОшибкахУИН();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьСтрокуПроверкиУИН()
	
	ПлатежиВБюджет.СформироватьСтрокуПроверкиУИН(ИдентификаторПлатежа,
		СчетПолучателя,
		"", // Статус плательщика требуется передавать только для платежей в бюджет
		ПериодДокумента,
		СтатусПредупреждения);
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеВидимостьюПредупрежденийОбОшибкахУИН();
	
	Если СтатусПредупреждения = 0 Тогда
		Элементы.НадписьУИНВведенНекорректно.Видимость = Ложь;
	ИначеЕсли СтатусПредупреждения = 1 Тогда
		Элементы.НадписьУИНВведенНекорректно.Видимость = Истина;
		Элементы.НадписьУИНСодержитБуквы.Видимость = Ложь;
	ИначеЕсли СтатусПредупреждения = 2 Тогда
		Элементы.НадписьУИНВведенНекорректно.Видимость = Ложь;
		Элементы.НадписьУИНСодержитБуквы.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
