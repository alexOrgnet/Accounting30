#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

// Возвращает ключ записи регистра сведений СредняяЧисленностьРаботниковОрганизаций.
//
// Параметры:
//   Период - Дата - период записи регистра
//   Организация - СправочникСсылка.Организации - организация
//
// Возвращаемое значение:
//   РегистрСведенийКлючЗаписи.СредняяЧисленностьРаботниковОрганизаций
//
Функция КлючЗаписи(Период, Организация) Экспорт
	
	Если Не ЗначениеЗаполнено(Период) Или Не ЗначениеЗаполнено(Организация) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период", НачалоКвартала(Период));
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	СредняяЧисленностьРаботниковОрганизаций.Организация КАК Организация,
	|	СредняяЧисленностьРаботниковОрганизаций.Период КАК Период
	|ИЗ
	|	РегистрСведений.СредняяЧисленностьРаботниковОрганизаций КАК СредняяЧисленностьРаботниковОрганизаций
	|ГДЕ
	|	СредняяЧисленностьРаботниковОрганизаций.Организация = &Организация
	|	И СредняяЧисленностьРаботниковОрганизаций.Период = &Период";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗначенияКлюча = Новый Структура("Период, Организация");
		ЗаполнитьЗначенияСвойств(ЗначенияКлюча, Выборка);
		Возврат СоздатьКлючЗаписи(ЗначенияКлюча);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Сдвигает границу актуальности выполнения регламентной операции "Расчет налога УСН".
//
// Параметры:
//   Организация - СправочникСсылка.Организации - ссылка на организацию.
//   Период - Дата - период, до которого необходимо сдвинуть границу актуальности.
//
Процедура СдвинутьГраницуАктуальности(Организация, Период) Экспорт
	
	РегистрыСведений.НеактуальныеРегламентныеОперации.СдвинутьГраницуАктуальностиНазад(
		Организация, 
		КонецМесяца(Период), 
		ЗакрытиеМесяца.ГруппаПоВидуОперации(Перечисления.ВидыРегламентныхОпераций.РасчетНалогаУСН), 
		Перечисления.ВидыРегламентныхОпераций.РасчетНалогаУСН, 
		Истина);
		
КонецПроцедуры

#КонецОбласти

#КонецЕсли