
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПериодАктуализации = Параметры.ПериодАктуализации;

	ПредставлениеПериодаАктуализации = ВыборПериодаКлиентСервер.ПолучитьПредставлениеПериодаОтчета(
		Перечисления.ДоступныеПериодыОтчета.Месяц, НачалоМесяца(ПериодАктуализации), КонецМесяца(ПериодАктуализации));
	ОстаткиАктуальны = Параметры.ОстаткиАктуальны;
	Если Не ОстаткиАктуальны Тогда
		ШаблонЗаголовка =
			НСтр("ru = 'Переход на учет НЗП с детализацией до продукции выполнен в %1
				 |Остатки на этот период не актуализированы. Требуется актуализация остатков.'");
	Иначе
		ШаблонЗаголовка =
			НСтр("ru = 'Переход на учет НЗП с детализацией до продукции выполнен в %1
				 |Остатки на этот период актуализированы.'");
	КонецЕсли;
	
	ПредложныйПадеж = 6;
	ПредставлениеПериода =
		ПереходНЗПДетализацияПоПродукции.ПредставлениеМесяцГод(ПериодАктуализации, ПредложныйПадеж);
	
	Элементы.НадписьИнформация.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ШаблонЗаголовка,
		ПредставлениеПериода);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПредставлениеПериодаАктуализацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПредставлениеПериодаАктуализацииНачалоВыбораЗавершение", ЭтотОбъект);
	ПараметрыВыбора = Новый Структура("НачалоПериода, КонецПериода", НачалоМесяца(ПериодАктуализации), КонецМесяца(ПериодАктуализации));
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериодаМесяц", ПараметрыВыбора, Элемент, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаАктуализацииПриИзменении(Элемент)
	ПредставлениеПериодаАктуализации = ВыборПериодаКлиентСервер.ПолучитьПредставлениеПериодаОтчета(
		ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Месяц"),
		ПериодАктуализации,
		КонецМесяца(ПериодАктуализации));
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаАктуализацииОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	Закрыть(ПериодАктуализации);
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть(Неопределено);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПредставлениеПериодаАктуализацииНачалоВыбораЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПериодАктуализации = РезультатВыбора.НачалоПериода;
		
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти
