
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК ЭтотСписок
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОбособленныеПодразделения 
	|	ПО ОбособленныеПодразделения.ГоловнаяОрганизация = ЭтотСписок.Организация.ГоловнаяОрганизация
	|;
	|РазрешитьЧтение
	|ГДЕ
	| ЗначениеРазрешено(ОбособленныеПодразделения.Ссылка)
	|
	|;
	|РазрешитьИзменениеЕслиРазрешеноЧтение
	|ГДЕ
	| ЗначениеРазрешено(ОбособленныеПодразделения.Ссылка)";
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Переносит документы остатков на заданную дату и корректирует данные документов.
//
// Параметры:
//    Параметры - Структура - Структура параметров с обязательными ключами:
//                * Организация - СправочникСсылка.Организации
//                * ДатаАктуализации - Дата
//                * СчетаУчета - Массив из ПланСчетовСсылка.Хозрасчетный
//    АдресРезультата - Строка
//
Процедура АктуализироватьОстатки(Параметры, АдресРезультата) Экспорт
	
	// Действия выполняются в транзакции, потому что создание и обновление документов ввода начальных остатков НЗП
	// должно быть согласовано с записью в регистр сведений.
	// Блокировку остатков счета не делаем: актуализацию имеет смысл делать только после того, как месяц закрыт и
	// не планируется изменять данные в периодах, которые анализируются при актуализации.
	НачатьТранзакцию();
	
	Попытка
		
		ПереходНЗПДетализацияПоПродукции.СформироватьИАктуализироватьДокументыПерехода(
			Параметры.Организация, Параметры.ДатаАктуализации, Параметры.СчетаУчета);
			
		ПоместитьВоВременноеХранилище(Параметры.ДатаАктуализации, АдресРезультата);
			
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Переход на учет НЗП с детализацией по продукции'", ОбщегоНазначения.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.РегистрыСведений.ДокументыПереходаНЗПДетализацияПоПродукции,
				,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
		ВызватьИсключение ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		
	КонецПопытки;

КонецПроцедуры


#КонецОбласти

#КонецЕсли
