#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.Свойство("СообщенияОтБанков") Тогда  
		Возврат;
	КонецЕсли; 
	
	Если Параметры.СообщенияОтБанков.Количество() > 1 Тогда    
		ЗаголовокКнопки = НСтр("ru='Создать счета'");
	КонецЕсли;
	
	МассивНадписей = Новый Массив;
	
	Для Каждого СтрокаСообщения Из Параметры.СообщенияОтБанков Цикл   
		Если СтрокаСообщения.КоличествоСчетов = 0 Тогда
        	Возврат;
		ИначеЕсли СтрокаСообщения.КоличествоСчетов = 1 Тогда
			ТекстНадписиСчета = НСтр("ru='расчетный счет'");
		Иначе 
			ТекстНадписиСчета = НСтр("ru='расчетные счета'");  
			ЗаголовокКнопки = НСтр("ru='Создать счета'");
		КонецЕсли;
		ШаблонНадписи = НСтр("ru='Банк %1 открыл %2 для организации %3'");  
		ТекстНадписи = СтрШаблон(ШаблонНадписи, СтрокаСообщения.Банк, ТекстНадписиСчета, СтрокаСообщения.Организация); 
		МассивНадписей.Добавить(ТекстНадписи);
	КонецЦикла; 
	
	Элементы.ТекстНадписи.Заголовок = СтрСоединить(МассивНадписей,Символы.ПС);
	Элементы.КнопкаСоздать.Заголовок = ЗаголовокКнопки;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаСоздать(Команда)
	
    СоздатьСчета();
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура БольшеНеПоказывать(Команда)     
	
	СообщенияПрочитаны = ЗаявкиНаОткрытиеСчетаВызовСервера.ПрочитаныНовыеСообщенияОбОткрытии();  
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьПозже(Команда)
	
	ПодключитьОбработчикОжидания("Подключаемый_ОповеститьОбОткрытииСчета", 10800, Истина);    
	Закрыть();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции  

&НаКлиенте
Процедура СоздатьСчета()
	
	МассивСообщений = Новый Массив();
	ОтключитьОбработчикОжидания("Подключаемый_ОповеститьОбОткрытииСчета");
	СчетаСозданы = ЗаявкиНаОткрытиеСчетаВызовСервера.СозданыСчетаПоНовымСообщениямОбОткрытии(МассивСообщений);
	
	Если СчетаСозданы = Ложь Тогда  
		ПодключитьОбработчикОжидания("Подключаемый_ОповеститьОбОткрытииСчета", 120, Истина);  
	КонецЕсли;   
	
	ВывестиОповещения(МассивСообщений);   
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиОповещения(МассивСообщений)   
	
	Ссылка = "";
	КоличествоСчетов = 0;   
	ЕстьОшибки = Ложь; 
	
	Для Каждого СтрокаСообщений Из МассивСообщений Цикл     
		
		Если СтрокаСообщений = "Ошибка" Тогда   
			ЗаголовокОшибки = НСтр("ru = 'Ошибки создания счета в'"); 
			СсылкаОшибки = "e1cib/app/РегистрСведений.СостояниеЗаявокНаОткрытиеСчета.Форма.СостояниеЗаявокНаОткрытиеСчета";  
			ПояснениеОшибки = НСтр("ru = 'Заявки 1С:Открытие счета'");
			ЕстьОшибки = Истина; 
		Иначе
			Если Ссылка = "" Тогда
				Ссылка = ПолучитьНавигационнуюСсылку(СтрокаСообщений);     
				ПояснениеСоздания = Строка(СтрокаСообщений);      
				Заголовок = НСтр("ru = 'Создан счет:'"); 
			КонецЕсли;
			КоличествоСчетов = КоличествоСчетов + 1;    
		КонецЕсли;   
		
	КонецЦикла;
	
	Если КоличествоСчетов > 1 Тогда 
		
		ШаблонЗаголовка = НСтр("ru = 'Создано %1 новых счета в'");
		Заголовок = СтрШаблон(ШаблонЗаголовка, КоличествоСчетов);
		Ссылка = "e1cib/app/Справочник.БанковскиеСчета.Форма.ФормаСписка"; 
		ПояснениеСоздания = НСтр("ru = 'справочнике Банковские счета'");   
	КонецЕсли;   
		
	Если ЕстьОшибки <> 0 Тогда
		ПоказатьОповещениеПользователя(ЗаголовокОшибки, СсылкаОшибки, ПояснениеОшибки,
									  БиблиотекаКартинок.Ошибка32, СтатусОповещенияПользователя.Важное);  		
	КонецЕсли;
	
	Если КоличествоСчетов <> 0 Тогда
		ПоказатьОповещениеПользователя(Заголовок, Ссылка, ПояснениеСоздания, 
									  БиблиотекаКартинок.Информация32, СтатусОповещенияПользователя.Важное);  		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
