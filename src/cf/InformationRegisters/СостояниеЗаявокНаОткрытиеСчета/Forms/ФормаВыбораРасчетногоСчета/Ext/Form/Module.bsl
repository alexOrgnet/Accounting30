
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПодробностиРешения = ПолучитьИзВременногоХранилища(Параметры.ПодробностиРешенияАдрес);
	Действие = Параметры.Действие;
	Организация = Параметры.Организация;

	Если ТипЗнч(ПодробностиРешения) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ИнформацияПоСчетам = ПодробностиРешения.РасчетныеСчета;
	
	Для Каждого РасчетныйСчет Из ИнформацияПоСчетам Цикл
		
		Банк = РаботаСБанкамиБП.НайтиБанк(РасчетныйСчет.БИК,, РасчетныйСчет.КоррСчет);
				
		СтрокаСчета = ТаблицаСчетов.Добавить();

		Если ЗначениеЗаполнено(Банк) Тогда
			НаименованиеБанка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Банк, "Наименование");  
			СтрокаСчета.Пометка =  Истина;
		Иначе
			НаименованиеБанка = НСтр("ru='<Обновите классификатор банков>'");
			СтрокаСчета.Пометка =  Ложь;
		КонецЕсли;
		
		СтрокаСчета.НомерСчета = РасчетныйСчет.НомерСчета;
		СтрокаСчета.БанкНаименование =  НаименованиеБанка;
		СтрокаСчета.БИК =  РасчетныйСчет.БИК;     
		СтрокаСчета.Банк =  Банк;    
		СтрокаСчета.КоррСчет =  РасчетныйСчет.КоррСчет;    
		СтрокаСчета.Валюта =  РасчетныйСчет.Валюта;
		СтрокаСчета.ДатаОткрытия =  РасчетныйСчет.ДатаОткрытия;
				
	КонецЦикла;
			
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ) 
	
	Если ТаблицаСчетов.Количество() = 1 Тогда 	
		ОтправитьПисьмо();		
	КонецЕсли;     
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УведомитьКонтрагентов(Команда)
	
	// Перед созданием нового письма проверим настройки учетной записи для отправки почты
	ВыбранныеСчета = Новый Структура("Пометка", Истина);
	МассивСчетов = ТаблицаСчетов.НайтиСтроки(ВыбранныеСчета);
	
	Если МассивСчетов.Количество() <> 0 Тогда 	
		ОтправитьПисьмо();		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПисьмо()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьПоПочтеНастройкаУчетнойЗаписиПредложена", ЭтотОбъект);
	РаботаСПочтовымиСообщениямиКлиент.ПроверитьНаличиеУчетнойЗаписиДляОтправкиПочты(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсе(Команда)
	
	Для Каждого СтрокаТЧ Из ТаблицаСчетов Цикл
		СтрокаТЧ.Пометка = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьВсе(Команда)
	
	Для Каждого СтрокаТЧ Из ТаблицаСчетов Цикл
		СтрокаТЧ.Пометка = Ложь;
	КонецЦикла;
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОтправитьПоПочтеНастройкаУчетнойЗаписиПредложена(УчетнаяЗаписьНастроена, ДополнительныеПараметры) Экспорт
	
	// Если нет настроенной учетной записи прерываем подготовку отправки
	Если УчетнаяЗаписьНастроена <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	Тема = СтрШаблон(НСтр("ru='Реквизиты %1'"), Организация);
	
	ЧастиТекстаПисьма = МассивСтрокТелаПисьма();
	
	ТекстПисьма = СтрСоединить(ЧастиТекстаПисьма, Символы.ПС);
	
	ПараметрыПисьма = РаботаСПочтовымиСообщениямиКлиент.ПараметрыОтправкиПисьма();
	ПараметрыПисьма.Тема = Тема;
	ПараметрыПисьма.Текст = ТекстПисьма;  
	ПараметрыПисьма.Вставить("ВыбиратьПолучателей", Истина);
	РаботаСПочтовымиСообщениямиКлиент.СоздатьНовоеПисьмо(ПараметрыПисьма);
	
	Закрыть();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПодписьОтветственного()
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	Если ЗначениеЗаполнено(ТекущийПользователь) Тогда
		ФизическоеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущийПользователь, "ФизическоеЛицо");
	Иначе
		ФизическоеЛицо = ТекущийПользователь;
	КонецЕсли;
	
	Возврат ФизическоеЛицо;
	
КонецФункции

&НаКлиенте
Функция МассивСтрокТелаПисьма()
	
	ФИООтветственногоЛица =  ПодписьОтветственного();

	ВыбранныеСчета = Новый Структура("Пометка", Истина);
	МассивСчетов = ТаблицаСчетов.НайтиСтроки(ВыбранныеСчета);
	ДатаОткрытия =  Формат(МассивСчетов[0].ДатаОткрытия, "ДЛФ=D");

	ЧастиТекстаПисьма = Новый Массив();
	
	ЧастиТекстаПисьма.Добавить(НСтр("ru='Уважаемые партнеры,'"));
	ЧастиТекстаПисьма.Добавить("");
	ЧастиТекстаПисьма.Добавить("");
	ТекстСтроки = НСтр("ru='настоящим уведомляем о том, что с %1 изменились банковские реквизиты %2.'");
	ЧастиТекстаПисьма.Добавить(СтрШаблон(ТекстСтроки, ДатаОткрытия, Организация));
	ЧастиТекстаПисьма.Добавить("");
	ТекстСтроки = НСтр("ru='С указанной даты все платежи в адрес %1 необходимо направлять по следующим реквизитам'");
	ЧастиТекстаПисьма.Добавить(СтрШаблон(ТекстСтроки, Организация));
	ЧастиТекстаПисьма.Добавить("");
	
	Для Каждого СтрокаРеквизитов Из МассивСчетов Цикл
		
		ЧастиТекстаПисьма.Добавить(СтрШаблон(НСтр("ru='Расчетный счет: %1'"), СтрокаРеквизитов.НомерСчета));	
		ЧастиТекстаПисьма.Добавить(СтрШаблон(НСтр("ru='Банк: %1'"), СтрокаРеквизитов.БанкНаименование));	
		ЧастиТекстаПисьма.Добавить(СтрШаблон(НСтр("ru='БИК: %1'"), СтрокаРеквизитов.БИК));	
		ЧастиТекстаПисьма.Добавить(СтрШаблон(НСтр("ru='Корр. счет: %1'"), СтрокаРеквизитов.КоррСчет));	
		ЧастиТекстаПисьма.Добавить("");
		
	КонецЦикла;		
	
	ЧастиТекстаПисьма.Добавить(НСтр("ru='Иные реквизиты остаются без изменений.'"));
	ЧастиТекстаПисьма.Добавить("");
	ЧастиТекстаПисьма.Добавить(СтрШаблон(НСтр("ru='С уважением, %1'"), ФИООтветственногоЛица));	
	
	Возврат ЧастиТекстаПисьма;
КонецФункции

#КонецОбласти
