#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьПараметрыВыбораСчета();
	УстановитьПодсказку();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПорядокПереоценкиПриИзменении(Элемент)
	
	ПроверитьСоответствиеСчетаПорядкуПереоценки();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПроверитьСоответствиеСчетаПорядкуПереоценки()
	
	УстановитьПараметрыВыбораСчета();
	УстановитьПодсказку();
	
	Если Не ЗначениеЗаполнено(Запись.Счет)
		Или Запись.ПорядокПереоценки <> Перечисления.ОсобыйПорядокПереоценки.ПереоцениватьПоМереПогашенияЗадолженности Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьНалоговыйУчет = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Запись.Счет, "НалоговыйУчет");
	Если Не ЕстьНалоговыйУчет Тогда
		Запись.Счет = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыВыбораСчета()
	
	ПараметрыВыбора = Новый Массив;
	Параметр = Новый ПараметрВыбора("Отбор.Валютный", Истина);
	ПараметрыВыбора.Добавить(Параметр);
	Если Запись.ПорядокПереоценки = Перечисления.ОсобыйПорядокПереоценки.ПереоцениватьПоМереПогашенияЗадолженности Тогда
		Параметр = Новый ПараметрВыбора("Отбор.НалоговыйУчет", Истина);
		ПараметрыВыбора.Добавить(Параметр);
	КонецЕсли;
	Элементы.Счет.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПодсказку()
	
	Если Запись.ПорядокПереоценки = Перечисления.ОсобыйПорядокПереоценки.НеПереоцениватьАвтоматически Тогда
		ТекстПодсказки = НСтр("ru='Курсовые разницы автоматически не начисляются.
			|При необходимости переоценка производится документами ""Операция, введенная вручную""'");
	Иначе
		ТекстПодсказки = НСтр("ru='Курсовые разницы начисляются автоматически. В налоговом учете по налогу на прибыль 
			|с 2022 года применяются правила переоценки требований и обязательств.
			|Для счетов 60, 62, 66, 67, 76, 55.23, 71 такой порядок переоценки применяется всегда,
			|добавлять субсчета этих счетов не требуется.'");
	КонецЕсли;
	Элементы.ПорядокПереоценкиРасширеннаяПодсказка.Заголовок = ТекстПодсказки;
	
КонецПроцедуры

#КонецОбласти