#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбновлениеИнформационнойБазы

Процедура ЗаполнитьПорядокПереоценки(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СчетаСОсобымПорядкомПереоценки.Счет КАК Счет
	|ИЗ
	|	РегистрСведений.СчетаСОсобымПорядкомПереоценкиБУ КАК СчетаСОсобымПорядкомПереоценки
	|ГДЕ
	|	СчетаСОсобымПорядкомПереоценки.ПорядокПереоценки = ЗНАЧЕНИЕ(Перечисление.ОсобыйПорядокПереоценки.ПустаяСсылка)";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Ложь;
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СчетаСОсобымПорядкомПереоценкиБУ");
		Блокировка.Заблокировать();
		
		Набор = РегистрыСведений.СчетаСОсобымПорядкомПереоценкиБУ.СоздатьНаборЗаписей();
		Набор.Прочитать();
		
		Для каждого Запись Из Набор Цикл
			Если ЗначениеЗаполнено(Запись.ПорядокПереоценки) Тогда
				Продолжить;
			КонецЕсли;
			Запись.ПорядокПереоценки = Перечисления.ОсобыйПорядокПереоценки.НеПереоцениватьАвтоматически;
		КонецЦикла;
		
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(Набор);
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'В процедуре РегистрСведений.СчетаСОсобымПорядкомПереоценкиБУ.ЗаполнитьПорядокПереоценки() не удалось обработать набор записей по причине:
				|%1'"),
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение ТекстСообщения;
		
	КонецПопытки;
	
	ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
		УровеньЖурналаРегистрации.Информация,
		Метаданные.РегистрыСведений.СчетаСОсобымПорядкомПереоценкиБУ, ,
		НСтр("ru = 'Процедура РегистрСведений.СчетаСОсобымПорядкомПереоценкиБУ.ЗаполнитьПорядокПереоценки() обработала набор записей'"));
	
	Параметры.ОбработкаЗавершена = Истина;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли