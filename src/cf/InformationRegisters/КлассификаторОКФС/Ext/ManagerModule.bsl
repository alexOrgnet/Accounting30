#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Загружает в фоне актуальный классификатор ОКФС из сервиса классификаторов
// Параметры см. в комментарии к ДлительныеОперации.ВыполнитьВФоне()
//
Процедура ЗагрузитьДанныеИзСервисаКлассификаторов(Параметры, АдресРезультата) Экспорт
	
	Классификаторы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИдентификаторВСервисеКлассификаторов());
	РезультатЗагрузки = РаботаСКлассификаторами.ОбновитьКлассификаторы(Классификаторы);
	
	Если Не ПустаяСтрока(РезультатЗагрузки.КодОшибки) Тогда
		
		ШаблонСообщения = НСтр("ru = 'При загрузке классификатора ОКФС из сервиса классификаторов возникла ошибка:
			|%1'", ОбщегоНазначения.КодОсновногоЯзыка());
		
		ТекстСообщения = СтрШаблон(ШаблонСообщения, РезультатЗагрузки.ИнформацияОбОшибке);
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Обновление классификатора ОКФС'", ОбщегоНазначения.КодОсновногоЯзыка()), 
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.РегистрыСведений.КлассификаторОКФС, ,
			ТекстСообщения);
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает идентификатор классификатора ОКФС в сервисе классификаторов
//
// Возвращаемое значение:
//   Строка
//
Функция ИдентификаторВСервисеКлассификаторов() Экспорт
	
	Возврат "OKFS";
	
КонецФункции

// Возвращает наименование кода ОКФС
//
// Параметры:
//	Код - Строка - Код, наименование которого необходимо получить
//
// Возвращаемое значение:
//   Строка - Наименование, соответствующее коду ОКФС
//
Функция НаименованиеПоКоду(Код) Экспорт
	
	Результат = Новый СписокЗначений;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КлассификаторОКФС.Наименование КАК Наименование
	|ИЗ
	|	РегистрСведений.КлассификаторОКФС КАК КлассификаторОКФС
	|ГДЕ
	|	КлассификаторОКФС.Код = &Код");
	
	Запрос.УстановитьПараметр("Код", Код);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.Наименование;
	
КонецФункции

// Возвращает фиксированное соответствие "код и наименование" классификатора ОКФС.
//
// Возвращаемое значение:
//	Соответствие - Фиксированное соответствие "код и наименование" классификатора ОКФС.
//
Функция ФиксированноеСоответствиеКодНаименование() Экспорт

	Классификатор = Новый Соответствие();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КлассификаторОКФС.Код КАК Код,
	|	КлассификаторОКФС.Наименование КАК Наименование
	|ИЗ
	|	РегистрСведений.КлассификаторОКФС КАК КлассификаторОКФС
	|
	|УПОРЯДОЧИТЬ ПО
	|	Код");
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Классификатор.Вставить(СокрЛП(Выборка.Код), СокрЛП(Выборка.Наименование));
	КонецЦикла;
	
	Возврат Новый ФиксированноеСоответствие(Классификатор);

КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// ИнтернетПоддержкаПользователей.РаботаСКлассификаторами
// См. РаботаСКлассификаторамиПереопределяемый.ПриДобавленииКлассификаторов.
Процедура ПриДобавленииКлассификаторов(Классификаторы) Экспорт
	
	Описатель = РаботаСКлассификаторами.ОписаниеКлассификатора();
	Описатель.Наименование           = НСтр("ru = 'Общероссийский классификатор форм собственности (ОКФС)'");
	Описатель.Идентификатор          = ИдентификаторВСервисеКлассификаторов();
	Описатель.ОбновлятьАвтоматически = Истина;
	Описатель.ОбщиеДанные            = Истина;
	
	Классификаторы.Добавить(Описатель);
	
КонецПроцедуры

// См. РаботаСКлассификаторамиПереопределяемый.ПриЗагрузкеКлассификатора.
Процедура ПриЗагрузкеКлассификатора(Идентификатор, Версия, Адрес, Обработан) Экспорт
	
	Если Идентификатор <> ИдентификаторВСервисеКлассификаторов() Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		ДвоичныеДанныеXML = ПолучитьИзВременногоХранилища(Адрес);
		ДанныеXML = ПолучитьСтрокуИзДвоичныхДанных(ДвоичныеДанныеXML);
		РезультатЧтенияXML = ОбщегоНазначения.ПрочитатьXMLВТаблицу(ДанныеXML);
		
		НаборЗаписей = РегистрыСведений.КлассификаторОКФС.СоздатьНаборЗаписей();
		НаборЗаписей.Загрузить(РезультатЧтенияXML.Данные);
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		
		Обработан = Истина;
	Исключение
		Обработан = Ложь;
		
		ЗаписьЖурналаРегистрации("ЗагрузкаКлассификатора",
			УровеньЖурналаРегистрации.Ошибка, РегистрыСведений.КлассификаторОКФС,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
КонецПроцедуры

// Конец ИнтернетПоддержкаПользователей.РаботаСКлассификаторами

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

Процедура ЗаполнитьКлассификаторПриОбновленииИнформационнойБазы() Экспорт
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Возврат;
	КонецЕсли;
	
	Макет = РегистрыСведений.КлассификаторОКФС.ПолучитьМакет("ОКФС");
	РезультатЧтенияXML = ОбщегоНазначения.ПрочитатьXMLВТаблицу(Макет.ПолучитьТекст());
	
	НаборЗаписей = РегистрыСведений.КлассификаторОКФС.СоздатьНаборЗаписей();
	НаборЗаписей.Загрузить(РезультатЧтенияXML.Данные);
	
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли