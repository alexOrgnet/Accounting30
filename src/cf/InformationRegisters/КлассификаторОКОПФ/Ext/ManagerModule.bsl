#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Загружает в фоне актуальный классификатор ОКОПФ из сервиса классификаторов
// Параметры см. в комментарии к ДлительныеОперации.ВыполнитьВФоне()
//
Процедура ЗагрузитьДанныеИзСервисаКлассификаторов(Параметры, АдресРезультата) Экспорт
	
	Классификаторы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИдентификаторВСервисеКлассификаторов());
	РезультатЗагрузки = РаботаСКлассификаторами.ОбновитьКлассификаторы(Классификаторы);
	
	Если Не ПустаяСтрока(РезультатЗагрузки.КодОшибки) Тогда
		
		ШаблонСообщения = НСтр("ru = 'При загрузке классификатора ОКОПФ из сервиса классификаторов возникла ошибка:
			|%1'", ОбщегоНазначения.КодОсновногоЯзыка());
		
		ТекстСообщения = СтрШаблон(ШаблонСообщения, РезультатЗагрузки.ИнформацияОбОшибке);
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Обновление классификатора ОКОПФ'", ОбщегоНазначения.КодОсновногоЯзыка()), 
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.РегистрыСведений.КлассификаторОКОПФ, ,
			ТекстСообщения);
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает идентификатор классификатора ОКОПФ в сервисе классификаторов
//
// Возвращаемое значение:
//   Строка
//
Функция ИдентификаторВСервисеКлассификаторов() Экспорт
	
	Возврат "OKOPF";
	
КонецФункции

// Возвращает наименование кода ОКОПФ
//
// Параметры:
//	Код - Строка - Код, наименование которого необходимо получить
//
// Возвращаемое значение:
//   Строка - Наименование, соответствующее коду ОКОПФ
//
Функция НаименованиеПоКоду(Код) Экспорт
	
	Результат = Новый СписокЗначений;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КлассификаторОКОПФ.Наименование КАК Наименование
	|ИЗ
	|	РегистрСведений.КлассификаторОКОПФ КАК КлассификаторОКОПФ
	|ГДЕ
	|	КлассификаторОКОПФ.Код = &Код");
	
	Запрос.УстановитьПараметр("Код", Код);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.Наименование;
	
КонецФункции

// Возвращает фиксированное соответствие "код и наименование" классификатора ОКОПФ.
//
// Возвращаемое значение:
//	Соответствие - Фиксированное соответствие "код и наименование" классификатора ОКОПФ.
//
Функция ФиксированноеСоответствиеКодНаименование() Экспорт

	Классификатор = Новый Соответствие();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КлассификаторОКОПФ.Код КАК Код,
	|	КлассификаторОКОПФ.Наименование КАК Наименование
	|ИЗ
	|	РегистрСведений.КлассификаторОКОПФ КАК КлассификаторОКОПФ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Код");
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Классификатор.Вставить(СокрЛП(Выборка.Код), СокрЛП(Выборка.Наименование));
	КонецЦикла;
	
	Возврат Новый ФиксированноеСоответствие(Классификатор);

КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// ИнтернетПоддержкаПользователей.РаботаСКлассификаторами
// См. РаботаСКлассификаторамиПереопределяемый.ПриДобавленииКлассификаторов.
Процедура ПриДобавленииКлассификаторов(Классификаторы) Экспорт
	
	Описатель = РаботаСКлассификаторами.ОписаниеКлассификатора();
	Описатель.Наименование           = НСтр("ru = 'Общероссийский классификатор организационно-правовых форм (ОКОПФ)'");
	Описатель.Идентификатор          = ИдентификаторВСервисеКлассификаторов();
	Описатель.ОбновлятьАвтоматически = Истина;
	Описатель.ОбщиеДанные            = Истина;
	
	Классификаторы.Добавить(Описатель);
	
КонецПроцедуры

// См. РаботаСКлассификаторамиПереопределяемый.ПриЗагрузкеКлассификатора.
Процедура ПриЗагрузкеКлассификатора(Идентификатор, Версия, Адрес, Обработан) Экспорт
	
	Если Идентификатор <> ИдентификаторВСервисеКлассификаторов() Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		ДвоичныеДанныеXML = ПолучитьИзВременногоХранилища(Адрес);
		ДанныеXML = ПолучитьСтрокуИзДвоичныхДанных(ДвоичныеДанныеXML);
		РезультатЧтенияXML = ОбщегоНазначения.ПрочитатьXMLВТаблицу(ДанныеXML);
		
		НаборЗаписей = РегистрыСведений.КлассификаторОКОПФ.СоздатьНаборЗаписей();
		НаборЗаписей.Загрузить(РезультатЧтенияXML.Данные);
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		
		Обработан = Истина;
	Исключение
		Обработан = Ложь;
		
		ЗаписьЖурналаРегистрации("ЗагрузкаКлассификатора",
			УровеньЖурналаРегистрации.Ошибка, РегистрыСведений.КлассификаторОКОПФ,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
КонецПроцедуры

// Конец ИнтернетПоддержкаПользователей.РаботаСКлассификаторами

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

Процедура ЗаполнитьКлассификаторПриОбновленииИнформационнойБазы() Экспорт
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Возврат;
	КонецЕсли;
	
	Макет = РегистрыСведений.КлассификаторОКОПФ.ПолучитьМакет("ОКОПФ");
	РезультатЧтенияXML = ОбщегоНазначения.ПрочитатьXMLВТаблицу(Макет.ПолучитьТекст());
	
	НаборЗаписей = РегистрыСведений.КлассификаторОКОПФ.СоздатьНаборЗаписей();
	НаборЗаписей.Загрузить(РезультатЧтенияXML.Данные);
	
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли