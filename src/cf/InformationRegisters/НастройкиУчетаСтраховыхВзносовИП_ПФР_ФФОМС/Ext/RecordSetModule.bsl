#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Справочники.Организации.ДополнитьДанныеЗаполненияПриОднофирменномУчете(ДанныеЗаполнения);
	
	Если ДанныеЗаполнения = Неопределено Или ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ПараметрыНастроек = ?(ДанныеЗаполнения = Неопределено, Новый Структура, ДанныеЗаполнения);
		Для каждого Запись Из ЭтотОбъект Цикл
			РегистрыСведений.НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС.УстановкаНастроекПоУмолчанию(
				Запись, ПараметрыНастроек);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ТекущаяЗапись Из ЭтотОбъект Цикл
		Если ТекущаяЗапись.Тариф = Перечисления.ИПТарифыФиксированныхВзносов.УчастникСЭЗнаНТ
			И Не УчетСтраховыхВзносовИПКлиентСервер.ПрименяетсяТарифУчастникСЭЗ(ТекущаяЗапись.Период) Тогда
			
			ТекстОшибки = СтрШаблон(
				НСтр("ru = 'С 2026 г. для участников СЭЗ пониженный тариф не действует. Выберите другой тариф для %1.'"),
				ТекущаяЗапись.Организация);
			
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , , , Отказ);
		КонецЕсли;
	КонецЦикла;
	
	НастройкиУчета.ПередЗаписью(ЭтотОбъект, Отказ, Замещение);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиУчета.ПриЗаписи(ЭтотОбъект, Отказ, Замещение);
	
	НеобходимоСоздатьВидыНалогов = Ложь;
	Для Каждого Запись Из ЭтотОбъект Цикл
		Если Запись.ПлательщикСтраховыхВзносовПФР_ФФОМС И УчетнаяПолитика.ПрименяетсяНалогНаПрофессиональныйДоход(
			Запись.Организация, Запись.Период) Тогда
			НеобходимоСоздатьВидыНалогов = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если НеобходимоСоздатьВидыНалогов Тогда
		Справочники.ВидыНалоговИПлатежейВБюджет.СоздатьПоставляемыеЭлементы();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли