#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Склад") Тогда
		Склад = Параметры.Склад;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Склад) Тогда
		Элементы.Склад.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	Если ТипЗнч(ТекущиеДанные.Оборудование) = Тип("СправочникСсылка.ПодключаемоеОборудование") Тогда
		ТипОборудования = "ПодключаемоеОборудование";
	Иначе
		ТипОборудования = "ОфлайнОборудование";
	КонецЕсли; 
	
	ПараметрыОткрытияФормы = Новый Структура();
	ПараметрыОткрытияФормы.Вставить("Ключ", ТекущиеДанные.Оборудование);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ЗавершениеРедактирования", ЭтотОбъект);
	
	ОткрытьФорму("Справочник."+ТипОборудования+".ФормаОбъекта", ПараметрыОткрытияФормы, ЭтотОбъект, , , , ОповещениеОЗакрытии); 
		
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ФискальныйРегистратор(Команда)
	
	НовыйЭкземплярОборудования("ПодключаемоеОборудование", ПредопределенноеЗначение("Перечисление.ТипыПодключаемогоОборудования.ФискальныйРегистратор"));
	
КонецПроцедуры

&НаКлиенте
Процедура МобильнаяКасса(Команда)
	
	НовыйЭкземплярОборудования("ОфлайнОборудование");
	
КонецПроцедуры

&НаКлиенте
Процедура СканерШтрихкода(Команда)
	
	НовыйЭкземплярОборудования("ПодключаемоеОборудование", ПредопределенноеЗначение("Перечисление.ТипыПодключаемогоОборудования.СканерШтрихкода"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПринтерЧеков(Команда)
	
	НовыйЭкземплярОборудования("ПодключаемоеОборудование", ПредопределенноеЗначение( "Перечисление.ТипыПодключаемогоОборудования.ПринтерЧеков"));
	
КонецПроцедуры

&НаКлиенте
Процедура ЭквайринговыйТерминал(Команда)
	
	НовыйЭкземплярОборудования("ПодключаемоеОборудование", ПредопределенноеЗначение("Перечисление.ТипыПодключаемогоОборудования.ЭквайринговыйТерминал"));
	
КонецПроцедуры

&НаКлиенте
Процедура ККМOffline(Команда)
	
	НовыйЭкземплярОборудования("ОфлайнОборудование");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура НовыйЭкземплярОборудования(ВидОборудования, ТипОборудования = Неопределено)
	
	Если НЕ МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("Склад",           Склад);
	ПараметрыОткрытияФормы.Вставить("РабочееМесто",    МенеджерОборудованияВызовСервера.РабочееМестоКлиента());
	ПараметрыОткрытияФормы.Вставить("ТипОборудования", ТипОборудования);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ЗавершениеРедактирования", ЭтотОбъект);
	ОткрытьФорму("Справочник."+ВидОборудования+".ФормаОбъекта", ПараметрыОткрытияФормы, ЭтотОбъект, , , , ОповещениеОЗакрытии);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеРедактирования(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	ЭтаФорма.Элементы.Список.Обновить();
	
КонецПРоцедуры

#КонецОбласти

