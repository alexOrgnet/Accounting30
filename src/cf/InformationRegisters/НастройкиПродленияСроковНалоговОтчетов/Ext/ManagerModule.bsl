#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает настройки для переноса сроков уплаты налогов и представления отчетов для организации
//
// Параметры:
//  Организация - СправочникСсылка.Организации - организация, для которой требуется получить настройки
// 
// Возвращаемое значение:
//   Структура - см. НовыйНастройкиПродленияСроков, поля:
//     * ОснованияПродления          - Массив из ПеречислениеСсылка.ОснованияПродленияСроковНалоговОтчетов
//     * РаботаетВНерабочиеДни       - Булево - признак, что организация работает в дни, объявленные нерабочими
//     * ПеренестиСрокиОтчетовВФонды - Булево - признак, что для организации нужно переносить сроки сдачи отчетов в ФСС и ПФР
//     * ПравилаОтчетовВФонды        - Массив из Строка - правила сдачи отчетов в фонды, для которых предусмотрен особый порядок переноса
//     * ПравилаБезПереносаСроков    - Массив из Строка - правила, для которых не предусмотрен перенос сроков
//
Функция ПараметрыПродленияСроков(Организация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НастройкиПродленияСроковНалоговОтчетов.МерыУстойчивогоРазвития КАК МерыУстойчивогоРазвития,
	|	НастройкиПродленияСроковНалоговОтчетов.РаботаетВНерабочиеДни КАК РаботаетВНерабочиеДни,
	|	НастройкиПродленияСроковНалоговОтчетов.ПеренестиСрокиОтчетовВФонды КАК ПеренестиСрокиОтчетовВФонды,
	|	НастройкиПродленияСроковНалоговОтчетов.ДатаУплатыУСНЗа2019Год КАК ДатаУплатыУСНЗа2019Год,
	|	НастройкиПродленияСроковНалоговОтчетов.ДатаУплатыУСНЗа1Кв2020 КАК ДатаУплатыУСНЗа1Кв2020,
	|	НастройкиПродленияСроковНалоговОтчетов.ДатаУплатыУСНЗа2Кв2020 КАК ДатаУплатыУСНЗа2Кв2020,
	|	НастройкиПродленияСроковНалоговОтчетов.ДатаУплатыУСНЗа3Кв2020 КАК ДатаУплатыУСНЗа3Кв2020,
	|	НастройкиПродленияСроковНалоговОтчетов.ДатаУплатыЕНВДЗа1Кв2020 КАК ДатаУплатыЕНВДЗа1Кв2020,
	|	НастройкиПродленияСроковНалоговОтчетов.ДатаУплатыЕНВДЗа2Кв2020 КАК ДатаУплатыЕНВДЗа2Кв2020,
	|	НастройкиПродленияСроковНалоговОтчетов.ДатаУплатыЕНВДЗа3Кв2020 КАК ДатаУплатыЕНВДЗа3Кв2020,
	|	НастройкиПродленияСроковНалоговОтчетов.ДеятельностьОтнесенаКПострадавшимОтКоронавируса КАК ДеятельностьОтнесенаКПострадавшимОтКоронавируса,
	|	НастройкиПродленияСроковНалоговОтчетов.ПрименяетсяПостановление512 КАК ПрименяетсяПостановление512,
	|	НастройкиПродленияСроковНалоговОтчетов.ПрименяетсяПостановление776П1 КАК ПрименяетсяПостановление776П1,
	|	НастройкиПродленияСроковНалоговОтчетов.ПрименяетсяПостановление776П2 КАК ПрименяетсяПостановление776П2
	|ИЗ
	|	РегистрСведений.НастройкиПродленияСроковНалоговОтчетов КАК НастройкиПродленияСроковНалоговОтчетов
	|ГДЕ
	|	НастройкиПродленияСроковНалоговОтчетов.Организация = &Организация";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Результат = НовыйНастройкиПродленияСроков();
	
	Результат.ПравилаОтчетовВФонды.Добавить("СтраховыеВзносы.2017_СведенияПФРЕжемесячные");
	Результат.ПравилаОтчетовВФонды.Добавить("СтраховыеВзносы.2020_СведенияЭТКЕжемесячные");
	Результат.ПравилаОтчетовВФонды.Добавить("СтраховыеВзносы.2020_СведенияЭТКЕжедневные");
	
	Результат.ПравилаБезПереносаСроков.Добавить("СтатистическаяОтчетность");
	Результат.ПравилаБезПереносаСроков.Добавить("ДекларированиеАлкогольнойПродукции");
	
	// Сроки представления налоговых деклараций (кроме НДС) продлены для всех организаций
	Результат.ОснованияПродления.Добавить(ОснованияПродленияСроковНалоговОтчетов.МерыУстойчивогоРазвитияП3Абз1());
	
	// Сроки представления налоговых деклараций по НДС и РСВ продлены для всех организаций
	Результат.ОснованияПродления.Добавить(ОснованияПродленияСроковНалоговОтчетов.МерыУстойчивогоРазвитияП3Абз2());
	
	// Сроки уплаты торгового сбора продлены для всех организаций-плательщиков торгового сбора
	Результат.ОснованияПродления.Добавить(ОснованияПродленияСроковНалоговОтчетов.Постановление212ПП_П2());
	
	// Сроки представления 4-ФСС продлены для всех организаций
	Результат.ОснованияПродления.Добавить(ОснованияПродленияСроковНалоговОтчетов.РазъясненияФСС());
	
	// Сроки представления отчетности в Минюст продлены для всех НКО
	Результат.ОснованияПродления.Добавить(ОснованияПродленияСроковНалоговОтчетов.РазъясненияМинюста());
	
	// Сроки уплаты авансов по налогу на прибыль за 1 квартал 2022 продлены для всех организаций
	Результат.ОснованияПродления.Добавить(ОснованияПродленияСроковНалоговОтчетов.ПостановлениеПравительства470());
	
	Если Выборка.Следующий() Тогда
		
		Результат.Вставить("РаботаетВНерабочиеДни",       Выборка.РаботаетВНерабочиеДни);
		Результат.Вставить("ПеренестиСрокиОтчетовВФонды", Выборка.ПеренестиСрокиОтчетовВФонды);
		Результат.Вставить("ДатаУплатыУСНЗа2019Год",      Выборка.ДатаУплатыУСНЗа2019Год);
		Результат.Вставить("ДатаУплатыУСНЗа1Кв2020",      Выборка.ДатаУплатыУСНЗа1Кв2020);
		Результат.Вставить("ДатаУплатыУСНЗа2Кв2020",      Выборка.ДатаУплатыУСНЗа2Кв2020);
		Результат.Вставить("ДатаУплатыУСНЗа3Кв2020",      Выборка.ДатаУплатыУСНЗа3Кв2020);
		Результат.Вставить("ДатаУплатыЕНВДЗа1Кв2020",     Выборка.ДатаУплатыЕНВДЗа1Кв2020);
		Результат.Вставить("ДатаУплатыЕНВДЗа2Кв2020",     Выборка.ДатаУплатыЕНВДЗа2Кв2020);
		Результат.Вставить("ДатаУплатыЕНВДЗа3Кв2020",     Выборка.ДатаУплатыЕНВДЗа3Кв2020);
		Результат.Вставить("ДеятельностьОтнесенаКПострадавшимОтКоронавируса", Выборка.ДеятельностьОтнесенаКПострадавшимОтКоронавируса);
		
		Если Выборка.МерыУстойчивогоРазвития Тогда
			
			Результат.ОснованияПродления.Добавить(ОснованияПродленияСроковНалоговОтчетов.МерыУстойчивогоРазвитияП1аАбз1());
			Результат.ОснованияПродления.Добавить(ОснованияПродленияСроковНалоговОтчетов.МерыУстойчивогоРазвитияП1аАбз2());
			Результат.ОснованияПродления.Добавить(ОснованияПродленияСроковНалоговОтчетов.МерыУстойчивогоРазвитияП1аАбз3());
			Результат.ОснованияПродления.Добавить(ОснованияПродленияСроковНалоговОтчетов.МерыУстойчивогоРазвитияП1аАбз4());
			Результат.ОснованияПродления.Добавить(ОснованияПродленияСроковНалоговОтчетов.МерыУстойчивогоРазвитияП1аАбз5());
			Результат.ОснованияПродления.Добавить(ОснованияПродленияСроковНалоговОтчетов.МерыУстойчивогоРазвитияП1б());
			Результат.ОснованияПродления.Добавить(ОснованияПродленияСроковНалоговОтчетов.МерыУстойчивогоРазвитияП1бИП());
			Результат.ОснованияПродления.Добавить(ОснованияПродленияСроковНалоговОтчетов.МерыУстойчивогоРазвитияП1в());
			
		КонецЕсли;
		
		Если Выборка.ПрименяетсяПостановление512 Тогда
			Результат.ОснованияПродления.Добавить(ОснованияПродленияСроковНалоговОтчетов.ПостановлениеПравительства512());
		КонецЕсли;
		
		Если Выборка.ПрименяетсяПостановление776П1 Тогда
			Результат.ОснованияПродления.Добавить(ОснованияПродленияСроковНалоговОтчетов.ПостановлениеПравительства776П1());
		КонецЕсли;
		
		Если Выборка.ПрименяетсяПостановление776П2 Тогда
			Результат.ОснованияПродления.Добавить(ОснованияПродленияСроковНалоговОтчетов.ПостановлениеПравительства776П2());
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает период, в рамках которого возможен перенос сроков уплаты налогов и авансовых платежей 
// в соответствии с Федеральным законом от 01.04.2020 № 102-ФЗ
// 
// Возвращаемое значение:
//   - Структура
//	 	- Начало - Дата
//		- Окончание - Дата
//
Функция ПериодПереносаСроковНалоговОтчетов() Экспорт
	
	Период = Новый Структура;
	Период.Вставить("Начало",    '20200101');
	Период.Вставить("Окончание", '20201231');
	
	Возврат Период;
	
КонецФункции	

// Проверяет, продлены ли для организации сроки уплаты налогов в соответствии с Постановлением правительства РФ №409 от 02.04.2020
// Внимание! Функция не учитывает период действия освобождения - его нужно проверять отдельно.
//
// Параметры:
//  Организация	 - СправочникСсылка.Организации	 - организация, для которой проверяются настройки
// 
// Возвращаемое значение:
//   - Булево
//
Функция ПродленыСрокиУплатыНалоговПредставленияОтчетов(Организация) Экспорт 

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НастройкиПродленияСроковНалоговОтчетов.МерыУстойчивогоРазвития КАК СрокиПродлены
	|ИЗ
	|	РегистрСведений.НастройкиПродленияСроковНалоговОтчетов КАК НастройкиПродленияСроковНалоговОтчетов
	|ГДЕ
	|	НастройкиПродленияСроковНалоговОтчетов.Организация = &Организация";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Ложь;
	КонецЕсли;	
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.СрокиПродлены;
	
КонецФункции 

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК ЭтотСписок
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОбособленныеПодразделения 
	|	ПО ОбособленныеПодразделения.ГоловнаяОрганизация = ЭтотСписок.Организация.ГоловнаяОрганизация
	|;
	|РазрешитьЧтение
	|ГДЕ
	| ЗначениеРазрешено(ОбособленныеПодразделения.Ссылка)
	|
	|;
	|РазрешитьИзменениеЕслиРазрешеноЧтение
	|ГДЕ
	| ЗначениеРазрешено(ЭтотСписок.Организация)";
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыйНастройкиПродленияСроков()
	
	Результат = Новый Структура;
	
	Результат.Вставить("ОснованияПродления",          Новый Массив);
	Результат.Вставить("РаботаетВНерабочиеДни",       Ложь);
	Результат.Вставить("ПеренестиСрокиОтчетовВФонды", Ложь);
	Результат.Вставить("ПравилаОтчетовВФонды",        Новый Массив);
	Результат.Вставить("ПравилаБезПереносаСроков",    Новый Массив);
	Результат.Вставить("ДатаУплатыУСНЗа2019Год",      '00010101');
	Результат.Вставить("ДатаУплатыУСНЗа1Кв2020",      '00010101');
	Результат.Вставить("ДатаУплатыУСНЗа2Кв2020",      '00010101');
	Результат.Вставить("ДатаУплатыУСНЗа3Кв2020",      '00010101');
	Результат.Вставить("ДатаУплатыЕНВДЗа1Кв2020",     '00010101');
	Результат.Вставить("ДатаУплатыЕНВДЗа2Кв2020",     '00010101');
	Результат.Вставить("ДатаУплатыЕНВДЗа3Кв2020",     '00010101');
	Результат.Вставить("ДеятельностьОтнесенаКПострадавшимОтКоронавируса", Ложь);
	
	Возврат Результат;
	
КонецФункции

#Область ОбработчикиОбновления

// Обработчик обновления 3.0.77.69.
// Заполняет настройку МерыУстойчивогоРазвития, если включена МерыУстойчивогоРазвитияМикро.
// Постановление Правительства №570 от 24.04.2020 распространило продление сроков уплаты страховых взносов
// на все организации-субъекты МСП, наиболее пострадавшие от коронавируса, независимо от того, являются ли они микропредприятиями.
//
Процедура ЗаполнитьНастройкиПродленияСроковДляМСП() Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиПродленияСроковНалоговОтчетов.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.НастройкиПродленияСроковНалоговОтчетов КАК НастройкиПродленияСроковНалоговОтчетов
	|ГДЕ
	|	НастройкиПродленияСроковНалоговОтчетов.МерыУстойчивогоРазвитияМикро
	|	И НЕ НастройкиПродленияСроковНалоговОтчетов.МерыУстойчивогоРазвития";
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			НаборЗаписей = РегистрыСведений.НастройкиПродленияСроковНалоговОтчетов.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
			НаборЗаписей.Прочитать();
			Для Каждого Запись Из НаборЗаписей Цикл
				Запись.МерыУстойчивогоРазвития = Истина;
			КонецЦикла;
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей, Истина);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли