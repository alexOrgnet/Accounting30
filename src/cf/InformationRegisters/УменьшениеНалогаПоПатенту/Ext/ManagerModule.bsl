#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает данные об уменьшениях налога по патентной системе налогооблоожения на суммы
// страховых взносов и больничных пособий
//
// Параметры:
//  Организация            - СправочникСсылка.Организации - организация, по которой осуществляется отбор данных
//  Дата - Дата            - определяет год, за который будут получены данные
//  ДокументПатент         - ДокументСсылка.ОперацияСПатентом - если заполнен, то данные будут получены с отбором по патенту
//  ИсключаемоеУведомление - ДокументСсылка.УведомлениеОСпецрежимахНалогообложения - если заполнен, то данные будут получены без учета 
//                                                                                   переданного уведомления
//
// Возвращаемое значение:
//  Соответствие - * Ключ - ДокументСсылка.ОперацияСПатентом
//                 * Значение - Структура - см. НовыеСведенияУменьшениеНалогаПоПатенту
//
Функция СведенияУменьшениеНалогаЗаГод(Организация, Дата, ДокументПатент = Неопределено, ИсключаемоеУведомление = Неопределено) Экспорт
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(
	"ВЫБРАТЬ
	|	ОперацияСПатентом.Ссылка КАК ДокументПатент,
	|	УменьшениеНалоговПСН.Уведомление КАК Уведомление,
	|	УменьшениеНалоговПСН.Сумма КАК Сумма,
	|	УведомлениеОбУменьшенииНалогаПоПатенту.Дата КАК Дата
	|ИЗ
	|	РегистрСведений.УменьшениеНалогаПоПатенту КАК УменьшениеНалоговПСН
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОперацияСПатентом КАК ОперацияСПатентом
	|		ПО УменьшениеНалоговПСН.ДокументПатент = ОперацияСПатентом.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УведомлениеОбУменьшенииНалогаПоПатенту КАК УведомлениеОбУменьшенииНалогаПоПатенту
	|		ПО УменьшениеНалоговПСН.Регистратор = УведомлениеОбУменьшенииНалогаПоПатенту.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УведомлениеОСпецрежимахНалогообложения КАК УведомлениеОСпецрежимахНалогообложения
	|		ПО УменьшениеНалоговПСН.Уведомление = УведомлениеОСпецрежимахНалогообложения.Ссылка
	|ГДЕ
	|	УменьшениеНалоговПСН.Организация = &Организация
	|	И НЕ УведомлениеОСпецрежимахНалогообложения.ПометкаУдаления
	|ИТОГИ
	|	СУММА(Сумма)
	|ПО
	|	ДокументПатент");
	
	УсловияЗапроса = СхемаЗапроса.ПакетЗапросов[0].Операторы[0].Отбор;
	
	Если ЗначениеЗаполнено(ДокументПатент) Тогда
		УсловияЗапроса.Добавить("УменьшениеНалоговПСН.ДокументПатент = &ДокументПатент");
	Иначе
		УсловияЗапроса.Добавить("ОперацияСПатентом.ДатаНачала МЕЖДУ &ДатаНачала И &ДатаОкончания");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИсключаемоеУведомление) Тогда
		УсловияЗапроса.Добавить("УменьшениеНалоговПСН.Уведомление <> &Уведомление");
		УсловияЗапроса.Добавить("УведомлениеОСпецрежимахНалогообложения.Дата < &Период");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДокументПатент", ДокументПатент);
	Запрос.УстановитьПараметр("Уведомление", ИсключаемоеУведомление);
	Запрос.УстановитьПараметр("ДатаНачала", НачалоГода(Дата));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецГода(Дата));
	Запрос.УстановитьПараметр("Период", НачалоДня(Дата));
	
	ВыборкаПатент = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	СведенияУменьшениеНалогаОбщие = Новый Соответствие;
	
	Пока ВыборкаПатент.Следующий() Цикл
		СведенияУменьшениеНалогаПоПатенту = НовыеСведенияУменьшениеНалогаПоПатенту();
		СведенияУменьшениеНалогаПоПатенту.Сумма = ВыборкаПатент.Сумма;
		ВыборкаДетальная = ВыборкаПатент.Выбрать();
		Пока ВыборкаДетальная.Следующий() Цикл
			РеквизитыУведомления = НовыеСведенияОбУведомлении();
			ЗаполнитьЗначенияСвойств(РеквизитыУведомления, ВыборкаДетальная);
			СведенияУменьшениеНалогаПоПатенту.Уведомления.Добавить(РеквизитыУведомления);
		КонецЦикла;
		СведенияУменьшениеНалогаОбщие.Вставить(ВыборкаПатент.ДокументПатент, СведенияУменьшениеНалогаПоПатенту);
	КонецЦикла;
	
	Возврат СведенияУменьшениеНалогаОбщие;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыеСведенияУменьшениеНалогаПоПатенту()
	
	Сведения = Новый Структура;
	Сведения.Вставить("Сумма", 0);
	Сведения.Вставить("Уведомления", Новый Массив);
	Возврат Сведения;
	
КонецФункции

Функция НовыеСведенияОбУведомлении()
	
	Сведения = Новый Структура;
	
	Сведения.Вставить("Уведомление", Документы.УведомлениеОСпецрежимахНалогообложения.ПустаяСсылка());
	Сведения.Вставить("Сумма", 0);
	Сведения.Вставить("Дата", Дата(1, 1, 1));
	
	Возврат Сведения;
	
КонецФункции

#КонецОбласти

#КонецЕсли