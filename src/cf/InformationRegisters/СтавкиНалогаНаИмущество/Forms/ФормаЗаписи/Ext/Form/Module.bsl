
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
    ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(СтавкиНалогаНаИмущество, ЭтотОбъект);
	
	Если Параметры.Ключ.Пустой() Тогда
		ПодготовитьФормуНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// Очистку регистра ПроверенныеДокументы должны сделать в любом случае. Она продолжится, даже если форма закроется.
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеЗаписиЗавершение", ЭтотОбъект);
	ПроведениеКлиент.НачатьОчисткуРегистраПроверенныхДокументов(
		ОповещениеОЗавершении, СтавкиНалогаНаИмущество.Период, СтавкиНалогаНаИмущество.Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ПараметрыОповещения = Новый Структура("Организация, Период", СтавкиНалогаНаИмущество.Организация, СтавкиНалогаНаИмущество.Период);
	Оповестить("Запись_СтавкиНалогаНаИмущество", ПараметрыОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ДокументыПодтверждающиеЛьготыПоИмущественнымНалогам" Тогда
		
		Если Параметр.Организация = СтавкиНалогаНаИмущество.Организация
			 И Параметр.Льгота = СтавкиНалогаНаИмущество.ОснованиеЛьготы Тогда
			УстановитьТекстПодсказкиЛьготы();
		КонецЕсли;
		
	КонецЕсли;	
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыВыбора = Новый Структура;
	ПараметрыВыбора.Вставить("НачалоПериода", НачалоМесяца(СтавкиНалогаНаИмущество.Период));
	ПараметрыВыбора.Вставить("КонецПериода",  КонецМесяца(СтавкиНалогаНаИмущество.Период));
	ПараметрыВыбора.Вставить("ВидПериода",    ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Месяц"));
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПериодЗавершениеВыбора", ЭтотОбъект);
	
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериодаМесяц", ПараметрыВыбора, ЭтотОбъект, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодЗавершениеВыбора(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Период = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
		РезультатВыбора.НачалоПериода,
		РезультатВыбора.КонецПериода,
		Истина);
	
	Модифицированность = Истина;
	
	СтавкиНалогаНаИмущество.Период = РезультатВыбора.НачалоПериода;
		
КонецПроцедуры

&НаКлиенте
Процедура ПериодОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	НачалоПериода = НачалоМесяца(СтавкиНалогаНаИмущество.Период);
	КонецПериода  = КонецМесяца(СтавкиНалогаНаИмущество.Период);
	
	ВыборПериодаКлиент.ПериодОбработкаВыбора(
		Элемент,
		ВыбранноеЗначение,
		СтандартнаяОбработка,
		ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Месяц"),
		Период,
		НачалоПериода,
		КонецПериода);
	
	Модифицированность = Истина;
	
	СтавкиНалогаНаИмущество.Период = НачалоПериода;
		
КонецПроцедуры

&НаКлиенте
Процедура ПериодАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	НачалоПериода = НачалоМесяца(СтавкиНалогаНаИмущество.Период);
	КонецПериода  = КонецМесяца(СтавкиНалогаНаИмущество.Период);
	
	ВыборПериодаКлиент.ПериодАвтоПодбор(
		Элемент,
		Текст,
		ДанныеВыбора,
		Ожидание,
		СтандартнаяОбработка,
		ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Месяц"),
		Период,
		НачалоПериода,
		КонецПериода);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	УстановитьТекстПодсказкиЛьготы(); 
	
КонецПроцедуры

&НаКлиенте
Процедура ОснованиеЛьготыПриИзменении(Элемент)
	
	ЗаполнитьПараметрыЛьготы();
	СтавкиНалогаНаИмуществоФормыКлиентСервер.УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОснованиеЛьготыРасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтавкиНалогаНаИмуществоФормыКлиент.ОснованиеЛьготыРасширеннаяПодсказкаОбработкаНавигационнойСсылки(
		ЭтотОбъект, "СтавкиНалогаНаИмущество", СтандартнаяОбработка);
			
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	Период = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
		НачалоМесяца(СтавкиНалогаНаИмущество.Период),
		КонецМесяца(СтавкиНалогаНаИмущество.Период),
		Истина);
	
	СтавкиНалогаНаИмуществоФормы.ПодготовитьФормуНаСервере(ЭтотОбъект, "СтавкиНалогаНаИмущество");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПараметрыЛьготы()
	
	СтавкиНалогаНаИмуществоФормы.ЗаполнитьПараметрыЛьготы(ЭтотОбъект, "СтавкиНалогаНаИмущество");
		
КонецПроцедуры	

&НаСервере
Процедура УстановитьТекстПодсказкиЛьготы()
	
	СтавкиНалогаНаИмуществоФормы.УстановитьТекстПодсказкиЛьготы(ЭтотОбъект, "СтавкиНалогаНаИмущество");
			
КонецПроцедуры

#КонецОбласти