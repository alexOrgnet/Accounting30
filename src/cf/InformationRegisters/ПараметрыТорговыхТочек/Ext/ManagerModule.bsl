#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает строковое представление уведомления о постановке на учет в качестве плательщика
// торгового сбора.
//
// Параметры:
//  Уведомление - ДокументСсылка.УведомленияОСпецрежимахНалогообложения - ссылка на уведомление.
//
// Возвращаемое значение:
//  Строка - представление уведомления.
//
Функция ПредставлениеУведомления(Уведомление) Экспорт
	
	Результат = "";
	
	Если НЕ ЗначениеЗаполнено(Уведомление) Тогда
		Возврат Результат;
	КонецЕсли;
	
	// БРО может поменять структуру данных уведомлений.
	Попытка
		
		ДанныеУведомления = Уведомление.ДанныеУведомления.Получить();
		
		Если Уведомление.ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ФормаТС1 Тогда
			КодПричины = ДанныеУведомления.Титульный[0].КодПричины;
			
			Если КодПричины = "1" Тогда
				Представление = НСтр("ru='О постановке на учет в ИФНС %1'");
				
			ИначеЕсли КодПричины = "2" Тогда
				Представление = НСтр("ru='Об изменении параметров в ИФНС %1'");
				
			Иначе
				Представление = НСтр("ru='О снятии с учета в ИФНС %1'");
				
			КонецЕсли;
			
			Результат = СтрШаблон(Представление, ДанныеУведомления.Титульный[0].КОД_НО);
			
		ИначеЕсли Уведомление.ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ФормаТС2 Тогда
			Представление = НСтр("ru='О снятии с учета в ИФНС %1'");
			Результат = СтрШаблон(Представление, ДанныеУведомления.Титульный[0].КОД_НО);
			
		Иначе
			Результат = Строка(Уведомление);
			
		КонецЕсли;
		
	Исключение
		Результат = Строка(Уведомление);
		
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Возвращает код льготы по наименованию.
//
// Параметры:
//  Льготы - ТаблицаЗначений - см. ТорговыйСбор.ПрочитатьТаблицуЛьгот().
//  НаименованиеЛьготы - Строка - Наименование льготы из поставляемых данных
//                                РегистрСведений.ПараметрыТорговыхТочек.Макеты.Льготы.
//  ТипТорговойТочки - ПеречислениеСсылка.ТипыТорговыхТочек - Тип торговой точки.
//
// Возвращаемое значение:
//  Строка - код налоговой льготы.
//
Функция КодЛьготыПоНаименованию(Льготы, НаименованиеЛьготы, ТипТорговойТочки) Экспорт
	
	Отбор = Новый Структура("Наименование, ТипТорговойТочки", НаименованиеЛьготы, ТипТорговойТочки);
	ПредставлениеЛьготы = Льготы.НайтиСтроки(Отбор);
	
	Если ПредставлениеЛьготы.Количество() = 1 Тогда
		Возврат ПредставлениеЛьготы[0].КодНалоговойЛьготы;
		
	Иначе
		// Код налоговой льготы "Не применяется".
		Возврат "000000000000";
		
	КонецЕсли;
	
КонецФункции

// Возвращает наименование налоговой льготы по коду.
//
// Параметры:
//  Льготы - ТаблицаЗначений - см. ТорговыйСбор.ПрочитатьТаблицуЛьгот().
//  КодНалоговойЛьготы - Строка - Код налоговой льготы.
//  ТипТорговойТочки - ПеречислениеСсылка.ТипыТорговыхТочек - Тип торговой точки.
//
// Возвращаемое значение:
//  Строка - Наименование льготы из поставляемых данных
//           РегистрСведений.ПараметрыТорговыхТочек.Макеты.Льготы.
//
Функция НаименованиеПоКодуЛьготы(Льготы, КодНалоговойЛьготы, ТипТорговойТочки) Экспорт
	
	Отбор = Новый Структура("КодНалоговойЛьготы, ТипТорговойТочки", КодНалоговойЛьготы, ТипТорговойТочки);
	ПредставлениеЛьготы = Льготы.НайтиСтроки(Отбор);
	
	Если ПредставлениеЛьготы.Количество() = 1 Тогда
		Возврат ПредставлениеЛьготы[0].Наименование;
		
	Иначе
		Возврат НСтр("ru='Не применяется'");
		
	КонецЕсли;
	
КонецФункции

Функция НовыйПараметрыТорговойТочки() Экспорт
	ПараметрыТорговойТочки = Новый Структура;
	ПараметрыТорговойТочки.Вставить("Организация",               Справочники.Организации.ПустаяСсылка());
	ПараметрыТорговойТочки.Вставить("ПодразделениеОрганизации",  Справочники.ПодразделенияОрганизаций.ПустаяСсылка());
	ПараметрыТорговойТочки.Вставить("ВладелецПодразделения",     Справочники.Организации.ПустаяСсылка());
	ПараметрыТорговойТочки.Вставить("Период",                    Дата("20140701"));
	ПараметрыТорговойТочки.Вставить("ДатаРегистрационныхДанных", Дата("20140701"));
	
	ПараметрыТорговойТочки.Вставить("КодПоОКТМО",                "");
	ПараметрыТорговойТочки.Вставить("ВидТорговойДеятельности",   Перечисления.ВидыТорговойДеятельностиОблагаемыеСбором.ПустаяСсылка());
	ПараметрыТорговойТочки.Вставить("ТипТорговойТочки",          Перечисления.ТипыТорговыхТочек.ПустаяСсылка());
	ПараметрыТорговойТочки.Вставить("ПлощадьТорговогоЗала",      0);
	ПараметрыТорговойТочки.Вставить("КодНалоговойЛьготы",        "");
	ПараметрыТорговойТочки.Вставить("СуммаСбора",                0);
	ПараметрыТорговойТочки.Вставить("Ставка",                    0);
	ПараметрыТорговойТочки.Вставить("СуммаЛьготы",               0);
	ПараметрыТорговойТочки.Вставить("РасшифровкаРасчета",        "");
	
	Возврат ПараметрыТорговойТочки;
КонецФункции 
 

// Расчет суммы уплачиваемого торгового сбора по торговой точке. Результат расчета сохраняется в 
// ПараметрыТорговойТочки.
//
// Параметры:
//  ПараметрыТорговойТочки - РегистрСведенийМенеджерЗаписи.ПараметрыТорговыхТочек - Параметры торговой точки.
//  СтавкиСбора - ТаблицаЗначений - см. ТорговыйСбор.ПрочитатьТаблицуСтавок().
//  Территории  - ТаблицаЗначений - см. ТорговыйСбор.ПрочитатьТаблицуТерриторий().
//
Процедура РассчитатьСуммуСбора(ПараметрыТорговойТочки, СтавкиСбора, Территории) Экспорт
	
	Если НЕ ДанныеДляРасчетаСтавкиУказаны(ПараметрыТорговойТочки) Тогда
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура("КодПоОКТМО", СокрЛП(ПараметрыТорговойТочки.КодПоОКТМО));
	Если ТипЗнч(Территории) = Тип("ТаблицаЗначений") Тогда
		ТаблицаТерриторий = Территории.Скопировать(Отбор, "Территория");
	Иначе
		ТаблицаТерриторий = Территории.Выгрузить(Отбор, "Территория");
	КонецЕсли;
	
	Если ТаблицаТерриторий.Количество() = 0 Тогда
		Территория = Справочники.ТерриторииОсуществленияТорговойДеятельности.ПустаяСсылка();
	Иначе
		Территория = ТаблицаТерриторий[0].Территория;
	КонецЕсли;
	
	Отбор = Новый Структура("Территория, ВидТорговойДеятельности", Территория, ПараметрыТорговойТочки.ВидТорговойДеятельности);
	
	Если ТипЗнч(СтавкиСбора) = Тип("ТаблицаЗначений") Тогда
		ТаблицаСтавок = СтавкиСбора.Скопировать(Отбор);
	Иначе
		ТаблицаСтавок = СтавкиСбора.Выгрузить(Отбор);
	КонецЕсли;
	
	ЕстьТорговыйЗал = ЕстьТорговыйЗал(ПараметрыТорговойТочки.ТипТорговойТочки);
	
	СтавкаСбораНайдена = Ложь;
	Для Каждого СтавкаСбора Из ТаблицаСтавок Цикл
		ТорговаяТочкаПериод  = НачалоДня(ПараметрыТорговойТочки.Период);
		ПлощадьТорговогоЗала = ПараметрыТорговойТочки.ПлощадьТорговогоЗала;
		
		Если (СтавкаСбора.ДействуетС <= ТорговаяТочкаПериод И ТорговаяТочкаПериод <= СтавкаСбора.ДействуетПо)
			И (НЕ ЕстьТорговыйЗал 
				ИЛИ (СтавкаСбора.НижняяГраница < ПлощадьТорговогоЗала И  ПлощадьТорговогоЗала <= СтавкаСбора.ВерхняяГраница)) Тогда
			
			СтавкаСбораНайдена = Истина;
			Прервать;
			
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ СтавкаСбораНайдена Тогда
		ПараметрыТорговойТочки.СуммаСбора         = 0;
		ПараметрыТорговойТочки.Ставка             = 0;
		ПараметрыТорговойТочки.СуммаЛьготы        = 0;
		ПараметрыТорговойТочки.РасшифровкаРасчета = НСтр("ru='Не удалось автоматически рассчитать ставку на основании введенных данных о торговой точке.
			|Возможно, допущена ошибка в адресе или коде ОКТМО. Проверьте указанные данные или укажите сумму уплачиваемого сбора самостоятельно.'");
		Возврат;
	КонецЕсли;
	
	Если ПараметрыТорговойТочки.Период >= Дата("20250101") 
		ИЛИ ПараметрыТорговойТочки.ТипТорговойТочки = Перечисления.ТипыТорговыхТочек.РозничныйРынок Тогда
		// Часть 2 статьи 6 Закон г. Москвы от 22.11.2023 N 34, касающаяся применения коэф.дефлятора, вступает в силу с 1 января 2025 года.
		Коэф = КоэффициентДефляторНаДату(ПараметрыТорговойТочки.Период);
	Иначе
		Коэф = 1;
	КонецЕсли;
	
	// домножаем ставки на дефлятор текущего года
	СтавкаСбора.БазоваяСтавка     = Окр(СтавкаСбора.БазоваяСтавка * Коэф, 2);
	СтавкаСбора.Превышение        = Окр(СтавкаСбора.Превышение * Коэф, 2);
	
	Если ПараметрыТорговойТочки.ТипТорговойТочки = Перечисления.ТипыТорговыхТочек.РозничныйРынок Тогда
		// Торговый сбор считается в полных рублях
		ПараметрыТорговойТочки.СуммаСбора = Окр(СтавкаСбора.Превышение * ПараметрыТорговойТочки.ПлощадьТорговогоЗала);
		ПараметрыТорговойТочки.Ставка = ПараметрыТорговойТочки.СуммаСбора;
	Иначе
		Если ЕстьТорговыйЗал И СтавкаСбора.Превышение > 0 Тогда
			// Если ставка сложная (базовая + превышение), то для уведомления используем расчетную ставку за кв.м.
			СтавкаЗаМетр = РасчетнаяСтавкаЗаМетр(ПараметрыТорговойТочки, СтавкаСбора);
			// Торговый сбор считается в полных рублях
			ПараметрыТорговойТочки.Ставка = Окр(СтавкаЗаМетр * ПараметрыТорговойТочки.ПлощадьТорговогоЗала);
		Иначе
			ПараметрыТорговойТочки.Ставка = СтавкаСбора.БазоваяСтавка;
		КонецЕсли;
		
		Если ПрименяетсяЛьгота(ПараметрыТорговойТочки.КодНалоговойЛьготы) Тогда
			// применение льготы освобождает от уплаты торгового сбора
			ПараметрыТорговойТочки.СуммаСбора = 0;
			ПараметрыТорговойТочки.СуммаЛьготы = ПараметрыТорговойТочки.Ставка;
			
		Иначе
			ПараметрыТорговойТочки.СуммаСбора = ПараметрыТорговойТочки.Ставка;
			ПараметрыТорговойТочки.СуммаЛьготы = 0;
		КонецЕсли;
	КонецЕсли;
	
	СоздатьРасшифровкуРасчетаСуммыСбора(ПараметрыТорговойТочки, СтавкаСбора);
КонецПроцедуры

// Возвращает значение параметров торговой точки на дату.
//
// Параметры:
//  ТорговаяТочка - СправочникСсылка.ТорговыеТочки - Торговая точка.
//  НаДату        - Дата - Дата на которую будут получены параметры торговой точки.
//
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса - значение параметров торговой точки.
//
Функция ПараметрыТорговойТочки(ТорговаяТочка, НаДату) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НаДату"       , НаДату);
	Запрос.УстановитьПараметр("ТорговаяТочка", ТорговаяТочка);
	Запрос.УстановитьПараметр("Организация"  , ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТорговаяТочка, "Организация"));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПараметрыТорговыхТочекСрезПоследних.Период,
	|	ПараметрыТорговыхТочекСрезПоследних.Организация,
	|	ПараметрыТорговыхТочекСрезПоследних.ТорговаяТочка,
	|	ПараметрыТорговыхТочекСрезПоследних.ПодразделениеОрганизации,
	|	ПараметрыТорговыхТочекСрезПоследних.КодПоОКТМО,
	|	ПараметрыТорговыхТочекСрезПоследних.ВидТорговойДеятельности,
	|	ПараметрыТорговыхТочекСрезПоследних.ОснованиеПользования,
	|	ПараметрыТорговыхТочекСрезПоследних.ПостановкаНаУчетВНалоговомОргане,
	|	ПараметрыТорговыхТочекСрезПоследних.НалоговыйОрган,
	|	ПараметрыТорговыхТочекСрезПоследних.ВидОбъектаНедвижимости,
	|	ПараметрыТорговыхТочекСрезПоследних.КадастровыйНомер,
	|	ПараметрыТорговыхТочекСрезПоследних.НомерРазрешения,
	|	ПараметрыТорговыхТочекСрезПоследних.ПлощадьТорговогоЗала,
	|	ПараметрыТорговыхТочекСрезПоследних.КодНалоговойЛьготы,
	|	ПараметрыТорговыхТочекСрезПоследних.Ставка,
	|	ПараметрыТорговыхТочекСрезПоследних.СуммаЛьготы,
	|	ПараметрыТорговыхТочекСрезПоследних.СуммаСбора,
	|	ПараметрыТорговыхТочекСрезПоследних.РасшифровкаРасчета,
	|	ПараметрыТорговыхТочекСрезПоследних.ТипТорговойТочки,
	|	ПараметрыТорговыхТочекСрезПоследних.ВидОперации
	|ИЗ
	|	РегистрСведений.ПараметрыТорговыхТочек.СрезПоследних(
	|			&НаДату,
	|			ТорговаяТочка = &ТорговаяТочка
	|				И Организация = &Организация) КАК ПараметрыТорговыхТочекСрезПоследних";
	
	Результат = Запрос.Выполнить().Выбрать();
	Если Результат.Следующий() Тогда
		Возврат Результат;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Возвращает дату запрета изменения.
//
// Параметры:
//  Организация - СправочникСсылка.Организации - Организация для которой будет получена дата запрета изменения.
//
// Возвращаемое значение:
//  Дата - Дата запрета изменения.
//
Функция ДатаЗапретаИзменения(Организация) Экспорт
	
	Результат = '00010101';
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьДатыЗапретаИзменения") Тогда
		Возврат Результат;
	КонецЕсли;
	
	// Дата запрета изменения определяется со следующими приоритетами (от высшего к низшему):
	// 1. Дата запрета установленная для пользователя, раздела и объекта;
	// 2. Дата установленная для всех пользователей раздела и объекта;
	// 3. Дата установленная для пользователя по всем разделам и объектам;
	// 4. Дата установленная для всех пользователей всех разделов и объектов.
	
	УстановитьПривилегированныйРежим(Истина);
	РазделБухгалтерскийУчет = ДатыЗапретаИзмененияБП.РазделБухгалтерскийУчет().Ссылка;
	УстановитьПривилегированныйРежим(Ложь);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Пользователь",            Пользователи.ТекущийПользователь());
	Запрос.УстановитьПараметр("ТекущаяДата" ,            ОбщегоНазначения.ТекущаяДатаПользователя());
	Запрос.УстановитьПараметр("Объект"      ,            Организация);
	Запрос.УстановитьПараметр("РазделБухгалтерскийУчет", РазделБухгалтерскийУчет);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МАКСИМУМ(ДатыЗапретаИзменения.ДатаЗапрета) КАК ДатаЗапрета
	|ИЗ
	|	РегистрСведений.ДатыЗапретаИзменения КАК ДатыЗапретаИзменения
	|ГДЕ
	|	ДатыЗапретаИзменения.ДатаЗапрета <= &ТекущаяДата
	|	И ДатыЗапретаИзменения.Пользователь = &Пользователь
	|	И ДатыЗапретаИзменения.Объект = &Объект
	|	И ДатыЗапретаИзменения.Раздел = &РазделБухгалтерскийУчет
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МАКСИМУМ(ДатыЗапретаИзменения.ДатаЗапрета)
	|ИЗ
	|	РегистрСведений.ДатыЗапретаИзменения КАК ДатыЗапретаИзменения
	|ГДЕ
	|	ДатыЗапретаИзменения.ДатаЗапрета <= &ТекущаяДата
	|	И ДатыЗапретаИзменения.Раздел = &РазделБухгалтерскийУчет
	|	И ДатыЗапретаИзменения.Объект = &Объект
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МАКСИМУМ(ДатыЗапретаИзменения.ДатаЗапрета)
	|ИЗ
	|	РегистрСведений.ДатыЗапретаИзменения КАК ДатыЗапретаИзменения
	|ГДЕ
	|	ДатыЗапретаИзменения.ДатаЗапрета <= &ТекущаяДата
	|	И ДатыЗапретаИзменения.Пользователь = &Пользователь
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МАКСИМУМ(ДатыЗапретаИзменения.ДатаЗапрета)
	|ИЗ
	|	РегистрСведений.ДатыЗапретаИзменения КАК ДатыЗапретаИзменения
	|ГДЕ
	|	ДатыЗапретаИзменения.ДатаЗапрета <= &ТекущаяДата
	|	И ДатыЗапретаИзменения.Пользователь = ЗНАЧЕНИЕ(Перечисление.ВидыНазначенияДатЗапрета.ДляВсехПользователей)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.ДатаЗапрета) Тогда
			Возврат Выборка.ДатаЗапрета;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЕстьТорговыйЗал(ТипТорговойТочки)
	
	Результат = Ложь;
	
	Если ТипТорговойТочки = Перечисления.ТипыТорговыхТочек.Магазин
		ИЛИ ТипТорговойТочки = Перечисления.ТипыТорговыхТочек.Павильон
		ИЛИ ТипТорговойТочки = Перечисления.ТипыТорговыхТочек.ПрочееСТорговымЗалом
		ИЛИ ТипТорговойТочки = Перечисления.ТипыТорговыхТочек.РозничныйРынок Тогда
		
		Результат = Истина;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПревышениеБазовойПлощади(ПлощадьЗала, БазоваяПлощадь)
	Если БазоваяПлощадь = 0 Тогда
		Возврат 0;
	КонецЕсли; 
	
	ПлощадьЗалаСвышеБазовой = ПлощадьЗала - БазоваяПлощадь;
	
	Если ПлощадьЗалаСвышеБазовой < 0 Тогда
		Возврат 0;
	КонецЕсли;
	
	ЦелаяЧасть = Цел(ПлощадьЗалаСвышеБазовой);
	
	Если ПлощадьЗалаСвышеБазовой - ЦелаяЧасть > 0 Тогда
		ПлощадьЗалаСвышеБазовой = ЦелаяЧасть + 1;
	КонецЕсли;
	
	Возврат ПлощадьЗалаСвышеБазовой;
	
КонецФункции

Функция ПрименяетсяЛьгота(КодНалоговойЛьготы)
	
	Возврат ЗначениеЗаполнено(Число(КодНалоговойЛьготы));
	
КонецФункции

Функция ДанныеДляРасчетаСтавкиУказаны(ПараметрыТорговойТочки)
	
	Если ЗначениеЗаполнено(ПараметрыТорговойТочки.ТипТорговойТочки)
		И ЗначениеЗаполнено(ПараметрыТорговойТочки.Период)
		И ЗначениеЗаполнено(ПараметрыТорговойТочки.КодПоОКТМО) Тогда
		
		Результат = Истина;
		
	Иначе
		Результат = Ложь;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура СоздатьРасшифровкуРасчетаСуммыСбора(ПараметрыТорговойТочки, СтавкаСбора)
	
	Если ПараметрыТорговойТочки.ТипТорговойТочки = Перечисления.ТипыТорговыхТочек.ТорговыйАвтомат Тогда
		
		Если ПрименяетсяЛьгота(ПараметрыТорговойТочки.КодНалоговойЛьготы) Тогда
			РасшифровкаРасчета = НСтр("ru='Торговля, осуществляемая с использованием вендинговых автоматов, не облагается сбором.'");
		Иначе
			РасшифровкаРасчета = НСтр("ru='Размер торгового сбора для торговли с использованием вендинговых автоматов фиксирован и составляет %1 руб.'");
			РасшифровкаРасчета = СтрШаблон(РасшифровкаРасчета, ПараметрыТорговойТочки.Ставка);
		КонецЕсли;
		
	ИначеЕсли ПрименяетсяЛьгота(ПараметрыТорговойТочки.КодНалоговойЛьготы) Тогда
		РасшифровкаРасчета = НСтр("ru='В случае применения льготы торговый сбор не уплачивается.'");
		
	ИначеЕсли ПараметрыТорговойТочки.ВидТорговойДеятельности =
		Перечисления.ВидыТорговойДеятельностиОблагаемыеСбором.РазвознаяРазноснаяТорговля Тогда
		
		РасшифровкаРасчета = НСтр("ru='Размер торгового сбора для развозной (разносной) торговли фиксирован и составляет %1 руб.'");
		РасшифровкаРасчета = СтрШаблон(РасшифровкаРасчета, ПараметрыТорговойТочки.Ставка);
		
	ИначеЕсли ПараметрыТорговойТочки.ВидТорговойДеятельности =
		Перечисления.ВидыТорговойДеятельностиОблагаемыеСбором.СтационарныеСетиСТорговымиЗалами
		И ПараметрыТорговойТочки.ПлощадьТорговогоЗала > 50 Тогда
		
		РасшифровкаРасчета = НСтр("ru='Торговый сбор = Расчетная ставка за кв.м. * Площадь торгового зала
			| Расчетная ставка за кв.м. = (Ставка за %9 кв.м. + (Площадь свыше %9 кв.м. * Ставка за полный кв.м. свыше %9 кв.м)) / Площадь торгового зала
			| Расчетная ставка за кв.м. = (%1 руб. + (%2 кв.м. * %3 руб.)) / %4 кв.м. = %5 руб.
			| Торговый сбор = %6 руб * %7 кв.м. = %8 руб.'");
		РасчетнаяСтавкаЗаМетр = РасчетнаяСтавкаЗаМетр(ПараметрыТорговойТочки, СтавкаСбора);
		РасшифровкаРасчета = СтрШаблон(РасшифровкаРасчета,
			СтавкаСбора.БазоваяСтавка,
			ПревышениеБазовойПлощади(ПараметрыТорговойТочки.ПлощадьТорговогоЗала, СтавкаСбора.НижняяГраница),
			СтавкаСбора.Превышение,
			ПараметрыТорговойТочки.ПлощадьТорговогоЗала,
			РасчетнаяСтавкаЗаМетр,
			РасчетнаяСтавкаЗаМетр,
			ПараметрыТорговойТочки.ПлощадьТорговогоЗала,
			ПараметрыТорговойТочки.Ставка,
			СтавкаСбора.НижняяГраница);
			
	ИначеЕсли ПараметрыТорговойТочки.ВидТорговойДеятельности =
		Перечисления.ВидыТорговойДеятельностиОблагаемыеСбором.СтационарныеСетиСТорговымиЗалами
		И СтавкаСбора.НижняяГраница = 0  Тогда
		
		РасшифровкаРасчета = НСтр("ru='Размер торгового сбора для торговых точек с площадью торгового зала менее %2 кв.м
			|фиксирован и составляет %1 руб.'");
		РасшифровкаРасчета = СтрШаблон(РасшифровкаРасчета, ПараметрыТорговойТочки.Ставка, СтавкаСбора.ВерхняяГраница);
		
	ИначеЕсли ПараметрыТорговойТочки.ТипТорговойТочки = Перечисления.ТипыТорговыхТочек.РозничныйРынок Тогда
		РасшифровкаРасчета = НСтр("ru='Торговый сбор = Площадь рынка * Ставка
			|%1 кв.м. * %2 руб. = %3 руб.'");
			
		РасшифровкаРасчета = СтрШаблон(РасшифровкаРасчета,
			ПараметрыТорговойТочки.ПлощадьТорговогоЗала,
			СтавкаСбора.Превышение,
			ПараметрыТорговойТочки.Ставка);
			
	Иначе
		РасшифровкаРасчета = НСтр("ru='Размер торгового сбора, зависит от адреса торговой точки и составляет %1 руб.'");
		РасшифровкаРасчета = СтрШаблон(РасшифровкаРасчета, ПараметрыТорговойТочки.Ставка);
		
	КонецЕсли;
	
	ПараметрыТорговойТочки.РасшифровкаРасчета = РасшифровкаРасчета;
	
КонецПроцедуры

Функция РасчетнаяСтавкаЗаМетр(ПараметрыТорговойТочки, СтавкаСбора)
	РасчетнаяСтавкаЗаМетр = (СтавкаСбора.БазоваяСтавка + ПревышениеБазовойПлощади(ПараметрыТорговойТочки.ПлощадьТорговогоЗала, СтавкаСбора.НижняяГраница) * СтавкаСбора.Превышение)
		/ ПараметрыТорговойТочки.ПлощадьТорговогоЗала;
	РасчетнаяСтавкаЗаМетр = Окр(РасчетнаяСтавкаЗаМетр, 2);
	
	Возврат РасчетнаяСтавкаЗаМетр
	
КонецФункции

Функция ЗаписиИдентичны(Запись1, Запись2)

	МетаданныеРегистрСведений = Метаданные.РегистрыСведений.ПараметрыТорговыхТочек;
	
	Для каждого Измерение Из МетаданныеРегистрСведений.Измерения Цикл
		Если Запись1[Измерение.Имя] <> Запись2[Измерение.Имя] Тогда
			Возврат Ложь;
		КонецЕсли; 
	КонецЦикла; 
	
	Для каждого Ресурс Из МетаданныеРегистрСведений.Ресурсы Цикл
		Если Запись1[Ресурс.Имя] <> Запись2[Ресурс.Имя] Тогда
			Возврат Ложь;
		КонецЕсли; 
	КонецЦикла; 
	
	Для каждого Реквизит Из МетаданныеРегистрСведений.Реквизиты Цикл
		Если Запись1[Реквизит.Имя] <> Запись2[Реквизит.Имя] Тогда
			Возврат Ложь;
		КонецЕсли; 
	КонецЦикла; 
	
	Возврат Истина;
КонецФункции 

Функция ОбновитьСтавкиПоСпискуТорговыхТочек(ПараметрыТорговыхТочек, НаДату)
	
	ТаблицаСтавок = ТорговыйСбор.ПрочитатьТаблицуСтавок();
	ТаблицаТерриторий = ТорговыйСбор.ПрочитатьТаблицуТерриторий();
	
	ОбработаноЗаписей = 0;
	НеУдалосьОбработать = 0;
	
	Для Каждого ПараметрыТорговойТочки Из ПараметрыТорговыхТочек Цикл
			ИсходныеПараметрыТорговойТочки = РегистрыСведений.ПараметрыТорговыхТочек.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(ИсходныеПараметрыТорговойТочки, ПараметрыТорговойТочки, "Период, Организация, ТорговаяТочка");
			ИсходныеПараметрыТорговойТочки.Прочитать();
			
			ДатаНовойЗаписи = Макс(ИсходныеПараметрыТорговойТочки.Период, НаДату);
			
			НаборЗаписейПараметрыТорговойТочки = РегистрыСведений.ПараметрыТорговыхТочек.СоздатьНаборЗаписей();
			
			НоваяЗаписьПараметрыТорговойТочки = НаборЗаписейПараметрыТорговойТочки.ВыгрузитьКолонки().Добавить();
			
			ЗаполнитьЗначенияСвойств(НоваяЗаписьПараметрыТорговойТочки, ИсходныеПараметрыТорговойТочки, , "Период");
			ЗаполнитьЗначенияСвойств(НоваяЗаписьПараметрыТорговойТочки, ПараметрыТорговойТочки, , "Период");
			
			НоваяЗаписьПараметрыТорговойТочки.Период = ДатаНовойЗаписи;
			НоваяЗаписьПараметрыТорговойТочки.ВидОперации = Перечисления.ВидыОперацийТорговыеТочки.ИзменениеСтавки;
			
			РассчитатьСуммуСбора(НоваяЗаписьПараметрыТорговойТочки, ТаблицаСтавок, ТаблицаТерриторий);
			
			Если ЗаписиИдентичны(НоваяЗаписьПараметрыТорговойТочки, ИсходныеПараметрыТорговойТочки) Тогда
				Продолжить;
			КонецЕсли;
			
			НачатьТранзакцию();
			
			Попытка
				Блокировка = Новый БлокировкаДанных;
				
				ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.РасчетТорговогоСбора");
				ЭлементБлокировки.УстановитьЗначение("Организация",   ПараметрыТорговойТочки.Организация);
				ЭлементБлокировки.УстановитьЗначение("ТорговаяТочка", ПараметрыТорговойТочки.ТорговаяТочка);
				
				Блокировка.Заблокировать();
				
				НаборЗаписейСуммыТорговогоСбора = РегистрыСведений.РасчетТорговогоСбора.СоздатьНаборЗаписей();
				НаборЗаписейСуммыТорговогоСбора.Отбор.Период.Установить(ДатаНовойЗаписи);
				НаборЗаписейСуммыТорговогоСбора.Отбор.Организация.Установить(ИсходныеПараметрыТорговойТочки.Организация);
				НаборЗаписейСуммыТорговогоСбора.Отбор.ТорговаяТочка.Установить(ИсходныеПараметрыТорговойТочки.ТорговаяТочка);
				
				ЗаполнитьЗначенияСвойств(НаборЗаписейСуммыТорговогоСбора.Добавить(), НоваяЗаписьПараметрыТорговойТочки);
				
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписейСуммыТорговогоСбора);
				
				ОбработаноЗаписей = ОбработаноЗаписей + 1;
				
				ЗафиксироватьТранзакцию();
			Исключение
				ОтменитьТранзакцию();
				ШаблонСообщения = НСтр("ru = 'Не удалось обработать параметры торговой точки по причине
	                                   |%1'");
				ТекстСообщения = СтрШаблон(ШаблонСообщения, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
				ЗаписьЖурналаРегистрации(
					ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
					УровеньЖурналаРегистрации.Ошибка,
					Метаданные.Справочники.ТорговыеТочки,
					ПараметрыТорговойТочки.ТорговаяТочка, 
					ТекстСообщения);
					
				НеУдалосьОбработать = НеУдалосьОбработать + 1;
			КонецПопытки;
	КонецЦикла;

	Возврат Новый Структура("ОбработаноЗаписей, НеУдалосьОбработать", ОбработаноЗаписей, НеУдалосьОбработать);
КонецФункции

Функция КоэффициентДефляторНаДату(Дата)
	Если Дата >= Дата("20240101") Тогда
		Возврат 1.828;
	Иначе
		Возврат 1;
	КонецЕсли;
КонецФункции


#Область ОбработчикиОбновления

Процедура ОбновитьСтавкиТорговогоСбора(Параметры) Экспорт
	
	ДатаПроверкиСтавки = Дата(2019, 07, 01);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаПроверкиСтавки", ДатаПроверкиСтавки);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПараметрыТорговыхТочекСрезПоследних.Период КАК Период,
	|	ПараметрыТорговыхТочекСрезПоследних.Организация КАК Организация,
	|	ПараметрыТорговыхТочекСрезПоследних.ТорговаяТочка КАК ТорговаяТочка,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийТорговыеТочки.ИзменениеПараметров) КАК ВидОперации
	|ИЗ
	|	РегистрСведений.ПараметрыТорговыхТочек.СрезПоследних(&ДатаПроверкиСтавки, ) КАК ПараметрыТорговыхТочекСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыТорговыхТочек КАК ПараметрыТорговыхТочекНаДатуСреза
	|		ПО ПараметрыТорговыхТочекСрезПоследних.Организация = ПараметрыТорговыхТочекНаДатуСреза.Организация
	|			И ПараметрыТорговыхТочекСрезПоследних.ТорговаяТочка = ПараметрыТорговыхТочекНаДатуСреза.ТорговаяТочка
	|			И (ПараметрыТорговыхТочекНаДатуСреза.Период = &ДатаПроверкиСтавки)
	|ГДЕ
	|	ПараметрыТорговыхТочекСрезПоследних.ВидОперации <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийТорговыеТочки.СнятиеСУчета)
	|	И ПараметрыТорговыхТочекСрезПоследних.ВидТорговойДеятельности = ЗНАЧЕНИЕ(Перечисление.ВидыТорговойДеятельностиОблагаемыеСбором.СтационарныеСетиСТорговымиЗалами)
	|	И ПараметрыТорговыхТочекНаДатуСреза.Период ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ПараметрыТорговыхТочек.Период,
	|	ПараметрыТорговыхТочек.Организация,
	|	ПараметрыТорговыхТочек.ТорговаяТочка,
	|	ПараметрыТорговыхТочек.ВидОперации
	|ИЗ
	|	РегистрСведений.ПараметрыТорговыхТочек КАК ПараметрыТорговыхТочек
	|ГДЕ
	|	ПараметрыТорговыхТочек.Период >= &ДатаПроверкиСтавки
	|	И ПараметрыТорговыхТочек.ВидТорговойДеятельности = ЗНАЧЕНИЕ(Перечисление.ВидыТорговойДеятельностиОблагаемыеСбором.СтационарныеСетиСТорговымиЗалами)";
	
	
	ПараметрыТорговыхТочек = Запрос.Выполнить().Выгрузить();
	
	РезультатВыполнения = ОбновитьСтавкиПоСпискуТорговыхТочек(ПараметрыТорговыхТочек, ДатаПроверкиСтавки);
	
	ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
		УровеньЖурналаРегистрации.Информация,
		Метаданные.РегистрыСведений.ПараметрыТорговыхТочек, ,
		СтрШаблон(
		НСтр("ru = 'Процедура РегистрСведений.ПараметрыТорговыхТочек.ОбновитьСтавкиТорговогоСбора() обработала %1 записей. Не удалось обработать %2'"), 
		РезультатВыполнения.ОбработаноЗаписей, РезультатВыполнения.НеУдалосьОбработать));
	
	Параметры.ОбработкаЗавершена = (РезультатВыполнения.ОбработаноЗаписей = 0 ИЛИ РезультатВыполнения.НеУдалосьОбработать = 0);
	
КонецПроцедуры

Процедура ОбновитьСтавкиТорговыеАвтоматы(Параметры) Экспорт
	
	Параметры.ОбработкаЗавершена = Ложь;

	Успешно = ИзменитьВидТорговойДеятельностиТорговыеАвтоматы();
	
	Если НЕ Успешно Тогда
		Возврат;
	КонецЕсли; 
	
	ДатаПроверкиСтавки = Дата(2021, 01, 01);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаПроверкиСтавки", ДатаПроверкиСтавки);
	Запрос.УстановитьПараметр("ТипТорговыйАвтомат", Перечисления.ТипыТорговыхТочек.ТорговыйАвтомат);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПараметрыТорговыхТочекСрезПоследних.Период КАК Период,
	|	ПараметрыТорговыхТочекСрезПоследних.Организация КАК Организация,
	|	ПараметрыТорговыхТочекСрезПоследних.ТорговаяТочка КАК ТорговаяТочка,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийТорговыеТочки.ИзменениеПараметров) КАК ВидОперации,
	|	""000000000000"" КАК КодНалоговойЛьготы,
	|	"""" КАК РасшифровкаРасчета
	|ИЗ
	|	РегистрСведений.ПараметрыТорговыхТочек.СрезПоследних(&ДатаПроверкиСтавки, ) КАК ПараметрыТорговыхТочекСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыТорговыхТочек КАК ПараметрыТорговыхТочекНаДатуСреза
	|		ПО ПараметрыТорговыхТочекСрезПоследних.Организация = ПараметрыТорговыхТочекНаДатуСреза.Организация
	|			И ПараметрыТорговыхТочекСрезПоследних.ТорговаяТочка = ПараметрыТорговыхТочекНаДатуСреза.ТорговаяТочка
	|			И (ПараметрыТорговыхТочекНаДатуСреза.Период = &ДатаПроверкиСтавки)
	|ГДЕ
	|	ПараметрыТорговыхТочекСрезПоследних.ВидОперации <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийТорговыеТочки.СнятиеСУчета)
	|	И ПараметрыТорговыхТочекСрезПоследних.ТипТорговойТочки = &ТипТорговыйАвтомат
	|	И ПараметрыТорговыхТочекНаДатуСреза.Период ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ПараметрыТорговыхТочек.Период,
	|	ПараметрыТорговыхТочек.Организация,
	|	ПараметрыТорговыхТочек.ТорговаяТочка,
	|	ПараметрыТорговыхТочек.ВидОперации,
	|	""000000000000"",
	|	""""
	|ИЗ
	|	РегистрСведений.ПараметрыТорговыхТочек КАК ПараметрыТорговыхТочек
	|ГДЕ
	|	ПараметрыТорговыхТочек.Период >= &ДатаПроверкиСтавки
	|	И ПараметрыТорговыхТочек.ТипТорговойТочки = &ТипТорговыйАвтомат";
	
	
	ПараметрыТорговыхТочек = Запрос.Выполнить().Выгрузить();
	
	РезультатВыполнения = ОбновитьСтавкиПоСпискуТорговыхТочек(ПараметрыТорговыхТочек, ДатаПроверкиСтавки);
	
	ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
		УровеньЖурналаРегистрации.Информация,
		Метаданные.РегистрыСведений.ПараметрыТорговыхТочек, ,
		СтрШаблон(
		НСтр("ru = 'Процедура РегистрСведений.ПараметрыТорговыхТочек.ОбновитьСтавкиТорговыеАвтоматы() обработала %1 записей. Не удалось обработать %2'"), 
		РезультатВыполнения.ОбработаноЗаписей, РезультатВыполнения.НеУдалосьОбработать));
	
	Параметры.ОбработкаЗавершена = (РезультатВыполнения.ОбработаноЗаписей = 0 ИЛИ РезультатВыполнения.НеУдалосьОбработать = 0);

КонецПроцедуры

Функция ИзменитьВидТорговойДеятельностиТорговыеАвтоматы() 
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ТипТорговыйАвтомат", Перечисления.ТипыТорговыхТочек.ТорговыйАвтомат);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПараметрыТорговыхТочек.Организация КАК Организация,
	|	ПараметрыТорговыхТочек.ТорговаяТочка КАК ТорговаяТочка
	|ИЗ
	|	РегистрСведений.ПараметрыТорговыхТочек КАК ПараметрыТорговыхТочек
	|ГДЕ
	|	ПараметрыТорговыхТочек.ТипТорговойТочки = &ТипТорговыйАвтомат";
	
	ОбработаноЗаписей = 0;
	НеУдалосьОбработать = 0;
	
	Результат = Запрос.Выполнить().Выбрать();
	Пока Результат.Следующий() Цикл
		НачатьТранзакцию();
		
		Попытка
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПараметрыТорговыхТочек");
			ЭлементБлокировки.УстановитьЗначение("Организация",   Результат.Организация);
			ЭлементБлокировки.УстановитьЗначение("ТорговаяТочка", Результат.ТорговаяТочка);
			
			Блокировка.Заблокировать();
			
			НаборЗаписей = РегистрыСведений.ПараметрыТорговыхТочек.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Организация.Установить(Результат.Организация);
			НаборЗаписей.Отбор.ТорговаяТочка.Установить(Результат.ТорговаяТочка);
			
			НаборЗаписей.Прочитать();
			Для каждого Запись Из НаборЗаписей Цикл
				
				Если Запись.ТипТорговойТочки = Перечисления.ТипыТорговыхТочек.ТорговыйАвтомат Тогда
					Запись.ВидТорговойДеятельности = Перечисления.ВидыТорговойДеятельностиОблагаемыеСбором.ТорговыеАвтоматы;
				КонецЕсли; 
				
			КонецЦикла; 
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
			ОбработаноЗаписей = ОбработаноЗаписей + 1;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ШаблонСообщения = НСтр("ru = 'Не удалось обработать параметры торговой точки по причине
			|%1'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Справочники.ТорговыеТочки,
			Результат.ТорговаяТочка, 
			ТекстСообщения);
			
			НеУдалосьОбработать = НеУдалосьОбработать + 1;
		КонецПопытки;
	КонецЦикла;
	
	ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
		УровеньЖурналаРегистрации.Информация,
		Метаданные.РегистрыСведений.ПараметрыТорговыхТочек, ,
		СтрШаблон(
		НСтр("ru = 'Процедура РегистрСведений.ПараметрыТорговыхТочек.ИзменитьВидТорговойДеятельностиТорговыеАвтоматы() обработала %1 записей. Не удалось обработать %2'"), 
		ОбработаноЗаписей, НеУдалосьОбработать));
	
	Возврат ОбработаноЗаписей = 0 ИЛИ НеУдалосьОбработать = 0;
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
