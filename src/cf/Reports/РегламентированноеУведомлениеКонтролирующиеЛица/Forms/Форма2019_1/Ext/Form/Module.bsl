&НаКлиенте
// Вызывается после загрузки из файла
Перем ОписаниеОповещенияОЗавершенииЗагрузки Экспорт;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	Данные = Неопределено;
	Параметры.Свойство("Данные", Данные);
	РазделительНомераСтроки = "___";
	ОТЧ = Новый ОписаниеТипов("Число");
	
	Объект.ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.УведомлениеКЛМК;
	УведомлениеОСпецрежимахНалогообложения.НачальныеОперацииПриСозданииНаСервере(ЭтотОбъект);
	УведомлениеОСпецрежимахНалогообложения.СформироватьСпискиВыбора(ЭтотОбъект, "СпискиВыбора2017_1");
	
	Если ТипЗнч(Данные) = Тип("Структура") Тогда
		СформироватьДеревоСтраниц();
		УведомлениеОСпецрежимахНалогообложения.СформироватьСтруктуруДанныхУведомленияНовогоОбразца(ЭтотОбъект);
		УведомлениеОСпецрежимахНалогообложения.ЗагрузитьДанныеПростогоУведомления(ЭтотОбъект, Данные, ПредставлениеУведомления)
	ИначеЕсли Параметры.Свойство("Ключ") И ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.Организация = Параметры.Ключ.Организация;
		ЗагрузитьДанные(Параметры.Ключ);
	ИначеЕсли Параметры.Свойство("ЗначениеКопирования") И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		Объект.Организация = Параметры.ЗначениеКопирования.Организация;
		ЗагрузитьДанные(Параметры.ЗначениеКопирования);
	ИначеЕсли Параметры.Свойство("ПредставлениеXML") Тогда 
		Параметры.Свойство("РегистрацияВНалоговомОргане", Объект.РегистрацияВИФНС);
		Параметры.Свойство("Организация", Объект.Организация);
		ЗагрузитьИзXMLНаСервере(Новый Структура("Организация, РегистрацияВНалоговомОргане, ПредставлениеXML", 
								Объект.Организация, Объект.РегистрацияВИФНС, Параметры.ПредставлениеXML));
	Иначе
		Параметры.Свойство("Организация", Объект.Организация);
		Объект.РегистрацияВИФНС = Документы.УведомлениеОСпецрежимахНалогообложения.РегистрацияВФНСОрганизации(Объект.Организация);
		СформироватьДеревоСтраниц();
		УведомлениеОСпецрежимахНалогообложения.СформироватьСтруктуруДанныхУведомленияНовогоОбразца(ЭтотОбъект);
		ЗаполнитьНачальныеДанные();
	КонецЕсли;
	
	УведомлениеОСпецрежимахНалогообложения.ЗаполнитьТаблицуФорматов(ЭтотОбъект, "Форматы2017_1");
	ИдДляСвор = УведомлениеОСпецрежимахНалогообложения.ПолучитьИдентификаторыДляСворачивания(ЭтотОбъект);
	СворачиваемыеЭлементы = ПоместитьВоВременноеХранилище(ИдДляСвор);
	РегламентированнаяОтчетностьКлиентСервер.ПриИнициализацииФормыРегламентированногоОтчета(ЭтотОбъект);
	Заголовок = УведомлениеОСпецрежимахНалогообложения.ДополнитьЗаголовокУведомления(Заголовок, Объект.Организация);
	Если НЕ ОнлайнСервисыРегламентированнойОтчетностиВызовСервера.СобытиеНаступило("Отпр.уведомление КЛМГК", '20230101') Тогда
		Элементы.ОтправитьВКонтролирующийОрган.Видимость = Ложь;
	КонецЕсли;
	УведомлениеОСпецрежимахНалогообложения.СпрятатьКнопкиВыгрузкиОтправкиУНеактуальныхФорм(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	РучнойВвод = Ложь;
	Элементы.ФормаРучнойВвод.Пометка = Ложь;
	УведомлениеОСпецрежимахНалогообложенияКлиент.ПриОткрытии(ЭтотОбъект, Отказ);
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	ПриЗакрытииНаСервере();
	Оповестить("Запись_УведомлениеОСпецрежимахНалогообложения",,Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	РегламентированнаяОтчетностьКлиент.ПередЗакрытиемРегламентированногоОтчета(ЭтотОбъект, Отказ, СтандартнаяОбработка, ЗавершениеРаботы, ТекстПредупреждения);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "УведомлениеОСпецрежимахНалогообложения_НавигацияПоОшибкам" Тогда 
		УведомлениеОСпецрежимахНалогообложенияКлиент.ОбработкаОповещенияНавигацииПоОшибкам(ЭтотОбъект, Параметр, Источник);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("Запись_УведомлениеОСпецрежимахНалогообложения", ПараметрыЗаписи, Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура КопироватьСПодчиненными(Команда)
	СПодчиненными = Не СПодчиненными;
	Элементы.ФормаКопироватьСПодчиненными.Пометка = СПодчиненными;
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура Очистить(Команда)
	УведомлениеОСпецрежимахНалогообложенияКлиент.ОчиститьУведомление(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ОчисткаОтчета() Экспорт
	Объект.РегистрацияВИФНС = Документы.УведомлениеОСпецрежимахНалогообложения.РегистрацияВФНСОрганизации(Объект.Организация);
	СформироватьДеревоСтраниц();
	УведомлениеОСпецрежимахНалогообложения.СформироватьСтруктуруДанныхУведомленияНовогоОбразца(ЭтотОбъект);
	ЗаполнитьНачальныеДанные();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНачальныеДанные() Экспорт
	ДанныеУведомленияТитульный = ДанныеУведомления["Титульная"];
	ДанныеУведомленияТитульный.Вставить("КодНО", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.РегистрацияВИФНС, "Код"));
	Объект.ДатаПодписи = ТекущаяДатаСеанса();
	ДанныеУведомленияТитульный.Вставить("ДАТА_ПОДПИСИ", Объект.ДатаПодписи);
	
	СтрокаСведений = "ИННЮЛ,НаимЮЛПол,КППЮЛ,ТелОрганизации,ОГРН";
	СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Объект.Организация, Объект.ДатаПодписи, СтрокаСведений);
	ДанныеУведомленияТитульный.Вставить("ИНН", СведенияОбОрганизации.ИННЮЛ);
	ДанныеУведомленияТитульный.Вставить("Наименование", СведенияОбОрганизации.НаимЮЛПол);
	ДанныеУведомленияТитульный.Вставить("КПП", СведенияОбОрганизации.КППЮЛ);
	ДанныеУведомленияТитульный.Вставить("Тлф", СведенияОбОрганизации.ТелОрганизации);
	ДанныеУведомленияТитульный.Вставить("ОГРН", СведенияОбОрганизации.ОГРН);
	
	Реквизиты = РегистрацияВНОСервер.ДанныеРегистрации(Объект.РегистрацияВИФНС);
	ДанныеУведомленияТитульный.Вставить("КодНО", Реквизиты.Код);
	ДанныеУведомленияТитульный.Вставить("КПП", Реквизиты.КПП);
	
	Если ЗначениеЗаполнено(Реквизиты.Представитель) Тогда
		УведомлениеОСпецрежимахНалогообложения.УстановитьПредставителяПоФизЛицу(ЭтотОбъект);
	Иначе
		УведомлениеОСпецрежимахНалогообложения.УстановитьПредставителяПоОрганизации(ЭтотОбъект);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СформироватьДеревоСтраниц() Экспорт
	ДеревоСтраниц.ПолучитьЭлементы().Очистить();
	КорневойУровень = ДеревоСтраниц.ПолучитьЭлементы();
	
	Стр001 = КорневойУровень.Добавить();
	Стр001.Наименование = "Титульная страница";
	Стр001.ИндексКартинки = 1;
	Стр001.ИмяМакета = "Титульная_2019";
	Стр001.Многостраничность = Ложь;
	Стр001.Многострочность = Ложь;
	Стр001.УИД = Новый УникальныйИдентификатор;
	Стр001.ИДНаименования = "Титульная";
	Стр001.МакетыПФ = "Печать_Форма2019_1_Титульная";
	
	Стр001 = КорневойУровень.Добавить();
	Стр001.Наименование = "Сведения об иностранных"+символы.ПС+"организациях (Лист А.1)";
	Стр001.ИндексКартинки = 1;
	Стр001.Многостраничность = Истина;
	Стр001.Многострочность = Ложь;
	
	Стр001 = Стр001.ПолучитьЭлементы().Добавить();
	Стр001.Наименование = "Стр. 1";
	Стр001.ИндексКартинки = 1;
	Стр001.ИмяМакета = "ЛистАРаздел1";
	Стр001.Многостраничность = Истина;
	Стр001.Многострочность = Ложь;
	Стр001.УИД = Новый УникальныйИдентификатор;
	Стр001.ИДНаименования = "ЛистАРаздел1";
	Стр001.МакетыПФ = "Печать_Форма2019_1_ЛистАРаздел1";
	
	Стр001 = КорневойУровень.Добавить();
	Стр001.Наименование = "Сведения о российских"+символы.ПС+"организациях (Лист А.2)";
	Стр001.ИндексКартинки = 1;
	Стр001.Многостраничность = Истина;
	Стр001.Многострочность = Ложь;
	
	Стр001 = Стр001.ПолучитьЭлементы().Добавить();
	Стр001.Наименование = "Стр. 1";
	Стр001.ИндексКартинки = 1;
	Стр001.ИмяМакета = "ЛистАРаздел2";
	Стр001.Многостраничность = Истина;
	Стр001.Многострочность = Ложь;
	Стр001.УИД = Новый УникальныйИдентификатор;
	Стр001.ИДНаименования = "ЛистАРаздел2";
	Стр001.МакетыПФ = "Печать_Форма2019_1_ЛистАРаздел2";
	
	Стр001 = КорневойУровень.Добавить();
	Стр001.Наименование = "Сведения о физических"+символы.ПС+"лицах (Лист А.3)";
	Стр001.ИндексКартинки = 1;
	Стр001.Многостраничность = Истина;
	Стр001.Многострочность = Ложь;
	
	Стр001 = Стр001.ПолучитьЭлементы().Добавить();
	Стр001.Наименование = "Стр. 1";
	Стр001.ИндексКартинки = 1;
	Стр001.ИмяМакета = "ЛистАРаздел3";
	Стр001.Многостраничность = Истина;
	Стр001.Многострочность = Ложь;
	Стр001.УИД = Новый УникальныйИдентификатор;
	Стр001.ИДНаименования = "ЛистАРаздел3";
	Стр001.МакетыПФ = "Печать_Форма2019_1_ЛистАРаздел3";
	
	Стр001 = КорневойУровень.Добавить();
	Стр001.Наименование = "Основания признания"+символы.ПС+"контролирующим"+символы.ПС+"лицом (Лист Б)";
	Стр001.ИндексКартинки = 1;
	Стр001.Многостраничность = Истина;
	Стр001.Многострочность = Ложь;
	
	Стр001 = Стр001.ПолучитьЭлементы().Добавить();
	Стр001.Наименование = "Стр. 1";
	Стр001.ИндексКартинки = 1;
	Стр001.ИмяМакета = "ЛистБ";
	Стр001.Многостраничность = Истина;
	Стр001.Многострочность = Ложь;
	Стр001.УИД = Новый УникальныйИдентификатор;
	Стр001.ИДНаименования = "ЛистБ";
	Стр001.МакетыПФ = "Печать_Форма2019_1_ЛистБ";

	
	Стр001 = КорневойУровень.Добавить();
	Стр001.Наименование = "Раскрытие порядка"+символы.ПС+"участия (Лист В)";
	Стр001.ИндексКартинки = 1;
	Стр001.Многостраничность = Истина;
	Стр001.Многострочность = Истина;
	
	Стр001 = Стр001.ПолучитьЭлементы().Добавить();
	Стр001.Наименование = "Стр. 1";
	Стр001.ИндексКартинки = 1;
	Стр001.ИмяМакета = "ЛистВ";
	Стр001.Многостраничность = Истина;
	Стр001.Многострочность = Истина;
	Стр001.УИД = Новый УникальныйИдентификатор;
	Стр001.ИДНаименования = "ЛистВ";
	Стр001.МногострочныеЧасти.Добавить("МнгСтр");
	Стр001.МакетыПФ = "Печать_Форма2019_1_ЛистВ";
	
	СтрГ1 = Стр001.ПолучитьЭлементы().Добавить();
	СтрГ1.Наименование = "Сведения об иностранной"+символы.ПС+"организации (Лист Г.1)";
	СтрГ1.ИндексКартинки = 1;
	СтрГ1.Многостраничность = Истина;
	СтрГ1.Многострочность = Ложь;
	
	СтрГ1 = СтрГ1.ПолучитьЭлементы().Добавить();
	СтрГ1.Наименование = "Стр. 1";
	СтрГ1.ИндексКартинки = 1;
	СтрГ1.ИмяМакета = "ЛистГ1_2019";
	СтрГ1.Многостраничность = Истина;
	СтрГ1.Многострочность = Ложь;
	СтрГ1.УИД = Новый УникальныйИдентификатор;
	СтрГ1.ИДНаименования = "ЛистГ1";
	СтрГ1.МакетыПФ = "Печать_Форма2019_1_ЛистГ1";
	
	СтрГ1 = Стр001.ПолучитьЭлементы().Добавить();
	СтрГ1.Наименование = "Сведения о российской"+символы.ПС+"организации (Лист Г.2)";
	СтрГ1.ИндексКартинки = 1;
	СтрГ1.Многостраничность = Истина;
	СтрГ1.Многострочность = Ложь;
	
	СтрГ1 = СтрГ1.ПолучитьЭлементы().Добавить();
	СтрГ1.Наименование = "Стр. 1";
	СтрГ1.ИндексКартинки = 1;
	СтрГ1.ИмяМакета = "ЛистГ2_2019";
	СтрГ1.Многостраничность = Истина;
	СтрГ1.Многострочность = Ложь;
	СтрГ1.УИД = Новый УникальныйИдентификатор;
	СтрГ1.ИДНаименования = "ЛистГ2";
	СтрГ1.МакетыПФ = "Печать_Форма2019_1_ЛистГ2";
	
	СтрГ1 = Стр001.ПолучитьЭлементы().Добавить();
	СтрГ1.Наименование = "Сведения об иностранной"+Символы.ПС+"структуре без образования"+Символы.ПС+"юридического лица (Лист Г.3)";
	СтрГ1.ИндексКартинки = 1;
	СтрГ1.Многостраничность = Истина;
	СтрГ1.Многострочность = Ложь;
	
	СтрГ1 = СтрГ1.ПолучитьЭлементы().Добавить();
	СтрГ1.Наименование = "Стр. 1";
	СтрГ1.ИндексКартинки = 1;
	СтрГ1.ИмяМакета = "ЛистГ3_2019";
	СтрГ1.Многостраничность = Истина;
	СтрГ1.Многострочность = Ложь;
	СтрГ1.УИД = Новый УникальныйИдентификатор;
	СтрГ1.ИДНаименования = "ЛистГ3";
	СтрГ1.МакетыПФ = "Печать_Форма2019_1_ЛистГ3";
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьМногострочныеЧасти(ТекущиеДанные)
	Если ТекущиеДанные.МногострочныеЧасти.Количество() > 0 Тогда 
		Возврат ТекущиеДанные.МногострочныеЧасти;
	ИначеЕсли ТекущиеДанные.ПолучитьЭлементы().Количество() > 0 Тогда 
		Возврат ТекущиеДанные.ПолучитьЭлементы()[0].МногострочныеЧасти;
	Иначе
		Возврат ТекущиеДанные.МногострочныеЧасти;
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура ДеревоСтраницПриАктивизацииСтроки(Элемент)
	Если УведомлениеОСпецрежимахНалогообложенияКлиент.НеобходимоФормированиеТабличногоДокумента(ЭтотОбъект, Элемент, ЭтотОбъект["УИДПереключение"]) Тогда
		ОтключитьОбработчикОжидания("ДеревоСтраницПриАктивизацииСтрокиЗавершение");
		ПодключитьОбработчикОжидания("ДеревоСтраницПриАктивизацииСтрокиЗавершение", 0.1, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСтраницПриАктивизацииСтрокиЗавершение() Экспорт 
	ПредУИД = ЭтотОбъект["УИДПереключение"];
	Элемент = Элементы.ДеревоСтраниц;
	
	Если Элемент.ТекущиеДанные.Многостраничность Тогда 
		ИмяМакета = УведомлениеОСпецрежимахНалогообложенияКлиент.ПолучитьИмяВыводимогоМакета(Элемент.ТекущиеДанные);
		ПоказатьТекущуюМногостраничнуюСтраницу(ИмяМакета, ПолучитьМногострочныеЧасти(Элемент.ТекущиеДанные), ПредУИД);
	Иначе 
		ПоказатьТекущуюСтраницу(Элемент.ТекущиеДанные.ИмяМакета, Элемент.ТекущиеДанные.МногострочныеЧасти, ПредУИД);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УстановитьФорматыВМногострочныхПолях(ТД) Экспорт
	Для Каждого Обл Из ТД.Области Цикл 
		Если Обл.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник И Обл.СодержитЗначение Тогда 
			ОблИмя = Лев(Обл.Имя, СтрНайти(Обл.Имя, РазделительНомераСтроки) - 1);
			ОблИмя = ?(ЗначениеЗаполнено(ОблИмя), ОблИмя, Обл.Имя);
			ПоляСФорматами = ЧисловыеФорматы.НайтиСтроки(Новый Структура("ИДСтраницы, Поле", ТекущееИДНаименования, ОблИмя));
			Если ПоляСФорматами.Количество() > 0 Тогда 
				Знак = ?(ПоляСФорматами[0].Неотрицательный, ДопустимыйЗнак.Неотрицательный, ДопустимыйЗнак.Любой);
				ОТ = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(ПоляСФорматами[0].Всего, ПоляСФорматами[0].ПослеЗапятой, Знак));
				Обл.ТипЗначения = ОТ;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ПоказатьТекущуюСтраницу(ИмяМакета, МногострочныеЧасти, ПредУИД)
	Если Не УдалениеСтраницы И ТекущиеМногострочныеЧасти.Количество() > 0 Тогда 
		УведомлениеОСпецрежимахНалогообложения.СобратьДанныеМногострочныхЧастейТекущейСтраницы(
					ЭтотОбъект, ТекущиеМногострочныеЧасти, ПредУИД);
	КонецЕсли;
	
	ТекущиеМногострочныеЧасти = ОбщегоНазначенияКлиентСервер.СкопироватьСписокЗначений(МногострочныеЧасти);
	ТекущийМакет = ИмяМакета;
	Макет = УведомлениеОСпецрежимахНалогообложения.ПолучитьМакетТабличногоДокумента(ЭтотОбъект, ИмяМакета);
	УведомлениеОСпецрежимахНалогообложения.ПоказатьТекущуюСтраницу(ЭтотОбъект, ИмяМакета, ПредУИД);
	УведомлениеОСпецрежимахНалогообложения.ПоказатьМногострочныеЧасти(ЭтотОбъект, Макет, МногострочныеЧасти, ЧисловыеФорматы);
КонецПроцедуры

&НаСервере
Процедура ПоказатьТекущуюМногостраничнуюСтраницу(ИмяМакета, МногострочныеЧасти, ПредУИД)
	Если Не УдалениеСтраницы И ТекущиеМногострочныеЧасти.Количество() > 0 Тогда 
		УведомлениеОСпецрежимахНалогообложения.СобратьДанныеМногострочныхЧастейТекущейСтраницы(
					ЭтотОбъект, ТекущиеМногострочныеЧасти, ПредУИД);
	КонецЕсли;
	
	ТекущиеМногострочныеЧасти = ОбщегоНазначенияКлиентСервер.СкопироватьСписокЗначений(МногострочныеЧасти);
	ТекущийМакет = ИмяМакета;
	Макет = УведомлениеОСпецрежимахНалогообложения.ПоказатьТекущуюМногостраничнуюСтраницу(ЭтотОбъект, ИмяМакета);
	УведомлениеОСпецрежимахНалогообложения.ПоказатьМногострочныеЧасти(ЭтотОбъект, Макет, МногострочныеЧасти, ЧисловыеФорматы);
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеУведомленияПриИзмененииСодержимогоОбласти(Элемент, Область)
	УведомлениеОСпецрежимахНалогообложенияКлиент.ПриИзмененииСодержимогоОбласти(ЭтотОбъект, Область, Истина);
	
	Если Область.Имя = "ДАТА_ПОДПИСИ" Тогда
		Объект.ДатаПодписи = Область.Значение;
		УстановитьДанныеПоРегистрацииВИФНС();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УстановитьДанныеПоРегистрацииВИФНС()
	Реквизиты = РегистрацияВНОСервер.ДанныеРегистрации(Объект.РегистрацияВИФНС);
	ПредставлениеУведомления.Области["КодНО"].Значение = Реквизиты.Код;
	ПредставлениеУведомления.Области["КПП"].Значение = Реквизиты.КПП;
	Если ЗначениеЗаполнено(Реквизиты.Представитель) Тогда
		УведомлениеОСпецрежимахНалогообложения.УстановитьПредставителяПоФизЛицу(ЭтотОбъект);
	Иначе
		УведомлениеОСпецрежимахНалогообложения.УстановитьПредставителяПоОрганизации(ЭтотОбъект);
	КонецЕсли;
	
	ДанныеУведомленияТитульный = ДанныеУведомления["Титульная"];
	ДанныеУведомленияТитульный.Вставить("ПРИЗНАК_НП_ПОДВАЛ", ПредставлениеУведомления.Области["ПРИЗНАК_НП_ПОДВАЛ"].Значение);
	ДанныеУведомленияТитульный.Вставить("НаимДок", ПредставлениеУведомления.Области["НаимДок"].Значение);
	ДанныеУведомленияТитульный.Вставить("ДАТА_ПОДПИСИ", ПредставлениеУведомления.Области["ДАТА_ПОДПИСИ"].Значение);
	ДанныеУведомленияТитульный.Вставить("ФИО_РУКОВОДИТЕЛЯ_ПРЕДСТАВИТЕЛЯ", ПредставлениеУведомления.Области["ФИО_РУКОВОДИТЕЛЯ_ПРЕДСТАВИТЕЛЯ"].Значение);
	ДанныеУведомленияТитульный.Вставить("КодНО", ПредставлениеУведомления.Области["КодНО"].Значение);
	ДанныеУведомленияТитульный.Вставить("КПП", ПредставлениеУведомления.Области["КПП"].Значение);
КонецПроцедуры

&НаСервере
Процедура СохранитьДанные() Экспорт
	Если ЗначениеЗаполнено(Объект.Ссылка) И Не Модифицированность Тогда 
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.Дата = ТекущаяДатаСеанса() 
	КонецЕсли;
	
	Если ТекущиеМногострочныеЧасти.Количество() > 0 Тогда 
		УведомлениеОСпецрежимахНалогообложения.СобратьДанныеМногострочныхЧастейТекущейСтраницы(
					ЭтотОбъект, ТекущиеМногострочныеЧасти, УИДТекущаяСтраница);
	КонецЕсли;
	
	ДанныеДопСтрокБД = Новый Структура;
	Для Каждого КЗ Из ДанныеДопСтрок Цикл 
		ДанныеДопСтрокБД.Вставить(КЗ.Ключ, ПолучитьИзВременногоХранилища(КЗ.Значение));
	КонецЦикла;
	
	СтруктураПараметров = Новый Структура;
			
	СтруктураПараметров.Вставить("ИдентификаторыОбычныхСтраниц", ИдентификаторыОбычныхСтраниц);
	СтруктураПараметров.Вставить("ДанныеДопСтрокБД", ДанныеДопСтрокБД);
	СтруктураПараметров.Вставить("ДеревоСтраниц", РеквизитФормыВЗначение("ДеревоСтраниц"));
	СтруктураПараметров.Вставить("ДанныеМногостраничныхРазделов", ДанныеМногостраничныхРазделов);
	СтруктураПараметров.Вставить("ДанныеУведомления", ДанныеУведомления);
	СтруктураПараметров.Вставить("РазрешитьВыгружатьСОшибками", РазрешитьВыгружатьСОшибками);
	
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.ДанныеУведомления = Новый ХранилищеЗначения(СтруктураПараметров);
	Документ.Записать();
	ЗначениеВДанныеФормы(Документ, Объект);
	
	РегламентированнаяОтчетность.СохранитьСтатусОтправкиУведомления(ЭтотОбъект);
	
	Модифицированность = Ложь;
	Заголовок = СтрЗаменить(Заголовок, " (создание)", "");
	
	УведомлениеОСпецрежимахНалогообложения.СохранитьНастройкиРучногоВвода(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	СохранитьДанные();
	Оповестить("Запись_УведомлениеОСпецрежимахНалогообложения",,Объект.Ссылка);
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДанные(СсылкаНаДанные)
	СтруктураПараметров = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаДанные, "ДанныеУведомления").Получить();
	ДанныеУведомления = СтруктураПараметров.ДанныеУведомления;
	ДанныеМногостраничныхРазделов = СтруктураПараметров.ДанныеМногостраничныхРазделов;
	ЗначениеВРеквизитФормы(СтруктураПараметров.ДеревоСтраниц, "ДеревоСтраниц");
	СтруктураПараметров.Свойство("ИдентификаторыОбычныхСтраниц", ИдентификаторыОбычныхСтраниц);
	УведомлениеОСпецрежимахНалогообложения.ЗагрузкаМногострочныхЧастейУведомления(ЭтотОбъект, СтруктураПараметров);
	СтруктураПараметров.Свойство("РазрешитьВыгружатьСОшибками", РазрешитьВыгружатьСОшибками);
КонецПроцедуры

&НаСервере
Процедура СкопироватьСтраницуНаСервере()
	УведомлениеОСпецрежимахНалогообложения.СобратьДанныеМногострочныхЧастейТекущейСтраницы(
				ЭтотОбъект, ТекущиеМногострочныеЧасти, УИДТекущаяСтраница);
	УведомлениеОСпецрежимахНалогообложения.КопироватьСтраницуУведомления(ЭтотОбъект, СПодчиненными);
КонецПроцедуры

&НаКлиенте
Процедура СкопироватьСтраницуНаКлиенте() Экспорт 
	СкопироватьСтраницуНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСтрокуНаКлиенте(ИмяОбласти) Экспорт 
	ДобавитьСтроку(ИмяОбласти);
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСтрокуНаКлиенте(ИмяОбласти) Экспорт 
	УдалитьСтроку(ИмяОбласти);
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеУведомленияВыбор(Элемент, Область, СтандартнаяОбработка)
	Если Область.Имя = "ПересчитатьДанныеЦепочек" Тогда 
		ПересчитатьДанныеЦепочек();
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
	Если УведомлениеОСпецрежимахНалогообложенияКлиент.ТиповойВыбор(ЭтотОбъект, Область, СтандартнаяОбработка) Или РучнойВвод Тогда 
		Возврат;
	КонецЕсли;
	
	Если СтандартнаяОбработка Тогда 
		УведомлениеОСпецрежимахНалогообложенияКлиент.ПредставлениеУведомленияВыбор(ЭтотОбъект, Область, СтандартнаяОбработка, Истина, Истина);
	КонецЕсли;
	
	Если СтандартнаяОбработка Тогда 
		УведомлениеОСпецрежимахНалогообложенияКлиент.ПредставлениеУведомленияВыборКодЗнач(ЭтотОбъект, Область, СтандартнаяОбработка, Истина);
	КонецЕсли;
	
	Если (Область.Имя = "НомерКонтрЛиц" И ТекущееИДНаименования = "ЛистБ") Тогда 
		СтандартнаяОбработка = Ложь;
		ВыборНомерУчастника = Новый ОписаниеОповещения("ВыборНомерУчастникаЗавершение", ЭтотОбъект);
		ПараметрыВыборНомерУчастника = Новый Структура("ЗначениеОбласти, ИмяОбласти, ДоступенВыборИО, ДоступенВыборРО, ДоступенВыборФЛ", Область.Значение, Область.Имя);
		ОткрытьФорму("Отчет." + СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИмяФормы, ".")[1] 
			+ ".Форма.ФормаВыборНомерУчастника", ПараметрыВыборНомерУчастника, ЭтотОбъект, , , , ВыборНомерУчастника, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		Возврат;
	ИначеЕсли (Область.Имя = "НомерКонтрЛиц" И ТекущееИДНаименования = "ЛистВ") Тогда 
		СтандартнаяОбработка = Ложь;
		ВыборНомерУчастника = Новый ОписаниеОповещения("ВыборНомерУчастникаЗавершение", ЭтотОбъект);
		ПараметрыВыборНомерУчастника = Новый Структура("ЗначениеОбласти, ИмяОбласти, ДоступенВыборИО, ДоступенВыборРО, ДоступенВыборФЛ", Область.Значение, Область.Имя);
		ОткрытьФорму("Отчет." + СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИмяФормы, ".")[1] 
			+ ".Форма.ФормаВыборНомерУчастника", ПараметрыВыборНомерУчастника, ЭтотОбъект, , , , ВыборНомерУчастника, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		Возврат;
	ИначеЕсли СтрНачинаетсяС(Область.Имя, "НомерУч" + РазделительНомераСтроки) Тогда 
		СтандартнаяОбработка = Ложь;
		ВыборНомерУчастника = Новый ОписаниеОповещения("ВыборНомерУчастникаЗавершение", ЭтотОбъект);
		ПараметрыВыборНомерУчастника = Новый Структура("ЗначениеОбласти, ИмяОбласти, ДоступенВыборУИ, ДоступенВыборУС, ДоступенВыборУР, ДоступенВыборМК", Область.Значение, Область.Имя);
		ОткрытьФорму("Отчет." + СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИмяФормы, ".")[1] 
			+ ".Форма.ФормаВыборНомерУчастника", ПараметрыВыборНомерУчастника, ЭтотОбъект, , , , ВыборНомерУчастника, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		Возврат;
	ИначеЕсли Область.Имя = "КодНО" Тогда 
		СтандартнаяОбработка = Ложь;
		РегламентированнаяОтчетностьКлиент.ОткрытьФормуВыбораРегистрацииВИФНС(ЭтотОбъект, Область.Имя);
	КонецЕсли;
	
	Если СтандартнаяОбработка И ЭтоОбластьОКСМ(Область) Тогда
		СтандартнаяОбработка = Ложь;
		ДополнительныеПараметры = Новый Структура("Область, СтандартнаяОбработка, Элемент", Область, СтандартнаяОбработка, Элемент);
		ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьФормуВыбораСтраныЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ОткрытьФорму("Справочник.СтраныМира.ФормаВыбора", Новый Структура("РежимВыбора", Истина), ЭтотОбъект,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПересчитатьДанныеЦепочек()
	НомерУчастника =НРег(ПредставлениеУведомления.Области["НомерКИК"].Значение);
	Если Не ЗначениеЗаполнено(НомерУчастника) Или 
		(Не СтрНачинаетсяС(НомерУчастника, "ио-") И Не СтрНачинаетсяС(НомерУчастника, "ис-")) Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Некорректно указан номер участника'"));
		Возврат;
	КонецЕсли;
	
	ОТЧ = Новый ОписаниеТипов("Число");
	ИО_Номер = ОТЧ.ПривестиЗначение(Сред(НомерУчастника, 4));
	Если Не ЗначениеЗаполнено(ИО_Номер) Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Некорректно указан номер участника'"));
		Возврат;
	КонецЕсли;
	
	ЛистыИО = Новый СписокЗначений;
	ЛистыДляОтбора = ?(СтрНачинаетсяС(НомерУчастника, "ио-"), ДанныеМногостраничныхРазделов.ЛистА, ДанныеМногостраничныхРазделов.ЛистБ);
	Для Каждого Лист Из ЛистыДляОтбора Цикл 
		Если Лист.Значение.НомерКИКЧисло = ИО_Номер Тогда
			ЛистыИО.Добавить(Лист.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Если ЛистыИО.Количество() = 0 Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Не найден соответствующий лист А/Б'"));
		Возврат;
	ИначеЕсли ЛистыИО.Количество() > 1 Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Найдено более одного листа А/Б с указанным номером'"));
		Возврат;
	КонецЕсли;
	
	Если Ждать ВопросАсинх("Произвести пересчет цепочек (операция не может быть отменена)?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
		ОбщаяДоляУчастия = 0;
		ПересчитатьДанныеЦепочекНаСервере(НомерУчастника, ОбщаяДоляУчастия);
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПересчитатьДанныеЦепочекНаСервере(НомерУчастника, ОбщаяДоляУчастия)
	УведомлениеОСпецрежимахНалогообложения.СобратьДанныеМногострочныхЧастейТекущейСтраницы(
				ЭтотОбъект, ТекущиеМногострочныеЧасти, УИДТекущаяСтраница);
	ОбщаяДоляУчастия = 0;
	ДанныеДопСтрокМнгСтр = ПолучитьИзВременногоХранилища(ДанныеДопСтрок.МнгСтр);
	
	Для Каждого Стр Из ДанныеМногостраничныхРазделов.ЛистГ Цикл 
		Если НРег(Стр.Значение.НомерКИК) <> НомерУчастника Тогда
			Продолжить;
		КонецЕсли;
		
		МнгСтр = ДанныеДопСтрокМнгСтр.НайтиСтроки(Новый Структура("УИД", Стр.Значение.УИД));
		ДУчИзн = Число(МнгСтр[0].ДоляУчПрям);
		Для Инд = 1 По МнгСтр.ВГраница() Цикл 
			ДУчИзн = ДУчИзн * Число(МнгСтр[Инд].ДоляУчПрям) / 100;
			МнгСтр[Инд].ДоляУчКосв = ДУчИзн;
		КонецЦикла;
		
		Стр.Значение.ДоляКосвПослИт = ДУчИзн;
		ОбщаяДоляУчастия = ОбщаяДоляУчастия + ДУчИзн;
	КонецЦикла;
	
	УдалениеСтраницы = Истина;
	ПоказатьТекущуюМногостраничнуюСтраницу(ТекущийМакет, ТекущиеМногострочныеЧасти, УИДТекущаяСтраница);
	УдалениеСтраницы = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ВыборНомерУчастникаЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	Если ТипЗнч(Ответ) = Тип("Структура") Тогда 
		ТипЧисло = Новый ОписаниеТипов("Число");
		МассивДопОбл = Неопределено;
		Результат = Неопределено;
		ИмяОбласти = Неопределено;
		
		Ответ.Свойство("ИмяОбласти", ИмяОбласти);
		Ответ.Свойство("Результат", Результат);
		Если Ответ.Свойство("Очистить") Тогда 
			Результат = "";
		КонецЕсли;
		Область = ПредставлениеУведомления.Области[ИмяОбласти];
		Область.Значение = Результат;
		ПредставлениеУведомленияПриИзмененииСодержимогоОбласти(ПредставлениеУведомления, Область);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаКодаНОЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Инфо = ДополнительныеПараметры.Инфо;
	
	Если Результат <> Неопределено Тогда 
		Объект.РегистрацияВИФНС = Результат;
		УстановитьДанныеПоРегистрацииВИФНС();
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораПодписантаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	УведомлениеОСпецрежимахНалогообложенияКлиент.ОткрытьФормуВыбораПодписантаЗавершение(ЭтотОбъект, Результат);
КонецПроцедуры

&НаКлиенте
Функция ЭтоОбластьОКСМ(Область)
	Если (Область.Имя = "СтрРег" И (ТекущееИДНаименования = "ЛистАРаздел1" Или ТекущееИДНаименования = "ЛистГ1" Или ТекущееИДНаименования = "ЛистГ3"))
		Или ((Область.Имя = "ОКСМ" Или Область.Имя = "СтрРегФЛ" Или Область.Имя = "КодСтр") И (ТекущееИДНаименования = "ЛистАРаздел3")) Тогда
		
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура ДобавитьСтраницу(Команда) Экспорт 
	ДобавитьСтраницуНаСервере();
КонецПроцедуры

&НаСервере
Процедура ДобавитьСтраницуНаСервере()
	УведомлениеОСпецрежимахНалогообложения.ДобавитьСтраницуУведомления(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСтраницу() Экспорт
	УдалениеСтраницы = Истина;
	УдалитьСтраницуНаСервере();
	УдалениеСтраницы = Ложь;
КонецПроцедуры

&НаСервере
Процедура УдалитьСтраницуНаСервере()
	Для Каждого Стр Из ТекущиеМногострочныеЧасти Цикл 
		ТЗ = ПолучитьИзВременногоХранилища(ДанныеДопСтрок[Стр.Значение]);
		Строки = ТЗ.НайтиСтроки(Новый Структура("УИД", УИДТекущаяСтраница));
		Для Каждого СтрМнг Из Строки Цикл 
			ТЗ.Удалить(СтрМнг);
		КонецЦикла;
		ДанныеДопСтрок[Стр.Значение] = ПоместитьВоВременноеХранилище(ТЗ, ДанныеДопСтрок[Стр.Значение]);
	КонецЦикла;
	
	УведомлениеОСпецрежимахНалогообложения.УдалитьСтраницуНаСервере(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ПриЗакрытииНаСервере()
	Если Модифицированность Тогда 
		СохранитьДанные();
	КонецЕсли;
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		УведомлениеОбъект = Объект.Ссылка.ПолучитьОбъект();
		Если УведомлениеОбъект.Заблокирован() Тогда 
			УведомлениеОбъект.Разблокировать();
		КонецЕсли;
		РазблокироватьДанныеДляРедактирования(Объект.Ссылка, УникальныйИдентификатор);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораСтраныЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		КодЭлементаСправочника = УведомлениеОСпецрежимахНалогообложенияВызовСервера.РеквизитЭлементаСправочника(Результат, "Код");
		Область = ДополнительныеПараметры.Область;
		Если Область.Значение <> КодЭлементаСправочника Тогда
			Область.Значение = КодЭлементаСправочника;
			Модифицированность = Истина;
		КонецЕсли;
		ПредставлениеУведомленияПриИзмененииСодержимогоОбласти(Элементы.ПредставлениеУведомления, Область);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ДобавитьСтроку(ИмяОбласти)
	УведомлениеОСпецрежимахНалогообложения.ДобавитьСтроку(ЭтотОбъект, ИмяОбласти);
	УстановитьФорматыВМногострочныхПолях(ПредставлениеУведомления);
КонецПроцедуры

&НаСервере
Процедура УдалитьСтроку(ИмяОбласти)
	УведомлениеОСпецрежимахНалогообложения.УдалитьСтроку(ЭтотОбъект, ИмяОбласти);
КонецПроцедуры

&НаКлиенте
Функция ОпределитьПринадлежностьОбластиКМногострочномуРазделу(ОбластьИмя) Экспорт 
КонецФункции

&НаСервере
Функция СформироватьXMLНаСервере(УникальныйИдентификатор)
	СохранитьДанные();
	Документ = РеквизитФормыВЗначение("Объект");
	Возврат Документ.ВыгрузитьДокумент(УникальныйИдентификатор);
КонецФункции

&НаКлиенте
Процедура СформироватьXML(Команда)
	
	ВыгружаемыеДанные = СформироватьXMLНаСервере(УникальныйИдентификатор);
	Если ВыгружаемыеДанные <> Неопределено Тогда 
		РегламентированнаяОтчетностьКлиент.ВыгрузитьФайлы(ВыгружаемыеДанные);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНаКлиенте(Автосохранение = Ложь,ВыполняемоеОповещение = Неопределено) Экспорт 
	
	СохранитьДанные();
	Если ВыполняемоеОповещение <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ВыполняемоеОповещение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	СохранитьДанные();
	Оповестить("Запись_УведомлениеОСпецрежимахНалогообложения",,Объект.Ссылка);
	Закрыть(Неопределено);
КонецПроцедуры

#Область ОтправкаВФНС
////////////////////////////////////////////////////////////////////////////////
// Отправка в ФНС
&НаКлиенте
Процедура ОтправитьВКонтролирующийОрган(Команда)
	
	РегламентированнаяОтчетностьКлиент.ПриНажатииНаКнопкуОтправкиВКонтролирующийОрган(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВИнтернете(Команда)
	
	РегламентированнаяОтчетностьКлиент.ПроверитьВИнтернете(ЭтотОбъект);
	
КонецПроцедуры
#КонецОбласти

#Область ПанельОтправкиВКонтролирующиеОрганы

&НаКлиенте
Процедура ОбновитьОтправку(Команда)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОбновитьОтправкуИзПанелиОтправки(ЭтотОбъект, "ФНС");
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаПротоколНажатие(Элемент)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьПротоколИзПанелиОтправки(ЭтотОбъект, "ФНС");
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьНеотправленноеИзвещение(Команда)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОтправитьНеотправленноеИзвещениеИзПанелиОтправки(ЭтотОбъект, "ФНС");
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыОтправкиНажатие(Элемент)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьСостояниеОтправкиИзПанелиОтправки(ЭтотОбъект, "ФНС");
КонецПроцедуры

&НаКлиенте
Процедура КритическиеОшибкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьКритическиеОшибкиИзПанелиОтправки(ЭтотОбъект, "ФНС");
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаНаименованиеЭтапаНажатие(Элемент)
	
	ПараметрыИзменения = Новый Структура;
	ПараметрыИзменения.Вставить("Форма", ЭтотОбъект);
	ПараметрыИзменения.Вставить("Организация", Объект.Организация);
	ПараметрыИзменения.Вставить("КонтролирующийОрган",
		ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФНС"));
	ПараметрыИзменения.Вставить("ТекстВопроса", НСтр("ru='Вы уверены, что уведомление уже сдано?'"));
	
	РегламентированнаяОтчетностьКлиент.ИзменитьСтатусОтправки(ПараметрыИзменения);
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Функция ПроверитьВыгрузкуНаСервере()
	СохранитьДанные();
	Документ = РеквизитФормыВЗначение("Объект");
	Возврат Документ.ПроверитьДокументСВыводомВТаблицу(УникальныйИдентификатор);
КонецФункции

&НаКлиенте
Процедура ПроверитьВыгрузку(Команда)
	УведомлениеОСпецрежимахНалогообложенияКлиент.ПроверитьВыгрузку(ЭтотОбъект, ПроверитьВыгрузкуНаСервере());
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОткрытьПрисоединенныеФайлы(Команда)
	
	РегламентированнаяОтчетностьКлиент.СохранитьУведомлениеИОткрытьФормуПрисоединенныеФайлы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьБРО(Команда)
	ПечатьБРОНаСервере();
	РегламентированнаяОтчетностьКлиент.ОткрытьФормуПредварительногоПросмотра(ЭтотОбъект, "Открыть", Ложь, СтруктураРеквизитовУведомления.СписокПечатаемыхЛистов);
КонецПроцедуры

&НаСервере
Процедура ПечатьБРОНаСервере()
	УведомлениеОСпецрежимахНалогообложения.ПечатьУведомленияБРО(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура РучнойВвод(Команда)
	РучнойВвод = Не РучнойВвод;
	Элементы.ФормаРучнойВвод.Пометка = РучнойВвод;
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОКСМ(Команда)
	ПредставлениеУведомления.ТекущаяОбласть.Значение = Неопределено;
	УведомлениеОСпецрежимахНалогообложенияКлиент.ПриИзмененииСодержимогоОбласти(ЭтотОбъект, ПредставлениеУведомления.ТекущаяОбласть, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеУведомленияПриАктивизации(Элемент)
	Если ПредставлениеУведомления.ТекущаяОбласть.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник Тогда 
		Элементы.ПредставлениеУведомленияКонтекстноеМенюОчиститьОКСМ.Доступность = ЭтоОбластьОКСМ(ПредставлениеУведомления.ТекущаяОбласть);
	Иначе
		Элементы.ПредставлениеУведомленияКонтекстноеМенюОчиститьОКСМ.Доступность = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьВыгружатьСОшибками(Команда)
	РазрешитьВыгружатьСОшибками = Не РазрешитьВыгружатьСОшибками;
	Элементы.ФормаРазрешитьВыгружатьСОшибками.Пометка = РазрешитьВыгружатьСОшибками;
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзXML(ПараметрыЗагрузкиXML) Экспорт
	ЗагрузитьИзXMLНаСервере(ПараметрыЗагрузкиXML);
	Элементы.ДеревоСтраниц.ТекущаяСтрока = ДеревоСтраниц.ПолучитьЭлементы()[0].ПолучитьИдентификатор();
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьИзXMLНаСервере(ПараметрыЗагрузкиXML)
	ДополнительныеПараметры = Новый Структура;
	УведомлениеОСпецрежимахНалогообложения.УстановитьОрганизациюПоПараметрамЗагрузки(ЭтотОбъект, ПараметрыЗагрузкиXML);
	
	ДеревоЗагрузки = УведомлениеОСпецрежимахНалогообложения.СформироватьДеревоЗагрузки(ПараметрыЗагрузкиXML.ПредставлениеXML);
	СхемаВыгрузки = Документы.УведомлениеОСпецрежимахНалогообложения.ИзвлечьСтруктуруXMLУведомления(Объект.ИмяОтчета, "СхемаВыгрузкиФорма2019_1");
	ДеревоСтраниц.ПолучитьЭлементы().Очистить();
	СформироватьДеревоСтраниц();
	УведомлениеОСпецрежимахНалогообложения.СформироватьСтруктуруДанныхУведомленияНовогоОбразца(ЭтотОбъект);
	УведомлениеОСпецрежимахНалогообложения.ЗагрузитьОбычныеСтраницы(ЭтотОбъект, ДеревоЗагрузки, СхемаВыгрузки, ДополнительныеПараметры);
	
	ВсеУчастники = Новый Соответствие;
	ЗагрузитьЛистыАБ(ДеревоЗагрузки, СхемаВыгрузки, ДополнительныеПараметры, ВсеУчастники)
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьЛистыАБ(ДеревоЗагрузки, СхемаВыгрузки, ДополнительныеПараметры, ВсеУчастники)
	УзелСвКонтрЛицМК = УведомлениеОСпецрежимахНалогообложения.НайтиУзелВДеревеПоПути(ДеревоЗагрузки, "Файл/Документ/СвКонтрЛицМК", "ИмяЭлемента");
	Если Не ТипЗнч(УзелСвКонтрЛицМК) = Тип("СтрокаДереваЗначений") Тогда 
		Возврат;
	КонецЕсли;
	
	МассивКолонок = Новый Соответствие;
	ДанныеДопСтрокМнгСтр = ПолучитьИзВременногоХранилища(ДанныеДопСтрок.МнгСтр);
	Для Каждого Кол Из ДанныеДопСтрокМнгСтр.Колонки Цикл 
		МассивКолонок.Вставить(Кол.Имя, Истина);
	КонецЦикла;
	
	ВсегоА1 = 0;
	ВсегоА2 = 0;
	ВсегоА3 = 0;
	ВсегоБ = 0;
	ВсегоВ = 0;
	
	ЛистА1Эталон = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ДанныеМногостраничныхРазделов.ЛистАРаздел1[0].Значение);
	ЛистА2Эталон = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ДанныеМногостраничныхРазделов.ЛистАРаздел2[0].Значение);
	ЛистА3Эталон = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ДанныеМногостраничныхРазделов.ЛистАРаздел3[0].Значение);
	ЛистБЭталон = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ДанныеМногостраничныхРазделов.ЛистБ[0].Значение);
	ЛистВЭталон = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ДанныеМногостраничныхРазделов.ЛистВ[0].Значение);
	ЛистГ1Эталон = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ДанныеМногостраничныхРазделов.ЛистГ1[0].Значение);
	ЛистГ2Эталон = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ДанныеМногостраничныхРазделов.ЛистГ2[0].Значение);
	ЛистГ3Эталон = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ДанныеМногостраничныхРазделов.ЛистГ3[0].Значение);
	
	Для Каждого УзелСвКонтрЛицМКТек Из УзелСвКонтрЛицМК.Родитель.Строки.НайтиСтроки(Новый Структура("ИмяЭлемента", "СвКонтрЛицМК"), Ложь) Цикл 
		СтрА1 = УведомлениеОСпецрежимахНалогообложения.НайтиУзелВДеревеПоПутиРек(УзелСвКонтрЛицМКТек.Строки, СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок("СвКонтрЛиц/СвИОКонтрЛиц", "/"), 0, "ИмяЭлемента");
		СтрА2 = УведомлениеОСпецрежимахНалогообложения.НайтиУзелВДеревеПоПутиРек(УзелСвКонтрЛицМКТек.Строки, СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок("СвКонтрЛиц/СвРосОКонтрЛиц", "/"), 0, "ИмяЭлемента");
		СтрА3 = УведомлениеОСпецрежимахНалогообложения.НайтиУзелВДеревеПоПутиРек(УзелСвКонтрЛицМКТек.Строки, СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок("СвКонтрЛиц/СвФЛКонтрЛиц", "/"), 0, "ИмяЭлемента");
		СтрБ = УведомлениеОСпецрежимахНалогообложения.НайтиУзелВДеревеПоПутиРек(УзелСвКонтрЛицМКТек.Строки, СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок("ОснПрКонтрЛицМК", "/"), 0, "ИмяЭлемента");
		УИДТек = Новый УникальныйИдентификатор;
		
		СтрВ = УведомлениеОСпецрежимахНалогообложения.НайтиУзелВДеревеПоПутиРек(УзелСвКонтрЛицМКТек.Строки, СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок("РаскрКонтрЛицМККосв", "/"), 0, "ИмяЭлемента");
		Если ТипЗнч(СтрВ) = Тип("СтрокаДереваЗначений") Тогда
			ДобавлениеВ = СтрВ.Строки.НайтиСтроки(Новый Структура("ИмяЭлемента", "РаскрУчМК"), Ложь).Количество();
			ВсегоВ = ВсегоВ + ?(ДобавлениеВ = 0, 1, ДобавлениеВ);
		КонецЕсли;
		
		Если ТипЗнч(СтрА1) = Тип("СтрокаДереваЗначений") Тогда 
			ВсегоА1 = ВсегоА1 + 1;
			Если Не ВсегоА1 = 1 Тогда 
				НовСтр = УведомлениеОСпецрежимахНалогообложения.СкопироватьСтрокуВДереве(ЭтотОбъект, ДеревоСтраниц.ПолучитьЭлементы()[1].ПолучитьЭлементы()[0]);
				НовСтр.Наименование = "Стр. " + Формат(НовСтр.ПолучитьРодителя().ПолучитьЭлементы().Количество(), "ЧГ=0");
			Иначе
				НовСтр = ДеревоСтраниц.ПолучитьЭлементы()[1].ПолучитьЭлементы()[0];
			КонецЕсли;
			ЛистА1 = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ЛистА1Эталон);
			ЛистА1.УИД = УИДТек;
			НовСтр.УИД = УИДТек;
			
			ЗагрузитьСтраницуВыгрузки(ЛистА1, СтрА1, СхемаВыгрузки, "_А1", ДополнительныеПараметры);
			ДанныеМногостраничныхРазделов.ЛистАРаздел1.Добавить(ЛистА1);
		ИначеЕсли ТипЗнч(СтрА2) = Тип("СтрокаДереваЗначений") Тогда
			ВсегоА2 = ВсегоА2 + 1;
			Если Не ВсегоА2 = 1 Тогда 
				НовСтр = УведомлениеОСпецрежимахНалогообложения.СкопироватьСтрокуВДереве(ЭтотОбъект, ДеревоСтраниц.ПолучитьЭлементы()[2].ПолучитьЭлементы()[0]);
				НовСтр.Наименование = "Стр. " + Формат(НовСтр.ПолучитьРодителя().ПолучитьЭлементы().Количество(), "ЧГ=0");
			Иначе
				НовСтр = ДеревоСтраниц.ПолучитьЭлементы()[2].ПолучитьЭлементы()[0];
			КонецЕсли;
			ЛистА2 = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ЛистА2Эталон);
			ЛистА2.УИД = УИДТек;
			НовСтр.УИД = УИДТек;
			
			ЗагрузитьСтраницуВыгрузки(ЛистА2, СтрА2, СхемаВыгрузки, "_А2", ДополнительныеПараметры);
			ДанныеМногостраничныхРазделов.ЛистАРаздел2.Добавить(ЛистА2);
		ИначеЕсли ТипЗнч(СтрА3) = Тип("СтрокаДереваЗначений") Тогда
			ВсегоА3 = ВсегоА3 + 1;
			Если Не ВсегоА3 = 1 Тогда 
				НовСтр = УведомлениеОСпецрежимахНалогообложения.СкопироватьСтрокуВДереве(ЭтотОбъект, ДеревоСтраниц.ПолучитьЭлементы()[3].ПолучитьЭлементы()[0]);
				НовСтр.Наименование = "Стр. " + Формат(НовСтр.ПолучитьРодителя().ПолучитьЭлементы().Количество(), "ЧГ=0");
			Иначе
				НовСтр = ДеревоСтраниц.ПолучитьЭлементы()[3].ПолучитьЭлементы()[0];
			КонецЕсли;
			ЛистА3 = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ЛистА3Эталон);
			ЛистА3.УИД = УИДТек;
			НовСтр.УИД = УИДТек;
			
			ЗагрузитьСтраницуВыгрузки(ЛистА3, СтрА3, СхемаВыгрузки, "_А3", ДополнительныеПараметры);
			ДанныеМногостраничныхРазделов.ЛистАРаздел3.Добавить(ЛистА3);
		КонецЕсли;
		
		Если ТипЗнч(СтрБ) = Тип("СтрокаДереваЗначений") Тогда
			УИДТек = Новый УникальныйИдентификатор;
			ВсегоБ = ВсегоБ + 1;
			Если Не ВсегоБ = 1 Тогда 
				НовСтр = УведомлениеОСпецрежимахНалогообложения.СкопироватьСтрокуВДереве(ЭтотОбъект, ДеревоСтраниц.ПолучитьЭлементы()[4].ПолучитьЭлементы()[0]);
				НовСтр.Наименование = "Стр. " + Формат(НовСтр.ПолучитьРодителя().ПолучитьЭлементы().Количество(), "ЧГ=0");
			Иначе
				НовСтр = ДеревоСтраниц.ПолучитьЭлементы()[4].ПолучитьЭлементы()[0];
			КонецЕсли;
			ЛистБ = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ЛистБЭталон);
			ЛистБ.УИД = УИДТек;
			НовСтр.УИД = УИДТек;
			
			ЗагрузитьСтраницуВыгрузки(ЛистБ, СтрБ, СхемаВыгрузки, "_Б", ДополнительныеПараметры);
			ДанныеМногостраничныхРазделов.ЛистБ.Добавить(ЛистБ);
		КонецЕсли;
	КонецЦикла;
	
	Для Инд = 2 По ВсегоВ Цикл 
		НовСтр = УведомлениеОСпецрежимахНалогообложения.СкопироватьСтрокуВДереве(ЭтотОбъект, ДеревоСтраниц.ПолучитьЭлементы()[5].ПолучитьЭлементы()[0]);
		НовСтр.Наименование = "Стр. " + Формат(НовСтр.ПолучитьРодителя().ПолучитьЭлементы().Количество(), "ЧГ=0");
	КонецЦикла;
	
	ИндексТекВ = 0;
	Для Каждого УзелСвКонтрЛицМКТек Из УзелСвКонтрЛицМК.Родитель.Строки.НайтиСтроки(Новый Структура("ИмяЭлемента", "СвКонтрЛицМК"), Ложь) Цикл
		СтрВ = УведомлениеОСпецрежимахНалогообложения.НайтиУзелВДеревеПоПутиРек(УзелСвКонтрЛицМКТек.Строки, СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок("РаскрКонтрЛицМККосв", "/"), 0, "ИмяЭлемента");
		Если Не ТипЗнч(СтрВ) = Тип("СтрокаДереваЗначений") Тогда
			Продолжить;
		КонецЕсли;
		СтрокиВ = СтрВ.Строки.НайтиСтроки(Новый Структура("ИмяЭлемента", "РаскрУчМК"), Ложь);
		Если СтрокиВ.Количество() > 0 Тогда 
			Для Каждого СтрокаВ Из СтрокиВ Цикл
				УИДТекВ = Новый УникальныйИдентификатор;
				ЛистВ = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ЛистВЭталон);
				ЛистВ.УИД = УИДТекВ;
				ДеревоСтраниц.ПолучитьЭлементы()[5].ПолучитьЭлементы()[ИндексТекВ].УИД = УИДТекВ;
				ЗагрузитьСтраницуВыгрузки(ЛистВ, СтрВ, СхемаВыгрузки, "_В", ДополнительныеПараметры);
				ЗагрузитьСтраницуВыгрузки(ЛистВ, СтрокаВ, СхемаВыгрузки, "_В", ДополнительныеПараметры);
				ДанныеМногостраничныхРазделов.ЛистВ.Добавить(ЛистВ);
				
				СтрокиМнг = СтрокаВ.Строки.НайтиСтроки(Новый Структура("ИмяЭлемента", "СведУчПосл"), Ложь);
				Для Каждого СтрМнг Из СтрокиМнг Цикл 
					НовСтр = ДанныеДопСтрокМнгСтр.Добавить();
					НовСтрСтр = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(НовСтр);
					ЗагрузитьСтраницуВыгрузки(НовСтрСтр, СтрМнг, СхемаВыгрузки, "", ДополнительныеПараметры);
					НовСтрСтр.УИД = УИДТекВ;
					ЗаполнитьЗначенияСвойств(НовСтр, НовСтрСтр);
				КонецЦикла;
				
				СтрокиСвУчПослКосв = СтрокаВ.Строки.НайтиСтроки(Новый Структура("ИмяЭлемента", "СвУчПослКосв"), Ложь);
				Если Не СтрокиСвУчПослКосв.Количество() = 1 Тогда
					Продолжить;
				КонецЕсли;
				
				СтрокиГ1 = СтрокиСвУчПослКосв[0].Строки.НайтиСтроки(Новый Структура("ИмяЭлемента", "СвИО"), Ложь);
				СтрВВДереве = ДеревоСтраниц.ПолучитьЭлементы()[5].ПолучитьЭлементы()[ИндексТекВ].ПолучитьЭлементы()[0];
				Для Инд = 2 По СтрокиГ1.Количество() Цикл 
					НовСтр = УведомлениеОСпецрежимахНалогообложения.СкопироватьСтрокуВДереве(ЭтотОбъект, СтрВВДереве.ПолучитьЭлементы()[0]);
					НовСтр.Наименование = "Стр. " + Формат(НовСтр.ПолучитьРодителя().ПолучитьЭлементы().Количество(), "ЧГ=0");
				КонецЦикла;
				
				Инд = 0;
				Для Каждого СтрМнг Из СтрокиГ1 Цикл
					УИДГ = Новый УникальныйИдентификатор;
					СтрВВДереве.ПолучитьЭлементы()[Инд].УИД = УИДГ;
					ЛистГ1 = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ЛистГ1Эталон);
					ЛистГ1.УИД = УИДГ;
					ЛистГ1.УИДРодителя = УИДТекВ;
					ЗагрузитьСтраницуВыгрузки(ЛистГ1, СтрМнг, СхемаВыгрузки, "", ДополнительныеПараметры);
					ДанныеМногостраничныхРазделов.ЛистГ1.Добавить(ЛистГ1);
					Инд = Инд + 1;
				КонецЦикла;
				
				СтрокиГ2 = СтрокиСвУчПослКосв[0].Строки.НайтиСтроки(Новый Структура("ИмяЭлемента", "СвРосОрг"), Ложь);
				СтрВВДереве = ДеревоСтраниц.ПолучитьЭлементы()[5].ПолучитьЭлементы()[ИндексТекВ].ПолучитьЭлементы()[1];
				Для Инд = 2 По СтрокиГ2.Количество() Цикл 
					НовСтр = УведомлениеОСпецрежимахНалогообложения.СкопироватьСтрокуВДереве(ЭтотОбъект, СтрВВДереве.ПолучитьЭлементы()[0]);
					НовСтр.Наименование = "Стр. " + Формат(НовСтр.ПолучитьРодителя().ПолучитьЭлементы().Количество(), "ЧГ=0");
				КонецЦикла;
				
				Инд = 0;
				Для Каждого СтрМнг Из СтрокиГ2 Цикл
					УИДГ = Новый УникальныйИдентификатор;
					СтрВВДереве.ПолучитьЭлементы()[Инд].УИД = УИДГ;
					ЛистГ2 = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ЛистГ2Эталон);
					ЛистГ2.УИД = УИДГ;
					ЛистГ2.УИДРодителя = УИДТекВ;
					ЗагрузитьСтраницуВыгрузки(ЛистГ2, СтрМнг, СхемаВыгрузки, "", ДополнительныеПараметры);
					ДанныеМногостраничныхРазделов.ЛистГ2.Добавить(ЛистГ2);
					Инд = Инд + 1;
				КонецЦикла;
				
				СтрокиГ3 = СтрокиСвУчПослКосв[0].Строки.НайтиСтроки(Новый Структура("ИмяЭлемента", "СведИСБезЮЛ"), Ложь);
				СтрВВДереве = ДеревоСтраниц.ПолучитьЭлементы()[5].ПолучитьЭлементы()[ИндексТекВ].ПолучитьЭлементы()[2];
				Для Инд = 2 По СтрокиГ3.Количество() Цикл 
					НовСтр = УведомлениеОСпецрежимахНалогообложения.СкопироватьСтрокуВДереве(ЭтотОбъект, СтрВВДереве.ПолучитьЭлементы()[0]);
					НовСтр.Наименование = "Стр. " + Формат(НовСтр.ПолучитьРодителя().ПолучитьЭлементы().Количество(), "ЧГ=0");
				КонецЦикла;
				
				Инд = 0;
				Для Каждого СтрМнг Из СтрокиГ3 Цикл
					УИДГ = Новый УникальныйИдентификатор;
					СтрВВДереве.ПолучитьЭлементы()[Инд].УИД = УИДГ;
					ЛистГ3 = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ЛистГ3Эталон);
					ЛистГ3.УИД = УИДГ;
					ЛистГ3.УИДРодителя = УИДТекВ;
					ЗагрузитьСтраницуВыгрузки(ЛистГ3, СтрМнг, СхемаВыгрузки, "", ДополнительныеПараметры);
					ДанныеМногостраничныхРазделов.ЛистГ3.Добавить(ЛистГ3);
					Инд = Инд + 1;
				КонецЦикла;
				
				ИндексТекВ = ИндексТекВ + 1;
			КонецЦикла;
		Иначе
			УИДТекВ = Новый УникальныйИдентификатор;
			ЛистВ = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ЛистВЭталон);
			ЛистВ.УИД = УИДТекВ;
			ДеревоСтраниц.ПолучитьЭлементы()[5].ПолучитьЭлементы()[ИндексТекВ].УИД = УИДТекВ;
			ЗагрузитьСтраницуВыгрузки(ЛистВ, СтрВ, СхемаВыгрузки, "_В", ДополнительныеПараметры);
			ДанныеМногостраничныхРазделов.ЛистВ.Добавить(ЛистВ);
			ИндексТекВ = ИндексТекВ + 1;
		КонецЕсли;
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(ДанныеДопСтрокМнгСтр, ДанныеДопСтрок.МнгСтр);
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьСтраницуВыгрузки(Страница, СтрокаДереваЗагрузки, СхемаВыгрузки, Постфикс, ДополнительныеПараметры)
	Для Каждого Стр Из СтрокаДереваЗагрузки.Строки Цикл
		ПолныйПуть = УведомлениеОСпецрежимахНалогообложения.ПолучитьПолныйПутьВДеревеЗагрузки(Стр);
		СтрокаВСхеме = УведомлениеОСпецрежимахНалогообложения.НайтиУзелВДеревеПоПути(СхемаВыгрузки,  Сред(ПолныйПуть, 6), "Код");
		ЗагрузитьСтраницуВыгрузки(Страница, Стр, СхемаВыгрузки, Постфикс, ДополнительныеПараметры);
		Если ЗначениеЗаполнено(СтрокаВСхеме.Ключ) Тогда 
			Если Страница.Свойство(СтрЗаменить(СтрокаВСхеме.Ключ, Постфикс, "")) Тогда
				Страница[СтрЗаменить(СтрокаВСхеме.Ключ, Постфикс, "")] = Стр.ЗначениеЭлемента;
				ОсобаяОбработкаЭлементов(ПолныйПуть, Стр, Страница);
			Иначе
				ОсобаяОбработкаЭлементов(ПолныйПуть, Стр, Страница);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ОсобаяОбработкаЭлементов(Путь, СтрЗагружаемоеЗначение, Страница) Экспорт 
	Если Путь = "Файл/Документ/СвКонтрЛицМК/СвКонтрЛиц/СвИОКонтрЛиц/НомерКонтрЛиц" Тогда 
		Страница.НомерКонтрЛицЧисло = ОТЧ.ПривестиЗначение(Прав(СтрЗагружаемоеЗначение.ЗначениеЭлемента, 5));
	ИначеЕсли Путь = "Файл/Документ/СвКонтрЛицМК/СвКонтрЛиц/СвРосОКонтрЛиц/НомерКонтрЛиц" Тогда 
		Страница.НомерРосОргЧисло = ОТЧ.ПривестиЗначение(Прав(СтрЗагружаемоеЗначение.ЗначениеЭлемента, 5));
	ИначеЕсли Путь = "Файл/Документ/СвКонтрЛицМК/СвКонтрЛиц/СвФЛКонтрЛиц/НомерКонтрЛиц" Тогда 
		Страница.НомерКонтрФЛицЧисло = ОТЧ.ПривестиЗначение(Прав(СтрЗагружаемоеЗначение.ЗначениеЭлемента, 5));
	ИначеЕсли Путь = "Файл/Документ/СвКонтрЛицМК/СвКонтрЛиц/СвФЛКонтрЛиц/ДатаРожд" Тогда
		Страница.ДатаРожд = РегламентированнаяОтчетностьЗагрузка.ДатаИзСтрокиЛюбогоФормата(СтрЗагружаемоеЗначение.ЗначениеЭлемента);
	ИначеЕсли Путь = "Файл/Документ/СвКонтрЛицМК/СвКонтрЛиц/СвФЛКонтрЛиц/УдЛичнФЛ/ДатаДок" Тогда
		Страница.ДатаДокЛичн = РегламентированнаяОтчетностьЗагрузка.ДатаИзСтрокиЛюбогоФормата(СтрЗагружаемоеЗначение.ЗначениеЭлемента);
	ИначеЕсли Путь = "Файл/Документ/СвКонтрЛицМК/СвКонтрЛиц/СвФЛКонтрЛиц/СвДокПодтвРег/ДатаДок" Тогда
		Страница.ДатаДокРег = РегламентированнаяОтчетностьЗагрузка.ДатаИзСтрокиЛюбогоФормата(СтрЗагружаемоеЗначение.ЗначениеЭлемента);
	ИначеЕсли Путь = "Файл/Документ/СвКонтрЛицМК/РаскрКонтрЛицМККосв/РаскрУчМК/СведУчПосл/НомерУчПосл" И Страница.Свойство("НомерУч") Тогда
		Страница.НомерУч = СтрЗагружаемоеЗначение.ЗначениеЭлемента;
	ИначеЕсли Путь = "Файл/Документ/СвКонтрЛицМК/РаскрКонтрЛицМККосв/РаскрУчМК/СвУчПослКосв/СвИО/НомерУчПосл" И Страница.Свойство("НомерУчПослЧисло") Тогда
		Страница.НомерУчПослЧисло = ОТЧ.ПривестиЗначение(Прав(СтрЗагружаемоеЗначение.ЗначениеЭлемента, 5));
	ИначеЕсли Путь = "Файл/Документ/СвКонтрЛицМК/РаскрКонтрЛицМККосв/РаскрУчМК/СвУчПослКосв/СвРосОрг/НомерУчПосл" И Страница.Свойство("НомерУчПослЧисло") Тогда
		Страница.НомерУчПослЧисло = ОТЧ.ПривестиЗначение(Прав(СтрЗагружаемоеЗначение.ЗначениеЭлемента, 5));
	ИначеЕсли Путь = "Файл/Документ/СвКонтрЛицМК/РаскрКонтрЛицМККосв/РаскрУчМК/СвУчПослКосв/СведИСБезЮЛ/НомерУчПосл" И Страница.Свойство("НомерУчПослЧисло") Тогда
		Страница.НомерУчПослЧисло = ОТЧ.ПривестиЗначение(Прав(СтрЗагружаемоеЗначение.ЗначениеЭлемента, 5));
	ИначеЕсли Путь = "Файл/Документ/СвКонтрЛицМК/РаскрКонтрЛицМККосв/РаскрУчМК/СвУчПослКосв/СведИСБезЮЛ/ДатаУчрИС" И Страница.Свойство("ДатаУчрИС") Тогда
		Страница.ДатаУчрИС = РегламентированнаяОтчетностьЗагрузка.ДатаИзСтрокиЛюбогоФормата(СтрЗагружаемоеЗначение.ЗначениеЭлемента);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайлаВФормуУведомление(Команда)
	ОписаниеОповещенияОЗавершенииЗагрузки = Новый ОписаниеОповещения("ЗагрузитьИзФайлаВФормуУведомлениеЗавершение", ЭтотОбъект);
	УведомлениеОСпецрежимахНалогообложенияКлиент.ЗагрузитьИзФайлаУведомление(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ПолучитьСворачиваемыеЭлементы()
	СворачиваемыеЭлементы = ПоместитьВоВременноеХранилище(УведомлениеОСпецрежимахНалогообложения.ПолучитьИдентификаторыДляСворачивания(ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайлаВФормуУведомлениеЗавершение(Результат, ДополнительныеПараметры) Экспорт 
	ПолучитьСворачиваемыеЭлементы();
	УведомлениеОСпецрежимахНалогообложенияКлиент.ПриОткрытии(ЭтотОбъект, Ложь);
КонецПроцедуры

&НаСервере
Процедура РазрешитьРедактированиеРеквизитовОбъекта() Экспорт
	РегламентированнаяОтчетность.РазрешитьРедактированиеРеквизитовОтчета(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	РегламентированнаяОтчетностьКлиент.РазрешитьРедактированиеРеквизитовОтчета(ЭтотОбъект);
КонецПроцедуры
