////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции для печати договоров.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

Функция ПараметрыДоговораДляОбъекта(Знач СсылкаНаОбъект) Экспорт

	ФайлДоговора = ПечатьДоговоров.ФайлДоговораДляОбъекта(СсылкаНаОбъект);
	ЭлектронныйФормат = ПечатьДоговоров.ЭлектронныйФорматДляОбъекта(СсылкаНаОбъект);
	
	ПараметрыДоговора = Новый Структура("ФайлДоговора, ЭлектронныйФормат", ФайлДоговора, ЭлектронныйФормат);
	
	Возврат ПараметрыДоговора;
	
КонецФункции

Функция НомерИДатаДоговораДляОбъекта(Знач СсылкаНаОбъект) Экспорт

	Возврат ПечатьДоговоров.НомерИДатаДоговораДляОбъекта(СсылкаНаОбъект);

КонецФункции

Функция ЭлектронныйФорматДоговорногоДокумента(Знач СсылкаНаОбъект, ИдентификаторОсновногоФайла) Экспорт

	Возврат ПечатьДоговоров.ЭлектронныйФорматДоговорногоДокумента(СсылкаНаОбъект, ИдентификаторОсновногоФайла);

КонецФункции

Функция СообщенияЗаполненияОбязательныхРеквизитов(ДоговорСсылка, ФорматированныйДокумент = Неопределено) Экспорт
	
	СообщенияОбОшибках = Новый Массив();
	
	ОбъектыУчета = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДоговорСсылка);
	НастройкиОтправки = ОбменСКонтрагентами.НастройкиОтправки(ОбъектыУчета);
	ФорматВыгрузки = ПечатьДоговоров.ФорматXML();
		
	Если НастройкиОтправки[ДоговорСсылка] <> Неопределено Тогда
		
		Формат = НастройкиОтправки[ДоговорСсылка].Формат;
		Если Формат = ВерсияФорматаPDF() Тогда
			ФорматВыгрузки = ПечатьДоговоров.ФорматPDF();
		КонецЕсли;
		
		ФайлДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорСсылка, "ФайлДоговора");
		
		// Дополнительно проверим на пометку на удаление, если проверяем записанный файл договора.
		// Могут быть случаи, когда файл договора не очищен.
		// В случае проверки текста договора, проверка не осуществляется
		Если ФорматированныйДокумент = Неопределено И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ФайлДоговора, "ПометкаУдаления") Тогда
			
			ТаблицаСообщений = ПечатьДоговоров.ТаблицаСообщений();
			СтрокаСообщений = ТаблицаСообщений.Добавить();
			СтрокаСообщений.РеквизитДоговорногоДокумента = НСтр("ru = 'Пометка удаления'");
			СтрокаСообщений.Описание = НСтр("ru = 'Файл договора помечен на удаление, необходимо очистить его в форме договора'");
			
			Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(ТаблицаСообщений);
			
		КонецЕсли;

		ТаблицаПроверки = ПечатьДоговоров.ТаблицаПроверкиОбязательныхРеквизитов(ДоговорСсылка, ФорматВыгрузки, ФорматированныйДокумент);
		СообщенияОбОшибках = ОбщегоНазначения.ТаблицаЗначенийВМассив(ТаблицаПроверки);
		
	КонецЕсли;
	
	Возврат СообщенияОбОшибках;
	
КонецФункции

Функция ЕстьЭлектронныйДоговорныйДокумент(Ссылка) Экспорт
	
	СтатусДокументооборота = ОбменСКонтрагентами.СтатусДокументооборота(Ссылка);
	
	Возврат ЗначениеЗаполнено(СтатусДокументооборота.ЭлектронныйДокумент);

КонецФункции 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ВерсияФорматаPDF()
	Возврат "ON_DOGDOC_1_999_01_01_01_01";
КонецФункции

#КонецОбласти
