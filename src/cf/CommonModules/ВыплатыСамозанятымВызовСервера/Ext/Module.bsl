#Область ПрограммныйИнтерфейс

// Функция возвращает признак - требуется ли проверка платформы для загрузки больших реестров.
// В разделенном режиме проверка не выполняется.
//
// Параметры:
//     Документ - ДокументСсылка.ВыплатыСамозанятым - Ссылка на документ, в который происходит загрузка
//
// Возвращаемое значение:
//     Булево - Признак
//
Функция ТребуетсяПроверкаПлатформыДляБольшихФайлов(Документ) Экспорт
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СистемнаяИнформация = Новый СистемнаяИнформация();
	ВерсияПлатформы = СистемнаяИнформация.ВерсияПриложения;
	ПоддерживаетсяНачинаяС = "8.3.20.1838";
	Результат = ОбщегоНазначенияКлиентСервер.СравнитьВерсии(ВерсияПлатформы, ПоддерживаетсяНачинаяС);
	Если Результат >= 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// После 8 строк файл разбивается на 2 и становится большим
	МаксимальноеКоличествоСтрок = 8;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Документ", Документ);
	Запрос.УстановитьПараметр("МаксимальноеКоличествоСтрок", 8);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВыплатыСамозанятым.СчетОрганизации КАК СчетОрганизации,
	|	МАКСИМУМ(ВыплатыСамозанятымВыплаты.НомерСтроки) КАК НомерСтроки
	|ПОМЕСТИТЬ ВТКоличествоСтрок
	|ИЗ
	|	Документ.ВыплатыСамозанятым.Выплаты КАК ВыплатыСамозанятымВыплаты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВыплатыСамозанятым КАК ВыплатыСамозанятым
	|		ПО ВыплатыСамозанятымВыплаты.Ссылка = ВыплатыСамозанятым.Ссылка
	|ГДЕ
	|	ВыплатыСамозанятым.Ссылка = &Документ
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыплатыСамозанятым.СчетОрганизации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТКоличествоСтрок.СчетОрганизации КАК СчетОрганизации
	|ИЗ
	|	ВТКоличествоСтрок КАК ВТКоличествоСтрок
	|ГДЕ
	|	ВТКоличествоСтрок.НомерСтроки >= &МаксимальноеКоличествоСтрок";
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	// Проверка нужна для файлов Сбербанка
	Возврат ИнтеграцияСБанкамиПовтИсп.ЭтоСчетСбербанка(Выборка.СчетОрганизации);
	
КонецФункции 

#КонецОбласти
