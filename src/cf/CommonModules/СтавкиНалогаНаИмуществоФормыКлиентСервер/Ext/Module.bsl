
////////////////////////////////////////////////////////////////////////////////
// Универсальные методы для формы записи регистра и формы настройки налогов
//
// Клиент-серверные методы формы записи регистра сведений СтавкиНалогаНаИмущество
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Процедура УправлениеФормой(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	Запись   = Форма.СтавкиНалогаНаИмущество;
	
	ОсвобождениеОтНалогообложения = (Форма.ВидЛьготыНалогНаИмущество = ПредопределенноеЗначение("Перечисление.ВидыЛьготПоИмущественнымНалогам.Освобождение"));
	
	Элементы.ОсвобождениеОтНалогообложенияДвижимогоИмущества.Доступность = Не ОсвобождениеОтНалогообложения;
	Элементы.СнижениеНалоговойСтавкиДвижимоеИмущество.Доступность = Не ОсвобождениеОтНалогообложения;
		
	ВидимостьНалогаНаДвижимоеИмущество = Год(Запись.Период) = 2018;
	Элементы.ГруппаСтавкаНалогаНаДвижимоеИмущество.Видимость = ВидимостьНалогаНаДвижимоеИмущество;
	Элементы.ОсвобождениеОтНалогообложенияДвижимогоИмущества.Видимость = ВидимостьНалогаНаДвижимоеИмущество;
	
	ПрименяетсяЛьгота = ЗначениеЗаполнено(Запись.Организация) И ЗначениеЗаполнено(Запись.ОснованиеЛьготы);
	
	Если ПрименяетсяЛьгота Тогда
		Элементы.ОснованиеЛьготы.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
	Иначе	
		Элементы.ОснованиеЛьготы.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	КонецЕсли; 
	
	ОблагаетсяПоСниженнойСтавке = (Форма.ВидЛьготыНалогНаИмущество = ПредопределенноеЗначение("Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиНаПроцент")
		 Или Форма.ВидЛьготыНалогНаИмущество = ПредопределенноеЗначение("Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиДоУказанной"));
		 
	Если ОблагаетсяПоСниженнойСтавке Тогда
		Элементы.СтавкаНалогаНаИмущество.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСправа;
	Иначе	
		Элементы.СтавкаНалогаНаИмущество.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	КонецЕсли; 
	
	ТекстПодсказки = "";
	
	Если ОсвобождениеОтНалогообложения Тогда
		
		ТекстПодсказки = НСтр("ru='Освобождение от налога применяется:
			|   - ко всей недвижимости, облагаемой по среднегодовой стоимости и расположенной по месту нахождения организации
			|   - ко всей недвижимости, облагаемой по кадастровой стоимости, в т.ч. находящейся вне места нахождения организации'");
		Элементы.ПодсказкаЛьготы.Видимость = Истина;
		
	ИначеЕсли ОблагаетсяПоСниженнойСтавке Тогда
		
		ТекстПодсказки = НСтр("ru='Указанная ставка применяется только к недвижимости, облагаемой по среднегодовой стоимости
			|и расположенной по месту нахождения организации'");
		ТекстПодсказки = СтрЗаменить(ТекстПодсказки, Символы.ПС, " ");
		Элементы.ПодсказкаЛьготы.Видимость = Истина;
		
	ИначеЕсли Форма.ВидЛьготыНалогНаИмущество = ПредопределенноеЗначение("Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаПроцент") Тогда
		
		ТекстПодсказки = НСтр("ru='Сумма налога снижается для всей недвижимости, расположенной по месту нахождения организации'");
		Элементы.ПодсказкаЛьготы.Видимость = Истина;
		
	Иначе
		Элементы.ПодсказкаЛьготы.Видимость = Ложь;
	КонецЕсли;	
	
	Элементы.ПодсказкаЛьготыРасширеннаяПодсказка.Заголовок = ТекстПодсказки;
	
	Элементы.ГруппаПериодДействияЛьготы.Видимость = ПрименяетсяЛьгота;
	
КонецПроцедуры

#КонецОбласти
