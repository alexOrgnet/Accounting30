////////////////////////////////////////////////////////////////////////////////
// Подсистема "СтатистикаПоПоказателям"
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Инициирует инкрементацию счетчика событий в сервисе статистики.
//
// Параметры:
//  ИмяСобытия - Строка - имя счетчика событий, до 100 символов
//  Комментарий - Строка - дополнительная информация, например для отладки; выборочно сохраняется в сервисе статистики
//  ЗаписыватьОтложенно - Булево - можно применять при вызове с клиента, чтобы сократить количество серверных вызовов.
//            Позволяет отложить вызов сервера до окончания групповых или критичных к времени отзыва операций пользователя.
//            После окончания этих операций следует вызвать ЖурналРегистрацииВызовСервера.ЗаписатьСобытияВЖурналРегистрации
//
Процедура ДобавитьСобытие(ИмяСобытия, Комментарий = "", ЗаписыватьОтложенно = Ложь) Экспорт
	
	РазделениеВключено = Ложь;
	
	#Если Клиент Тогда
		РазделениеВключено = СтандартныеПодсистемыКлиент.ПараметрыРаботыКлиента().РазделениеВключено;	
	#Иначе
		РазделениеВключено = ОбщегоНазначения.РазделениеВключено();
	#КонецЕсли
	
	Если Не РазделениеВключено Тогда
		Возврат;
	КонецЕсли;
	
	#Если Клиент Тогда
		
		ЖурналРегистрацииКлиент.ДобавитьСообщениеДляЖурналаРегистрации(
			МаркерСобытияСтатистики(),
			"Информация",
			ЗаписатьСобытиеИКомментарий(ИмяСобытия, Комментарий),
			, // ДатаСобытия: при отложенной записи события будут записаны позже, чем произошли
			Не ЗаписыватьОтложенно);
		
	#Иначе
		ЗаписьЖурналаРегистрации(
			МаркерСобытияСтатистики(), УровеньЖурналаРегистрации.Информация,,, ЗаписатьСобытиеИКомментарий(ИмяСобытия, Комментарий));
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция МаркерСобытияСтатистики() Экспорт
	
	Возврат "Статистика_abd4d044ff5798fa";
               
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЗаписатьСобытиеИКомментарий(ИмяСобытия, Комментарий)
	
	Возврат МаркерСобытияСтатистики() + ". " + СокрЛП(ИмяСобытия) + Символы.ПС + Комментарий;
	
КонецФункции

#КонецОбласти