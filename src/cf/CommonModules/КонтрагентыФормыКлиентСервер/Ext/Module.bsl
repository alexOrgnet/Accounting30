
#Область СлужебныйПрограммныйИнтерфейс

// Возвращает наименование организации по сокращенному.
//
// Параметры:
//   СокращенноеНаименование - Строка - Сокращенное наименование организации.
//
// Возвращаемое значение: 
//   Наименование - Строка - Наименование организации, полученное из сокращенного.
//
Функция НаименованиеПоСокращенномуНаименованию(СокращенноеНаименование) Экспорт
	
	СтруктураНаименования = ОбщегоНазначенияБПКлиентСервер.РазложитьСтрокуНаименования(СокращенноеНаименование);
	
	Возврат СтруктураНаименования.Наименование
			+ ?(ПустаяСтрока(СтруктураНаименования.ОрганизационноПравоваяФормаСокращенно), "", " ")
			+ СтруктураНаименования.ОрганизационноПравоваяФормаСокращенно;
	
КонецФункции
	
// Возвращает результат проверки является ли переданная строка ИНН
//
// Параметры:
//  СтрокаИНН - Строка - проверяемая строка
//
// Возвращаемое значение:
//  Булево
//
Функция ЭтоИНН(СтрокаИНН) Экспорт
	
	Возврат ЗначениеЗаполнено(СтрокаИНН)
		И (СтрДлина(СтрокаИНН) = 10 Или СтрДлина(СтрокаИНН) = 12)
		И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СтрокаИНН);
	
КонецФункции

// Возвращает результат проверки доступности ввода КПП для контрагента
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма.
//
// Возвращаемое значение:
//  Булево
//
Функция ДоступенКПП(Форма) Экспорт
	
	КонтрагентФизическоеЛицо = Форма.Объект.ЮридическоеФизическоеЛицо = ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо");
	
	Возврат Не КонтрагентФизическоеЛицо;
	
КонецФункции

#КонецОбласти