
#Область ПрограммныйИнтерфейс

// Дозаполнение параметров структуры с настройками.
//
// Параметры:
//   Настройки   - Структура - Описание в ОМ.РасчетРезервов.СтруктураНастроекРезервов().
//   Организация - Спр.Организации.СправочникСсылка - Организация.
//   Резерв      - Спр.Резервы.СправочникСсылка - Резерв.
//   Период      - Дата - Период дат.
//
Процедура ЗаполнитьНастройкиРезервов(Настройки, Организация, Резерв, Период) Экспорт

КонецПроцедуры

// Уточняет необходимость выполнять расчет резервов, устанавливается в Ложь, 
// когда резервы рассчитываются в другой программе.
//
// Параметры:
//	РезервыРассчитываются - тип булево.
//
Процедура ПолучитьЗначениеРезервыРассчитываются(РезервыРассчитываются) Экспорт

КонецПроцедуры

// Уточняет возможность использования автоматического расчета резервов, 
// устанавливается в Ложь, когда авторасчет резервов отключен.
//
// Параметры:
//	Автоматически - тип булево.
//
Процедура РезервыРассчитываютсяАвтоматически(Автоматически) Экспорт

КонецПроцедуры

// Процедура предназначена для формирования движений по месту внедрения.
//
// Параметры:
//	Объект - конкретный экземпляр документа Начисление оценочных обязательств по отпускам (ДокументОбъект.РезервыПоОплатеТруда)
//	Отказ  - булево, признак отказа от проведения документа.
//	РежимПроведения - режим проведения документа.
//
Процедура СформироватьДвижения(Объект, Отказ, РежимПроведения) Экспорт

КонецПроцедуры

// Процедура предназначена для дополнения таблицы параметров данными об остатках отпусков 
// и ФОТ с учетом специфики места внедрения.
//
// Параметры:
//   Организация - Спр.Организации.СправочникСсылка - Организация.
//   Период - Дата - Период дат.
//   ДанныеПоказателей - таблица значений.
//		Структура таблицы ДанныеПоказателей.
//			Сотрудник
//			СреднийЗаработок
//
Процедура ДополнитьТаблицуОстатковОтпусков(Организация, Период, ДанныеПоказателей) Экспорт
	
	ПараметрыПолученияСотрудниковОрганизаций = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("Организация",                   Организация);
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("ОтбиратьПоГоловнойОрганизации", Ложь);
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("НачалоПериода",                 КонецГода(Период));
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("ОкончаниеПериода",              КонецГода(Период));
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("КадровыеДанные",                "ДатаПриема");
	ПараметрыПолученияСотрудниковОрганизаций.Вставить("Отборы");
	
	ТаблицаСотрудников = КадровыйУчет.СотрудникиОрганизации(Истина, ПараметрыПолученияСотрудниковОрганизаций);
	
	НастройкиУчетаЗарплаты    = РегистрыСведений.НастройкиУчетаЗарплаты.Получить(Новый Структура("Организация", Организация));
	ПродолжительностьОтпуска  = НастройкиУчетаЗарплаты.ПродолжительностьОтпуска;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",              Организация);
	Запрос.УстановитьПараметр("Период",                   Период);
	Запрос.УстановитьПараметр("НачалоПериода",            НачалоГода(Период));
	Запрос.УстановитьПараметр("ОкончаниеПериода",         КонецГода(Период));
	Запрос.УстановитьПараметр("СотрудникиОрганизации",    ТаблицаСотрудников);
	Запрос.УстановитьПараметр("ОстаткиОтпусков",          ДанныеПоказателей);
	Запрос.УстановитьПараметр("ПродолжительностьОтпуска", ПродолжительностьОтпуска);
	Запрос.УстановитьПараметр("ЦенаМесяца",               Окр(ПродолжительностьОтпуска/12,2));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОстаткиОтпусков.Сотрудник КАК Сотрудник,
	|	ОстаткиОтпусков.СреднийЗаработок КАК СреднийЗаработок
	|ПОМЕСТИТЬ ВТ_ОстаткиОтпусков
	|ИЗ
	|	&ОстаткиОтпусков КАК ОстаткиОтпусков
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СотрудникиОрганизации.Сотрудник КАК Сотрудник,
	|	ВЫБОР
	|		КОГДА РАЗНОСТЬДАТ(СотрудникиОрганизации.ДатаПриема, &Период, МЕСЯЦ) >= 11
	|			ТОГДА &ПродолжительностьОтпуска
	|		ИНАЧЕ (РАЗНОСТЬДАТ(СотрудникиОрганизации.ДатаПриема, &Период, МЕСЯЦ) + 1) * &ЦенаМесяца
	|	КОНЕЦ КАК ЗаработаноДней
	|ПОМЕСТИТЬ ВТ_Сотрудники
	|ИЗ
	|	&СотрудникиОрганизации КАК СотрудникиОрганизации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетРезерваОтпусков.Сотрудник КАК Сотрудник,
	|	МАКСИМУМ(РасчетРезерваОтпусков.ОстатокОтпуска) КАК НакопленоДней
	|ПОМЕСТИТЬ ВТ_РасчетРезервов
	|ИЗ
	|	РегистрСведений.РасчетРезерваОтпусков КАК РасчетРезерваОтпусков
	|ГДЕ
	|	РасчетРезерваОтпусков.Организация = &Организация
	|	И КОНЕЦПЕРИОДА(РасчетРезерваОтпусков.ПериодРасчета, МЕСЯЦ) = КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(&Период, ГОД, -1), МЕСЯЦ)
	|	И РасчетРезерваОтпусков.Сотрудник В
	|			(ВЫБРАТЬ
	|				ВТ_Сотрудники.Сотрудник
	|			ИЗ
	|				ВТ_Сотрудники КАК ВТ_Сотрудники)
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетРезерваОтпусков.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Отпуск.Сотрудник КАК Сотрудник,
	|	СУММА(Отпуск.КоличествоДнейОсновногоОтпуска) КАК ИспользованоДней
	|ПОМЕСТИТЬ ВТ_Отпуска
	|ИЗ
	|	Документ.Отпуск КАК Отпуск
	|ГДЕ
	|	Отпуск.Организация = &Организация
	|	И Отпуск.Проведен
	|	И Отпуск.ПериодРегистрации МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|
	|СГРУППИРОВАТЬ ПО
	|	Отпуск.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	ВТ_Сотрудники.Сотрудник КАК Сотрудник,
	|	ЕСТЬNULL(ВТ_РасчетРезервов.НакопленоДней, 0) + ЕСТЬNULL(ВТ_Сотрудники.ЗаработаноДней, 0) - ЕСТЬNULL(ВТ_Отпуска.ИспользованоДней, 0) КАК ОстатокОтпусков,
	|	ЕСТЬNULL(ВТ_ОстаткиОтпусков.СреднийЗаработок, 0) КАК СреднийЗаработок
	|ИЗ
	|	ВТ_Сотрудники КАК ВТ_Сотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РасчетРезервов КАК ВТ_РасчетРезервов
	|		ПО ВТ_Сотрудники.Сотрудник = ВТ_РасчетРезервов.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Отпуска КАК ВТ_Отпуска
	|		ПО ВТ_Сотрудники.Сотрудник = ВТ_Отпуска.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОстаткиОтпусков КАК ВТ_ОстаткиОтпусков
	|		ПО ВТ_Сотрудники.Сотрудник = ВТ_ОстаткиОтпусков.Сотрудник";
	
	ДанныеПоказателей = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры

// Процедура предназначена для получения настройки использования обмена
// с учетом специфики места внедрения.
//
// Параметры:
// 	ОбменИспользуется - Булево - в параметр помещается вычисленное значение настройки.
// 	Организация - СправочникСсылка.Организации - организация (филиал), по которой получается настройка.
//
Процедура ПроверитьИспользованиеОбмена(ОбменИспользуется, Организация) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОбменДаннымиУниверсальныйФормат") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиУниверсальныйФормат");
		ОбменИспользуется = ОбменИспользуется Или Модуль.ИспользуетсяОбменБП3(Организация);
	КонецЕсли;
	
КонецПроцедуры

//Процедура предназначена для уточнения сведений о видах отпусков по месту внедрения
// Параметры:
//   НачисленнаяЗарплатаИВзносы - таблица значений, см ОтражениеЗарплатыВБухучете.НоваяТаблицаБухучетНачисленнаяЗарплатаИВзносы().
//
Процедура ДополнитьТаблицуСведениямиОбОсобенностяхОтпусков(НачисленнаяЗарплатаИВзносы) Экспорт
	
	РезервыПоОплатеТрудаДляНебольшихОрганизаций.ДополнитьТаблицуСведениямиОбОсобенностяхОтпусков(НачисленнаяЗарплатаИВзносы);
	
КонецПроцедуры

//Процедура предназначена для уточнения сведений о видах операций по месту внедрения
// Параметры:
//   СписокВидовОпераций - Список значений, где 
//               Значение - ПеречислениеСсылка.ВидыОперацийРезервовПоОплатеТруда,
//               Представление - подробное описание операции
//
Процедура ЗаполнитьПодробноеОписаниеВидовОперациий(СписокВидовОпераций) Экспорт
	
	СписокВидовОпераций.Очистить();
	
	НоваяОперация = СписокВидовОпераций.Добавить();
	НоваяОперация.Значение      = Перечисления.ВидыОперацийРезервовПоОплатеТруда.Начисление;
	НоваяОперация.Представление = НСтр("ru = 'Плановое начисление резерва за выбранный период.
	|В учете отражается начисление по  Кт счета 96 в корреспонденции с Дт счета затрат.'");
	
	НоваяОперация = СписокВидовОпераций.Добавить();
	НоваяОперация.Значение      = Перечисления.ВидыОперацийРезервовПоОплатеТруда.Инвентаризация;
	НоваяОперация.Представление = НСтр("ru = 'Сравнение накопленных и рассчитанных сумм.
	|В учете отражается доначисление или списание по счету 96 в корреспонденции со счетом 91.'");
	
	НоваяОперация = СписокВидовОпераций.Добавить();
	НоваяОперация.Значение      = Перечисления.ВидыОперацийРезервовПоОплатеТруда.Списание;
	НоваяОперация.Представление = НСтр("ru = 'Списание резервов по выбранным сотрудникам.
	|В учете отражается списание по Дт счета 96 в корреспонденции с Кт выбранного счета.'");
	
	НоваяОперация = СписокВидовОпераций.Добавить();
	НоваяОперация.Значение      = Перечисления.ВидыОперацийРезервовПоОплатеТруда.Корректировка;
	НоваяОперация.Представление = НСтр("ru = 'Доначисление резервов по выбранным сотрудникам.
	|В учете отражается начисление по Кт счета 96 в корреспонденции с Дт выбранного счета.'");
	
КонецПроцедуры

//Процедура расширение обработчика ПередЗаписью документа по месту внедрения
// Параметры:
//	Объект - конкретный экземпляр документа Начисление оценочных обязательств по отпускам (ДокументОбъект.РезервыПоОплатеТруда)
//	Отказ  - булево, признак отказа от записи документа.
//	РежимЗаписи  - режим записи документа.
//	РежимПроведения - режим проведения документа.
//
Процедура ПередЗаписью(Объект, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
КонецПроцедуры

//Процедура расширение обработчика ПриЗаписи документа по месту внедрения
// Параметры:
//	Объект - конкретный экземпляр документа Начисление оценочных обязательств по отпускам (ДокументОбъект.РезервыПоОплатеТруда)
//	Отказ  - булево, признак отказа от записи документа.
//
Процедура ПриЗаписи(Объект, Отказ) Экспорт
	
КонецПроцедуры

//Процедура расширение обработчика ОбработкаУдаленияПроведения документа по месту внедрения
// Параметры:
//	Объект - конкретный экземпляр документа Начисление оценочных обязательств по отпускам (ДокументОбъект.РезервыПоОплатеТруда)
//	Отказ  - булево, признак отказа от удаления проведения документа.
//
Процедура ОбработкаУдаленияПроведения(Объект, Отказ) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область УстаревшиеПроцедурыИФункции

// Устарела. Следует использовать РезервыПоОплатеТрудаПереопределяемый.ПроверитьИспользованиеОбмена
// Процедура предназначена для получения настройки использования обмена с БП3
// с учетом специфики места внедрения.
//
// Параметры:
// 	ОбменИспользуется - Булево - в параметр помещается вычисленное значение настройки.
// 	Организация - СправочникСсылка.Организации - организация (филиал), по которой получается настройка.
//
Процедура ИспользуетсяОбменЗУПБП(ОбменИспользуется, Организация) Экспорт
	
	ПроверитьИспользованиеОбмена(ОбменИспользуется, Организация);
	
КонецПроцедуры

#КонецОбласти

