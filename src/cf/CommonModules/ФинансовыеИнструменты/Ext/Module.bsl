
#Область ПрограммныйИнтерфейс

// Вычисляет приблизительную годовую ставку IRR для графика платежей с произвольной периодичностью. Соответствует функции Excel ЧИСТВНДОХ.
// Вычисление производится методом Ньютона.
// IRR - внутренняя норма доходности, при которой инвестиции равны приведенной стоимости будущих поступлений, т.е. NPV = 0.
//
// Параметры:
//    Платежи - Массив из Число - Суммы платежей по графику. По крайней мере один платеж в графике должен быть положительный и один отрицательный.
//    Даты - Массив из Дата - Даты платежей. Индексы дат в массиве должны совпадать с соответствующими индексами сумм в массиве платежей.
//    Предположение - Число - Предполагаемая ставка (задается в виде числа: например, 0.25 соответствует 25%).
//                            Необязательный параметр, по умолчанию исходная ставка = 0.1.
//
// Возвращаемое значение:
//    Число, Неопределено - Ставка IRR. Если рассчитать ставку не удалось или входные данные некорректны, вовращается Неопределено.
//
Функция XIRR(Платежи, Даты, Предположение = Неопределено) Экспорт 
	
	Если Предположение = Неопределено Тогда
		Х0 = 0.1;
	Иначе
		Х0 = Предположение;
	КонецЕсли;
	
	Сроки = Новый Массив;
	Для Инд = 0 По Даты.Количество() - 1 Цикл
		Сроки.Добавить((Даты[0] - Даты[Инд]) / 31536000); // В долях года
	КонецЦикла;
	
	Х1 = 0.0;
	Допуск = 0.00000001;
	
	МаксИтераций = 500;
	ЧислоИтераций = 0;
	
	Пока ЧислоИтераций < МаксИтераций Цикл
		
		Знаменатель = Всего_ДФ_XIRR(Платежи, Сроки, Х0);
		Если Знаменатель = 0 Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Х1 = Х0 - Всего_Ф_XIRR(Платежи, Сроки, Х0) / Знаменатель;
		Ошибка = МодульЧисла(Х1 - Х0);
		Х0 = Х1;
		
		Если Ошибка <= Допуск Тогда
			Возврат Х0;
		КонецЕсли;
		
		ЧислоИтераций = ЧислоИтераций + 1;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

// Вычисляет значение NPV для графика платежей с одинаковой периодичностью. Соответствует функции Excel ЧПС.
// Платежи не должны включать первоначальную инвестицию, она вычитается из результата.
// Чистая приведенная стомость (NPV) - сумма дисконтированных потоков платежей, приведенных к начальной дате.
//
// Параметры:
//    Ставка - Число - Ставка дисконтирования (задается в виде числа: например, 0.25 соответствует 25%).
//    Платежи - Массив из Число - Суммы платежей по графику.
//
// Возвращаемое значение:
//    Число - Значение NPV.
//
Функция NPV(Ставка, Платежи) Экспорт
	
	Рез = 0;
	
	Для НомерПериода = 0 По Платежи.Количество()-1 Цикл
		Рез = Рез + ПриведенныйПлатежNPV(Ставка, Платежи[НомерПериода], НомерПериода+1);
	КонецЦикла;
	
	Возврат Рез;
	
КонецФункции

// Вычисляет значение NPV для графика платежей с произвольной периодичностью. Соответствует функции Excel ЧИСТНЗ.
// Платежи должны включать первоначальную инвестицию.
// Чистая приведенная стомость (NPV) - сумма дисконтированных потоков платежей, приведенных к начальной дате.
//
// Параметры:
//    Ставка - Число - Ставка дисконтирования (задается в виде числа: например, 0.25 соответствует 25%).
//    Платежи - Массив из Число - Суммы платежей по графику.
//    Даты - Массив из Дата - Даты платежей. Индексы дат в массиве должны совпадать с соответствующими индексами сумм в массиве платежей.
//
// Возвращаемое значение:
//    Число - Значение NPV.
//
Функция XNPV(Ставка, Платежи, Даты) Экспорт
	
	Рез = 0;
	
	Сроки = Новый Массив;
	Для Инд = 0 По Даты.Количество() - 1 Цикл
		Сроки.Добавить((Даты[Инд] - Даты[0]) / 31536000); // В днях
	КонецЦикла;
	
	Для НомерПериода = 0 По Платежи.Количество()-1 Цикл
		Рез = Рез + ПриведенныйПлатежXNPV(Ставка, Платежи[НомерПериода], Сроки[НомерПериода]);
	КонецЦикла;
	
	Возврат Рез;
	
КонецФункции

// Вычисляет сумму процентов по обязательству за указанный период.
//
// Параметры:
//  Обязательство  - Число - Сумма обязательства на начало периода.
//  СтавкаДисконтирования - Число - Ставка для расчета, % годовых.
//  КоличествоДней - Число - дней в периоде, за который нужно рассчитать проценты.
// 
// Возвращаемое значение:
//  Число - сумма процентов.
//
Функция ПроцентыЗаПериод(Обязательство, СтавкаДисконтирования, КоличествоДней) Экспорт
	
	ПроцентнаяСтавка = Pow((1 + СтавкаДисконтирования / 100), КоличествоДней / 365) - 1;
	СуммаПроцентов = Окр(Обязательство * ПроцентнаяСтавка, 2);
	
	Возврат СуммаПроцентов;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция Всего_Ф_XIRR(Платежи, Сроки, Х)
	
	Рез = 0;
	
	Для Инд = 0 По Платежи.Количество() - 1 Цикл
		Рез = Рез + Ф_XIRR(Платежи[Инд], Сроки[Инд], Х);
	КонецЦикла;
	
	Возврат Рез;
	
КонецФункции

Функция Всего_ДФ_XIRR(Платежи, Сроки, Х)
	
	Рез = 0;
	
	Для Инд = 0 По Платежи.Количество() - 1 Цикл
		Рез = Рез + ДФ_XIRR(Платежи[Инд], Сроки[Инд], Х);
	КонецЦикла;
	
	Возврат Рез;
	
КонецФункции

Функция Ф_XIRR(Платеж, Срок, Знач Х)
	
	Если Х <= -1 Тогда
		Х = -1 + 0.000000000001;
	КонецЕсли;
	
	Возврат Платеж * Pow((1 + Х), Срок);
	
КонецФункции

Функция ДФ_XIRR(Платеж, Срок, Х)
	
	Возврат Срок * Платеж * Pow((1 + Х), (Срок - 1));
	
КонецФункции

Функция МодульЧисла(Число)
	
	Возврат Макс(Число, -Число);
	
КонецФункции

Функция ПриведенныйПлатежNPV(Ставка, Платеж, НомерПериода)
	
	Возврат Платеж / Pow((Ставка + 1), НомерПериода);
	
КонецФункции

Функция ПриведенныйПлатежXNPV(Ставка, Платеж, Срок)
	
	Возврат Платеж / Pow((Ставка + 1), Срок);
	
КонецФункции

#КонецОбласти