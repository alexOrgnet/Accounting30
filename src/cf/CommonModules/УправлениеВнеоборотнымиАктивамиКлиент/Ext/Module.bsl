
#Область ПрограммныйИнтерфейс

//  Функция вовзращает ОС выбранное в текущей строке таблицы ОС документа для дальнешей передачи
// в функцию ДозаполнитьТабличнуюЧастьОсновнымиСредствамиПоНаименованию. В случае невозможности
// определения ОС выдает сообщение об ошибке.
//
// Параметры
//  ПараметрыФормы   - Структура с параметрами заполнения, ключи структуры:
//  	Форма             - форма заполняемого документа
//  	Объект            - Значение основного реквизита формы - документа для заполнения
//  	ИмяТабличнойЧасти - Имя табличной части основных средств документа, значение по умолчанию "ОС"
//
// Возвращаемое значение:
//   СправочникСсылка.ОсновныеСредства, Неопределено - В случае невозможности определения ОС функция
//   	возвращает Неопределено, в противном случае функция возвращает значение ОС.
//
Функция ПолучитьОСДляЗаполнениеПоНаименованию(Параметры) Экспорт
	
	ОчиститьСообщения();
	
	Форма = Параметры.Форма;
	Объект = Параметры.Объект;
	Если Параметры.Свойство("ИмяТабличнойЧасти") Тогда
		ИмяТабличнойЧасти = Параметры.ИмяТабличнойЧасти;
	Иначе
		ИмяТабличнойЧасти = "ОС";
	КонецЕсли;
	
	Если Форма.Элементы[ИмяТабличнойЧасти].ТекущаяСтрока = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Данные для заполнения отсутствуют.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Объект." + ИмяТабличнойЧасти);
		Возврат Неопределено;
	КонецЕсли;
	
	ОсновноеСредство = Форма.Элементы[ИмяТабличнойЧасти].ТекущиеДанные.ОсновноеСредство;
	
	Если НЕ ЗначениеЗаполнено(ОсновноеСредство) Тогда
		ТекстСообщения = НСтр("ru = 'Не выбрано основное средство'");
		ИндексСтроки = Формат(Объект[ИмяТабличнойЧасти].Индекс(Форма.Элементы[ИмяТабличнойЧасти].ТекущиеДанные), "ЧН=0; ЧГ=");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Объект." + ИмяТабличнойЧасти + "[" + ИндексСтроки + "].ОсновноеСредство");
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ОсновноеСредство;
	
КонецФункции // ПолучитьОСДляЗаполнениеПоНаименованию()

Процедура ОткрытьФормуВыбораНМА(ВидОбъектаУчета, СтандартнаяОбработка, Владелец, Ключ = неопределено,ВыбратьНесколькоЗначений = Ложь) Экспорт

	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", НЕ ВыбратьНесколькоЗначений);
	ПараметрыФормы.Вставить("Отбор", Новый Структура("ВидОбъектаУчета", ВидОбъектаУчета));
	
	ОткрытьФорму("Справочник.НематериальныеАктивы.ФормаВыбора", ПараметрыФормы, Владелец, Ключ);

КонецПроцедуры

// Процедура-обработчик нажатия ссылки выбора ОС в регламентированном отчете Сообщение о наличии ТС и зем участков
// 
// Параметры:
//  ПараметрыОтчета - параметры формы регл. отчета (см. ПодобратьОС() в форме отчета РегламентированноеУведомлениеСообщениеТранспортЗемля)
//  ФормаОтчета - форма регл. отчета
//  ОповещениеЗавершения - оповещение, выполняемое в форме регл. отчета после завершения текущего обработчика события.
//       В параметре Результат обработчика оповещения ожидается структура с ключами, равными именам областей страницы сообщения
//       (см. УправлениеВнеоборотнымиАктивамиВызовСервера.ДанныеСтраницыТранспортногоСредства() и
//       УправлениеВнеоборотнымиАктивамиВызовСервера.ДанныеСтраницыЗемельногоУчастка()).
Процедура ПередЗаполнениемСообщенияОНаличииТранспортаИЗемли(ПараметрыОтчета, ФормаОтчета, ОповещениеЗавершения) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОписаниеОповещения", ОповещениеЗавершения);
	ДополнительныеПараметры.Вставить("ТекущееИДНаименования", ПараметрыОтчета.ТекущееИДНаименования);
	ДополнительныеПараметры.Вставить("Организация", ПараметрыОтчета.Организация);
	ДополнительныеПараметры.Вставить("ДатаПодписи", ПараметрыОтчета.ДатаПодписи);
	
	ОповещениеОВыборе = Новый ОписаниеОповещения("ПередЗаполнениемСообщенияОНаличииТранспортаИЗемлиЗавершение",
		ЭтотОбъект, ДополнительныеПараметры);
	
	Отбор = Новый Структура;
	
	ПараметрыФормыВыбора = Новый Структура;
	ПараметрыФормыВыбора.Вставить("РежимВыбора", Истина);
	ПараметрыФормыВыбора.Вставить("МножественныйВыбор", Ложь);
	Если ПараметрыОтчета.Свойство("Организация") И ЗначениеЗаполнено(ПараметрыОтчета.Организация) Тогда
		Отбор.Вставить("Организация", ПараметрыОтчета.Организация);
	КонецЕсли;
	
	ПараметрыФормыВыбора.Вставить("Отбор", Отбор);
	
	Если ПараметрыОтчета.ТекущееИДНаименования = "Транспорт" Тогда
		ИмяФормы = "Справочник.ОсновныеСредства.Форма.ФормаСпискаТранспорт";
	ИначеЕсли ПараметрыОтчета.ТекущееИДНаименования = "Земля" Тогда
		ИмяФормы = "Справочник.ОсновныеСредства.Форма.ФормаСпискаЗемля";
	КонецЕсли;		
	
	ОткрытьФорму(ИмяФормы, ПараметрыФормыВыбора, ФормаОтчета, , , , ОповещениеОВыборе, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПередЗаполнениемСообщенияОНаличииТранспортаИЗемлиЗавершение(ОсновноеСредство, ДополнительныеПараметры) Экспорт
	
	Если ОсновноеСредство <> Неопределено Тогда
		Период = УправлениеВнеоборотнымиАктивамиВызовСервера.ПоследнийОтчетныйПериодПоДатеУведомления(
			ДополнительныеПараметры.ДатаПодписи);
		Если ДополнительныеПараметры.ТекущееИДНаименования = "Транспорт" Тогда
			ПараметрыЗаполнения = УправлениеВнеоборотнымиАктивамиВызовСервера.ДанныеСтраницыТранспортногоСредства(
				ДополнительныеПараметры.Организация, 
				ОсновноеСредство,
				Период);
		ИначеЕсли ДополнительныеПараметры.ТекущееИДНаименования = "Земля" Тогда
			ПараметрыЗаполнения = УправлениеВнеоборотнымиАктивамиВызовСервера.ДанныеСтраницыЗемельногоУчастка(
				ДополнительныеПараметры.Организация, 
				ОсновноеСредство,
				Период);
		КонецЕсли;		
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОписаниеОповещения, ПараметрыЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

