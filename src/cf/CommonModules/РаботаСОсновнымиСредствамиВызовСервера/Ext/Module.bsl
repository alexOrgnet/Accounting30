////////////////////////////////////////////////////////////////////////////////
// Работа с основными средствами.
//  
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Функция ДанныеВыбораОС(Параметры, ОкончаниеВводаТекста = Ложь) Экспорт
	
	ДанныеВыбора = Справочники.ОсновныеСредства.ПолучитьДанныеВыбора(Параметры);
	
	Если ПолучитьФункциональнуюОпцию("ВедетсяУчетГрупповыхОС") Тогда
	
		Строка = НСтр("ru='Создать: инвентарный объект '") + """";
		ДанныеВыбора.Добавить(Перечисления.ЕдиницыУчетаОС.ИнвентарныйОбъект,
			ПредставлениеСтроки(Строка, Параметры));
				
		Если УчетГрупповыхОСКлиентСервер.ГрупповаяЕдиницаВозможна(Параметры.ГруппаОС) Тогда
					
			Строка = НСтр("ru='Создать: групповой объект '") + """";
			ДанныеВыбора.Добавить(Перечисления.ЕдиницыУчетаОС.ГрупповойОбъект,
				ПредставлениеСтроки(Строка, Параметры));
			
		КонецЕсли;
			
	Иначе
		
		Строка = НСтр("ru='Создать: '") + """";
		ДанныеВыбора.Добавить(Перечисления.ЕдиницыУчетаОС.ИнвентарныйОбъект,
			ПредставлениеСтроки(Строка, Параметры));
			
	КонецЕсли;
	
	Если ОкончаниеВводаТекста Тогда
		// Добавляем вторую строку, чтобы в этом режиме не срабатывал автовыбор единственной строки.
		ДанныеВыбора.Добавить(""); 
	КонецЕсли;
	
	Возврат ДанныеВыбора;
	
КонецФункции

Функция СоздатьОСБыстро(Знач ЕдиницаУчета, Знач ПараметрыСоздания) Экспорт
	
	НовыйОбъект = Справочники.ОсновныеСредства.СоздатьЭлемент();
	ГруппаОС = ПараметрыСоздания.ДополнительныеПараметрыСоздания.ГруппаОС;
	
	Если ПараметрыСоздания.Свойство("ЗначенияЗаполнения") Тогда
		НовыйОбъект.Заполнить(ПараметрыСоздания.ЗначенияЗаполнения);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ГруппаОС) Тогда
		НовыйОбъект.ГруппаОС = ГруппаОС;
		НовыйОбъект.НедвижимоеИмущество = УправлениеВнеоборотнымиАктивамиКлиентСервер.ЭтоНедвижимоеИмущество(ГруппаОС);
	Иначе
		НовыйОбъект.ГруппаОС = Перечисления.ГруппыОС.ДругиеВидыОсновныхСредств;
	КонецЕсли;
	
	НовыйОбъект.Наименование = ПараметрыСоздания.ДополнительныеПараметрыСоздания.Наименование;
	НовыйОбъект.НаименованиеПолное = НовыйОбъект.Наименование;
	НовыйОбъект.ЕдиницаУчета = ЕдиницаУчета;
	Попытка
		НовыйОбъект.Записать();
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат НовыйОбъект.Ссылка;
	
КонецФункции

Функция ПредставлениеСтроки(Строка, Параметры)
	
	ШрифтКоманды    = Новый Шрифт(ШрифтыСтиля.ОбычныйШрифтТекста);
	ШрифтПодСтроки  = Новый Шрифт(ШрифтыСтиля.ОбычныйШрифтТекста, , , Истина);
	ЦветПодстроки   = Новый Цвет(0, 175, 0);
	
	Представление = Новый ФорматированнаяСтрока(
		Новый ФорматированнаяСтрока(Строка, ШрифтКоманды),
		Новый ФорматированнаяСтрока(СокрЛП(Параметры.СтрокаПоиска), ШрифтПодСтроки, ЦветПодстроки),
		Новый ФорматированнаяСтрока("""", ШрифтКоманды)
		);
		
	Возврат Представление;
КонецФункции

#КонецОбласти