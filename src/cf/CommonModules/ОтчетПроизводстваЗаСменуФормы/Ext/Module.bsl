
#Область СлужебныеПроцедурыИФункции

Процедура УстановитьЗаголовокФормы(Форма) Экспорт
	
	Объект = Форма.Объект;
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийОтчетПроизводстваЗаСмену.ВыпускПродукции Тогда
		ТекстЗаголовка = НСтр("ru = 'Выпуск продукции'");
	Иначе
		ТекстЗаголовка = НСтр("ru = 'Отчет производства за смену'");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ТекстЗаголовка = ТекстЗаголовка + СтрШаблон(НСтр("ru=' %1 от %2'"), Объект.Номер, Объект.Дата);
	Иначе
		ТекстЗаголовка = ТекстЗаголовка + " " + НСтр("ru = '(создание)'");
	КонецЕсли;
	
	Форма.Заголовок = ТекстЗаголовка;
	
КонецПроцедуры

Процедура ЗаполнитьСписокВыбораМатериалыОтражениеВУСН(Форма, ПерезаполнитьНедоступныеЗначения = Ложь) Экспорт
	
	ДанныеОбъекта = Новый Структура("Организация, Дата, Материалы");
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "Объект") Тогда
		ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Форма.Объект);
	Иначе
		ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Форма);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДанныеОбъекта.Организация) Или Не ЗначениеЗаполнено(ДанныеОбъекта.Дата)
		Или ДанныеОбъекта.Материалы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не УчетнаяПолитика.ПрименяетсяУСНДоходыМинусРасходы(ДанныеОбъекта.Организация, ДанныеОбъекта.Дата) Тогда
		Возврат;
	КонецЕсли;
	
	Если Форма.Элементы.Найти("МатериалыОтражениеВУСН") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПрименяетсяОсобыйНалоговыйРежим = УчетнаяПолитика.ПрименяетсяУСНПатент(ДанныеОбъекта.Организация, ДанныеОбъекта.Дата)
		Или УчетнаяПолитика.ПлательщикЕНВД(ДанныеОбъекта.Организация, ДанныеОбъекта.Дата);
	
	ДоступныеОтражения = Перечисления.ОтражениеВУСН.СписокВыбора(ПрименяетсяОсобыйНалоговыйРежим, Истина);
	
	СписокВыбора = Форма.Элементы.МатериалыОтражениеВУСН.СписокВыбора;
	СписокВыбора.Очистить();
	Для Каждого ЭлементОтражения Из ДоступныеОтражения Цикл
		СписокВыбора.Добавить(ЭлементОтражения.Значение, ЭлементОтражения.Представление);
	КонецЦикла;
	
	Если ПерезаполнитьНедоступныеЗначения Тогда
		
		Для Каждого СтрокаТаблицы Из ДанныеОбъекта.Материалы Цикл
			Если ДоступныеОтражения.НайтиПоЗначению(СтрокаТаблицы.ОтражениевУСН) = Неопределено Тогда
				СтрокаТаблицы.ОтражениеВУСН = Перечисления.ОтражениеВУСН.Принимаются;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
