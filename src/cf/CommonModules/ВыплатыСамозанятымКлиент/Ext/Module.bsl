#Область ПрограммныйИнтерфейс

// Процедура выполняет загрузку чеков самозанятых из файла реестра, предоставляемого банком
// Параметры:
//     ОписаниеОповещения - ОписаниеОповещения - Обработчик описания оповещения, который следует вызвать после загрузки файла
//     УникальныйИдентификатор - Строки - Уникальный идентификатор формы
//     Документ - ДокументСсылка.ВыплатыСамозанятым - Ссылка на документ, в который происходит загрузка
//
Процедура ЗагрузитьЧеки(ОписаниеОповещения, УникальныйИдентификатор, Документ) Экспорт
	
	ПараметрыЗагрузки = ЗагрузкаДанныхИзВнешнихФайловКлиент.НовыйПараметрыЗагрузкиВТабЧасть();
	
	ПараметрыЗагрузки.ИмяТабличнойЧасти = "Документ.ВыплатыСамозанятым.Выплаты";
	ПараметрыЗагрузки.Заголовок = НСтр("ru = 'Загрузка чеков самозанятых'");
	ПараметрыЗагрузки.Оповещение = ОписаниеОповещения;
	ПараметрыЗагрузки.Пояснение = 
		НСтр("ru = 'Укажите названия реквизитов в заголовках колонок. <span style=""color: НезаполненныйРеквизит"">Ненайденные</span> строки не будут загружены.'");
	
	ПараметрыЗагрузки.ПрикладнаяЗагрузка = Истина;
	ПараметрыЗагрузки.ПараметрыПрикладнойЗагрузки.ОбъектДляЗагрузки = Документ;
	ПараметрыЗагрузки.ПараметрыПрикладнойЗагрузки.ИмяМакета = "ЗагрузкаИзФайла";
	ПараметрыЗагрузки.ПараметрыПрикладнойЗагрузки.ПострочнаяОбработка = Истина;
	ПараметрыЗагрузки.ПараметрыПрикладнойЗагрузки.СопоставлятьКолонки = Истина;
	ПараметрыЗагрузки.ПараметрыПрикладнойЗагрузки.ТолькоПоследнийЛист = Истина;
	
	Если ВыплатыСамозанятымВызовСервера.ТребуетсяПроверкаПлатформыДляБольшихФайлов(Документ) Тогда
		ПараметрыЗагрузки.ПараметрыПрикладнойЗагрузки.ПоказатьИнформациюОжидания = Истина;
		ПараметрыЗагрузки.ПараметрыПрикладнойЗагрузки.ТекстИнформацииОжидания = 
			НСтр("ru='Если загрузка не выполняется, то:
				| - подготовьте файл Excel. Например, удалите первый лист, если он содержит избыточную информацию
				| - или обновите программу 1С:Предприятие на версию 8.3.20.1838 и выше'");
	КонецЕсли;
	
	РеквизитыДляСопоставления = Новый СписокЗначений;
	ПараметрыЗагрузки.РеквизитыДляСопоставления = ПоместитьВоВременноеХранилище(
		РеквизитыДляСопоставления, УникальныйИдентификатор);
	
	СтатистикаПоПоказателямКлиентСервер.ДобавитьСобытие("ВыплатыСамозанятым.ЗагрузкаЧеков.Реестр");
	
	ЗагрузкаДанныхИзВнешнихФайловКлиент.ВыбратьФайлДляЗагрузки(
		"Обработка.ЗагрузкаДанныхВТабличнуюЧасть.Форма",
		УникальныйИдентификатор,
		ПараметрыЗагрузки);
		
КонецПроцедуры

// Процедура открывает форму документа Выплаты самозанятым с отбором по незагруженным или всем чекам,
// в зависимости от статуса загрузки чеков
// Параметры:
//     РеестрВыплатСамозанятым - Документ.ВыплатыСамозанятым - Документ, для которого следует открыть форму
//
Процедура ОткрытьРеестрПоСсылкеЧеков(РеестрВыплатСамозанятым) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", РеестрВыплатСамозанятым);
	ПараметрыФормы.Вставить("ОткрытПоСсылкеЧеков", Истина);
	ОткрытьФорму("Документ.ВыплатыСамозанятым.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти