
#Область ПрограммныйИнтерфейс

// Формирует таблицу документов со статусами проверки РНПТ в зависимости от переданных параметров поиска.
//
// Параметры:
//  ПараметрыПоиска - Структура с полями:
//    * Организация - СправочникСсылка - ссылка на организацию, по которой происходит поиск
//    * НачалоПериода - Дата - дата начала поиска
//    * КонецПериода - Дата - дата окончания поиска
//    * Контрагент - СправочникСсылка - контрагент по которому происходит поиск (может быть пустой)
//    * НастройкаОтображенияДокументов - Число (0 - все, 1 - с ошибками, 2 - без ошибок)
//    * РежимРасшифровки - Булево - признак, необходимый для получения сведений из отчета "снаружи"
//    * ЭтоВходящийДокумент - Булево - признак того, что нужно отбирать входящие документы
//
// Возвращаемое значение:
//  ТаблицаЗначений - см. текст запроса функции
//
Функция ДокументыСоСтатусомПроверкиРНПТ(ПараметрыПоискаДокументов) Экспорт
	
	Если НЕ ПравоДоступа("Просмотр", Метаданные.Отчеты.АнализРНПТДокументов) Тогда
		// Для пользователей без доступа к отчету не получаем список документов с ошибками.
		Возврат Новый ТаблицаЗначений;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",                    ПараметрыПоискаДокументов.Организация);
	Запрос.УстановитьПараметр("НачалоПериода",                  ПараметрыПоискаДокументов.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",                   ПараметрыПоискаДокументов.КонецПериода);
	Запрос.УстановитьПараметр("НастройкаОтображенияДокументов", ПараметрыПоискаДокументов.НастройкаОтображенияДокументов);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СтатусыПроверокРНПТДокументов.Организация КАК Организация,
	|	СтатусыПроверокРНПТДокументов.Документ КАК Документ,
	|	СтатусыПроверокРНПТДокументов.Контрагент КАК Контрагент,
	|	СтатусыПроверокРНПТДокументов.СтатусПроверки КАК СтатусПроверки,
	|	ВЫБОР
	|		КОГДА СтатусыПроверокРНПТДокументов.СтатусПроверки = ЗНАЧЕНИЕ(Перечисление.СтатусыПроверкиРНПТ.НекорректныйРНПТ)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Ошибка,
	|	ВЫБОР
	|		КОГДА СтатусыПроверокРНПТДокументов.Документ ССЫЛКА Документ.ПоступлениеТоваровУслуг
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоВходящийДокумент,
	|	ДанныеПервичныхДокументов.Номер КАК Номер,
	|	ДанныеПервичныхДокументов.Дата КАК Дата,
	|	ВЫБОР
	|		КОГДА СтатусыПроверокРНПТДокументов.Документ ССЫЛКА Документ.ПоступлениеТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(СтатусыПроверокРНПТДокументов.Документ КАК Документ.ПоступлениеТоваровУслуг).СуммаДокумента
	|		КОГДА СтатусыПроверокРНПТДокументов.Документ ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(СтатусыПроверокРНПТДокументов.Документ КАК Документ.РеализацияТоваровУслуг).СуммаДокумента
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаДокумента,
	|	ВЫБОР
	|		КОГДА СтатусыПроверокРНПТДокументов.Документ ССЫЛКА Документ.ПоступлениеТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(СтатусыПроверокРНПТДокументов.Документ КАК Документ.ПоступлениеТоваровУслуг).ВалютаДокумента
	|		КОГДА СтатусыПроверокРНПТДокументов.Документ ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(СтатусыПроверокРНПТДокументов.Документ КАК Документ.РеализацияТоваровУслуг).ВалютаДокумента
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВалютаДокумента,
	|	ТИПЗНАЧЕНИЯ(СтатусыПроверокРНПТДокументов.Документ) КАК ДокументТип,
	|	СтатусыПроверокРНПТДокументов.ОписаниеОшибки КАК ОписаниеОшибки
	|ИЗ
	|	РегистрСведений.СтатусыПроверокРНПТДокументов КАК СтатусыПроверокРНПТДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО СтатусыПроверокРНПТДокументов.Организация = ДанныеПервичныхДокументов.Организация
	|			И СтатусыПроверокРНПТДокументов.Документ = ДанныеПервичныхДокументов.Документ
	|ГДЕ
	|	ВЫБОР
	|			КОГДА СтатусыПроверокРНПТДокументов.Документ ССЫЛКА Документ.ПоступлениеТоваровУслуг
	|				ТОГДА ВЫРАЗИТЬ(СтатусыПроверокРНПТДокументов.Документ КАК Документ.ПоступлениеТоваровУслуг).Дата
	|			КОГДА СтатусыПроверокРНПТДокументов.Документ ССЫЛКА Документ.РеализацияТоваровУслуг
	|				ТОГДА ВЫРАЗИТЬ(СтатусыПроверокРНПТДокументов.Документ КАК Документ.РеализацияТоваровУслуг).Дата
	|			ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|		КОНЕЦ >= &НачалоПериода
	|	И ВЫБОР
	|			КОГДА &КонецПериода = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ИСТИНА
	|			КОГДА СтатусыПроверокРНПТДокументов.Документ ССЫЛКА Документ.ПоступлениеТоваровУслуг
	|				ТОГДА ВЫРАЗИТЬ(СтатусыПроверокРНПТДокументов.Документ КАК Документ.ПоступлениеТоваровУслуг).Дата
	|			КОГДА СтатусыПроверокРНПТДокументов.Документ ССЫЛКА Документ.РеализацияТоваровУслуг
	|				ТОГДА ВЫРАЗИТЬ(СтатусыПроверокРНПТДокументов.Документ КАК Документ.РеализацияТоваровУслуг).Дата
	|			ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|		КОНЕЦ <= &КонецПериода
	|	И ВЫБОР
	|			КОГДА &НастройкаОтображенияДокументов = 0
	|				ТОГДА ИСТИНА
	|			КОГДА &НастройкаОтображенияДокументов = 1
	|				ТОГДА СтатусыПроверокРНПТДокументов.СтатусПроверки = ЗНАЧЕНИЕ(Перечисление.СтатусыПроверкиРНПТ.НекорректныйРНПТ)
	|			ИНАЧЕ СтатусыПроверокРНПТДокументов.СтатусПроверки = ЗНАЧЕНИЕ(Перечисление.СтатусыПроверкиРНПТ.Проверено)
	|		КОНЕЦ
	|	И ДанныеПервичныхДокументов.Организация В(&Организация)
	|	И СтатусыПроверокРНПТДокументов.Контрагент = &Контрагент
	|	И &ОтборВходящийДокумент
	|	И ВЫБОР
	|			КОГДА СтатусыПроверокРНПТДокументов.Документ ССЫЛКА Документ.ПоступлениеТоваровУслуг
	|				ТОГДА ВЫРАЗИТЬ(СтатусыПроверокРНПТДокументов.Документ КАК Документ.ПоступлениеТоваровУслуг).Проведен
	|			КОГДА СтатусыПроверокРНПТДокументов.Документ ССЫЛКА Документ.РеализацияТоваровУслуг
	|				ТОГДА ВЫРАЗИТЬ(СтатусыПроверокРНПТДокументов.Документ КАК Документ.РеализацияТоваровУслуг).Проведен
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Контрагент,
	|	Документ";
	
	Если Не ЗначениеЗаполнено(ПараметрыПоискаДокументов.Организация) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДанныеПервичныхДокументов.Организация В (&Организация)", "ИСТИНА");
	КонецЕсли;
	
	// Отбор передаем из списка документов.
	Если ПараметрыПоискаДокументов.Свойство("Контрагент")
		И ЗначениеЗаполнено(ПараметрыПоискаДокументов.Контрагент) Тогда
		Запрос.УстановитьПараметр("Контрагент", ПараметрыПоискаДокументов.Контрагент);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СтатусыПроверокРНПТДокументов.Контрагент = &Контрагент", "ИСТИНА");
	КонецЕсли;
	
	Если ПараметрыПоискаДокументов.Свойство("ЭтоВходящийДокумент") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборВходящийДокумент", 
		"ВЫБОР
		|			КОГДА СтатусыПроверокРНПТДокументов.Документ ССЫЛКА Документ.ПоступлениеТоваровУслуг
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ = &ЭтоВходящийДокумент");
		Запрос.УстановитьПараметр("ЭтоВходящийДокумент", ПараметрыПоискаДокументов.ЭтоВходящийДокумент);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборВходящийДокумент", "ИСТИНА");
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Добавляет дополнительные поля для проверки РНПТ.
//
// Параметры:
//  ТаблицаДанныеРНПТ - ТаблицаЗначений - таблица, которую необходимо дополнить реквизитами для проверки
//
Процедура ДобавитьДополнительныеКолонкиДляПроверки(ТаблицаДанныеРНПТ) Экспорт
	
	// Дополнительно сверяем код ТНВЭД и код страны происхождения
	ТаблицаДанныеРНПТ.Колонки.Добавить("КодТНВЭД", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(10)));
	
КонецПроцедуры

// Заполняет дополнительные поля для проверки нужными значениями
//
// Параметры:
//  ДанныеРНПТ - ТаблицаЗначений - см.ПроверкаРНПТ.НовыйТаблицаДанныхРНПТ()
//  Параметры - ФормаКлиентскогоПриложения или Структура с полями для заполнения дополнительных колонок для проверки.
//
Процедура ЗаполнитьДополнительныеКолонкиДляПроверки(ДанныеРНПТ, Параметры) Экспорт
	
	Если ТипЗнч(Параметры) = Тип("ФормаКлиентскогоПриложения") Тогда
		
		ДокументОбъект = Параметры.Объект;
		Если Не ДокументОбъект.Свойство("Товары") 
			И Не ДокументОбъект.Свойство("Оборудование") 
			И Не ДокументОбъект.Свойство("ОсновныеСредства")
			И Не ДокументОбъект.Свойство("Услуги") Тогда
			Возврат;
		КонецЕсли;
		
		МассивИдентификаторов = ОбщегоНазначенияБпВызовСервера.УдалитьПовторяющиесяЭлементыМассива(
			ДанныеРНПТ.ВыгрузитьКолонку("ИдентификаторСтроки"));
		ТаблицаДопРеквизитовПоИдентификатору = Новый ТаблицаЗначений;
		ТаблицаДопРеквизитовПоИдентификатору.Колонки.Добавить("ИдентификаторСтроки");
		ТаблицаДопРеквизитовПоИдентификатору.Колонки.Добавить("Номенклатура");
		
		Для Каждого ИдентификаторСтроки Из МассивИдентификаторов Цикл
			Если ДокументОбъект.Свойство("Товары") Тогда
				ДополнитьТаблицуДопРеквизитов(
					ТаблицаДопРеквизитовПоИдентификатору, ДокументОбъект, ИдентификаторСтроки, "Товары");
			КонецЕсли;
			Если ДокументОбъект.Свойство("Оборудование") Тогда
				ДополнитьТаблицуДопРеквизитов(
					ТаблицаДопРеквизитовПоИдентификатору, ДокументОбъект, ИдентификаторСтроки, "Оборудование");
			КонецЕсли;
			Если ДокументОбъект.Свойство("ОсновныеСредства") Тогда
				ДополнитьТаблицуДопРеквизитов(
				ТаблицаДопРеквизитовПоИдентификатору, ДокументОбъект, ИдентификаторСтроки, "ОсновныеСредства");
			КонецЕсли;
			Если ДокументОбъект.Свойство("Услуги") Тогда
				ДополнитьТаблицуДопРеквизитов(
				ТаблицаДопРеквизитовПоИдентификатору, ДокументОбъект, ИдентификаторСтроки, "СведенияПрослеживаемости");
			КонецЕсли;
		КонецЦикла;
		ТаблицаДопРеквизитовПоИдентификатору.Свернуть("ИдентификаторСтроки,Номенклатура");
		Номенклатура = ОбщегоНазначенияБпВызовСервера.УдалитьПовторяющиесяЭлементыМассива(
			ТаблицаДопРеквизитовПоИдентификатору.ВыгрузитьКолонку("Номенклатура"));
		ТНВЭДы = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Номенклатура, "КодТНВЭД");
		Для Каждого СтрокаТаблицыДопРеквизитов Из ТаблицаДопРеквизитовПоИдентификатору Цикл
			ИскомыеСтроки = ДанныеРНПТ.НайтиСтроки(
				Новый Структура("ИдентификаторСтроки", СтрокаТаблицыДопРеквизитов.ИдентификаторСтроки));
			Для Каждого НайденнаяСтрока Из ИскомыеСтроки Цикл
				Если Не ЗначениеЗаполнено(НайденнаяСтрока.КодТНВЭД) Тогда
					НайденнаяСтрока.КодТНВЭД               = Строка(ТНВЭДы[СтрокаТаблицыДопРеквизитов.Номенклатура]);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
	Иначе
		ТНВЭД = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Номенклатура, "КодТНВЭД");
		ДанныеРНПТ.ЗаполнитьЗначения(ТНВЭД, "КодТНВЭД");
	КонецЕсли;

КонецПроцедуры

// Проверяет дополнительные колонки РНПТ.
//
// Параметры:
//  ПроверяемаяСтрока - Структура - строка с доп. полями по данным документа.
//  ЭталоннаяСтрока - Структура с доп. полями по данным ФНС.
//  ТолькоТекстОшибки - Булево - признак, указывающий, что процедура вызывается только для формирования текста ошибки.
//
Процедура ПроверитьДополнительныеКолонки(ПроверяемаяСтрока, ЭталоннаяСтрока, ТолькоТекстОшибки = Ложь) Экспорт
	
	Если Не ЗначениеЗаполнено(ПроверяемаяСтрока.КодТНВЭД)
		ИЛИ Не ЗначениеЗаполнено(ЭталоннаяСтрока.КодТНВЭД) Тогда
		// Не считаем ошибкой несовпадение ТН ВЭД если в программе или по данным ФНС он не заполнен.
		Возврат;
	КонецЕсли;
	
	// Сверяем первые 4 цифры ТН ВЭД, определяющие категорию товара.
	КатегорияТовараВПрограмме = Лев(ПроверяемаяСтрока.КодТНВЭД, 4);
	КатегорияТовараФНС        = Лев( ЭталоннаяСтрока.КодТНВЭД, 4);
	Если КатегорияТовараВПрограмме <> КатегорияТовараФНС Тогда
		ОшибкаТНВЭД = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'По данным ФНС ТН ВЭД %1'"),
			ЭталоннаяСтрока.КодТНВЭД);
		ПроверяемаяСтрока.ОписаниеСостояния = 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1%2%3'"),
			ПроверяемаяСтрока.ОписаниеСостояния,
			?(ЗначениеЗаполнено(ПроверяемаяСтрока.ОписаниеСостояния), ";", ""),
			ОшибкаТНВЭД);
		Если ТолькоТекстОшибки Тогда
			Возврат;
		КонецЕсли;
		ПроверяемаяСтрока.Ошибка = Истина;
		ПроверяемаяСтрока.СписокОшибок.Добавить("КодТНВЭД");
		// В пояснении показываем только подсказку по первой ошибке.
		Если НЕ ЗначениеЗаполнено(ПроверяемаяСтрока.Пояснение) Тогда
			ПроверяемаяСтрока.Пояснение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'в программе %1 - по данным ФНС %2'"), ПроверяемаяСтрока.КодТНВЭД, ЭталоннаяСтрока.КодТНВЭД);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Проверяет соответствует ли код РНПТ формату.
//
// Параметры:
//  КодРНПТ - Строка - проверяемый код
// Возвращаемое значение - Булево - истина если соответствует, ложь если не соответствует.
//
Функция РНПТСоответствуетФормату(КодРНПТ) Экспорт
	
	Возврат Справочники.НомераГТД.ЭтоРНПТ(КодРНПТ);
	
КонецФункции

// Возвращает для документа какие табличные части могут быть с прослеживаемым товаром.
//
// Параметры:
//  ДокументОбъект - проверяемы документ
// Возвращаемое значение - Массив - наименование табличных частей с прослеживаемостью
//
Функция ТабличныеЧастиДокументаСПрослеживаемостью(ДокументОбъект) Экспорт
	
	ТабличныеЧастиДокументаСПрослеживаемостью = Новый Массив;
	Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПоступлениеТоваровУслуг") Тогда
		Если ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары
			ИЛИ ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия Тогда
			ТабличныеЧастиДокументаСПрослеживаемостью.Добавить("Товары");
		ИначеЕсли ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОсновныеСредства Тогда
			ТабличныеЧастиДокументаСПрослеживаемостью.Добавить("ОсновныеСредства");
		ИначеЕсли ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Оборудование Тогда 
			ТабличныеЧастиДокументаСПрослеживаемостью.Добавить("Оборудование");
		КонецЕсли;
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.РеализацияТоваровУслуг") Тогда 
		Если ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.Услуги Тогда
			ТабличныеЧастиДокументаСПрослеживаемостью.Добавить("Услуги");
		Иначе
			ТабличныеЧастиДокументаСПрослеживаемостью.Добавить("Товары");
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТабличныеЧастиДокументаСПрослеживаемостью;
	
КонецФункции

// Возвращает текст запроса для проверки РНПТ документов регламентным заданием.
//
// Возвращаемое значение - Строка - текст запроса
//
Функция ТекстЗапросаПроверкиРНПТРегЗаданием() Экспорт
	
	// Отбираем для обработки порции по 500 документов.
	Возврат 
	"ВЫБРАТЬ ПЕРВЫЕ 500
	|	СтатусыПроверокРНПТДокументов.Документ КАК Документ
	|ПОМЕСТИТЬ ДокументыКПроверке
	|ИЗ
	|	РегистрСведений.СтатусыПроверокРНПТДокументов КАК СтатусыПроверокРНПТДокументов
	|ГДЕ
	|	(СтатусыПроверокРНПТДокументов.СтатусПроверки = ЗНАЧЕНИЕ(Перечисление.СтатусыПроверкиРНПТ.ПустаяСсылка)
	|			ИЛИ СтатусыПроверокРНПТДокументов.СтатусПроверки = ЗНАЧЕНИЕ(Перечисление.СтатусыПроверкиРНПТ.Выполняется))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ДокументыСРНПТБезСведений
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.СведенияПрослеживаемости КАК ПоступлениеТоваровУслугСведенияПрослеживаемости
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОРНПТИзФНС КАК СведенияОРНПТИзФНС
	|		ПО ПоступлениеТоваровУслугСведенияПрослеживаемости.РНПТ = СведенияОРНПТИзФНС.РНПТ
	|ГДЕ
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.Ссылка В
	|			(ВЫБРАТЬ
	|				ДокументыКПроверке.Документ
	|			ИЗ
	|				ДокументыКПроверке)
	|	И ЕСТЬNULL(СведенияОРНПТИзФНС.ДатаПолученияСведений, ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.СведенияПрослеживаемости КАК РеализацияТоваровУслугСведенияПрослеживаемости
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОРНПТИзФНС КАК СведенияОРНПТИзФНС
	|		ПО РеализацияТоваровУслугСведенияПрослеживаемости.РНПТ = СведенияОРНПТИзФНС.РНПТ
	|ГДЕ
	|	РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка В
	|			(ВЫБРАТЬ
	|				ДокументыКПроверке.Документ
	|			ИЗ
	|				ДокументыКПроверке)
	|	И ЕСТЬNULL(СведенияОРНПТИзФНС.ДатаПолученияСведений, ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1)
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыКПроверке.Документ КАК Документ
	|ПОМЕСТИТЬ ДокументыКПроверкеРегЗаданием
	|ИЗ
	|	ДокументыКПроверке КАК ДокументыКПроверке
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДокументыСРНПТБезСведений КАК ДокументыСРНПТБезСведений
	|		ПО ДокументыКПроверке.Документ = ДокументыСРНПТБезСведений.Ссылка
	|ГДЕ
	|	ДокументыСРНПТБезСведений.Ссылка ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.Ссылка КАК Ссылка,
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.РНПТ КАК РНПТ,
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслугСведенияПрослеживаемости.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ВЫБОР
	|					КОГДА НЕ ПоступлениеТоваровУслугТовары.Номенклатура ЕСТЬ NULL
	|						ТОГДА ПоступлениеТоваровУслугТовары.Номенклатура.КодТНВЭД.Код
	|					ИНАЧЕ ЕСТЬNULL(ПоступлениеТоваровУслугОборудование.Номенклатура.КодТНВЭД.Код, ПоступлениеТоваровУслугОсновныеСредства.ОсновноеСредство.КодТНВЭД.Код)
	|				КОНЕЦ
	|		ИНАЧЕ ПоступлениеТоваровУслугСведенияПрослеживаемости.Номенклатура.КодТНВЭД.Код
	|	КОНЕЦ КАК КодТНВЭД,
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.Ссылка.Организация КАК Организация,
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.Ссылка.Контрагент КАК Контрагент,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЕСТЬNULL(СведенияОРНПТИзФНС.ДатаПолученияСведений, ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК НетСведений
	|ПОМЕСТИТЬ РНПТДокументовКПроверке
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.СведенияПрослеживаемости КАК ПоступлениеТоваровУслугСведенияПрослеживаемости
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
	|		ПО ПоступлениеТоваровУслугСведенияПрослеживаемости.Ссылка = ПоступлениеТоваровУслугТовары.Ссылка
	|			И ПоступлениеТоваровУслугСведенияПрослеживаемости.ИдентификаторСтроки = ПоступлениеТоваровУслугТовары.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Оборудование КАК ПоступлениеТоваровУслугОборудование
	|		ПО ПоступлениеТоваровУслугСведенияПрослеживаемости.Ссылка = ПоступлениеТоваровУслугОборудование.Ссылка
	|			И ПоступлениеТоваровУслугСведенияПрослеживаемости.ИдентификаторСтроки = ПоступлениеТоваровУслугОборудование.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.ОсновныеСредства КАК ПоступлениеТоваровУслугОсновныеСредства
	|		ПО ПоступлениеТоваровУслугСведенияПрослеживаемости.Ссылка = ПоступлениеТоваровУслугОсновныеСредства.Ссылка
	|			И ПоступлениеТоваровУслугСведенияПрослеживаемости.ИдентификаторСтроки = ПоступлениеТоваровУслугОсновныеСредства.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОРНПТИзФНС КАК СведенияОРНПТИзФНС
	|		ПО ПоступлениеТоваровУслугСведенияПрослеживаемости.РНПТ = СведенияОРНПТИзФНС.РНПТ
	|ГДЕ
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.Ссылка В
	|			(ВЫБРАТЬ
	|				ДокументыКПроверкеРегЗаданием.Документ
	|			ИЗ
	|				ДокументыКПроверкеРегЗаданием)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.Ссылка,
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.РНПТ,
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.КоличествоПрослеживаемости,
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.Ссылка.Организация,
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.Ссылка.Контрагент,
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслугСведенияПрослеживаемости.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ВЫБОР
	|					КОГДА НЕ ПоступлениеТоваровУслугТовары.Номенклатура ЕСТЬ NULL
	|						ТОГДА ПоступлениеТоваровУслугТовары.Номенклатура.КодТНВЭД.Код
	|					ИНАЧЕ ЕСТЬNULL(ПоступлениеТоваровУслугОборудование.Номенклатура.КодТНВЭД.Код, ПоступлениеТоваровУслугОсновныеСредства.ОсновноеСредство.КодТНВЭД.Код)
	|				КОНЕЦ
	|		ИНАЧЕ ПоступлениеТоваровУслугСведенияПрослеживаемости.Номенклатура.КодТНВЭД.Код
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.РНПТ,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.КоличествоПрослеживаемости,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслугСведенияПрослеживаемости.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА РеализацияТоваровУслугТовары.Номенклатура.КодТНВЭД.Код
	|		ИНАЧЕ РеализацияТоваровУслугСведенияПрослеживаемости.Номенклатура.КодТНВЭД.Код
	|	КОНЕЦ,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка.Организация,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка.Контрагент,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЕСТЬNULL(СведенияОРНПТИзФНС.ДатаПолученияСведений, ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ)
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.СведенияПрослеживаемости КАК РеализацияТоваровУслугСведенияПрослеживаемости
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|		ПО РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка = РеализацияТоваровУслугТовары.Ссылка
	|			И РеализацияТоваровУслугСведенияПрослеживаемости.ИдентификаторСтроки = РеализацияТоваровУслугТовары.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОРНПТИзФНС КАК СведенияОРНПТИзФНС
	|		ПО РеализацияТоваровУслугСведенияПрослеживаемости.РНПТ = СведенияОРНПТИзФНС.РНПТ
	|ГДЕ
	|	РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка В
	|			(ВЫБРАТЬ
	|				ДокументыКПроверкеРегЗаданием.Документ
	|			ИЗ
	|				ДокументыКПроверкеРегЗаданием)
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.РНПТ,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.КоличествоПрослеживаемости,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка.Организация,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка.Контрагент,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслугСведенияПрослеживаемости.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА РеализацияТоваровУслугТовары.Номенклатура.КодТНВЭД.Код
	|		ИНАЧЕ РеализацияТоваровУслугСведенияПрослеживаемости.Номенклатура.КодТНВЭД.Код
	|	КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	РНПТ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РНПТДокументовКПроверке.Ссылка КАК Ссылка,
	|	РНПТДокументовКПроверке.РНПТ КАК РНПТ,
	|	РНПТДокументовКПроверке.Организация КАК Организация,
	|	РНПТДокументовКПроверке.Контрагент КАК Контрагент,
	|	СведенияОРНПТИзФНС.ОшибкаПроверкиРНПТ КАК ОшибкаПроверкиРНПТ,
	|	СведенияОРНПТИзФНС.ОписаниеОшибки КАК ОписаниеОшибки,
	|	ЕСТЬNULL(КлассификаторЕдиницИзмерения.Наименование, СведенияОРНПТИзФНС.ЕдиницаИзмеренияНаименование) КАК ЕдиницаИзмерения,
	|	СведенияОРНПТИзФНС.КодТНВЭД КАК ТНВЭДФНС,
	|	СведенияОРНПТИзФНС.КоличествоТовараВвезенного КАК КоличествоФНС,
	|	РНПТДокументовКПроверке.КодТНВЭД КАК ТНВЭДДокумент,
	|	РНПТДокументовКПроверке.КоличествоПрослеживаемости КАК КоличествоДокумент
	|ИЗ
	|	РНПТДокументовКПроверке КАК РНПТДокументовКПроверке
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОРНПТИзФНС КАК СведенияОРНПТИзФНС
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторЕдиницИзмерения КАК КлассификаторЕдиницИзмерения
	|			ПО СведенияОРНПТИзФНС.ЕдиницаИзмеренияКод = КлассификаторЕдиницИзмерения.Код
	|		ПО РНПТДокументовКПроверке.РНПТ = СведенияОРНПТИзФНС.РНПТ
	|
	|СГРУППИРОВАТЬ ПО
	|	РНПТДокументовКПроверке.Ссылка,
	|	РНПТДокументовКПроверке.РНПТ,
	|	РНПТДокументовКПроверке.Организация,
	|	РНПТДокументовКПроверке.Контрагент,
	|	СведенияОРНПТИзФНС.ОшибкаПроверкиРНПТ,
	|	СведенияОРНПТИзФНС.ОписаниеОшибки,
	|	СведенияОРНПТИзФНС.КодТНВЭД,
	|	СведенияОРНПТИзФНС.КоличествоТовараВвезенного,
	|	РНПТДокументовКПроверке.КодТНВЭД,
	|	РНПТДокументовКПроверке.КоличествоПрослеживаемости,
	|	ЕСТЬNULL(КлассификаторЕдиницИзмерения.Наименование, СведенияОРНПТИзФНС.ЕдиницаИзмеренияНаименование)
	|ИТОГИ
	|	МАКСИМУМ(Организация),
	|	МАКСИМУМ(Контрагент),
	|	МАКСИМУМ(ОшибкаПроверкиРНПТ),
	|	МАКСИМУМ(ОписаниеОшибки),
	|	МАКСИМУМ(ЕдиницаИзмерения),
	|	МАКСИМУМ(ТНВЭДФНС),
	|	МАКСИМУМ(КоличествоФНС),
	|	МАКСИМУМ(ТНВЭДДокумент),
	|	МАКСИМУМ(КоличествоДокумент)
	|ПО
	|	Ссылка,
	|	РНПТ";
	
КонецФункции

// Меняет текст запроса для получения сведений о РНПТ из ФНС
// Параметры:
//  Запрос - запрос, текст которого необходимо переопределить.
//
Процедура ПереопределитьТекстЗапросаДляПолученияСведенийОРНПТ(Запрос) Экспорт
	
	// Дополнительно получаем сведения по РНПТ из документов, по которым нет записей в регистре "Сведения о РНПТ из ФНС".
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтатусыПроверокРНПТДокументов.Документ КАК Документ
	|ПОМЕСТИТЬ ДокументыКПроверке
	|ИЗ
	|	РегистрСведений.СтатусыПроверокРНПТДокументов КАК СтатусыПроверокРНПТДокументов
	|ГДЕ
	|	(СтатусыПроверокРНПТДокументов.СтатусПроверки = ЗНАЧЕНИЕ(Перечисление.СтатусыПроверкиРНПТ.ПустаяСсылка)
	|			ИЛИ СтатусыПроверокРНПТДокументов.СтатусПроверки = ЗНАЧЕНИЕ(Перечисление.СтатусыПроверкиРНПТ.Выполняется))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СведенияОРНПТИзФНС.РНПТ КАК РНПТ,
	|	СведенияОРНПТИзФНС.КодРНПТ КАК КодРНПТ,
	|	СведенияОРНПТИзФНС.ДатаПолученияСведений КАК ДатаПолученияСведений
	|ПОМЕСТИТЬ ТаблицаРНПТПредварительная
	|ИЗ
	|	РегистрСведений.СведенияОРНПТИзФНС КАК СведенияОРНПТИзФНС
	|ГДЕ
	|	СведенияОРНПТИзФНС.ДатаПолученияСведений = ДАТАВРЕМЯ(1, 1, 1)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.РНПТ,
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.РНПТ.Код,
	|	ДАТАВРЕМЯ(1, 1, 1)
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.СведенияПрослеживаемости КАК ПоступлениеТоваровУслугСведенияПрослеживаемости
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОРНПТИзФНС КАК СведенияОРНПТИзФНС
	|		ПО ПоступлениеТоваровУслугСведенияПрослеживаемости.РНПТ = СведенияОРНПТИзФНС.РНПТ
	|ГДЕ
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.Ссылка В
	|			(ВЫБРАТЬ
	|				ДокументыКПроверке.Документ
	|			ИЗ
	|				ДокументыКПроверке)
	|	И СведенияОРНПТИзФНС.ДатаПолученияСведений ЕСТЬ NULL
	|	И ПоступлениеТоваровУслугСведенияПрослеживаемости.РНПТ <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеализацияТоваровУслугСведенияПрослеживаемости.РНПТ,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.РНПТ.Код,
	|	ДАТАВРЕМЯ(1, 1, 1)
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.СведенияПрослеживаемости КАК РеализацияТоваровУслугСведенияПрослеживаемости
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОРНПТИзФНС КАК СведенияОРНПТИзФНС
	|		ПО РеализацияТоваровУслугСведенияПрослеживаемости.РНПТ = СведенияОРНПТИзФНС.РНПТ
	|ГДЕ
	|	РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка В
	|			(ВЫБРАТЬ
	|				ДокументыКПроверке.Документ
	|			ИЗ
	|				ДокументыКПроверке)
	|	И СведенияОРНПТИзФНС.ДатаПолученияСведений ЕСТЬ NULL
	|	И РеализацияТоваровУслугСведенияПрослеживаемости.РНПТ <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 500
	|	ТаблицаРНПТПредварительная.РНПТ КАК РНПТ,
	|	ТаблицаРНПТПредварительная.КодРНПТ КАК КодРНПТ,
	|	ТаблицаРНПТПредварительная.ДатаПолученияСведений КАК ДатаПолученияСведений
	|ИЗ
	|	ТаблицаРНПТПредварительная КАК ТаблицаРНПТПредварительная";
	
КонецПроцедуры

// Возвращает тип справочника РНПТ.
//
// Возвращаемое значение - Тип - тип справочника РНПТ
//
Функция ТипСправочникаРНПТ() Экспорт
	
	Возврат Тип("СправочникСсылка.НомераГТД");
	
КонецФункции

// Ищет элемент справочника РНПТ по коду.
// 
// Параметры:
// КодРНПТ - строка - код РНПТ элемента справочника.
// 
// Возвращаемое значение - СправочникСсылка - ссылка на элемент справочника РНПТ
//
Функция РНПТПоКоду(КодРНПТ) Экспорт
	
	Возврат Справочники.НомераГТД.НайтиПоКоду(КодРНПТ);
	
КонецФункции

// Возвращает текст запроса для получения кода РНПТ.
//
// Возвращаемое значение - Строка - текст запроса
//
Функция ТекстПолученияКодаРНПТВЗапросе() Экспорт
	
	Возврат "ВЫРАЗИТЬ(ДанныеРНПТ.РНПТ КАК Справочник.НомераГТД).Код";
	
КонецФункции

// Удаляет повторяющиеся элементы массива.
//
// Параметры:
//  ОбрабатываемыйМассив - Массив - элементы произвольных типов, из которых удаляются неуникальные.
//
// Возвращаемое значение:
//   Массив      - элементы ОбрабатываемыйМассив после удаления лишних.
//
Функция УдалитьПовторяющиесяЭлементыМассива(ОбрабатываемыйМассив) Экспорт
	Возврат ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(ОбрабатываемыйМассив);
КонецФункции

// Проверяет есть ли прослеживаемые товары в документе
//
// Параметры:
// ДокументОбъект - документ, который требуется проверить
//
// Возвращаемое значение - Булево - признак того, что в документе есть прослеживаемые товары
//
Функция ЕстьПрослеживаемыеТовары(ДокументОбъект) Экспорт
	
	Возврат ДокументОбъект.СведенияПрослеживаемости.Количество() > 0;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДополнитьТаблицуДопРеквизитов(ТаблицаДопРеквизитовПоИдентификатору, ДокументОбъект, ИдентификаторСтроки, ИмяТабличнойЧасти)
	
	ИскомыеТовары = ДокументОбъект[ИмяТабличнойЧасти].НайтиСтроки(
		Новый Структура("ИдентификаторСтроки", ИдентификаторСтроки));
	Если ИскомыеТовары.Количество() <> 0 Тогда
		СтрокаТаблицыДопРеквизитов = ТаблицаДопРеквизитовПоИдентификатору.Добавить();
		СтрокаТаблицыДопРеквизитов.ИдентификаторСтроки = ИдентификаторСтроки;
		Если ИмяТабличнойЧасти = "ОсновныеСредства" Тогда
			СтрокаТаблицыДопРеквизитов.Номенклатура = ИскомыеТовары[0].ОсновноеСредство;
		Иначе
			СтрокаТаблицыДопРеквизитов.Номенклатура = ИскомыеТовары[0].Номенклатура;
		КонецЕсли;
	Конецесли;
	
КонецПроцедуры

#КонецОбласти