#Область СлужебныйПрограммныйИнтерфейс

#Область УпаковкиЕдиницыИзмерения

// Возвращает единицу измерения, указанную в константе ЕдиницаИзмеренияКилограммИС.
//
// Возвращаемое значение:
// 	ОпределяемыйТип.Упаковка
// 
Функция ЕдиницаИзмеренияКилограмм() Экспорт
	
	ЕдиницаИзмеренияКилограмм = ИнтеграцияИСВызовСервера.ЕдиницаИзмеренияКилограмм();
	Если Не ЗначениеЗаполнено(ЕдиницаИзмеренияКилограмм) Тогда
		ВызватьИсключение НСтр("ru = 'Не указана единица измерения для учета ""Килограмм"".
			|Перейдите в панель администрирования и укажите единицу измерения ""Килограмм"".'");
	КонецЕсли;
	
	Возврат ЕдиницаИзмеренияКилограмм;
	
КонецФункции

// Возвращает единицу измерения, указанную в константе ЕдиницаИзмеренияЛитрИС.
//
// Возвращаемое значение:
// 	ОпределяемыйТип.Упаковка
// 
Функция ЕдиницаИзмеренияЛитр() Экспорт
	
	ЕдиницаИзмеренияЛитр = ИнтеграцияИСВызовСервера.ЕдиницаИзмеренияЛитр();
	Если Не ЗначениеЗаполнено(ЕдиницаИзмеренияЛитр) Тогда
		ВызватьИсключение НСтр("ru = 'Не указана единица измерения для учета ""Литр"".
			|Перейдите в панель администрирования и укажите единицу измерения ""Литр"".'");
	КонецЕсли;
	
	Возврат ЕдиницаИзмеренияЛитр;
	
КонецФункции

Функция ПустоеЗначениеУпаковки() Экспорт
	
	Возврат ИнтеграцияИСВызовСервера.ПустоеЗначениеУпаковки();
	
КонецФункции

#КонецОбласти

#Область ЧастичноеВыбытие

// Возвращает признак возможности для вида продукции участвовать в частичном выбытии.
// 
// Параметры:
//  ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС                - вид маркируемой продукции.
//  ОперацияИС   - ПеречислениеСсылка.ВидыОперацийИСМП, ПеречислениеСсылка.ВидыДокументовЕГАИС, Неопределено - вид операции ИС.
// Возвращаемое значение:
//  Булево - Вид продукци может выбывать частично.
Функция ПоддерживаетсяЧастичноеВыбытие(ВидПродукции, ОперацияИС = Неопределено) Экспорт
	
	Возврат ИнтеграцияИСВызовСервера.ПоддерживаетсяЧастичноеВыбытие(ВидПродукции, ОперацияИС);
	
КонецФункции

Функция ЗапрашиватьДанныеЧастичногоВыбытияИзСервиса(ВидПродукции) Экспорт
	
	Если ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Алкогольная") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат ИнтеграцияИСМПКлиентСерверПовтИсп.ЗапрашиватьДанныеСервиса();
	
КонецФункции

Функция ЗаголовокРежимаВыбытияПоВидуОперации(ОперацияИС) Экспорт
	
	Если ТипЗнч(ОперацияИС) = Тип("ПеречислениеСсылка.ВидыОперацийИСМП") Тогда
		Если ОперацияИС = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВыводИзОборотаРозничнаяПродажа")
			Или ОперацияИС = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ОтгрузкаПродажа") Тогда
			Возврат НСтр("ru = 'Продается'");
		Иначе
			Возврат НСтр("ru = 'Возвращается'");
		КонецЕсли;
	Иначе
		Возврат НСтр("ru = 'Продается'");
	КонецЕсли;
	
КонецФункции

Функция ПредставлениеРежимаВыбытияПоВидуПродукции(РежимВыбытия, ВидПродукции) Экспорт
	
	ВозвращаемоеЗначение = Неопределено;
	
	Если РежимВыбытия = "Полностью" Тогда
		
		Если ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Духи") Тогда
			ВозвращаемоеЗначение = НСтр("ru = 'Флакон'"); 
		ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.АльтернативныйТабак")
			Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.НикотиносодержащаяПродукция") Тогда
			ВозвращаемоеЗначение = НСтр("ru = 'Пачка'");
		ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Пиво") Тогда
			ВозвращаемоеЗначение = НСтр("ru = 'Кега'");
		ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Алкогольная") Тогда
			ВозвращаемоеЗначение = НСтр("ru = 'Бутылка'");
		КонецЕсли;
		
	Иначе
		
		Если ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Духи")
			Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Пиво")
			Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Алкогольная") Тогда
			ВозвращаемоеЗначение = НСтр("ru = 'На разлив'"); 
		ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.АльтернативныйТабак")
			Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.НикотиносодержащаяПродукция") Тогда
			ВозвращаемоеЗначение = НСтр("ru = 'Поштучно'");
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

#КонецОбласти

// Классифицирует текущий сеанс, как сеанс, запущенный в фоновом задании в клиент-серверном варианте, в остальных
// случаях, сеанс имеет ту же файловую систему на стороне сервера, что и основной сеанс.
//	
// Возвращаемое значение:
// 	Булево - Описание
Функция ЭтоФоновоеЗаданиеНаСервере() Экспорт
	Возврат ИнтеграцияИСВызовСервера.ЭтоФоновоеЗаданиеНаСервере();
КонецФункции

// Возвращает признак принадлежности переданного в параметре вида продукции к наборам или групповым упаковкам.
// 
// Параметры:
// 	ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции для анализа.
// Возвращаемое значение:
// 	Булево - Принадлежность к виду продукции ИСМП
//
Функция ВидПродукцииИспользуетНаборыИлиГрупповыеУпаковки(ВидПродукции, ВидУпаковки = Неопределено) Экспорт
	
	Если ВидУпаковки = Неопределено Тогда
		Если ИнтеграцияИСКлиентСервер.ВидПродукцииИспользуетНаборы(ВидПродукции) Тогда
			Возврат Истина;
		ИначеЕсли ИнтеграцияИСКлиентСервер.ВидПродукцииИспользуетГрупповыеУпаковки(ВидПродукции) Тогда
			Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	Иначе
		Если ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИС.Групповая")
			И ИнтеграцияИСКлиентСервер.ВидПродукцииИспользуетГрупповыеУпаковки(ВидПродукции) Тогда
			Возврат Истина;
		ИначеЕсли ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИС.Набор")
			И ИнтеграцияИСКлиентСервер.ВидПродукцииИспользуетНаборы(ВидПродукции) Тогда
			Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Функция ИменаПараметровРаботыКлиентаВыборТипаПодписиБСП() Экспорт
	
	ВозвращаемоеЗначение = Новый Структура();
	
	ВозвращаемоеЗначение.Вставить("ИмяПараметраПриложения", "ГОСИС_ВыборТипаПодписиБСП");
	ВозвращаемоеЗначение.Вставить(
		"ЗначениеПараметраВыборТипаПодписиНеПоддерживается",
		"ИнтеграцияГОСИС.ВыборТипаПодписиБСП.ВыборТипаПодписиНеПоддерживается");
	ВозвращаемоеЗначение.Вставить(
		"ЗначениеПараметраВыборТипаПодписиИспользуетКонструктор",
		"ИнтеграцияГОСИС.ВыборТипаПодписиБСП.ВыборТипаПодписиИспользуетКонструктор");
	ВозвращаемоеЗначение.Вставить(
		"ЗначениеПараметраВыборТипаПодписиЗадаетсяПеречислением",
		"ИнтеграцияГОСИС.ВыборТипаПодписиБСП.ВыборТипаПодписиЗадаетсяПеречислением");
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ЗемельныйУчастокИСПереопределен() Экспорт
	
	Возврат ИнтеграцияИСВызовСервера.ЗемельныйУчастокИСПереопределен();
	
КонецФункции

#КонецОбласти