#Область СлужебныйПрограммныйИнтерфейс

Процедура ВопросОткрытьГосконтрактЗавершение(Результат, Форма) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗаполнениеГосконтрактаЗавершение", Форма);
		ЭлектронноеАктированиеЕИСКлиент.ОткрытьФормуКонтракта(Форма, Форма.ГосконтрактЕИС, ОповещениеОЗавершении);
	КонецЕсли;
		
КонецПроцедуры

Процедура ВопросЗаполнитьПоГосконтрактуЗавершение(Результат, Форма) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Форма.ЗаполнениеГосконтрактаЗавершение(Неопределено, Неопределено);
	КонецЕсли;
		
КонецПроцедуры

Процедура УслугиСодержаниеПриИзменении(Форма, ТекущиеДанные) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Форма.ГосконтрактЕИС) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОбработки = РеализацияТоваровУслугФормыКлиент.ПодготовитьПараметрыОбработкиУслугиНоменклатураПриИзменении(
		Форма, ТекущиеДанные);
	
	ЭлектронноеАктированиеБПВызовСервера.ЗаполнитьДанныеСтрокиПриИзмененииСодержанияУслуги(
		ПараметрыОбработки.ДанныеСтрокиТаблицы, ПараметрыОбработки.ДанныеОбъекта);
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ПараметрыОбработки.ДанныеСтрокиТаблицы);
	
	ДанныеОбъекта = ПараметрыОбработки.ДанныеОбъекта;
	ТекущиеДанные.Всего = ТекущиеДанные.Сумма + ?(ДанныеОбъекта.СуммаВключаетНДС, 0, ТекущиеДанные.СуммаНДС);
	
КонецПроцедуры

Процедура УслугиСодержаниеПриИзмененииКорректировка(Форма, ТекущиеДанные) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Форма.ГосконтрактЕИС) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСтрокиТаблицы = Новый Структура("Содержание, Количество,
		|Цена, Сумма, СтавкаНДС, СуммаНДС,
		|ИдентификаторСтрокиГосконтрактаЕИС");
	ЗаполнитьЗначенияСвойств(ДанныеСтрокиТаблицы, ТекущиеДанные);
	
	ДанныеОбъекта = Новый Структура("СуммаВключаетНДС, ГосконтрактЕИС");
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Форма.Объект);
	ДанныеОбъекта.ГосконтрактЕИС = Форма.ГосконтрактЕИС;
	
	ЭлектронноеАктированиеБПВызовСервера.ЗаполнитьДанныеСтрокиПриИзмененииСодержанияУслуги(
		ДанныеСтрокиТаблицы, ДанныеОбъекта);
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ДанныеСтрокиТаблицы);
	
	ТекущиеДанные.Всего = ТекущиеДанные.Сумма + ?(ДанныеОбъекта.СуммаВключаетНДС, 0, ТекущиеДанные.СуммаНДС);
	
КонецПроцедуры

Процедура ДоговорКонтрагентаПриИзменении(Форма) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Форма.ГосконтрактЕИС) Тогда
		Возврат;
	КонецЕсли;
	
	Объект = Форма.Объект;
	ДокументЗаполнен = Ложь;
	Если Объект.Свойство("Товары") И Объект.Товары.Количество() > 0 Тогда
		ДокументЗаполнен = Истина;
	КонецЕсли;
	Если Объект.Свойство("Услуги") И Объект.Услуги.Количество() > 0 Тогда
		ДокументЗаполнен = Истина;
	КонецЕсли;
	
	Если Форма.ГосконтрактЕИСНеЗаполнен Тогда
		//Предложить заполнить госконтракт
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ВопросОткрытьГосконтрактЗавершение", ЭлектронноеАктированиеБПКлиент, Форма);
		ТекстПредупреждения   = НСтр("ru = 'Для электронного актирования в карточке госконтракта необходимо заполнить номенклатуру.
		|Открыть карточку госконтракта для заполнения?'");
		
		ПоказатьВопрос(ОповещениеОЗавершении, ТекстПредупреждения, РежимДиалогаВопрос.ДаНет);
		
	ИначеЕсли ДокументЗаполнен Тогда
		//Предложить заполнить по госконтракту
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ВопросЗаполнитьПоГосконтрактуЗавершение", ЭлектронноеАктированиеБПКлиент, Форма);
		ТекстПредупреждения   = НСтр("ru = 'Заполнить документ по данным госконтракта?'");
		
		ПоказатьВопрос(ОповещениеОЗавершении, ТекстПредупреждения, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		Форма.ЗаполнениеГосконтрактаЗавершение(Неопределено, Неопределено);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ЭтоПодборНоменклатурыЕИС(ВыбранноеЗначение) Экспорт
	
	Возврат ТипЗнч(ВыбранноеЗначение) = Тип("Структура")
		И ВыбранноеЗначение.Свойство("ПодборИзГосконтрактаЕИС");
	
КонецФункции

Процедура НоменклатураОбработкаВыбора(ТекущиеДанные, ВыбранноеЗначение, СтандартнаяОбработка) Экспорт
	
	Если Не ЭтоПодборНоменклатурыЕИС(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	Если ВыбранноеЗначение.Свойство("Номенклатура") Тогда
		ТекущиеДанные.Номенклатура = ВыбранноеЗначение.Номенклатура;
	КонецЕсли;
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущиеДанные, "Содержание") Тогда
		ТекущиеДанные.Содержание = ВыбранноеЗначение.Наименование;
	КонецЕсли;
	ТекущиеДанные.ИдентификаторСтрокиГосконтрактаЕИС = ВыбранноеЗначение.ИдентификаторСтрокиГосконтрактаЕИС;
	
КонецПроцедуры

#КонецОбласти
 
