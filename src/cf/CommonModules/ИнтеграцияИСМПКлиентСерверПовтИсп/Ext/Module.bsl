#Область ПрограммныйИнтерфейс

// Возвращает дату обязательной маркировки маркируемой продукци переданного вида.
// 
// Параметры:
//  ВидМаркируемойПродукции - ПеречислениеСсылка.ВидыПродукцииИС - вид маркируемой продукции
// Возвращаемое значение:
// 	Дата - дата обязательной маркировки маркируемой продукции.
//
Функция ДатаОбязательнойМаркировкиПродукции(ВидМаркируемойПродукции) Экспорт
	
	Возврат ИнтеграцияИСМПВызовСервера.ДатаОбязательнойМаркировкиПродукции(ВидМаркируемойПродукции);
	
КонецФункции

// Возвращает признак ведения учета маркируемой продукци переданного вида.
// 
// Параметры:
//  ВидМаркируемойПродукции - ПеречислениеСсылка.ВидыПродукцииИС - вид маркируемой продукции
// Возвращаемое значение:
// 	Булево - признак ведения учета маркируемой продукции переданного вида.
//
Функция ВестиУчетМаркируемойПродукции(ВидМаркируемойПродукции = Неопределено) Экспорт
	
	Возврат ИнтеграцияИСМПВызовСервера.ВестиУчетМаркируемойПродукции(ВидМаркируемойПродукции);
	
КонецФункции

//Возвращает виды маркируемой продукции по которым ведется учет.
// Параметры:
//  ПродукцияИСМП - Булево - Будет добавлена продукция ИСМП
//  ПродукцияМОТП - Булево - Будет добавлена продукция МОТП
//Возвращаемое значение:
//   ФиксированныйМассив Из ПеречислениеСсылка.ВидыПродукцииИС - виды маркируемой продукции.
//
Функция УчитываемыеВидыМаркируемойПродукции(ПродукцияИСМП = Истина, ПродукцияМОТП = Истина) Экспорт
	
	ВидыМаркируемойПродукции = ИнтеграцияИСМПВызовСервера.УчитываемыеВидыМаркируемойПродукции();
	
	Если ПродукцияИСМП И ПродукцияМОТП Тогда
		Возврат ВидыМаркируемойПродукции;
	КонецЕсли;
	
	ВидыПродукции = Новый Массив;
	
	Для Каждого ВидПродукции Из ВидыМаркируемойПродукции Цикл
		Если ПродукцияИСМП И ИнтеграцияИСКлиентСервер.ЭтоПродукцияИСМП(ВидПродукции)
			Или ПродукцияМОТП И ИнтеграцияИСКлиентСервер.ЭтоПродукцияМОТП(ВидПродукции) Тогда
			ВидыПродукции.Добавить(ВидПродукции);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Новый ФиксированныйМассив(ВидыПродукции);
	
КонецФункции

// Проверяет необходимость обязательной регистрации оборота маркируемой продукции переданного вида на переданную дату.
// 
// Параметры:
//  ВидМаркируемойПродукции - ПеречислениеСсылка.ВидыПродукцииИС - вид маркируемой продукции
//  НаДату - Дата - дата оборота маркируемой продукции
// Возвращаемое значение:
//  Булево - Истина, если на переданную дату в системе установлен признак ведения учета по переданному виду маркируемой продукции.
//
Функция ОбязательнаяРегистрацияОборотаМаркируемойПродукции(ВидМаркируемойПродукции, НаДату) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ВидМаркируемойПродукции) Тогда
		Возврат Ложь;
	ИначеЕсли НЕ ИнтеграцияИСМПКлиентСерверПовтИсп.ВестиУчетМаркируемойПродукции(ВидМаркируемойПродукции) Тогда
		Возврат Ложь;
	Иначе
		Возврат НаДату >= ИнтеграцияИСМПКлиентСерверПовтИсп.ДатаОбязательнойМаркировкиПродукции(ВидМаркируемойПродукции);
	КонецЕсли;
	
КонецФункции

// Проверяет что регистрация оборота маркируемой продукции переданного вида производится в тестовом режиме на переданную дату.
// 
// Параметры:
//  ВидМаркируемойПродукции - ПеречислениеСсылка.ВидыПродукцииИС - вид маркируемой продукции
//  НаДату - Дата - дата оборота маркируемой продукции
// Возвращаемое значение:
//  Булево - Истина, если в системе установлен признак ведения учета по переданному виду маркируемой продукции и дата оборота менее даты обязательной регистрации.
//
Функция ТестоваяРегистрацияОборотаМаркируемойПродукции(ВидМаркируемойПродукции, НаДату) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ВидМаркируемойПродукции) Тогда
		Возврат Ложь;
	ИначеЕсли НЕ ИнтеграцияИСМПКлиентСерверПовтИсп.ВестиУчетМаркируемойПродукции(ВидМаркируемойПродукции) Тогда
		Возврат Ложь;
	Иначе
		Возврат НаДату < ИнтеграцияИСМПКлиентСерверПовтИсп.ДатаОбязательнойМаркировкиПродукции(ВидМаркируемойПродукции);
	КонецЕсли;
	
КонецФункции

//Возвращает виды маркируемой продукции в тестовом режиме на переданную дату.
//
//Параметры:
//   НаДату - Дата - дата оборота маркируемой продукции
//
//Возвращаемое значение:
//   ФиксированныйМассив Из ПеречислениеСсылка.ВидыПродукцииИС - виды маркируемой продукции, по которым установлен 
//   признак ведения учета и дата оборота менее даты обязательной регистрации.
//
Функция ВидыПродукцииТестовогоПериода(НаДату) Экспорт
	
	Возврат ИнтеграцияИСМПВызовСервера.УчитываемыеВидыМаркируемойПродукции(НаДату, Истина);
	
КонецФункции

//Возвращает виды маркируемой продукции в тестовом режиме на переданную дату.
//
//Параметры:
//   НаДату - Дата - дата оборота маркируемой продукции
//
//Возвращаемое значение:
//   ФиксированныйМассив Из ПеречислениеСсылка.ВидыПродукцииИС - виды маркируемой продукции, по которым установлен 
//   признак ведения учета и дата оборота менее даты обязательной регистрации.
//
Функция ВидыПродукцииОбязательнойМаркировки(НаДату) Экспорт
	
	Возврат ИнтеграцияИСМПВызовСервера.УчитываемыеВидыМаркируемойПродукции(НаДату, Ложь);
	
КонецФункции

//Возвращает признак запроса данных из сервиса ИС МП.
//
//Возвращаемое значение:
//   Булево - Истина, в случае необходимости запроса данных сервиса.
//
Функция ЗапрашиватьДанныеСервиса() Экспорт
	
	Возврат ИнтеграцияИСМПВызовСервера.ЗапрашиватьДанныеСервиса();

КонецФункции

//Возвращает признак необходимости контроля статусов кодов маркировок ИС МП.
//
//Параметры:
//	 ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции
//	 ВидОперации  - ПеречислениеСсылка.ВидыОперацийИСМП, Неопределено - Вид операции.
//
//Возвращаемое значение:
//   Булево - Истина, в случае необходимости контроля статусов
//
Функция КонтролироватьСтатусыКодовМаркировки(ВидПродукции = Неопределено, ВидОперации = Неопределено) Экспорт
	
	Если Не ЗапрашиватьДанныеСервиса() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВидОперации) Тогда
		ОперацииКонтроля = Новый Массив();
		ОперацииКонтроля.Добавить(ВидОперации);
	Иначе
		ОперацииКонтроля = ОбщегоНазначенияКлиентСервер.РазностьМассивов(
			НастройкаПараметровСканированияСлужебныйКлиентСерверПовтИсп.ДопустимыеВидыОпераций().ВыгрузитьЗначения(),
			НастройкаПараметровСканированияСлужебныйКлиентСерверПовтИсп.ОперацииРозничнойТорговли());
	КонецЕсли;
	
	ОперацияИсключена = НастройкаПараметровСканированияСлужебныйКлиентСервер.ОперацииИсключеныИзКонтроляСтатусовИВладельцев(ОперацииКонтроля, ВидПродукции);
	
	Возврат Не (ОперацияИсключена);
	
КонецФункции

//Возвращает признак необходимости контроля статусов кодов маркировок ИС МП при розничной торговле.
//
//Параметры:
//	 ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции
//	 ВидОперации  - ПеречислениеСсылка.ВидыОперацийИСМП, Неопределено - Вид операции.
//Возвращаемое значение:
//   Булево - Истина, в случае необходимости контроля статусов.
//
Функция КонтролироватьСтатусыКодовМаркировкиВРознице(ВидПродукции = Неопределено, ВидОперации = Неопределено) Экспорт
	
	Если Не ЗапрашиватьДанныеСервиса() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВидОперации) Тогда
		ОперацииКонтроля = Новый Массив();
		ОперацииКонтроля.Добавить(ВидОперации);
	Иначе
		ОперацииКонтроля = НастройкаПараметровСканированияСлужебныйКлиентСерверПовтИсп.ОперацииРозничнойТорговли();
	КонецЕсли;
	
	ОперацияИсключена = НастройкаПараметровСканированияСлужебныйКлиентСервер.ОперацииИсключеныИзКонтроляСтатусовИВладельцев(ОперацииКонтроля, ВидПродукции);
	
	Возврат Не (ОперацияИсключена);
	
КонецФункции

// Возвращает настройки сканирования кодов маркировки ИС МП.
//
// Возвращаемое значение:
//  Булево - Истина, в случае необходимости контроля статусов.
Функция НастройкиСканированияКодовМаркировки() Экспорт
	
	Возврат ИнтеграцияИСМПВызовСервера.НастройкиСканированияКодовМаркировки();

КонецФункции

Функция СлужебныйШтрихкодПечатиУпаковки() Экспорт
	
	Возврат ИнтеграцияИСМПВызовСервера.НастройкиСканированияКодовМаркировки().СлужебныйШтрихкодПечатиУпаковки;
	
КонецФункции

// Определяет признак учета в системе МРЦ.
// 
// Возвращаемое значение:
// 	Булево - Включено ведение учетаю
Функция УчитыватьМРЦ() Экспорт
	
	Возврат ИнтеграцияИСМПКлиентСерверПовтИсп.НастройкиСканированияКодовМаркировки().УчитыватьМРЦ;
	
КонецФункции

// Возвращает признак включения режима работы с тестовым контуром ИС МП
//
// Возвращаемое значение:
//  Булево - Истина, если включен режим работы с тестовым контуром ИС МП.
//
Функция РежимРаботыСТестовымКонтуромИСМП() Экспорт
	
	Возврат ИнтеграцияИСМПВызовСервера.РежимРаботыСТестовымКонтуромИСМП();
	
КонецФункции

// Возвращает признак включения режима проверки товарных групп при розничных продажах
//  методом codes/check в ГИС МТ
// 
// Возвращаемое значение:
//  Булево - Истина, если режим проверки кодов маркировки методом проверки в ГИС МТ кодов с криптохвостами включен
Функция ПроверкаТоварныхГруппПриРозничнойПродажеГИСМТ() Экспорт
	
	// розничный режим начинает действовать согласно датам включения. ИнтеграцияИСМПВызовСервера.СписокУчитываемойПродукцииТребующейОбязательнойПроверкиПриПродажеВРозницуНаДату()
	Возврат Истина;
	
КонецФункции

// Возвращает список продукции, по которой необходима онлайн-проверки при продаже на ККТ
// 
// Возвращаемое значение:
//  Массив из ПеречислениеСсылка.ВидыПродукцииИС- список продукции с обязательными проверками
//   перед продажей на кассе
Функция ВидыПродукцийОбязательнойОнлайнПроверкиПередРозничнойПродажей() Экспорт
	
	ВидыПродукцийСПроверкой = Новый Массив;
	ВидыПродукцийСПроверкой.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.АльтернативныйТабак"));
	ВидыПродукцийСПроверкой.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Антисептики"));
	ВидыПродукцийСПроверкой.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.БАДы"));
	ВидыПродукцийСПроверкой.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.БезалкогольноеПиво"));
	ВидыПродукцийСПроверкой.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Духи"));
	ВидыПродукцийСПроверкой.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ЛегкаяПромышленность"));
	ВидыПродукцийСПроверкой.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.НикотиносодержащаяПродукция"));
	ВидыПродукцийСПроверкой.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МолочнаяПродукцияПодконтрольнаяВЕТИС"));
	ВидыПродукцийСПроверкой.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МолочнаяПродукцияБезВЕТИС"));
	ВидыПродукцийСПроверкой.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Обувь"));
	ВидыПродукцийСПроверкой.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.СоковаяПродукция"));
	ВидыПродукцийСПроверкой.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Пиво"));
	ВидыПродукцийСПроверкой.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Табак"));
	ВидыПродукцийСПроверкой.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.УпакованнаяВода"));
	ВидыПродукцийСПроверкой.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Фотоаппараты"));
	ВидыПродукцийСПроверкой.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Шины"));
	
	Возврат ВидыПродукцийСПроверкой;
	
КонецФункции

// Проверка, подлежит ли продукция обязательной онлайн-проверке перед розничной продажей.
// 
// Параметры:
//  ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции для проверки
//  ВидОперации - Неопределено, ПеречислениеСсылка.ВидыОперацийИСМП - Вид операции для проверки
//  ТребуетсяПроверкаСредствамиККТ - Булево - проверка на версию ФФД 1.2
// 
// Возвращаемое значение:
//  Булево - Продукция подлежит обязательной онлайн проверке перед розничной продажей
Функция ПродукцияПодлежитОбязательнойОнлайнПроверкеПередРозничнойПродажей(ВидПродукции, ВидОперации = Неопределено, ТребуетсяПроверкаСредствамиККТ = Истина) Экспорт

	ПродукцияПодлежитПроверке = Ложь;
	
	Если Не ТребуетсяПроверкаСредствамиККТ Тогда
		Возврат ПродукцияПодлежитПроверке;
	КонецЕсли;
	
	Если ВидОперации = Неопределено Тогда
		ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВыводИзОборотаРозничнаяПродажа");
	ИначеЕсли Не ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВыводИзОборотаРозничнаяПродажа") Тогда
		Возврат ПродукцияПодлежитПроверке;
	КонецЕсли;
	
	МассивПродукцийСПроверкой = ИнтеграцияИСМПВызовСервера.СписокУчитываемойПродукцииТребующейОбязательнойПроверкиПриПродажеВРозницуНаДату();
	ПродукцияПодлежитПроверке = МассивПродукцийСПроверкой.Найти(ВидПродукции) <> Неопределено;
	
	Возврат ПродукцияПодлежитПроверке;

КонецФункции

// Возвращает настройку частоты актуализации и загрузки CDN-площадок
//  для обращения к розничному методу продажи ГИС МТ
//
// Возвращаемое значение:
//  Число - частота обновления, в часах
Функция ЧастотаОбновленияCDNПлощадок() Экспорт
	
	Возврат ИнтеграцияИСМПВызовСервера.НастройкиСканированияКодовМаркировки().ЧастотаОбновленияCDNПлощадок;
	
КонецФункции

// Возвращает настройку времени ожидания ответа от ГИС МТ при пробитии чека
//  при работе с разрешительным режимом
// 
// Возвращаемое значение:
//  Число - время ожидания ответа от CDN-площадок при пробитии чека, в секундах
Функция ВремяОтветаCDNПлощадокПриПробитииЧека() Экспорт
	
	Возврат ИнтеграцияИСМПВызовСервера.НастройкиСканированияКодовМаркировки().ВремяОтветаCDNПлощадокПриПробитииЧека;
	
КонецФункции

// Возвращает настройку времени действия идентификатора разрешительного режима при
//  розничной продаже, после истечение времени идентификатор требуется обновить и переполучить
// 
// Возвращаемое значение:
//  Число - срок действия идентификатора, в секундах
Функция СрокДействияИдентификатораНаПродажуРазрешительногоРежима() Экспорт
	
	Возврат ИнтеграцияИСМПВызовСервера.НастройкиСканированияКодовМаркировки().СрокДействияИдентификатораРазрешительногоРежима;
	
КонецФункции

// Возвращает настройку времени блокировки CDN-площадок при их некорретных ответах
//  при работе с разрешительным режимом
// 
// Возвращаемое значение:
//  Число - время блокировки CDN-площадок, в минутах
Функция ВремяБлокировкиCDNПлощадки() Экспорт
	
	Возврат ИнтеграцияИСМПВызовСервера.НастройкиСканированияКодовМаркировки().ВремяБлокировкиCDNПлощадок;
	
КонецФункции

// Возвращает признак действия аварийного режима при розничных продажах
//  Запросы разрешительного режима не выполняются, их результаты не контролируются
// 
// Возвращаемое значение:
//  Булево - Истина, если аварийный режим включен
Функция ДействуетАварийныйРежимДляРозничныхПродаж() Экспорт
	
	Возврат ИнтеграцияИСМПВызовСервера.ДействуетАварийныйРежимДляРозничныхПродаж();
	
КонецФункции

// Возвращает код ответа, зафиксированный ГИС МТ как код ответа, означающий наступление аварийного режима
// 
// Возвращаемое значение:
//  Число - Код ответа аварийного режима
Функция АварийныйРежимКодОтветаСервисаГИСМТ() Экспорт
	
	Возврат 203;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ЧастичноеВыбытие

// Возвращает признак возможности для вида продукции (и вида операции) участвовать в частичном выбытии.
// 
// Параметры:
//  ВидПродукции    - ПеречислениеСсылка.ВидыПродукцииИС                - вид маркируемой продукции.
//  ВидОперацииИСМП - ПеречислениеСсылка.ВидыОперацийИСМП, Неопределено - вид операции ИСМП.
// Возвращаемое значение:
//  Булево - Вид продукци (в текущей операции) может выбывать частично.
Функция ПоддерживаетсяЧастичноеВыбытие(ВидПродукции, ВидОперацииИСМП = Неопределено) Экспорт
	
	ПоддерживаетсяЧастичноеВыбытие = Ложь;
	
	Если ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Духи")
		Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.АльтернативныйТабак")
		Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.НикотиносодержащаяПродукция")
		Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Пиво") Тогда
		
		РозничныеОперации = НастройкаПараметровСканированияСлужебныйКлиентСерверПовтИсп.ОперацииРозничнойТорговли();
		
		Если ВидОперацииИСМП = Неопределено
			Или РозничныеОперации.Найти(ВидОперацииИСМП) <> Неопределено Тогда
			ПоддерживаетсяЧастичноеВыбытие = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ПоддерживаетсяЧастичноеВыбытие;
	
КонецФункции

#КонецОбласти

Функция ВидПродукцииПоддерживаетОбъемноСортовойУчет(ВидПродукции) Экспорт
	Возврат (ВидыПродукцииОбъемноСортовогоУчета().Найти(ВидПродукции) <> Неопределено);
КонецФункции

Функция ВидыПродукцииОбъемноСортовогоУчета() Экспорт
	
	ВидыПродукции = Новый Массив();
	
	ВидыПродукции.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.УпакованнаяВода"));
	ВидыПродукции.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МолочнаяПродукцияБезВЕТИС"));
	ВидыПродукции.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МолочнаяПродукцияПодконтрольнаяВЕТИС"));
	ВидыПродукции.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Антисептики"));
	ВидыПродукции.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.БАДы"));
	ВидыПродукции.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.СоковаяПродукция"));
	
	Возврат ВидыПродукции;
	
КонецФункции

Функция ДоступноНесколькоПотребительскихШаблоновКодовДляВидаПродукции(ВидПродукции) Экспорт
	
	Если ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.АльтернативныйТабак") Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КОнецФункции

// Возвращает редакцию библиотеки подключаемого оборудования(БПО).
// В редакции 2 данные возвращаются как первый элемент массива, возращаемого в свойстве ВыходныеПараметры.
// В редакции 3 данные возвращаются в составе возвращаемого значение.
// 
// Возвращаемое значение:
//  Число - Редакция БПО. Первая цифра номера версии.
Функция РедакцияБПО() Экспорт
	
	ВерсияБиблиотеки = МенеджерОборудованияВызовСервера.ВерсияБиблиотеки();
	
	Возврат Число(СтрРазделить(ВерсияБиблиотеки, ".")[0]);
	
КонецФункции

Функция ФоноваяПроверкаДокументовГИСМТРазрешена() Экспорт
	Возврат СоответствиеТребованиямГИСМТВызовСервера.ФоноваяПроверкаДокументовГИСМТРазрешена();
КонецФункции

#КонецОбласти
