
////////////////////////////////////////////////////////////////////////////////
// Универсальные методы для формы записи регистра и формы настройки налогов
//
// Серверные методы формы записи регистра сведений СтавкиНалогаНаИмущество
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьПараметрыЛьготы(Форма, ИмяРеквизитаЗапись = "Запись") Экспорт 
	
	Запись = Форма[ИмяРеквизитаЗапись];
	
	ЕстьРеквизитПроцентУменьшенияСтавки = ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(
		Форма,
		"ПроцентУменьшенияСтавкиНалогаНаИмущество");	
	
	Если ЗначениеЗаполнено(Запись.ОснованиеЛьготы) Тогда
		РеквизитыЛьготы = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Запись.ОснованиеЛьготы, "ВидЛьготы, ПроцентУменьшения");
		Форма.ВидЛьготыНалогНаИмущество = РеквизитыЛьготы.ВидЛьготы;
		Если ЕстьРеквизитПроцентУменьшенияСтавки
			 И Форма.ВидЛьготыНалогНаИмущество = Перечисления.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиНаПроцент Тогда
			Форма.ПроцентУменьшенияСтавкиНалогаНаИмущество = РеквизитыЛьготы.ПроцентУменьшения;
		ИначеЕсли ЕстьРеквизитПроцентУменьшенияСтавки Тогда
			Форма.ПроцентУменьшенияСтавкиНалогаНаИмущество = 0;
		КонецЕсли;	
	Иначе	
		Форма.ВидЛьготыНалогНаИмущество = Перечисления.ВидыЛьготПоИмущественнымНалогам.ПустаяСсылка();
		Если ЕстьРеквизитПроцентУменьшенияСтавки Тогда
			Форма.ПроцентУменьшенияСтавкиНалогаНаИмущество = 0;
		КонецЕсли;	
	КонецЕсли;	
	
	УстановитьТекстПодсказкиЛьготы(Форма, ИмяРеквизитаЗапись);
	
КонецПроцедуры	

Процедура УстановитьТекстПодсказкиЛьготы(Форма, ИмяРеквизитаЗапись = "Запись") Экспорт
	
	Запись = Форма[ИмяРеквизитаЗапись];
	Элементы = Форма.Элементы;
	
	ЕстьРеквизитОсновноеСредство = ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Запись, "ОсновноеСредство");
	
	Если ЗначениеЗаполнено(Запись.ОснованиеЛьготы)
		 И ЗначениеЗаполнено(Запись.Организация)
		 И (Не ЕстьРеквизитОсновноеСредство Или ЗначениеЗаполнено(Запись.ОсновноеСредство)) Тогда
		Элементы.ОснованиеЛьготыРасширеннаяПодсказка.Заголовок = ЛьготыПоИмущественнымНалогам.ТекстПодсказкиЛьготы(
			Запись.ОснованиеЛьготы,
			Запись.Организация,
			ОбщегоНазначения.ТекущаяДатаПользователя(),
			?(ЕстьРеквизитОсновноеСредство, Запись.ОсновноеСредство, Неопределено));
	Иначе	 
		Элементы.ОснованиеЛьготыРасширеннаяПодсказка.Заголовок = "";
	КонецЕсли;	 
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьФормуНаСервере(Форма, ИмяРеквизитаЗапись) Экспорт
	
	ЗаполнитьПараметрыЛьготы(Форма, ИмяРеквизитаЗапись);
	СтавкиНалогаНаИмуществоФормыКлиентСервер.УправлениеФормой(Форма);
	
КонецПроцедуры

#КонецОбласти