#Область СлужебныйПрограммныйИнтерфейс

Процедура ОткрытьКалькуляциюРасходов(Форма, ВыбраннаяСтрока, АдресСРасчетамиКалькуляции, Знач ДатаДокумента = Неопределено, ТолькоНаПросмотр = Неопределено) Экспорт
	
	Объект = Форма.Объект;
	
	Если ДатаДокумента = Неопределено Тогда
		ДатаДокумента = Объект.Дата;
	КонецЕсли;
	
	Склад = Неопределено;
	Объект.Свойство("Склад", Склад);
	
	ПодразделениеОрганизации = Неопределено;
	Объект.Свойство("ПодразделениеОрганизации", ПодразделениеОрганизации);
	
	ПараметрыЗакрытияФормы = Новый Структура;
	ПараметрыЗакрытияФормы.Вставить("ВыбраннаяСтрока",          ВыбраннаяСтрока);
	ПараметрыЗакрытияФормы.Вставить("КлючКалькуляцииРасходов",  ВыбраннаяСтрока.КлючКалькуляцииРасходов);
	ПараметрыЗакрытияФормы.Вставить("АдресВременногоХранилища", АдресСРасчетамиКалькуляции);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПерезаполнитьКалькуляциюРасходов",
		Форма, ПараметрыЗакрытияФормы);
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("Продукция",                ВыбраннаяСтрока.Номенклатура);
	ПараметрыОткрытияФормы.Вставить("КоличествоПродукции",      ВыбраннаяСтрока.Количество);
	ПараметрыОткрытияФормы.Вставить("ЦенаПродукции",            ВыбраннаяСтрока.Цена);
	ПараметрыОткрытияФормы.Вставить("Организация",              Объект.Организация);
	ПараметрыОткрытияФормы.Вставить("Склад",                    Склад);
	ПараметрыОткрытияФормы.Вставить("ПодразделениеОрганизации", ПодразделениеОрганизации);
	ПараметрыОткрытияФормы.Вставить("ДатаДокумента",            ДатаДокумента);
	ПараметрыОткрытияФормы.Вставить("КлючКалькуляцииРасходов",  ВыбраннаяСтрока.КлючКалькуляцииРасходов);
	ПараметрыОткрытияФормы.Вставить("СчетНаОплатуПокупателю",   Объект.Ссылка);
	ПараметрыОткрытияФормы.Вставить("АдресВременногоХранилища", АдресСРасчетамиКалькуляции);
	ПараметрыОткрытияФормы.Вставить("ЗаполнитьПоСпецификации",  Не ЗначениеЗаполнено(ВыбраннаяСтрока.КлючКалькуляцииРасходов));
	ПараметрыОткрытияФормы.Вставить("ТолькоПросмотр",           Форма.ТолькоПросмотр Или ТолькоНаПросмотр = Истина);
	
	ОткрытьФорму("РегистрСведений.КалькуляцияРасходовВСчетеНаОплатуПокупателю.Форма.ФормаКалькуляцияРасходов",
		ПараметрыОткрытияФормы, , , , , ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти