#Область ПредодобренныйКредитВСбербанке

Функция ПредодобренныйКредитВСбербанкеПолучитьСписокОрганизаций() Экспорт
	
	МассивИНН = ПерсонализированныеПредложенияСервисов.ПредодобренныйКредитВСбербанкеПолучитьПредодобренныеИНН();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивИНН", МассивИНН);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Организации.Наименование КАК Наименование,
	|	Организации.ИНН КАК ИНН
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.ИНН В(&МассивИНН)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Результат = Новый СписокЗначений;
	
	Пока Выборка.Следующий() Цикл
		Результат.Добавить(Выборка.ИНН, Выборка.Наименование);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ПредодобренныйКредитВСбербанкеГиперссылкаПоИНН(ИНН, ИмяБаннера) Экспорт
	
	Возврат ПерсонализированныеПредложенияСервисов.ПредодобренныйКредитВСбербанкеИмяСервера()
		+ "/api/advertisement/banner?inn=" + КодироватьСтроку(ИНН, СпособКодированияСтроки.КодировкаURL)
		+ "&bannerNick=" + КодироватьСтроку(ИмяБаннера, СпособКодированияСтроки.КодировкаURL)
		+ "&programName=" + КодироватьСтроку(Метаданные.Имя, СпособКодированияСтроки.КодировкаURL)
		+ "&programVersion=" + КодироватьСтроку(Метаданные.Версия, СпособКодированияСтроки.КодировкаURL);
	
КонецФункции

Функция ПараметрыФормыУчетнойПолитики(Организация) Экспорт
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ОрганизацияУчетнойПолитики = Организация;
	Иначе
		ОрганизацияУчетнойПолитики  = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;
	Ключ = НастройкиУчета.КлючЗаписиДействующейУчетнойПолитики("УчетнаяПолитика", Организация);
	
	ПараметрыФормы = Новый Структура;
	Если Ключ <> Неопределено Тогда
		ПараметрыФормы.Вставить("Ключ", Ключ);
	Иначе
		ЗначенияЗаполнения = Новый Структура("Организация ", ОрганизацияУчетнойПолитики);
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	КонецЕсли;
	
	ПараметрыФормы.Вставить("АктивныйЭлемент", "УчетВыпускаПродукцииПоДаннымПродаж");
	ПараметрыФормы.Вставить("РежимНастройки", Истина);
	
	Возврат ПараметрыФормы;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ОбработатьЗакрытиеБаннера(Баннер, Организация = Неопределено) Экспорт
	
	Если Организация = Неопределено Тогда
		Организация = Справочники.Организации.ПустаяСсылка();
	КонецЕсли;
	
	ПерсонализированныеПредложенияСервисов.ОбработатьЗакрытиеБаннера(Баннер, Организация);
	
КонецПроцедуры

#КонецОбласти