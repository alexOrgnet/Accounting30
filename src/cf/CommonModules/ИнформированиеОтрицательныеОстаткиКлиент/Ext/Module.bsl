#Область СлужебныйПрограммныйИнтерфейс

// Фоновая проверка отрицательных остатков ТМЦ в учете. Проверяется отрицательный остаток ТМЦ
// на конец периода отчета, из которого вызывается проверка.
// В случае обнаружения отрицательных остатков, в отчет выводится баннер с информацией о количестве
// позиций с отрицательными остатками.
// 
// Проверка проводится в справках-расчетах, в манифесте которых указан параметр "КонтрольОтрицательныхОстатков".
// для примера см. функцию ПолучитьПараметрыИсполненияОтчета менеджера отчета
// СправкаРасчетРасходМатериаловПоДаннымПродаж. Так же в модуле формы отчета, должны быть добавлены
// две процедуры ОтрицательныеОстаткиСкрытьИнформацию и ОтрицательныеОстаткиОбработкаНавигационнойСсылки
// см. пример ОбщаяФорма.СправкаРасчет
//
// Если требуется актуализация данных, то проверка не выполняется.
//
// Баннер с выводом информации создается программно ИнформированиеОтрицательныеОстатки.СоздатьЭлементИнформированияОтрицательныеОстатки.
//
// Параметры:
//  Форма			 - 	УправляемаяФорма - форма, откуда вызывается проверка
//
Процедура ЗапуститьПроверкуОтрицательныхОстатковВФоне(Форма) Экспорт
	
	ФонИнформацияОтрицательныеОстатки = Форма.Элементы.Найти(
		ИнформированиеОтрицательныеОстаткиКлиентСервер.ИмяГруппыБаннера());
	
	Если ФонИнформацияОтрицательныеОстатки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ФонИнформацияОтрицательныеОстатки.Видимость = Ложь;
	
	БазовыеРеквизиты = СправкиРасчетыКлиентСервер.БазовыеРеквизиты(Форма);
	
	ДлительнаяОперация = ИнформированиеОтрицательныеОстаткиВызовСервера.ЗапуститьПроверкуОтрицательныхОстатковНаСервере(
		БазовыеРеквизиты.Организация, КонецДня(БазовыеРеквизиты.КонецПериода), Форма.УникальныйИдентификатор);
	
	ОжидатьЗавершенияПроверкиОтрицательныхОстатковВФоне(Форма, ДлительнаяОперация);
	
КонецПроцедуры

Процедура СкрытьИнформацию(Форма) Экспорт
	
	Форма.Элементы[ИнформированиеОтрицательныеОстаткиКлиентСервер.ИмяГруппыБаннера()].Видимость = Ложь;
	
КонецПроцедуры

Процедура ОткрытьКонтрольОтрицательныхОстатков(Форма) Экспорт
	
	БазовыеРеквизиты = СправкиРасчетыКлиентСервер.БазовыеРеквизиты(Форма);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Организация",                 БазовыеРеквизиты.Организация);
	ПараметрыОткрытия.Вставить("НачалоПериода",               БазовыеРеквизиты.НачалоПериода);
	ПараметрыОткрытия.Вставить("КонецПериода",                БазовыеРеквизиты.КонецПериода);
	ПараметрыОткрытия.Вставить("СформироватьПриОткрытии",     Истина);
	
	// Отчет не поддерживает параметр СформироватьприОткрытии, поэтому получим форму и запустим отчет.
	ФормаОтчета = ПолучитьФорму("Отчет.КонтрольОтрицательныхОстатков.Форма.ФормаОтчета");
	ФормаОтчета.ОткрытьИСформировать(ПараметрыОткрытия);
	
КонецПроцедуры

// Вывод информации об отрицательных остатках на форму отчета.
//
// Если отрицательные остатки есть, то информация о них выводится на элемент формы
// ИнформацияОтрицательныеОстатки с ссылкой на команду открытия отчета КонтрольОтрицательныхОстатков
// 
// Элемент ИнформацияОтрицательныеОстатки создается на форме програмным способом.
// см ИнформированиеОтрицательныеОстатки.СоздатьЭлементИнформированияОтрицательныеОстатки
//
// Параметры:
//  ДлительнаяОперация		 - Структура	- Параметры длительной операции. см ДлительныеОперации.ВыполнитьВФоне 
//  ДополнительныеПараметры	 - Структура	- Содержит данные о форме отчета. См. ОжидатьПроверкиОтрицательныхОстатковВФоне
//
Процедура ЗавершениеПроверкиОтрицательныхОстатков(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	ФормаОтчета = ДополнительныеПараметры.Форма;
	
	ФонИнформацияОтрицательныеОстатки = ФормаОтчета.Элементы.Найти(
		ИнформированиеОтрицательныеОстаткиКлиентСервер.ИмяГруппыБаннера());
	
	Если ФонИнформацияОтрицательныеОстатки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не (ЗначениеЗаполнено(ДлительнаяОперация) И ДлительнаяОперация.Статус = "Выполнено") Тогда
		ФонИнформацияОтрицательныеОстатки.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	КоличествоПозицийСОтрицательнымиОстатками = ИнформированиеОтрицательныеОстаткиВызовСервера.РезультатПроверки(
		ДлительнаяОперация.АдресРезультата);
	
	Если КоличествоПозицийСОтрицательнымиОстатками = 0 Тогда
		ФонИнформацияОтрицательныеОстатки.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	ФонИнформацияОтрицательныеОстатки.Видимость = Истина;
	ФормаОтчета.ИнформацияОтрицательныеОстатки = ТекстПредупрежденияОбОтрицательныхОстатках(
		КоличествоПозицийСОтрицательнымиОстатками);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстПредупрежденияОбОтрицательныхОстатках(КоличествоПозицийСОтрицательнымиОстатками)
	
	ТекстКоличествоПозиций = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
		НСтр("ru='; %1 позиции;; %1 позициям; %1 позициям; %1 позициям';"), 
		КоличествоПозицийСОтрицательнымиОстатками,
		,
		"ЧДЦ=0");
	
	ЧастиСтроки = Новый Массив;
	ЧастиСтроки.Добавить(НСтр("ru='Обнаружены отрицательные остатки по'"));
	ЧастиСтроки.Добавить(" ");
	ЧастиСтроки.Добавить(Новый ФорматированнаяСтрока(ТекстКоличествоПозиций,,,,"ОткрытьОтчетОбОтрицательныхОстатках"));
	Возврат Новый ФорматированнаяСтрока(ЧастиСтроки)
	
КонецФункции

Процедура ОжидатьЗавершенияПроверкиОтрицательныхОстатковВФоне(Форма, ДлительнаяОперация)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", Форма);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеПроверкиОтрицательныхОстатков", ЭтотОбъект, ДополнительныеПараметры);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
	
КонецПроцедуры

#КонецОбласти

