#Область ПрограммныйИнтерфейс

Процедура ЗагрузитьИзОФДПоСпискуКасс(СписокКассДляЗагрузки, ИдентификаторФормы, ОповещениеОЗавершении) Экспорт
	МаксИндекс = СписокКассДляЗагрузки.Количество()-1;
	Для Индекс = 0 По МаксИндекс Цикл
		Если НЕ СписокКассДляЗагрузки[МаксИндекс-Индекс].ЗагрузитьДанные Тогда
			СписокКассДляЗагрузки.Удалить(МаксИндекс-Индекс);
		КонецЕсли;
	КонецЦикла;
	
	Если СписокКассДляЗагрузки.Количество() = 0 Тогда
		ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, Неопределено);
		Возврат;
	КонецЕсли;
	
	ПараметрыЗагрузки = Новый Структура;
	ПараметрыЗагрузки.Вставить("СписокКассДляЗагрузки", СписокКассДляЗагрузки); // Список касс по которым загружаем данные
	ПараметрыЗагрузки.Вставить("СозданныеДокументы",    Новый Массив); // Созданные в результате загрузки ОРП
	ПараметрыЗагрузки.Вставить("ИдентификаторФормы",    ИдентификаторФормы); 
	ПараметрыЗагрузки.Вставить("ОповещениеОЗавершении", ОповещениеОЗавершении);
	
	НачатьЗагрузкуИзОФДПоКассе(ПараметрыЗагрузки);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура НачатьЗагрузкуИзОФДПоКассе(ПараметрыЗагрузки)
	ПараметрыКассыОФД = ПараметрыЗагрузки.СписокКассДляЗагрузки[0];
	
	Если ПараметрыКассыОФД.Свойство("ДатаЗагрузки") Тогда
		// Если дата загрузки уже выбрана в форме настройки
		ЗагрузкаИзОФДВызовСервера.УстановитьПараметрыУстройства(ПараметрыКассыОФД.ДатаЗагрузки, ПараметрыКассыОФД.ИдентификаторУстройства);
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузкаИзОФДВыборПериодаПослеЗагрузки", ЭтотОбъект, ПараметрыЗагрузки);
		МенеджерОфлайнОборудованияКлиент.НачатьЗагрузкуДанныхИзККМ(ПараметрыКассыОФД.ИдентификаторУстройства,  ПараметрыЗагрузки.ИдентификаторФормы, ОповещениеОЗавершении);
		
	ИначеЕсли МенеджерОфлайнОборудованияВызовСервера.ПроверитьИсториюЗагрузкиУстройства(ПараметрыКассыОФД.ИдентификаторУстройства) Тогда
		// Это первая загрузка - надо выбрать дату
		ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузкаИзОФДВыборПериодаЗавершение", ЭтотОбъект, ПараметрыЗагрузки);
		ПараметрыФормы = Новый Структура("ИдентификаторУстройства", ПараметрыКассыОФД.ИдентификаторУстройства);
		
		ОткрытьФорму("Обработка.ЗагрузкаИзОФД.Форма.ВыборДатыНачалаЗагрузки", ПараметрыФормы, ЭтотОбъект,,,,
			ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	ИначеЕсли ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыКассыОФД, "ПериодИзмененВручную", Ложь) Тогда
		// Если период изменен в форме настройки, то установим автовыбор периода после загрузки
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузкаИзОФДВыборПериодаПослеЗагрузки", ЭтотОбъект, ПараметрыЗагрузки);
		МенеджерОфлайнОборудованияКлиент.НачатьЗагрузкуДанныхИзККМ(ПараметрыКассыОФД.ИдентификаторУстройства,  ПараметрыЗагрузки.ИдентификаторФормы, ОповещениеОЗавершении);
	Иначе
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузкаИзОФДЗавершение", ЭтотОбъект, ПараметрыЗагрузки);
		МенеджерОфлайнОборудованияКлиент.НачатьЗагрузкуДанныхИзККМ(ПараметрыКассыОФД.ИдентификаторУстройства, ПараметрыЗагрузки.ИдентификаторФормы, ОповещениеОЗавершении);
	КонецЕсли;

КонецПроцедуры

Процедура ЗагрузкаИзОФДВыборПериодаЗавершение(Результат, ПараметрыЗагрузки) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") 
		ИЛИ НЕ Результат.Результат
		ИЛИ НЕ ЗагрузкаИзОФДВызовСервера.УстановитьПараметрыУстройства(Результат.ДатаНачалаЗагрузки, Результат.ИдентификаторУстройства) Тогда
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не удалось загрузить данные. Попробуйте снова.'"));
		Возврат;
	КонецЕсли;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузкаИзОФДВыборПериодаПослеЗагрузки", ЭтотОбъект, ПараметрыЗагрузки);
	МенеджерОфлайнОборудованияКлиент.НачатьЗагрузкуДанныхИзККМ(Результат.ИдентификаторУстройства,  ПараметрыЗагрузки.ИдентификаторФормы, ОповещениеОЗавершении);
КонецПроцедуры

Процедура ЗагрузкаИзОФДВыборПериодаПослеЗагрузки(РезультатЗагрузки, ПараметрыЗагрузки) Экспорт
	ЗагрузкаИзОФДВызовСервера.УстановитьАвтоматическийПериодЗагрузки(РезультатЗагрузки.ИдентификаторУстройства); 
	
	ЗагрузкаИзОФДЗавершение(РезультатЗагрузки, ПараметрыЗагрузки);
КонецПроцедуры 

Процедура ЗагрузкаИзОФДЗавершение(Результат, ПараметрыЗагрузки) Экспорт  
	Если НЕ Результат.Результат Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("При загрузке из "+Результат.ИдентификаторУстройства+" произошла ошибка " + Результат.ОписаниеОшибки);
	Иначе
		// Дополним ранее созданные документы документами текущей итерации
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			ПараметрыЗагрузки.СозданныеДокументы,
			ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат.ДополнительныеПараметры, "СозданныеДокументы", Новый Массив));
	КонецЕсли;
		
	Если ПараметрыЗагрузки.СписокКассДляЗагрузки.Количество() > 1 Тогда
		// Если все прошло хорошо, удаляем загруженную кассу из списка
		Для Индекс = 0 По ПараметрыЗагрузки.СписокКассДляЗагрузки.ВГраница() Цикл
			Если ПараметрыЗагрузки.СписокКассДляЗагрузки[Индекс].ИдентификаторУстройства = Результат.ИдентификаторУстройства Тогда
				ПараметрыЗагрузки.СписокКассДляЗагрузки.Удалить(Индекс);
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		НачатьЗагрузкуИзОФДПоКассе(ПараметрыЗагрузки);
	Иначе
		ВыполнитьОбработкуОповещения(ПараметрыЗагрузки.ОповещениеОЗавершении, ПараметрыЗагрузки.СозданныеДокументы);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

