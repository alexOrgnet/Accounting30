////////////////////////////////////////////////////////////////////////////////
// Работа с контрагентами в конфигурации "Бухгалтерия предприятия".
//  
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Выполняет действие ОбработкаВыбора при создании нового контрагента из формы документа
//
// Параметры:
//  Элемент                  - ПолеФормы - элемент формы, с которым связано событие.
//  ВыбранноеЗначение        - Произвольный - см. описание параметра обработчика ОбработкаВыбора.
//  СтандартнаяОбработка     - Булево - признак выполнения стандартной (системной) обработки события
//  ЗначенияЗаполнения       - Структура - Параметры заполнения нового объекта.
//  ПоказыватьБанковскийСчет - Булево - признак необходимости отображать поля ввода банковского счета в упрощенной форме.
//
Процедура КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка, ЗначенияЗаполнения = Неопределено, ПоказыватьБанковскийСчет = Ложь) Экспорт
	
	СоздатьКонтрагента(Элемент, ВыбранноеЗначение, СтандартнаяОбработка, ЗначенияЗаполнения, ПоказыватьБанковскийСчет);
	
КонецПроцедуры

// Выполняет действие Создание при создании нового контрагента из формы документа
//
// Параметры:
//  Элемент                  - ПолеФормы - элемент формы, с которым связано событие.
//  ВведенноеЗначение        - Строка - значение наименования либо ИНН, введенное в поле ввода.
//  СтандартнаяОбработка     - Булево - признак выполнения стандартной (системной) обработки события
//  ЗначенияЗаполнения       - Структура - Параметры заполнения нового объекта.
//  ПоказыватьБанковскийСчет - Булево - признак необходимости отображать поля ввода банковского счета в упрощенной форме.
//
Процедура КонтрагентСоздание(Элемент, ВведенноеЗначение, СтандартнаяОбработка, ЗначенияЗаполнения = Неопределено, ПоказыватьБанковскийСчет = Ложь) Экспорт
	
	СоздатьКонтрагента(Элемент, ВведенноеЗначение, СтандартнаяОбработка, ЗначенияЗаполнения, ПоказыватьБанковскийСчет);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура КонтрагентАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка) Экспорт
	
	Если НЕ ПустаяСтрока(Текст) 
		И (НЕ ЗначениеЗаполнено(Элемент.ОграничениеТипа)
		ИЛИ Элемент.ОграничениеТипа.СодержитТип(Тип("СправочникСсылка.Контрагенты"))) Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = РаботаСКонтрагентамиБПВызовСервера.ДанныеВыбораКонтрагента(ПараметрыПолученияДанных, Ложь);
	КонецЕсли;
	
КонецПроцедуры

Процедура КонтрагентОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка) Экспорт
	
	Если НЕ ПустаяСтрока(Текст) 
		И (НЕ ЗначениеЗаполнено(Элемент.ОграничениеТипа)
		ИЛИ Элемент.ОграничениеТипа.СодержитТип(Тип("СправочникСсылка.Контрагенты"))) Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = РаботаСКонтрагентамиБПВызовСервера.ДанныеВыбораКонтрагента(ПараметрыПолученияДанных, Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьКонтрагента(Элемент, ВведенноеЗначение, СтандартнаяОбработка, ЗначенияЗаполнения, ПоказыватьБанковскийСчет)
	
	Если ТипЗнч(ВведенноеЗначение) = Тип("Строка") 
		И (Не ЗначениеЗаполнено(Элемент.ОграничениеТипа)
		Или Элемент.ОграничениеТипа.СодержитТип(Тип("СправочникСсылка.Контрагенты"))) Тогда
		
		СтандартнаяОбработка = Ложь;
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("РежимВыбора",     Истина);
		ПараметрыФормы.Вставить("ТекстЗаполнения", ВведенноеЗначение);
		ПараметрыФормы.Вставить("ПоказыватьБанковскийСчет", ПоказыватьБанковскийСчет);
		
		Если ЗначенияЗаполнения <> Неопределено Тогда
			ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		КонецЕсли;
		
		Если ОбщегоНазначенияБПВызовСервера.ЭтоПростойИнтерфейс() Тогда
			ОткрытьФорму("Справочник.Контрагенты.Форма.ФормаНовогоЭлемента", ПараметрыФормы, Элемент);
		Иначе
			ОткрытьФорму("Справочник.Контрагенты.ФормаОбъекта", ПараметрыФормы, Элемент);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
