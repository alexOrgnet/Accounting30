#Область ПрограммныйИнтерфейс

// Возвращает имя формы заявления
//
// Параметры:
//  ВидЗаявления - ПеречислениеСсылка.ВидыУведомленийОСпецрежимахНалогообложения - вид заявления
//
// Возвращаемое значение:
//  Строка - полный путь к форме заявления
Функция ИмяФормыЗаявления(ВидЗаявления) Экспорт
	
	ИмяОтчета = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ПолучитьИмяОтчетаПоВидуУведомления(ВидЗаявления);
	Если ПустаяСтрока(ИмяОтчета) Тогда
		Возврат "";
	КонецЕсли;
	
	ИмяФормы = РегламентированнаяОтчетность.ПолучитьАктуальнуюФормуУведомления(ВидЗаявления);
	Если ПустаяСтрока(ИмяФормы) Тогда
		Возврат "";
	КонецЕсли;
	
	Возврат СтрШаблон("Отчет.%1.Форма.%2", ИмяОтчета, ИмяФормы);
	
КонецФункции

// Возвращает данные для заполнения уведомления об уменьшении налога на расходы на онлайн-кассы
//
// Параметры:
//   Организация - СправочникСсылка.Организации - организация уведомления
//   РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане - налоговый орган, в который подается уведомление
//
// Возвращаемое значение:
//   Структура>   - данные для заполнения нового уведомления, см. Отчеты.РегламентированноеУведомлениеУменьшениеНалогаККТ.СформироватьУведомление()
//
Функция ДанныеЗаполненияУведомленияОбУменьшенииНалога(Организация, РегистрацияВНалоговомОргане) Экспорт
	
	Возврат УчетПСН.ДанныеЗаполненияУведомленияОбУменьшенииНалога(Организация, РегистрацияВНалоговомОргане);
	
КонецФункции

// Возвращает признак: 
//  ИСТИНА - указанный вид уведомления предназначен для Заявления на получение патента,
//  ЛОЖЬ   - иначе
//
// Параметры:
//   ВидУведомления - ПеречислениеСсылка.ВидыУведомленийОСпецрежимахНалогообложения
//
// Возвращаемое значение:
//   Булево
//
Функция ЭтоУведомлениеЗаявлениеНаПолучениеПатента(ВидУведомления) Экспорт
	
	ВидыУведомленияПатент = ПредопределенноеЗначение(
		"Перечисление.ВидыУведомленийОСпецрежимахНалогообложения.ЗаявлениеНаПолучениеПатентаРекомендованнаяФорма");
	
	Возврат ВидУведомления = ВидыУведомленияПатент;
	
КонецФункции

// Возвращает вид деятельности по патенту
//
// Параметры:
//  ОперацияСПатентом - ДокументСсылка.ОперацияСПатентом - документ получения патента
//
// Возвращаемое значение:
//  СправочникСсылка.Патенты - вид деятельности по патенту
//
Функция ДеятельностьПоПатенту(Знач ОперацияСПатентом) Экспорт
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОперацияСПатентом, "Патент");
	
КонецФункции

// Возвращает дату платежа по патенту с учетом переноса на первый рабочий день
//
// Параметры:
//  Дата  - Дата - дата платежа, которую необходимо перенести на первый рабочий день
//
// Возвращаемое значение:
//   Дата  - срок платежа по патенту с учетом переноса на первый рабочий день
//
Функция УточнитьСрокОплатыПатента(Знач Дата) Экспорт
	
	Возврат УчетПСН.УточнитьСрокОплатыПатента(Дата);
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Определяет, что на организацию распространяется освобождение от налогов,
// установленное статьей 2 Федерального закона № 172-ФЗ от 08.06.2020
//
// Параметры:
//  Организация - СправочникСсылка.Организации
// 
// Возвращаемое значение:
//  Булево
//
Функция ДеятельностьОтнесенаКПострадавшимОтКоронавируса(Знач Организация) Экспорт
	
	Возврат НалоговыйУчет.ДеятельностьОтнесенаКПострадавшимОтКоронавируса(Организация);
	
КонецФункции

Функция НастройкиПечатиУведомлений(МассивДокументов) Экспорт
	
	МассивУведомлений = Новый Массив;
	
	СвойстваДокументов = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивДокументов, "Уведомление");
	Для Каждого ЗаписьМассива Из МассивДокументов Цикл
		МассивУведомлений.Добавить(СвойстваДокументов[ЗаписьМассива]);
	КонецЦикла;
	
	Возврат МассивУведомлений;
	
КонецФункции

Функция ЗаголовкиФормыПечати(МассивДокументов) Экспорт
	
	Заголовки = Новый Соответствие;
	
	Для Каждого ДокументСсылка Из МассивДокументов Цикл
		Заголовки.Вставить(ДокументСсылка, ЗаголовокФормыПечати(ДокументСсылка));
	КонецЦикла;
	
	Возврат Заголовки;
	
КонецФункции

Функция ЗаголовокФормыПечати(Ссылка) Экспорт
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "ВидУведомления, Организация");
	Возврат Новый Структура("ЗаголовокФормы", "" + Реквизиты.ВидУведомления + " (" + Реквизиты.Организация + ")");
	
КонецФункции

#КонецОбласти