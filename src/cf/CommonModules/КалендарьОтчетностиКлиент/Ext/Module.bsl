
#Область ПрограммныйИнтерфейс

// Показывает форму помощника подготовки регламентированных отчетов, уведомлений ЕНС.
//
// Параметры:
//   Параметры - Структура - см. КалендарьОтчетностиКлиентСервер.НовыйПараметрыПомощника
//
Процедура ПоказатьПомощник(Параметры) Экспорт
	
	Событие = ВыполнениеЗадачБухгалтераКлиентСервер.НовыеПараметрыКомандЗадачи();
	ЗаполнитьЗначенияСвойств(Событие, Параметры);
	ОписаниеДействия = ВыполнениеЗадачБухгалтераВызовСервера.ОписаниеДействия(Событие);
	ОткрытьФорму(ОписаниеДействия.ИмяФормы, ОписаниеДействия.ПараметрыФормы);
	
КонецПроцедуры

// Пробует выполнить переход по навигационной ссылке и показать помощник.
//
// Параметры:
//   ПараметрыСсылки -  Структура - структура переданных параметров.
//                      Чтобы переход по ссылке был выполнен, структура параметров должна соответствовать
//                      конструктору КалендарьОтчетностиКлиентСервер.НовыйПараметрыНавигационнойСсылки
//
// Возвращаемое значение:
//   Булево - Истина, если переход по ссылке был выполнен.
//
Функция ВыполненПереходПоНавигационнойСсылке(ПараметрыСсылки) Экспорт
	
	// Проверим, что были переданы все необходимые параметры.
	ТребуемыеПараметры = КалендарьОтчетностиКлиентСервер.НовыйПараметрыНавигационнойСсылки();
	Для Каждого ТребуемыйПараметр Из ТребуемыеПараметры Цикл
		ЗначениеПараметра = ПараметрыСсылки.Получить(ТребуемыйПараметр.Ключ);
		Если ЗначениеПараметра = Неопределено Тогда
			Возврат Ложь;
		КонецЕсли;
		ТребуемыеПараметры[ТребуемыйПараметр.Ключ] = ЗначениеПараметра;
	КонецЦикла;
	
	// Обработчик перехода по навигационной ссылки срабатывает дважды:
	// 1. В приложении, из которого выполняется переход.
	// 2. В приложении, в которое выполнен переход.
	// В первом случае не следует обрабатывать переход по навигационной ссылке.
	Если ТребуемыеПараметры.application_id = КалендарьОтчетностиВызовСервера.ИдентификаторЭтогоПриложения() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ТребуемыеПараметры.action <> "open_assistant" Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПараметрыПомощника = КалендарьОтчетностиВызовСервера.ПодготовитьПараметрыПомощника(ТребуемыеПараметры);
	
	Если ПараметрыПомощника = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПоказатьПомощник(ПараметрыПомощника);
	Возврат Истина;
КонецФункции

#КонецОбласти