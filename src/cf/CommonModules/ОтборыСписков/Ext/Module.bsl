
#Область ПрограммныйИнтерфейс

// Сбрасывает использование пользовательских отборов в Пользовательских настройках динамического списка.
//
// Параметры:
//  ПользовательскиеНастройки - ПользовательскиеНастройкиКомпоновкиДанных - пользовательские настройки динамического списка
//
Процедура СброситьИспользованиеПользовательскихОтборовВНастройке(ПользовательскиеНастройки) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("АвтоматическиВосстанавливатьОтборыПоПроизвольнымПолямВСписках") Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ЭлементНастройки Из ПользовательскиеНастройки.Элементы Цикл
		Если ТипЗнч(ЭлементНастройки) = Тип("ОтборКомпоновкиДанных") Тогда
			СброситьНастройкиРекурсивно(ЭлементНастройки.Элементы);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СброситьНастройкиРекурсивно(КоллекцияЭлементов)
	
	Для Каждого ЭлементОтбора Из КоллекцияЭлементов Цикл
		Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			Если ЭлементОтбора.Использование Тогда
				ЭлементОтбора.Использование = Ложь;
			КонецЕсли;
		Иначе
			СброситьНастройкиРекурсивно(ЭлементОтбора.Элементы);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
