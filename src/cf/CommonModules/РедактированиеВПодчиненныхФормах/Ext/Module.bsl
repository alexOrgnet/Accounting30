#Область СлужебныйПрограммныйИнтерфейс

Процедура УстановитьСвойстваЭлементовПоРеквизитамМетаданных(ЭлементыИМетаданные) Экспорт
	
	// Реквизиты подчиненной формы - это копии реквизитов объекта или табличной части.
	// К ним должны применяться все те же правила, что и к самим реквизитам.
	// Например, связи параметров выбора, параметры выбора, форма выбора и т.д.
	// Для реквизитов формы такие ограничения задать нельзя, поэтому зададим их
	// элементам формы.
	
	Для Каждого КлючЗначение Из ЭлементыИМетаданные Цикл
		НастроитьЭлементФормыПоРеквизитуМетаданных(
			КлючЗначение.Ключ, КлючЗначение.Значение);
	КонецЦикла;
	
КонецПроцедуры

Функция НовыйПроверяемыйСписок() Экспорт
	
	ПроверяемыйСписок = Новый Структура;
	ПроверяемыйСписок.Вставить("Таблица", Неопределено);
	ПроверяемыйСписок.Вставить("Имя", "");
	ПроверяемыйСписок.Вставить("Синоним", "");
	Возврат ПроверяемыйСписок;
	
КонецФункции

Процедура ВыполнитьПроверкуЗаполненияТаблицыПоРеквизитамМетаданных(ПроверяемыйСписок, ЭлементыИМетаданные, Отказ) Экспорт
	
	// Нужно проверить заполнение реквизитов, если в метаданных для них установлена проверка заполнения.
	
	Если Не ЗначениеЗаполнено(ПроверяемыйСписок)
		Или Не ЗначениеЗаполнено(ПроверяемыйСписок.Таблица) Тогда
		Возврат;
	КонецЕсли;
	
	НомерСтроки = 0;
	Для Каждого Строка Из ПроверяемыйСписок.Таблица Цикл
		НомерСтроки = НомерСтроки + 1;
		ПроверитьСтрокуТабличнойЧасти(Строка, НомерСтроки, ПроверяемыйСписок, ЭлементыИМетаданные, Отказ);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункци

Процедура НастроитьЭлементФормыПоРеквизитуМетаданных(Элемент, МетаданныеРеквизита)
	
	// Связи параметров выбора
	СвязиПараметровВыбора = Новый Массив(Элемент.СвязиПараметровВыбора);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СвязиПараметровВыбора, МетаданныеРеквизита.СвязиПараметровВыбора);
	Элемент.СвязиПараметровВыбора = Новый ФиксированныйМассив(СвязиПараметровВыбора);
	
	// Параметры выбора
	ПараметрыВыбора = Новый Массив(Элемент.ПараметрыВыбора);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПараметрыВыбора, МетаданныеРеквизита.ПараметрыВыбора);
	Элемент.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	
	// Форма выбора
	Если Не ЗначениеЗаполнено(Элемент.ФормаВыбора) Тогда
		Элемент.ФормаВыбора = МетаданныеРеквизита.ФормаВыбора;
	КонецЕсли;
	
	// Связь по типу
	Элемент.СвязьПоТипу = МетаданныеРеквизита.СвязьПоТипу;
	
	// Отметка незаполненного
	Элемент.АвтоОтметкаНезаполненного = 
		МетаданныеРеквизита.ПроверкаЗаполнения = ПроверкаЗаполнения.ВыдаватьОшибку;
	
КонецПроцедуры

Процедура ПроверитьСтрокуТабличнойЧасти(Строка, НомерСтроки, ПроверяемыйСписок, ЭлементыИМетаданные, Отказ)
	
	ПутьКСтроке = ПроверяемыйСписок.Имя + "["+Формат(НомерСтроки-1, "ЧДЦ=0; ЧГ=0")+"]";
	
	Для Каждого КлючЗначение Из ЭлементыИМетаданные Цикл
		
		МетаданныеРеквизита = КлючЗначение.Значение;
		Если МетаданныеРеквизита.ПроверкаЗаполнения <> ПроверкаЗаполнения.ВыдаватьОшибку Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяРеквизита = КлючЗначение.Ключ;
		
		Если ЗначениеЗаполнено(Строка[ИмяРеквизита]) Тогда
			Продолжить;
		КонецЕсли;
		
		ТекстОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
			"Колонка", "Заполнение", МетаданныеРеквизита.Синоним, НомерСтроки, ПроверяемыйСписок.Синоним);
		
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , , ПутьКСтроке + "." + ИмяРеквизита , Отказ);
		
	КонецЦикла
	
	
КонецПроцедуры


#КонецОбласти