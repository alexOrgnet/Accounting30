#Область ПрограммныйИнтерфейс

// Определяет идентификатор семейства конфигураций.
//
// Параметры:
//	Семейство - Строка - возвращаемый параметр. Идентификатор семейства.
//
Процедура ОпределитьСемействоКонфигураций(Семейство) Экспорт
	
	Семейство = "БП";

КонецПроцедуры

// Процедура возвращает краткое название конфигурации.
// Длина возвращаемого значения не должна превышать 30 символов.
//
// Параметры:
//   ИмяПрограммы - Строка - в параметре возвращается краткое название конфигурации.
//
Процедура КраткоеНазваниеПрограммы(ИмяПрограммы) Экспорт
	
	МетаданныеИмя = Метаданные.Имя;
	
	Если МетаданныеИмя = "БухгалтерияПредприятия" 
		ИЛИ МетаданныеИмя = "БухгалтерияПредприятияБазовая"
		ИЛИ МетаданныеИмя = "БухгалтерияПредприятияБазовая1"
		ИЛИ МетаданныеИмя = "БухгалтерияПредприятияУчебная" Тогда
		ИмяПрограммы = "1С:Бухгалтерия";
	ИначеЕсли МетаданныеИмя = "БухгалтерияПредприятияКОРП" Тогда
		ИмяПрограммы = "1С:Бухгалтерия КОРП";
	Иначе
		ИмяПрограммы = МетаданныеИмя;
	КонецЕсли;
	
КонецПроцедуры

// Возвращает заданные реквизиты организации в параметре Результат.
// Параметры:
//   Организация - СправочникСсылка.Организации - ссылка на организацию;
//   Реквизиты   - Строка - список реквизитов, разделенные запятыми.
//   Результат   - Структура - структура с полями, заданными в параметре реквизиты.
// Возвращаемое значение:
//   Структура - структура с ключами, определяемыми строкой Реквизиты.
//
Процедура РеквизитыОрганизации(Организация, Реквизиты, Результат) Экспорт
	
	Массив = Новый Массив;
	Массив.Добавить(Организация);
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(Массив, Реквизиты, Истина);
	Для каждого КлючЗначение Из ЗначенияРеквизитов.Получить(Организация) Цикл
		Результат.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);
	КонецЦикла;
	
КонецПроцедуры

Процедура НайтиОрганизациюПоИНН(ИНН, Организация) Экспорт
	
	Организация = Справочники.Организации.НайтиПоРеквизиту("ИНН", ИНН);
	
КонецПроцедуры

Процедура ДатаНачалаУчетаВПрограмме(Организация, ДатаНачалаУчета) Экспорт

	// Если указана дата начальных остатков, возвращаем следующий за ней день.
	Результат = Справочники.Организации.ДатаНачалаВеденияУчета(Организация);
	Если ЗначениеЗаполнено(Результат) Тогда
		ДатаНачалаУчета = Результат;
	Иначе	
		ДатаНачалаУчета = '0001-01-01';
	КонецЕсли;

КонецПроцедуры

#КонецОбласти