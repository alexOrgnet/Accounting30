#Область ПрограммныйИнтерфейс

// Возвращает Истина, если в базе есть хотя бы одна действующая настройка интеграции.
// 
// Возвращаемое значение:
//   - Булево
//
Функция ИнтеграцияВИнформационнойБазеВключена() Экспорт
	
	Возврат Справочники.НастройкиИнтеграцииСБанками.ИнтеграцияВИнформационнойБазеВключена();
	
КонецФункции

// Возвращает Истина, если для хотя бы для одного расчетного счета включена интеграция.
//
// Параметры:
//  Организация	 - СправочникСсылка.Организации - Ссылка на организацию.
// 
// Возвращаемое значение:
//   - Булево
//
Функция ИнтеграцияОрганизацииВключена(Организация) Экспорт
	
	Возврат Справочники.НастройкиИнтеграцииСБанками.ИнтеграцияОрганизацииВключена(Организация);
	
КонецФункции

// Возвращает Истина, если для расчетного счета включена интеграция.
//
// Параметры:
//  СчетОрганизации	 - СправочникСсылка.БанковскиеСчета - Ссылка на расчетный счет организации.
// 
// Возвращаемое значение:
//   - Булево
//
Функция ИнтеграцияВключена(СчетОрганизации) Экспорт
	
	Возврат Справочники.НастройкиИнтеграцииСБанками.ИнтеграцияВключена(СчетОрганизации);
	
КонецФункции

// Возвращает префикс информационной базы для платежных поручений из банка.
//
// Параметры:
//  Банк - СправочникСсылка.Банки - Банк, для которого нужно определить префикс.
//
// Возвращаемое значение:
//   Строка - префикс информационной базы для платежных поручений.
//
Функция ПрефиксБанкаИнтеграции(Банк) Экспорт
	
	Возврат Справочники.НастройкиИнтеграцииСБанками.ПрефиксБанкаИнтеграции(Банк);
	
КонецФункции

// Возвращает признак, что счет открыт в Сбербанке
//
// Параметры:
//  БанковскийСчет - СправочникСсылка.БанковскиеСчета - Ссылка на банковский счет, который стоит проверить.
//
// Возвращаемое значение:
//   Булево - Истина, если счет открыт в Сбербанке
//
Функция ЭтоСчетСбербанка(БанковскийСчет) Экспорт
	
	РеквизитыСчета = Справочники.БанковскиеСчета.РеквизитыСчета(БанковскийСчет);
	Если РеквизитыСчета = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	БИК = РеквизитыСчета.БИКБанка;
	ИННСбербанка = "7707083893";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("БИК", БИК);
	Запрос.УстановитьПараметр("ИНН", ИННСбербанка);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	КлассификаторБанков.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.КлассификаторБанков КАК КлассификаторБанков
	|ГДЕ
	|	НЕ КлассификаторБанков.ПометкаУдаления
	|	И КлассификаторБанков.ИНН = &ИНН
	|	И КлассификаторБанков.Код = &БИК";
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти
