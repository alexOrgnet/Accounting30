#Область ПрограммныйИнтерфейс

// Записывает ошибку в журнал регистрации.
//
// Параметры:
//	ИнформацияОбОшибке - ИнформацияОбОшибке - информация об ошибке, полученная методом ИнформацияОбОшибке()
//
Процедура ЗаписатьОшибку(ОписаниеОшибки) Экспорт
	
	ЗаписьЖурналаРегистрации(ИмяСобытия(),
		УровеньЖурналаРегистрации.Ошибка,,,
		ОписаниеОшибки);
	
КонецПроцедуры

// Записыввает информацию в журнал регистрации.
//
// Параметры:
//	Информация - Строка - произвольный текст
//
Процедура ЗаписатьИнформацию(Информация) Экспорт
	
	ЗаписьЖурналаРегистрации(ИмяСобытия(),
		УровеньЖурналаРегистрации.Информация,,,
		Информация);
	
КонецПроцедуры

// Записывает полную информацию о запросе/ответе.
//
// Параметры:
//	Объект - HTTPЗапрос, HTTPОтвет - источник информации о запросе/ответе 
//
Процедура ЛогироватьВыполнениеМетода(Объект) Экспорт

	Событие = СтрШаблон("%1.%2", ИмяСобытия(), Объект.БазовыйURL);
	Уровень = УровеньЖурналаРегистрации.Примечание;
	
	КодОсновногоЯзыка = ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка();
	ПризнакОтвета = ?(ТипЗнч(Объект) = Тип("HTTPСервисОтвет"), НСтр("ru=' (ответ)'", КодОсновногоЯзыка), "");
	
	Шаблон = НСтр("ru='%1%2
		|%3
		|--
		|%4'");
	
	Комментарий = СтрШаблон(Шаблон,
		Объект.БазовыйURL,
		ПризнакОтвета,
		ОплатаСервисаHTTPБП.ПредставлениеЗаголовков(Объект),
		СокрЛП(Объект.ПолучитьТелоКакСтроку()));
	
	ЗаписьЖурналаРегистрации(Событие, Уровень,,, Комментарий);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает строковую константу — имя корневого события ЖР
//
// Параметры:
//	нет
//
// Возвращаемое значение:
//	Строка - имя события
//
Функция ИмяСобытия()
	
	КодОсновногоЯзыка = ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка();
	Возврат НСтр("ru='Биллинг'", КодОсновногоЯзыка);
	
КонецФункции

#КонецОбласти