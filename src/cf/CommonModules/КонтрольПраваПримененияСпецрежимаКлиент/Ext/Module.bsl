#Область ПрограммныйИнтерфейс

// Возвращает признак необходимости контролировать право применения спецрежима для организации
// 
// Параметры:
//   Организация - СправочникСсылка.Организации
//   НаДату - Дата, Неопределено - дата проверки
// 
// Возвращаемое значение:
//   Булево - Истина, если нужно контролировать
//
Функция Контролировать(Организация, Знач НаДату = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НаДату) Тогда
		НаДату = ОбщегоНазначенияКлиент.ДатаСеанса();
	КонецЕсли;
	
	Возврат КонтрольПраваПримененияСпецрежимаКлиентПовтИсп.Контролировать(Организация, НачалоМесяца(НаДату));
	
КонецФункции

// Обработчик нажатия гиперссылки "Подробнее" в баннере контроля права применения спецрежима
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма, из которой открывается пояснение ссылки
//
Процедура ПодробнееНажатие(Форма) Экспорт
	
	Если Форма.СсылкаНаПояснение = "УтратаПраваПримененияНПД" Тогда
		
		ПараметрыВыполнения = Новый Структура;
		ПараметрыВыполнения.Вставить("ВидОрганизации", ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо"));
		Если Форма.Элементы.Найти("ОтборОрганизация") <> Неопределено Тогда
			ПараметрыВыполнения.Вставить("Организация", Форма.ОтборОрганизация);
		ИначеЕсли Форма.Элементы.Найти("ОтборТекущаяОрганизация") <> Неопределено Тогда
			ПараметрыВыполнения.Вставить("Организация", Форма.ОтборТекущаяОрганизация);
		КонецЕсли;
		
		ПараметрыВыполнения.Вставить("УтратаПраваНПД", Истина);
		
		ОткрытьФорму("Обработка.СравнениеРежимовНалогообложения.Форма",
			ПараметрыВыполнения,
			Форма,
			Форма.УникальныйИдентификатор);
		
	Иначе
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(Форма.СсылкаНаПояснение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти