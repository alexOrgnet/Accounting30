#Область ПрограммныйИнтерфейс

// Открывает форму соответствующего вида заявления
//
// Параметры:
//  ВидЗаявления - ПеречислениеСсылка.ВидыУведомленийОСпецрежимахНалогообложения - вид заявления
//  ПараметрыФормы - Структура - параметры формы заявления
//   * Организация - СправочникСсылка.Организации
//  ВладелецФормы - Управляемая форма - владелец формы
//
Процедура ОткрытьФормуЗаявления(ВидЗаявления, ПараметрыФормы, ВладелецФормы = Неопределено) Экспорт
	
	ИмяФормыЗаявления = УчетПСНВызовСервера.ИмяФормыЗаявления(ВидЗаявления);
	Если ПустаяСтрока(ИмяФормыЗаявления) Тогда
		Возврат;
	КонецЕсли;
	
	Если ВидЗаявления = ПредопределенноеЗначение(
			"Перечисление.ВидыУведомленийОСпецрежимахНалогообложения.ЗаявлениеОбУтратеПраваНаПатент") Тогда
		
		// При утрате права, спрашиваем у пользователя дату события
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ИмяФормыЗаявления", ИмяФормыЗаявления);
		ДополнительныеПараметры.Вставить("ПараметрыФормы",    ПараметрыФормы);
		ДополнительныеПараметры.Вставить("ВладелецФормы",     ВладелецФормы);
		
		ОповещениеОВыборе = Новый ОписаниеОповещения("ВыборДатыУтратыПраваЗаявленияЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		
		ОткрытьФорму("Справочник.Патенты.Форма.ФормаВыборДатыУтратыПрава", , ВладелецФормы, , , , ОповещениеОВыборе);
		
	Иначе
		
		ОткрытьФорму(ИмяФормыЗаявления, ПараметрыФормы, ВладелецФормы);
		
	КонецЕсли;
	
КонецПроцедуры

// Дополнительная обработка перед заполнением заявления по кнопке Заполнить
//
// Параметры:
//   ПараметрыОтчета - Структура
//     * Организация - СправочникСсылка.Организации
//   ФормаОтчета - Управляемая форма - форма заявления
//   ОписаниеОповещения - ОписаниеОповещения - обратный вызов формы отчета
//
Процедура ПередЗаполнениемЗаявленияУтратаПраваНаПатент(ПараметрыОтчета, ФормаОтчета, ОписаниеОповещения) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОписаниеОповещения", ОписаниеОповещения);

	ОповещениеОВыборе = Новый ОписаниеОповещения("ПередЗаполнениемЗаявленияУтратаПраваПатентЗавершение",
		ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму("Справочник.Патенты.Форма.ФормаВыборДатыУтратыПрава", , ФормаОтчета, , , , ОповещениеОВыборе);
	
КонецПроцедуры

// Дополнительная обработка перед заполнением заявления по кнопке Заполнить
//
// Параметры:
//   ПараметрыОтчета - Структура
//     * Организация - СправочникСсылка.Организации
//   ФормаОтчета - Управляемая форма - форма заявления
//   ОписаниеОповещения - ОписаниеОповещения - обратный вызов формы отчета
//
Процедура ПередЗаполнениемЗаявленияПрекращениеПатента(ПараметрыОтчета, ФормаОтчета, ОписаниеОповещения) Экспорт
	
	Перем Организация;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОписаниеОповещения", ОписаниеОповещения);
	
	ОповещениеОВыборе = Новый ОписаниеОповещения("ПередЗаполнениемЗаявленияПрекращениеПатентаЗавершение",
		ЭтотОбъект, ДополнительныеПараметры);
	
	Отбор = Новый Структура;
	
	ПараметрыФормыВыбора = Новый Структура;
	Если ПараметрыОтчета.Свойство("Организация", Организация) И ЗначениеЗаполнено(Организация) Тогда
		Отбор.Вставить("Организация", Организация);
	КонецЕсли;
	
	ПараметрыФормыВыбора.Вставить("Отбор", Отбор);
	
	ОткрытьФорму("Документ.ОперацияСПатентом.ФормаВыбора", ПараметрыФормыВыбора, ФормаОтчета, , , , ОповещениеОВыборе);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область Печать

Функция ВыполнитьКомандуПечати(ОписаниеКоманды) Экспорт
	
	УведомленияДляПечати = УчетПСНВызовСервера.НастройкиПечатиУведомлений(ОписаниеКоманды.ОбъектыПечати);
	ЗаголовкиФормыПечати = УчетПСНВызовСервера.ЗаголовкиФормыПечати(УведомленияДляПечати);
	
	Для Каждого УведомлениеДляПечати Из УведомленияДляПечати Цикл
		
		СписокПечатаемыхЛистов =
			УведомлениеОСпецрежимахНалогообложенияВызовСервера.ПечатьУведомленияБРО(УведомлениеДляПечати);
		
		РегламентированнаяОтчетностьКлиент.ОткрытьФормуПредварительногоПросмотра(ОписаниеКоманды.Форма, , Ложь,
			СписокПечатаемыхЛистов, ЗаголовкиФормыПечати[УведомлениеДляПечати]);
		
	КонецЦикла;
	
КонецФункции

Функция ВыполнитьКомандуПечатиPDF417(ОписаниеКоманды) Экспорт
	
	// Команда для формы документа, в котором есть экспортный метод
	ОписаниеКоманды.Форма.ПоказатьСДвухмернымШтрихкодомPDF417();
	
КонецФункции

#КонецОбласти

#Область СозданиеНовогоПатента

Процедура ОткрытьФормуСозданияНовогоПатента(ВладелецФормы, Организация, Дата, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ДанныеЗаполнения = Новый Структура();
	ДанныеЗаполнения.Вставить("Организация",   Организация);
	ДанныеЗаполнения.Вставить("ДатаНачала",    НачалоГода(Дата));
	ДанныеЗаполнения.Вставить("ДатаОкончания", КонецГода(Дата));
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ДанныеЗаполнения);
	
	ОткрытьФорму(
		"Документ.ОперацияСПатентом.ФормаОбъекта",
		ПараметрыФормы,
		ВладелецФормы,
		,
		,
		,
		,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Процедура ОтражениеДоходаОбработкаВыбора(Форма, Элемент, ВыбранноеЗначение, СтандартнаяОбработка) Экспорт
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект = Форма.Объект;
	
	ВариантыОтраженияДоходов = БанкИКассаФормыКлиентСервер.ВариантыОтраженияДоходов(Форма);
	
	НовоеОтражениеДохода = БанкИКассаФормыКлиентСервер.ВариантОтраженияДоходовЗначение(ВыбранноеЗначение, ВариантыОтраженияДоходов);
	
	ОтражениеДоходаСоздатьПатент = УчетПСНКлиентСервер.ОтражениеДоходовСоздатьПатентЗначение();
	
	Если НовоеОтражениеДохода = ОтражениеДоходаСоздатьПатент Тогда
		
		// Первый вызов - выбран пункт "Создать патент..."
		// Перед открытием формы нового патента выбранное значение очищается и дальнейшая обработка прерывается.
		
		ВыбранноеЗначение = Неопределено;
		
		ОткрытьФормуСозданияНовогоПатента(Элемент, Объект.Организация, Объект.Дата, СтандартнаяОбработка);
		
	ИначеЕсли НовоеОтражениеДохода = Неопределено
		И ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.ОперацияСПатентом") Тогда
		
		// После создания нового патента обработка выбора вызывается повторно.
		// Необходимо дополнить варианты отражения доходов и список выбора поля,
		// а затем продолжить выбор стандартным поведением поля: в выбранном значении заменить патент представлением нового варианта.
		
		ЭлементНовыйПатент = ВариантыОтраженияДоходов.НайтиПоЗначению(ОтражениеДоходаСоздатьПатент);
		
		Если ЭлементНовыйПатент <> Неопределено Тогда
			
			// Вставим вариант отражения доходов по новому патенту перед пунктом "Создать патент..."
			
			ВыбранныйПатент = УчетПСНВызовСервера.ДеятельностьПоПатенту(ВыбранноеЗначение);
			
			ПредставлениеВарианта = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Доход по патенту ""%1""'"),
				ВыбранныйПатент);
			
			ВариантыОтраженияДоходов.Вставить(ВариантыОтраженияДоходов.Индекс(ЭлементНовыйПатент), ВыбранныйПатент, ПредставлениеВарианта);
			
			БанкИКассаФормыКлиентСервер.ОбновитьСписокВыбораОтраженияДоходов(Элемент.СписокВыбора, ВариантыОтраженияДоходов);
			
			// В выбранное значение передадим представление отражения доходов по новому патенту.
			ВыбранноеЗначение = ПредставлениеВарианта;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПатентОбработкаВыбора(Форма, Элемент, ВыбранноеЗначение, СтандартнаяОбработка) Экспорт
	
	// Обработчик не должен вызываться из неподготовленных форм, поэтому вызовем исключение,
	// если вызывающая форма не соответствует требованиям.
	УчетПСНКлиентСервер.ПроверитьВозможностьВыбораПатента(Форма);
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект = Форма.Объект;
	
	СписокПатентов = УчетПСНКлиентСервер.СписокПатентов(Форма);
	
	ВыбранныйПатент = УчетПСНКлиентСервер.ВыбранныйПатентЗначение(ВыбранноеЗначение, СписокПатентов);
	
	ЗначениеСоздатьПатент = УчетПСНКлиентСервер.ОтражениеДоходовСоздатьПатентЗначение();
	
	Если ВыбранныйПатент = ЗначениеСоздатьПатент Тогда
		
		// Первый вызов - выбран пункт "Создать патент..."
		// Перед открытием формы нового патента выбранное значение очищается и дальнейшая обработка прерывается.
		
		ВыбранноеЗначение = Неопределено;
		
		ОткрытьФормуСозданияНовогоПатента(Элемент, Объект.Организация, Объект.Дата, СтандартнаяОбработка);
		
	ИначеЕсли ВыбранныйПатент = Неопределено
		И ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.ОперацияСПатентом") Тогда
		
		// После создания нового патента обработка выбора вызывается повторно.
		// Необходимо дополнить варианты отражения доходов и список выбора поля,
		// а затем продолжить выбор стандартным поведением поля: в выбранном значении заменить патент представлением нового варианта.
		
		ЭлементНовыйПатент = СписокПатентов.НайтиПоЗначению(ЗначениеСоздатьПатент);
		
		Если ЭлементНовыйПатент <> Неопределено Тогда
			
			ВыбранныйПатент = УчетПСНВызовСервера.ДеятельностьПоПатенту(ВыбранноеЗначение);
			
			ПредставлениеПатента = Строка(ВыбранныйПатент);
			
			СписокПатентов.Вставить(СписокПатентов.Индекс(ЭлементНовыйПатент), ВыбранныйПатент, ПредставлениеПатента);
			
			УчетПСНКлиентСервер.ОбновитьСписокВыбораПатента(Элемент.СписокВыбора, СписокПатентов);
			
			ВыбранноеЗначение = ПредставлениеПатента;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПатентПриИзменении(Форма, ИзменяемыйРеквизит, НовоеПредставление) Экспорт
	
	// Обработчик не должен вызываться из неподготовленных форм, поэтому вызовем исключение,
	// если вызывающая форма не соответствует требованиям.
	УчетПСНКлиентСервер.ПроверитьВозможностьВыбораПатента(Форма);
	
	СписокПатентов = УчетПСНКлиентСервер.СписокПатентов(Форма);
	
	Если ЗначениеЗаполнено(НовоеПредставление) Тогда
		
		// Пользователь выбрал представление - обновим значение связанного реквизита.
		ИзменяемыйРеквизит = УчетПСНКлиентСервер.ВыбранныйПатентЗначение(НовоеПредставление, СписокПатентов);
		
	Иначе
		
		// Если новое представление пользователем не выбрано - очистим связанный реквизит.
		ИзменяемыйРеквизит = Неопределено;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

// См. РегламентированнаяОтчетностьКлиентПереопределяемый.ОбработчикСозданияУведомления
//
// Параметры:
//   Форма     - ФормаКлиентскогоПриложения - Форма, из которой создаетсся уведомление
//   Параметры - Структура - Параметры формы создания уведомления
//
Процедура ОбработчикСозданияУведомления(Форма, Параметры) Экспорт
	
	Если ТипЗнч(Параметры) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("Документ.УведомлениеОбУменьшенииНалогаПоПатенту.Форма.ФормаСозданияУведомления", Параметры, Форма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВыборДатыУтратыПраваЗаявленияЗавершение(ДатаУтратыПрава, ДополнительныеПараметры) Экспорт
	
	Если ДатаУтратыПрава = Неопределено Или ТипЗнч(ДатаУтратыПрава) <> Тип("Дата") Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ДополнительныеПараметры.ПараметрыФормы);
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("ДатаУтратыПрава", ДатаУтратыПрава);
	ПараметрыФормы.Вставить("ПараметрыЗаполнения", ПараметрыЗаполнения);
	
	ОткрытьФорму(
		ДополнительныеПараметры.ИмяФормыЗаявления,
		ПараметрыФормы,
		ДополнительныеПараметры.ВладелецФормы);
	
КонецПроцедуры

Процедура ПередЗаполнениемЗаявленияУтратаПраваПатентЗавершение(ДатаУтратыПрава, ДополнительныеПараметры) Экспорт
	
	Если ДатаУтратыПрава <> Неопределено Тогда
		ПараметрыЗаполнения = Новый Структура;
		ПараметрыЗаполнения.Вставить("ДатаУтратыПрава", ДатаУтратыПрава);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОписаниеОповещения, ПараметрыЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаполнениемЗаявленияПрекращениеПатентаЗавершение(Патент, ДополнительныеПараметры) Экспорт
	
	Если Патент <> Неопределено Тогда
		ПараметрыЗаполнения = Новый Структура;
		ПараметрыЗаполнения.Вставить("Патент", Патент);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОписаниеОповещения, ПараметрыЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

