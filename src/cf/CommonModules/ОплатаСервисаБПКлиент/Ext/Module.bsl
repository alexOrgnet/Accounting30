#Область ПрограммныйИнтерфейс

Процедура ОткрытьФормуВыбораТарифа(Владелец = Неопределено) Экспорт
	
	ОбслуживающиеОрганизации = ОплатаСервисаБПВызовСервера.ПолучитьСписокОбслуживающихОрганизаций();
	
	Если ОбслуживающиеОрганизации.Количество() = 1 Тогда
		КодОО = ОбслуживающиеОрганизации[0].Значение;
		ОткрытьФормуВыбораТарифаОбслуживающейОрганизации(КодОО, Владелец);
	Иначе
		ОбработчикВыбораОО = Новый ОписаниеОповещения("ОбработкаВыбораОбслуживающейОрганизации",
			ЭтотОбъект, Новый Структура("Владелец", Владелец));
		ОбслуживающиеОрганизации.ПоказатьВыборЭлемента(ОбработчикВыбораОО,
			НСтр("ru = 'Выберите обслуживающую организацию'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработкаВыбораОбслуживающейОрганизации(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(ВыбранныйЭлемент) <> Тип("ЭлементСпискаЗначений") Тогда
		Возврат;
	КонецЕсли;
	
	КодОО = ВыбранныйЭлемент.Значение;
	
	ОткрытьФормуВыбораТарифаОбслуживающейОрганизации(КодОО, ДополнительныеПараметры.Владелец);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораТарифаОбслуживающейОрганизации(КодОО, Владелец = Неопределено)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("КодОбслуживающейОрганизации", КодОО);
	ДополнительныеПараметры.Вставить("Владелец", Владелец);
	
	Оповещение = Новый ОписаниеОповещения("ВсеТарифыЗакрытие", ЭтотОбъект, ДополнительныеПараметры);
	
	ОплатаСервисаКлиент.ОткрытьФормуВыбораТарифа(КодОО, Владелец, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ВсеТарифыЗакрытие(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Если ЗначениеЗаполнено(Результат.КодТарифа) Тогда
			ОплатаСервисаБПВызовСервера.СохранитьВыбранныйТарифНаВремяСеанса(Результат.КодТарифа);
			
			РезультатЗапроса = ОплатаСервисаБПВызовСервера.ЗапроситьСчетПоВыбранномуТарифу(
				ДополнительныеПараметры.КодОбслуживающейОрганизации, Результат.КодТарифа, Результат.КодПериода);
			Если РезультатЗапроса.Ошибка Тогда
				ВызватьИсключение РезультатЗапроса.ОписаниеОшибки;
			Иначе
				ОткрытьФормуОбработкиДанных(РезультатЗапроса.ИмяФормыОбработкиОтвета,
					РезультатЗапроса.ИдентификаторОбъекта, РезультатЗапроса.ИдентификаторПравила,
					ДополнительныеПараметры.Владелец);
			КонецЕсли;
			
			Оповестить("ОплатаСервиса_Изменение");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуОбработкиДанных(ИмяФормыОбработкиОтвета, ИдентификаторОбъекта, ИдентификаторПравила, Владелец = Неопределено)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ИдентификаторОбъекта", ИдентификаторОбъекта);
	ПараметрыФормы.Вставить("ИдентификаторПравила", ИдентификаторПравила);
	ОткрытьФорму(ИмяФормыОбработкиОтвета, ПараметрыФормы, Владелец,,,,,
		?(Владелец <> Неопределено,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца,
			РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс));
	
КонецПроцедуры

#КонецОбласти