#Область СобытияФормВЕТИС

//Началовыбора транзакции
//Параметры:
//			Форма Тип("Форма")
//			Элемент Тип("ЭлементФормы")
//			ПараметрыОтбора Тип("Структура")
//			СтрандартнаяОбработка Тип("Булево")
//
//Возвращаемое значение: Нет
//
Процедура ВыбратьТразакцию_НачалоВыбора(Форма, Элемент, ПараметрыОтбора, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка 	= Ложь;
	ПараметрыФормы 			= Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора"		  , Истина);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе"  , Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор"  , Ложь);
	ПараметрыФормы.Вставить("ДокументОснование"   , Форма.Объект.ДокументОснование);
	ПараметрыФормы.Вставить("ХозяйствующийСубъект", Форма.Объект.ХозяйствующийСубъект);
	ПараметрыФормы.Вставить("Предприятие"         , Форма.Объект.Предприятие);
	ОткрытьФорму("Справочник.ИдентификаторыПроизводственныхТранзакцийБПВЕТИС.Форма.ФормаВыбора", ПараметрыФормы, Элемент);
	
КонецПроцедуры

//Обработка выбора
//Параметры:
//			Форма Тип("Форма")
//			Элемент Тип("ЭлементФормы")
//			ВыбранноеЗначение Тип("Любой")
//			СтрандартнаяОбработка Тип("Булево")
//
//Возвращаемое значение: Нет
//
Процедура ПриВыбореДействия_ОбработкаВыбора(Форма, Элемент, ВыбранноеЗначение, СтандартнаяОбработка) Экспорт
	
	УстановитьВидимостьФлагаЗавершенияТранзакции(Форма);
	УстановитьФлагЗавершенияТранзакции(Форма.Объект);
	
КонецПроцедуры

//Обработчик ПриСоздании новогоэлемента
//Параметры: 
//			Форма Тип("Форма")
//			Элемент Тип("ЭлементФормы")
//			СтандартнаяОбработка Тип("Булево")
//
//Возвращаемое значение: нет
//
Процедура ПроизводственнаяТранзакция_ПриСоздании(Форма, Элемент, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка 	= Ложь;
	ПараметрыФормы 			= Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Истина);
	ПараметрыФормы.Вставить("Основание", Форма.Объект);
	ОткрытьФорму("Справочник.ИдентификаторыПроизводственныхТранзакцийБПВЕТИС.Форма.ФормаЭлемента", ПараметрыФормы, Элемент);
	
КонецПроцедуры

//Устанавливает видимость флага завершения транзакции
//Параметры: Форма Тип("Форма")
//
//Возвращаемое значение: нет
//
Процедура УстановитьВидимостьФлагаЗавершенияТранзакции(Форма)
	
	Форма.Элементы.ЗавершениеПроизводственнойТранзакции.Видимость = 
		ИнтеграцияВЕТИСБПВызовСервера.ПолучитьВидимостьЭлементаЗавершенияПроизводственнойТранзакцииПоОснованию(
			Форма.Объект.ДокументОснование);
	
КонецПроцедуры

//Устанавливает флаг завршения производственной транзакции
//Параметры Объект Тип("ОбъектФормы")
//
//Возвращаемое значение: нет
//
Процедура УстановитьФлагЗавершенияТранзакции(Объект) Экспорт
	
	Если ИнтеграцияВЕТИСБПВызовСервера.ЭтоВыпускПродукции(Объект.ДокументОснование) Тогда
		Объект.ЗавершениеПроизводственнойТранзакции = Истина;
	ИначеЕсли ИнтеграцияВЕТИСБПВызовСервера.ЭтоОтчетПроизводстваЗаСмену(Объект.ДокументОснование) Тогда
		Транзакция = ИнтеграцияВЕТИСБПВызовСервера.НайтиТранзакцию(Объект.ДокументОснование);
		Если ЗначениеЗаполнено(Транзакция) Тогда
			Объект.ЗавершениеПроизводственнойТранзакции = Истина;
		Иначе
			Объект.ЗавершениеПроизводственнойТранзакции = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

//Открывает форму нового документа
//Параметры: ОснованиеСсылка 
//							Тип("ДокуметСсылка.ТребованиеНакладная")
//							Тип("ДокументСсылка.ОтчетПроизводстваЗаСмену")
//
//Возвращаемое значение: нет
//
Процедура ОткрытьФормуДокументаПроизводственнойОперацииВЕТИС(ОснованиеСсылка) Экспорт
	
	ДокСсылка = ИнтеграцияВЕТИСБПВызовСервера.НайтиСсылкуПроизводственнойОперациВЕТИС(ОснованиеСсылка);
	Если ЗначениеЗаполнено(ДокСсылка) Тогда
		ПараметрыФормы = Новый Структура("Ключ",ДокСсылка);
		ОткрытьФорму("Документ.ПроизводственнаяОперацияВЕТИС.Форма.ФормаДокумента",ПараметрыФормы);
	Иначе
		ДополнительныеПараметры = Новый Структура("Основание", ОснованиеСсылка);
		ОткрытьФорму("Документ.ПроизводственнаяОперацияВЕТИС.ФормаОбъекта", ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти