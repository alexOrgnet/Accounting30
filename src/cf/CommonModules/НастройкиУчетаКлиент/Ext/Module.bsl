#Область ПрограммныйИнтерфейс

#Область Антикризис

// Открывает форму настроек налогов и отчетов на закладке Антикризис.
// Для интерфейса интеграции с банком открывает форму настроек антикризисных мер.
//
// Параметры:
//  Организация                - СправочникСсылка.Организации
//  ИнтерфейсИнтеграцииСБанком - Булево - Признак, указывающий, что форма открыта в интерфейсе интеграции с банком
//
Процедура ОткрытьНастройкиАнтикризис(Организация, ИнтерфейсИнтеграцииСБанком = Ложь) Экспорт
	
	Если Не ИнтерфейсИнтеграцииСБанком Тогда
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("Организация",     Организация);
		ПараметрыОткрытия.Вставить("ТекущаяЗакладка", "Антикризис");
		ОткрытьФорму("ОбщаяФорма.НалогиИОтчеты", ПараметрыОткрытия, , Организация);
	Иначе
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("Организация", Организация);
		ИмяФормы = "РегистрСведений.НастройкиПродленияСроковНалоговОтчетов.Форма.ФормаАнтикризисИнтеграцияСБанком";
		НавигационнаяСсылка = "e1cib/command/РегистрСведений.НастройкиПродленияСроковНалоговОтчетов.Команда.НастройкиИнтеграцияСБанком";
		ОткрытьФорму(ИмяФормы, ПараметрыОткрытия, , Организация, , НавигационнаяСсылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

// Открываем форму отчета по документам, отображающую список документов,
// препятствующих включению плановой себестоимости.
//
// Параметры:
//  Период      - Дата - начало периода списка документов
//  Организация - СправочникСсылка.Организации - организация, документы которой отображаются
//  Владелец    - ФормаКлиентскогоПриложения - владелец формы отчета
//
Процедура ПоказатьСписокДокументовБезПлановойСебестоимости(Период, Организация, Владелец) Экспорт
	
	// Открываем форму отчета по документам и передаем туда все необходимые данные:
	// - имя метода, который готовит отчет
	// - наименование фонового задания
	// - прочие параметры, специфичные для выполняемого метода (процедуры)
	// Форма при открытии запускает формирование результата в фоновом режиме.
	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("Период",      Период);
	ПараметрыФормы.Вставить("Организация", Организация);
	
	ПараметрыФормы.Вставить(
		"ИмяМетода",
		"РегистрыСведений.УчетнаяПолитика.СформироватьСписокДокументовПрепятствующихВключениюПлановойСебестоимости");
	
	НаименованиеФоновогоЗадания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Построение списка документов без плановой себестоимости по организации %1 с даты %2'"),
		Организация,
		Формат(Период, "ДЛФ=D"));
	
	ПараметрыФормы.Вставить("НаименованиеФоновогоЗадания", НаименованиеФоновогоЗадания);
	
	ОткрытьФорму(
		"Обработка.ФункциональностьПрограммы.Форма.ФормаОтчетаПоДокументам",
		ПараметрыФормы,
		Владелец,
		Ложь);
	
КонецПроцедуры

// Открываем форму отчета по документам, отображающую список документов,
// препятствующих переключению режима инвентаризации НЗП.
//
// Параметры:
//  Владелец - ФормаКлиентскогоПриложения - владелец формы отчета
//
Процедура ПоказатьСписокДокументовИнвентаризацияНЗП(Владелец) Экспорт
	
	// Открываем форму отчета по документам и передаем туда все необходимые данные:
	// - имя метода, который готовит отчет
	// - наименование фонового задания
	// - прочие параметры, специфичные для выполняемого метода (процедуры)
	// Форма при открытии запускает формирование результата в фоновом режиме.
	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("Период",      Владелец.Период);
	ПараметрыФормы.Вставить("Организация", Владелец.Организация);
	
	ДополнительныеПараметрыОтчета = Новый Структура;
	ДополнительныеПараметрыОтчета.Вставить("УказыватьСуммуНезавершенногоПроизводстваВручную", Владелец.УказыватьСуммуНезавершенногоПроизводстваВручную);
	ДополнительныеПараметрыОтчета.Вставить("СчетУчета", Владелец.Счет);
	АдресДополнительныхПараметровОтчета = ПоместитьВоВременноеХранилище(ДополнительныеПараметрыОтчета);
	
	ПараметрыФормы.Вставить("АдресДополнительныхПараметровОтчета", АдресДополнительныхПараметровОтчета);
	
	ПараметрыФормы.Вставить(
		"ИмяМетода",
		"РегистрыСведений.УчетнаяПолитика.СформироватьСписокДокументовПрепятствующихПереключениюРежимаНЗП");
	
	НаименованиеФоновогоЗадания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Построение списка документов Инвентаризация незавершенного производства %1 по организации %2 с даты %3'"),
		?(Владелец.УказыватьСуммуНезавершенногоПроизводстваВручную, НСтр("ru = 'с автоматической оценкой'"), НСтр("ru = 'с указанием сумм вручную'")),
		Владелец.Организация,
		Формат(Владелец.Период, "ДЛФ=D"));
	
	ПараметрыФормы.Вставить("НаименованиеФоновогоЗадания", НаименованиеФоновогоЗадания);
	
	ОткрытьФорму(
		"Обработка.ФункциональностьПрограммы.Форма.ФормаОтчетаПоДокументам",
		ПараметрыФормы,
		Владелец,
		Ложь);
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// Универсальные методы для формы записи регистра и формы настройки налогов
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Процедура ОбработкаНавигационнойСсылкиСистемаНалогообложения(Форма, Организация, Период) Экспорт
	
	Если ЗначениеЗаполнено(Организация) И ЗначениеЗаполнено(Период) Тогда
		КлючЗаписиУчетнойПолитики = НастройкиУчетаВызовСервера.КлючЗаписиУчетнойПолитики("НастройкиСистемыНалогообложения", Организация, Период);
	КонецЕсли;
	
	Если КлючЗаписиУчетнойПолитики <> Неопределено Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ключ", КлючЗаписиУчетнойПолитики);
		
		ОткрытьФорму("РегистрСведений.НастройкиСистемыНалогообложения.ФормаЗаписи", ПараметрыФормы, Форма);
		
	Иначе
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Отбор", Новый Структура("Организация", Организация));
		
		ОткрытьФорму("РегистрСведений.НастройкиСистемыНалогообложения.ФормаСписка", ПараметрыФормы, Форма);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти