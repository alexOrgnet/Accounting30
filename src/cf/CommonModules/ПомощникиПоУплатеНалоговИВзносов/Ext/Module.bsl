#Область ПрограммныйИнтерфейс

Функция ПоказыватьКомандыОплаты() Экспорт
	
	Возврат Не ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком()
		Или ПолучитьФункциональнуюОпцию("ПоказыватьОплатуНалогаВПомощникахИнтерфейсИнтеграцииСБанком");
	
КонецФункции

// Возвращает таблицу со списком документов уплаты налогов и взносов.
//
// Параметры:
//  Организация	  - СправочникСсылка.Организации - организация-налогоплательщик.
//  НачалоПериода - Дата - начало периода, за который будут отбираться платежи.
//  КонецПериода  - Дата - окончание периода, за который будут отбираться платежи.
//  ПараметрыНалогов - Структура - Параметры налогов, для которых будут отбираться платежи. См.НовыеПараметрыУплатыНалогов()
// 
// Возвращаемое значение:
//  ТаблицаЗначений - таблица со списком документов оплаты налога.
//
Функция ДокументыУплатыНалогов(Организация, НачалоПериода, КонецПериода, ПараметрыНалогов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",   Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",  КонецДня(КонецПериода));
	Запрос.УстановитьПараметр("ВидыНалогов",   ПараметрыНалогов.ВидыНалогов);
	Запрос.УстановитьПараметр("ВидыНалоговыхОбязательств", ПараметрыНалогов.ВидыНалоговыхОбязательств);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВидыНалоговИПлатежейВБюджет.Ссылка КАК Ссылка,
	|	ВидыНалоговИПлатежейВБюджет.ВидНалога КАК ВидНалога
	|ПОМЕСТИТЬ ОтборНалог
	|ИЗ
	|	Справочник.ВидыНалоговИПлатежейВБюджет КАК ВидыНалоговИПлатежейВБюджет
	|ГДЕ
	|	ВидыНалоговИПлатежейВБюджет.ВидНалога В(&ВидыНалогов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиБухгалтераНалоговыеПлатежи.ПлатежноеПоручение КАК Ссылка,
	|	ЗадачиБухгалтераНалоговыеПлатежи.Правило КАК Правило,
	|	ЗадачиБухгалтераНалоговыеПлатежи.ПериодСобытия КАК ПериодСобытия
	|ПОМЕСТИТЬ ОтборПравило
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтераНалоговыеПлатежи КАК ЗадачиБухгалтераНалоговыеПлатежи
	|ГДЕ
	|	ЗадачиБухгалтераНалоговыеПлатежи.Организация = &Организация
	|	И ЗадачиБухгалтераНалоговыеПлатежи.ПериодСобытия МЕЖДУ &НачалоПериода И &КонецПериода
	|	И &УсловиеПоПравилу
	|	И &УсловиеПоТипамДокументов
	|	И &УсловиеПоОплатеЗадолженности
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПлатежноеПоручение.Ссылка КАК Ссылка,
	|	ПлатежноеПоручение.Дата КАК Дата,
	|	ПлатежноеПоручение.Номер КАК Номер,
	|	ПлатежноеПоручение.ВидНалоговогоОбязательства КАК ВидНалоговогоОбязательства,
	|	ПлатежноеПоручение.СуммаДокумента КАК Сумма,
	|	ЕСТЬNULL(СостоянияБанковскихДокументов.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияБанковскихДокументов.Подготовлено)) КАК Состояние,
	|	ВЫБОР
	|		КОГДА СостоянияБанковскихДокументов.Состояние ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		КОГДА СостоянияБанковскихДокументов.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияБанковскихДокументов.Оплачено)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Оплачено,
	|	ОтборНалог.ВидНалога КАК ВидНалога,
	|	ОтборПравило.ПериодСобытия КАК Период,
	|	ПлатежноеПоручение.ПоказательПериода КАК ПоказательПериода
	|ИЗ
	|	Документ.ПлатежноеПоручение КАК ПлатежноеПоручение
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтборПравило КАК ОтборПравило
	|		ПО ПлатежноеПоручение.Ссылка = ОтборПравило.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтборНалог КАК ОтборНалог
	|		ПО ПлатежноеПоручение.Налог = ОтборНалог.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияБанковскихДокументов КАК СостоянияБанковскихДокументов
	|		ПО (СостоянияБанковскихДокументов.Организация = &Организация)
	|			И ПлатежноеПоручение.Ссылка = СостоянияБанковскихДокументов.СсылкаНаОбъект
	|ГДЕ
	|	ПлатежноеПоручение.Организация = &Организация
	|	И НЕ ПлатежноеПоручение.ПометкаУдаления
	|	И ПлатежноеПоручение.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога)
	|	И ПлатежноеПоручение.ВидНалоговогоОбязательства В(&ВидыНалоговыхОбязательств)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасходныйКассовыйОрдер.Ссылка,
	|	РасходныйКассовыйОрдер.Дата,
	|	РасходныйКассовыйОрдер.Номер,
	|	РасходныйКассовыйОрдер.ВидНалоговогоОбязательства,
	|	РасходныйКассовыйОрдер.СуммаДокумента,
	|	ВЫБОР
	|		КОГДА РасходныйКассовыйОрдер.Проведен
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияБанковскихДокументов.Оплачено)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостоянияБанковскихДокументов.Подготовлено)
	|	КОНЕЦ,
	|	РасходныйКассовыйОрдер.Проведен,
	|	ОтборНалог.ВидНалога,
	|	ОтборПравило.ПериодСобытия,
	|	РасходныйКассовыйОрдер.ПоказательПериода
	|ИЗ
	|	Документ.РасходныйКассовыйОрдер КАК РасходныйКассовыйОрдер
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтборПравило КАК ОтборПравило
	|		ПО РасходныйКассовыйОрдер.Ссылка = ОтборПравило.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтборНалог КАК ОтборНалог
	|		ПО РасходныйКассовыйОрдер.Налог = ОтборНалог.Ссылка
	|ГДЕ
	|	РасходныйКассовыйОрдер.Организация = &Организация
	|	И НЕ РасходныйКассовыйОрдер.ПометкаУдаления
	|	И РасходныйКассовыйОрдер.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.УплатаНалога)
	|	И РасходныйКассовыйОрдер.ВидНалоговогоОбязательства В(&ВидыНалоговыхОбязательств)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СписаниеСРасчетногоСчета.Ссылка,
	|	СписаниеСРасчетногоСчета.Дата,
	|	СписаниеСРасчетногоСчета.Номер,
	|	СписаниеСРасчетногоСчета.ВидНалоговогоОбязательства,
	|	СписаниеСРасчетногоСчета.СуммаДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияБанковскихДокументов.ПустаяСсылка),
	|	СписаниеСРасчетногоСчета.Проведен,
	|	ОтборНалог.ВидНалога,
	|	ОтборПравило.ПериодСобытия,
	|	""""
	|ИЗ
	|	Документ.СписаниеСРасчетногоСчета КАК СписаниеСРасчетногоСчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтборПравило КАК ОтборПравило
	|		ПО СписаниеСРасчетногоСчета.Ссылка = ОтборПравило.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтборНалог КАК ОтборНалог
	|		ПО СписаниеСРасчетногоСчета.Налог = ОтборНалог.Ссылка
	|ГДЕ
	|	СписаниеСРасчетногоСчета.Организация = &Организация
	|	И НЕ СписаниеСРасчетногоСчета.ПометкаУдаления
	|	И СписаниеСРасчетногоСчета.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога)
	|	И СписаниеСРасчетногоСчета.ВидНалоговогоОбязательства В(&ВидыНалоговыхОбязательств)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка";
	
	Если ЗначениеЗаполнено(ПараметрыНалогов.Правило) Тогда
		Запрос.УстановитьПараметр("Правило", ПараметрыНалогов.Правило);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоПравилу", "ЗадачиБухгалтераНалоговыеПлатежи.Правило = &Правило");
	Иначе
		Запрос.УстановитьПараметр("КодыЗадач", ПараметрыНалогов.КодыЗадач);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоПравилу", "ЗадачиБухгалтераНалоговыеПлатежи.Правило.Владелец.Код В(&КодыЗадач)");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыНалогов.ТипыДокументов) Тогда
		Запрос.УстановитьПараметр("ТипыДокументов", ПараметрыНалогов.ТипыДокументов);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоТипамДокументов",
			"ТИПЗНАЧЕНИЯ(ЗадачиБухгалтераНалоговыеПлатежи.ПлатежноеПоручение) В (&ТипыДокументов)");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоТипамДокументов", "ИСТИНА");
	КонецЕсли;
	
	Если ТипЗнч(ПараметрыНалогов.ОтборОплатаЗадолженности) = Тип("Булево") Тогда
		ТекстУсловия = ?(ПараметрыНалогов.ОтборОплатаЗадолженности,
			"ЗадачиБухгалтераНалоговыеПлатежи.ОплатаЗадолженности",
			"НЕ ЗадачиБухгалтераНалоговыеПлатежи.ОплатаЗадолженности");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоОплатеЗадолженности", ТекстУсловия);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоОплатеЗадолженности", "ИСТИНА");
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Платежи = Запрос.Выполнить().Выгрузить();
	Платежи.Колонки.Добавить("ПредставлениеДокумента", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки()));
	Для Каждого Платеж Из Платежи Цикл
		Платеж.ПредставлениеДокумента = ПредставлениеДокументаОплаты(Платеж);
	КонецЦикла;
	
	Возврат Платежи;
	
КонецФункции

// Возвращает таблицу со списком не оплаченных платежных поручений по уплате налога ЕНС
//
// Параметры:
//  Организация  - СправочникСсылка.Организации - организация-налогоплательщик.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - таблица со списком платежных поручений по оплате налога ЕНС.
//
Функция НеИсполненныеПлатежныеПорученияОплатыНалогаЕНС(Организация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПлатежноеПоручение.Ссылка КАК Ссылка,
	|	ПлатежноеПоручение.Дата КАК Дата,
	|	ПлатежноеПоручение.Номер КАК Номер,
	|	ПлатежноеПоручение.ВидНалоговогоОбязательства КАК ВидНалоговогоОбязательства,
	|	ПлатежноеПоручение.СуммаДокумента КАК Сумма,
	|	ЕСТЬNULL(СостоянияБанковскихДокументов.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияБанковскихДокументов.Подготовлено)) КАК Состояние,
	|	ЛОЖЬ КАК Оплачено,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыНалогов.ЕдиныйНалоговыйПлатеж) КАК ВидНалога,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК Период,
	|	ПлатежноеПоручение.ПоказательПериода КАК ПоказательПериода
	|ИЗ
	|	Документ.ПлатежноеПоручение КАК ПлатежноеПоручение
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияБанковскихДокументов КАК СостоянияБанковскихДокументов
	|		ПО (СостоянияБанковскихДокументов.Организация = &Организация)
	|			И ПлатежноеПоручение.Ссылка = СостоянияБанковскихДокументов.СсылкаНаОбъект
	|ГДЕ
	|	ПлатежноеПоручение.Организация = &Организация
	|	И НЕ ПлатежноеПоручение.ПометкаУдаления
	|	И (СостоянияБанковскихДокументов.Состояние ЕСТЬ NULL
	|			ИЛИ НЕ(СостоянияБанковскихДокументов.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияБанковскихДокументов.Оплачено)
	|					ИЛИ СостоянияБанковскихДокументов.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияБанковскихДокументов.Отклонено)))
	|	И ПлатежноеПоручение.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога)
	|	И ПлатежноеПоручение.ВидНалоговогоОбязательства = ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВГосБюджет.Налог)
	|	И ПлатежноеПоручение.Налог = ЗНАЧЕНИЕ(Справочник.ВидыНалоговИПлатежейВБюджет.ЕдиныйНалоговыйПлатеж)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка";
	
	Запрос.Текст = ТекстЗапроса;
	
	Платежи = Запрос.Выполнить().Выгрузить();
	Платежи.Колонки.Добавить("ПредставлениеДокумента", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки()));
	Для Каждого Платеж Из Платежи Цикл
		Платеж.ПредставлениеДокумента = ПредставлениеДокументаОплаты(Платеж);
	КонецЦикла;
	
	Возврат Платежи;
	
КонецФункции

Функция ДополнитьТаблицуПлатежейСостояниямиОплаты(КоллекцияПлатежей) Экспорт
	
	Для Каждого Платеж Из КоллекцияПлатежей Цикл
		Если ТипЗнч(Платеж.Ссылка) = Тип("ДокументСсылка.ПлатежноеПоручение") Тогда
			Платеж.Состояние = РегистрыСведений.СостоянияБанковскихДокументов.ТекущееСостояниеДокумента(Платеж.Ссылка);
			Платеж.Оплачено  = Платеж.Состояние = Перечисления.СостоянияБанковскихДокументов.Оплачено;
		Иначе
			Платеж.Состояние = Перечисления.СостоянияБанковскихДокументов.ПустаяСсылка();
			Платеж.Оплачено  = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Платеж.Ссылка, "Проведен");
		КонецЕсли;
		Платеж.ПредставлениеДокумента = ПредставлениеДокументаОплаты(Платеж);
	КонецЦикла;
	
КонецФункции

// Возвращает структуру параметров для передачи в процедуру ДокументыУплатыНалогов
//
Функция НовыеПараметрыУплатыНалогов() Экспорт
	
	ТипПравило = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПустаяСсылка();
	
	ПараметрыУплатыНалогов = Новый Структура;
	ПараметрыУплатыНалогов.Вставить("ВидыНалогов",               Новый Массив); // Массив видов налогов.
	ПараметрыУплатыНалогов.Вставить("ВидыНалоговыхОбязательств", Новый Массив); // Массив видов налогов.
	ПараметрыУплатыНалогов.Вставить("Правило",                   ТипПравило);   // Правило, по которому оплачивались налоги и взносы.
	ПараметрыУплатыНалогов.Вставить("КодыЗадач",                 Новый Массив); // Массив кодов задач.
	ПараметрыУплатыНалогов.Вставить("ТипыДокументов",            Новый Массив); // Массив типов платежных документов.
	ПараметрыУплатыНалогов.Вставить("ОтборОплатаЗадолженности",  Неопределено); // По умолчанию отбора нет; если Ложь - только текущие платежи, если Истина - только погашение долга
	
	Возврат ПараметрыУплатыНалогов;
	
КонецФункции

Функция ПредставлениеДокументаОплаты(СтрокаКоллекцииПлатежей, РасчетСуммыВзноса = Ложь) Экспорт
	
	СвойстваПлатежа = Новый Структура("Ссылка, Дата, Номер, Сумма, Оплачено, Состояние, НомерВходящегоДокумента");
	ЗаполнитьЗначенияСвойств(СвойстваПлатежа, СтрокаКоллекцииПлатежей);
	
	Если Не ЗначениеЗаполнено(СвойстваПлатежа.Ссылка) Тогда
		Возврат "";
	КонецЕсли;
	
	ЭтоИнтерфейсИнтеграцииСБанком = ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
	Если ЭтоИнтерфейсИнтеграцииСБанком И РасчетСуммыВзноса Тогда
		НомерДокумента = СвойстваПлатежа.НомерВходящегоДокумента;
	Иначе
		НомерДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(СвойстваПлатежа.Номер, Истина, Ложь);
	КонецЕсли;
	
	ДатаДокумента = Формат(СвойстваПлатежа.Дата, "ДЛФ=D");
	
	ПредставлениеСуммы = ?(РасчетСуммыВзноса Или СвойстваПлатежа.Сумма = 0, "", 
		СтрШаблон(НСтр("ru = ' на сумму %1'"), ОбщегоНазначенияБПВызовСервера.ФорматСумм(СвойстваПлатежа.Сумма)));
		
	ТипДокумента = ТипЗнч(СвойстваПлатежа.Ссылка);
	Если ТипДокумента = Тип("ДокументСсылка.ПлатежноеПоручение") Тогда
		ШаблонПредставления = НСтр("ru = 'Платежное поручение %1 от %2%3'");
		ПредставлениеДокумента = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПредставления,
			НомерДокумента, ДатаДокумента, ПредставлениеСуммы);
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.СписаниеСРасчетногоСчета") Тогда
		ШаблонПредставления = НСтр("ru = 'Списание с расчетного счета %1 от %2%3'");
		ПредставлениеДокумента = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПредставления,
			НомерДокумента, ДатаДокумента, ПредставлениеСуммы);
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
		Если ЭтоИнтерфейсИнтеграцииСБанком Тогда
			ШаблонПредставления = НСтр("ru = 'Оплата наличными от %1%2'");
			ПредставлениеДокумента = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПредставления,
				ДатаДокумента, ПредставлениеСуммы);
		Иначе
			ШаблонПредставления = НСтр("ru = 'Выдача наличных %1 от %2%3'");
			ПредставлениеДокумента = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПредставления,
				НомерДокумента, ДатаДокумента, ПредставлениеСуммы);
		КонецЕсли;
	Иначе
		ШаблонПредставления = НСтр("ru = '%1 %2 от %3%4'");
		СинонимДокумента = СвойстваПлатежа.Ссылка.Метаданные().Синоним;
		ПредставлениеДокумента = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПредставления,
			СинонимДокумента, НомерДокумента, ДатаДокумента, ПредставлениеСуммы);
	КонецЕсли;
	
	Возврат ПредставлениеДокумента;
	
КонецФункции

// Устанавливает свойства декораций формы, отображающих платежи с суммой в тексте вместе с документом платежа.
// Добавляет элементы, если платежей больше, чем задано декораций в конфигураторе.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения
//   КоллекцияПлатежей - ТаблицаЗначений, ДанныеФормыКоллекция - коллекция со свойствами платежей
//     * Ссылка - ДокументСсылка - ссылка на документ
//     * Дата - Дата - дата документа
//     * Номер - Строка - номер документа
//     * Оплачено - Булево - Истина, если платежное поручение подтверждено выпиской
//     * Сумма - Число(15, 2) - Сумма документа
//   ПрефиксИмени - Строка - префикс имени декорации (например, "ДекорацияПлатеж")
//
Процедура ОтобразитьПлатежи(Форма, КоллекцияПлатежей, ПрефиксИмени) Экспорт
	
	Элементы = Форма.Элементы;
	
	КоличествоПредопределенных = КоличествоПредопределенныхЭлементов(Форма, ПрефиксИмени);
	Для Индекс = КоличествоПредопределенных По КоллекцияПлатежей.Количество() - 1 Цикл
		ДобавитьПредставление(Форма, Индекс, ПрефиксИмени);
	КонецЦикла;
	
	Для Индекс = 0 По КоллекцияПлатежей.Количество() - 1 Цикл
		ЭлементФормы = Элементы[ИмяЭлементаПредставления(Индекс, ПрефиксИмени)];
		ЭлементФормы.Видимость = Истина;
		ЭлементФормы.Заголовок = ПредставлениеПлатежа(КоллекцияПлатежей[Индекс], Истина);
	КонецЦикла;
	
	Для Индекс = КоллекцияПлатежей.Количество() По КоличествоПредопределенных - 1 Цикл
		ЭлементФормы = Элементы[ИмяЭлементаПредставления(Индекс, ПрефиксИмени)];
		ЭлементФормы.Видимость = Ложь;
	КонецЦикла;
	
КонецПроцедуры

// Устанавливает свойства декораций формы, отображающих документы в списке.
// Добавляет элементы, если документов больше, чем задано декораций в конфигураторе
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  КоллекцияДокументов - ДанныеФормыКоллекция - коллекция со свойствами документов
//   * Ссылка - ДокументСсылка.УведомлениеОСпецрежимахНалогообоожения - ссылка на уведомление
//   * Дата - Дата - дата уведомления
//   * Сумма - Число - сумма уменьшения налога по данным уведомления
//   * Сдано - Булево - используется для регламентированных уведомлений, признак того, что уведомление сдано
//   * СтатусПредставление - Строка - представление статуса документа
//   * Представление - Строка - представление документа для отображения в списке
//  ПрефиксИмени - Строка - префикс имени декорации (например, "Уведомление")
//
Процедура ОтобразитьДокументы(Форма, КоллекцияДокументов, ПрефиксИмени) Экспорт
	
	Элементы = Форма.Элементы;
	
	КоличествоПредопределенных = КоличествоПредопределенныхЭлементов(Форма, ПрефиксИмени);
	Для Индекс = КоличествоПредопределенных По КоллекцияДокументов.Количество() - 1 Цикл
		ДобавитьПредставление(Форма, Индекс, ПрефиксИмени);
	КонецЦикла;
	
	Для Индекс = 0 По КоллекцияДокументов.Количество() - 1 Цикл
		ЭлементФормы = Элементы[ИмяЭлементаПредставления(Индекс, ПрефиксИмени)];
		ЭлементФормы.Видимость = Истина;
		ЭлементФормы.Заголовок = ПредставлениеУведомления(КоллекцияДокументов[Индекс]);
	КонецЦикла;
	
	Для Индекс = КоллекцияДокументов.Количество() По КоличествоПредопределенных - 1 Цикл
		ЭлементФормы = Элементы[ИмяЭлементаПредставления(Индекс, ПрефиксИмени)];
		ЭлементФормы.Видимость = Ложь;
	КонецЦикла;
	
КонецПроцедуры

// Устанавливает свойства декораций формы, отображающих платежи с суммой в отдельной колонке.
// Добавляет элементы, если платежей больше, чем задано декораций в конфигураторе.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения
//   КоллекцияПлатежей - ДанныеФормыКоллекция - коллекция со свойствами платежей
//     * Ссылка - ДокументСсылка - ссылка на документ
//     * Дата - Дата - дата документа
//     * Номер - Строка - номер документа
//     * Оплачено - Булево - Истина, если платежное поручение подтверждено выпиской
//     * Сумма - Число(15, 2) - Сумма документа
//   ПрефиксИмени - Строка - префикс имени декорации (например, "ДекорацияПлатеж")
//
Процедура ОтобразитьПлатежиССуммойВОтдельнойКолонке(Форма, КоллекцияПлатежей, ПрефиксИмени) Экспорт
	
	Элементы = Форма.Элементы;
	
	КоличествоПредопределенных = КоличествоПредопределенныхЭлементов(Форма, ПрефиксИмени);
	Для Индекс = КоличествоПредопределенных По КоллекцияПлатежей.Количество() - 1 Цикл
		ДобавитьПредставлениеПлатежаССуммойВОтдельнойКолонке(Форма, Индекс, ПрефиксИмени);
	КонецЦикла;
	
	Для Индекс = 0 По КоллекцияПлатежей.Количество() - 1 Цикл
		ЭлементФормы = Элементы[ИмяЭлементаПредставления(Индекс, ПрефиксИмени)];
		ЭлементФормы.Видимость = Истина;
		ЭлементФормы.Заголовок = ПредставлениеПлатежа(КоллекцияПлатежей[Индекс], Ложь);
		
		ЭлементФормы = Элементы[ИмяЭлементаПредставлениеСуммыПлатежа(Индекс, ПрефиксИмени)];
		ЭлементФормы.Видимость = Истина;
		ЭлементФормы.Заголовок = Формат(КоллекцияПлатежей[Индекс].Сумма, "Л=ru; ЧЦ=15; ЧДЦ=2; ЧН=0,00");
	КонецЦикла;
	
	Для Индекс = КоллекцияПлатежей.Количество() По КоличествоПредопределенных - 1 Цикл
		ЭлементФормы = Элементы[ИмяЭлементаПредставления(Индекс, ПрефиксИмени)];
		ЭлементФормы.Видимость = Ложь;
		
		ЭлементФормы = Элементы[ИмяЭлементаПредставлениеСуммыПлатежа(Индекс, ПрефиксИмени)];
		ЭлементФормы.Видимость = Ложь;
	КонецЦикла;
	
КонецПроцедуры

Функция ПроверитьПолнотуУплаты(СуммаКУплате, КоллекцияПлатежей) Экспорт
	
	РезультатПроверки = Новый Структура();
	РезультатПроверки.Вставить("НалогУплаченНеВерно",           Ложь);
	РезультатПроверки.Вставить("СообщениеОбОшибкеУплатыНалога", "");
	
	СуммаПлатежей = КоллекцияПлатежей.Итог("Сумма");
	
	Если СуммаПлатежей <> 0 И СуммаПлатежей <> СуммаКУплате Тогда
		
		РезультатПроверки.НалогУплаченНеВерно = Истина;
		
		ЧастиСообщения = Новый Массив;
		
		ЧастиСообщения.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Сумма оплаты (%1) отличается от рассчитанной (%2)'"),
				ОбщегоНазначенияБПВызовСервера.ФорматСумм(СуммаПлатежей),
				ОбщегоНазначенияБПВызовСервера.ФорматСумм(СуммаКУплате, , НСтр("ru = '0,00'"))));
		
		Разница = СуммаКУплате - СуммаПлатежей;
		
		Если Разница > 0 Тогда
			ЧастиСообщения.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'не оплачено %1'"),
				ОбщегоНазначенияБПВызовСервера.ФорматСумм(Разница)));
		Иначе
			ЧастиСообщения.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'переплата %1'"),
				ОбщегоНазначенияБПВызовСервера.ФорматСумм(-Разница)));
		КонецЕсли;
		
		РезультатПроверки.СообщениеОбОшибкеУплатыНалога = СтрСоединить(ЧастиСообщения, ", ");
		
	КонецЕсли;
	
	РезультатПроверки.Вставить("НалогОплаченПолностью", СуммаПлатежей >= СуммаКУплате);
	
	Возврат РезультатПроверки;
	
КонецФункции

Функция СуммаОплаты(КоллекцияПлатежей) Экспорт
	
	СуммаОплачено = 0;
	
	Если КоллекцияПлатежей = Неопределено Тогда
		Возврат СуммаОплачено;
	КонецЕсли;
	
	Для Каждого Платеж Из КоллекцияПлатежей Цикл
		
		Если Платеж.Оплачено Тогда
			СуммаОплачено = СуммаОплачено + Платеж.Сумма;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СуммаОплачено;
	
КонецФункции

Функция ПоказыватьПереплатуПоНалогуВзносу(Организация, Период) Экспорт
	
	Если НЕ ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ДатаРегистрации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ДатаРегистрации");
	
	Возврат НачалоГода(ДатаРегистрации) < НачалоГода(Период);
	
КонецФункции

// Возвращает структуру параметров, используемых в процедурах изменения остатков расчетов по налогам и взносам.
//
Функция НовыеПараметрыИзмененияОстатковРасчетовПоНалогамВзносамИП() Экспорт
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Организация",             Справочники.Организации.ПустаяСсылка());
	СтруктураПараметров.Вставить("ПериодСобытия",           Дата(1,1,1));
	СтруктураПараметров.Вставить("ТаблицаОстатков",         Неопределено);
	СтруктураПараметров.Вставить("Комментарий",             "");
	
	Возврат СтруктураПараметров;
	
КонецФункции

// Возвращает описание таблицы для изменения остатков расчетов по налогам и взносам.
//
Функция НоваяТаблицаИзмененияРасчетовПоНалогамВзносамИП() Экспорт
	
	ТаблицаИзмененияРасчетов = Обработки.ВводНачальныхОстатков.НоваяТаблицаВводаОстатков("НалогиИВзносы");
	ТаблицаИзмененияРасчетов.Колонки.Добавить("СуммаИзмененияПереплаты", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	
	Возврат ТаблицаИзмененияРасчетов;
	
КонецФункции

// Изменяет остатки расчетов по налогам и взносам документом "ОперацияБух".
//
// Параметры:
//  Параметры - Структура - структура параметров выполнения операции. 
//                          см. НовыеПараметрыИзмененияОстатковРасчетовПоНалогамВзносамИП()
//  АдресРезультата - Строка - не используется.
//
Процедура СохранитьПереплатуПоНалогамВзносамИнтеграцияСБанком(Параметры, АдресРезультата) Экспорт
	
	Если НЕ ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком() Тогда
		Возврат;
	КонецЕсли;
	
	ДатаДокумента = НачалоГода(Параметры.ПериодСобытия) - 1;
	
	ОперацияБух = Документы.ОперацияБух.СоздатьДокумент();
	ОперацияБух.Организация = Параметры.Организация;
	ОперацияБух.Дата = ДатаДокумента;
	ОперацияБух.Комментарий = Параметры.Комментарий;
	
	ОперацияБух.Записать(РежимЗаписиДокумента.Запись);
	
	Набор = РегистрыБухгалтерии.Хозрасчетный.СоздатьНаборЗаписей();
	Набор.Отбор.Регистратор.Установить(ОперацияБух.Ссылка, Истина);
	Для Каждого СтрокаТаблицы Из Параметры.ТаблицаОстатков Цикл
		
		Проводка = Набор.Добавить();
		
		Проводка.Активность  = Истина;
		Проводка.Организация = Параметры.Организация;
		Проводка.Период      = ДатаДокумента;
		Проводка.СчетДт      = СтрокаТаблицы.СчетУчета;
		Проводка.СчетКт      = ПланыСчетов.Хозрасчетный.ПрибыльПодлежащаяРаспределению;
		
		БухгалтерскийУчет.УстановитьСубконто(
			Проводка.СчетДт,
			Проводка.СубконтоДт,
			"ВидыПлатежейВГосБюджет",
			СтрокаТаблицы.ВидПлатежаВБюджет);
		
		Проводка.Сумма       = СтрокаТаблицы.СуммаИзмененияПереплаты;
		
	КонецЦикла;
	
	Набор.Записать(Истина);
	
КонецПроцедуры

Функция ПлатежиДляОтображения(КоллекцияПлатежей, СостоянияИнтеграцииДокументов) Экспорт
	
	ПлатежиДляОтображения = КоллекцияПлатежей.Выгрузить();
	
	Если ЗначениеЗаполнено(СостоянияИнтеграцииДокументов) Тогда
		РегистрыСведений.ДокументыИнтеграцииСБанком.УдалитьИзТаблицыПлатежейДокументыКОтправке(
			ПлатежиДляОтображения, СостоянияИнтеграцииДокументов);
	КонецЕсли;
	
	Возврат ПлатежиДляОтображения;
	
КонецФункции

// Проверяет, является ли данный отчетный период самым первым с даты начала деятельности организации.
//
// Параметры:
//  Организация     - СправочникСсылка.Организации - проверяемая организация.
//  Период          - Дата - проверяемый период.
//  ДанныеПравила   - Структура - сведения об особенностях отчета/налога.
//                       Ключи структуры:
//                        * ФинансовыйПериод - ПеречислениеСсылка.Периодичность - периодичность предоставления отчета / уплаты налога.
//                        * РасширенныйПервыйНалоговыйПериод - ПеречислениеСсылка.ВариантыРасширенногоПервогоНалоговогоПериода -
//                                             вариант особенностей законодательства для первого налогового/отчетного периода.
//                  - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов -
//                       правило, описывающее отчет/налог. Особенности отчета/налога будут определены из реквизитов правила.
//  ДатаРегистрации - Дата - дата регистрации проверяемой организации; если не передана - будет получена в данной функции.
//
// Возвращаемое значение:
//   Булево   - Если ИСТИНА, это первый отчетный период.
//
Функция ЭтоПервыйОтчетныйПериод(Организация, Период, ДанныеПравила, ДатаРегистрации = Неопределено) Экспорт
	
	ОсобенностиОтчета = Новый Структура("ФинансовыйПериод, РасширенныйПервыйНалоговыйПериод");
	
	ТипДанныхПравила = ТипЗнч(ДанныеПравила);
	
	Если ТипДанныхПравила = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ОсобенностиОтчета, ДанныеПравила);
	ИначеЕсли ТипДанныхПравила = Тип("СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов") Тогда
		ОсобенностиОтчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеПравила, ОсобенностиОтчета);
	Иначе
		// Невозможно получить данные об отчетном периоде без особенностей отчета, описанных в правиле.
		Возврат Неопределено;
	КонецЕсли;
	
	ПервыйОтчетныйПериод = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПервыйФинансовыйПериод(
		ОсобенностиОтчета, Организация, ДатаРегистрации);
	
	НачалоПроверяемогоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(ОсобенностиОтчета.ФинансовыйПериод, Период);
	
	Возврат НачалоПроверяемогоПериода <= ПервыйОтчетныйПериод;
	
КонецФункции

// Определяет границу периода, в котором отчетность в программе
// не заполняется автоматически, т.к. пользователь не вел учет в программе. Если такой границы нет,
// например, пользователь начал вести учет сразу после того, как зарегистрировался, то возвращается
// Неопределено.
//
// Параметры:
//  Организация	 - СправочникСсылка.Органиации - организация, для которой нужно определить границу.
// 
// Возвращаемое значение:
//   - Дата, Неопределено - граница отчетности прошлых лет. Если отчетности прошлых лет в программе
//                          быть не может, возвращается Неопределено.
//
Функция ГраницаОтчетностиПрошлыхПериодов(Организация) Экспорт
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	НачалоВеденияУчета = Справочники.Организации.ДатаНачалаВеденияУчета(Организация);
	
	Если Не ЗначениеЗаполнено(НачалоВеденияУчета) Тогда
		// Пользователь не ввел начальные остатки и не провел ни одного документа.
		// В этом случае считаем, что он начнет вести учет в этом году.
		Возврат НачалоГода(ТекущаяДатаСеанса()) - 1;
	КонецЕсли;
	
	// Если пользователь начал вести учет в том же году, что и зарегистрировался - то у него нет
	// отчетности прошлых лет и ему возвращается Неопределено.
	// Если пользователь начал вести учет в последующих годах - то период отчетности
	// прошлых лет заканчивается 31 декабря года, который идет перед годом начала ведения учета.
	ГодНачалаВеденияУчета = НачалоГода(НачалоВеденияУчета);
	Если ГодНачалаВеденияУчета > КалендарьБухгалтера.ДатаНачалаДеятельности(Организация) Тогда
		Возврат ГодНачалаВеденияУчета - 1;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#Область ОписаниеОтчетов

// Возвращает структуру для вывода описания отчета в формах помощников.
// Структура описывается в НовыйОписаниеОтчета()
//
// Параметры:
//  Организация		 - СправочникСсылка.Организации - Организация, для которой нужно вывести описание отчета.
//  Правило			 - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов - Правило, по которому нужно вывести описание отчета.
//  ПериодСобытия	 - Дата - Период отчета.
// 
// Возвращаемое значение:
//   - Структура - Структура с описанием отчета. См. НовыйОписаниеОтчета
//
Функция ОписаниеОтчета(Организация, Правило, ПериодСобытия) Экспорт
	
	Описание = НовыйОписаниеОтчета();
	
	Если НЕ ЗначениеЗаполнено(Правило) Тогда
		Возврат Описание;
	КонецЕсли;
	
	ДатаНачалаДеятельности = КалендарьБухгалтера.ДатаНачалаДеятельности(Организация);
	
	ОписаниеРеквизитов = Новый Структура;
	ОписаниеРеквизитов.Вставить("ИдентификаторЗадачи", "Владелец.Код");
	ОписаниеРеквизитов.Вставить("ФинансовыйПериод");
	ОписаниеРеквизитов.Вставить("РасширенныйПервыйНалоговыйПериод");
	
	РеквизитыПравила = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Правило, ОписаниеРеквизитов);
	
	ВидНалога = РасчетыСБюджетом.ВидНалогаПоКодуЗадачи(РеквизитыПравила.ИдентификаторЗадачи, Организация);
	
	Порядок = РегистрыСведений.ЗадачиБухгалтера.ПорядокПредоставленияОтчетаУплатыНалогаЗаПериод(Организация,
		ВидНалога, ПериодСобытия, Перечисления.ВидыДействийКалендаряБухгалтера.Отчет);
		
	Если Порядок = Неопределено Тогда
		Возврат Описание;
	КонецЕсли;
	
	НачалоВыполнения   = Порядок.Отчет.НачалоВыполнения;
	Срок               = Порядок.Отчет.Срок;
	НаименованиеОтчета = Порядок.Отчет.Наименование;
	
	ЭтоПервыйОтчет = ПомощникиПоУплатеНалоговИВзносов.ЭтоПервыйОтчетныйПериод(Организация, ПериодСобытия, РеквизитыПравила, ДатаНачалаДеятельности);
	
	ПорядокСдачи = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		Нстр("ru = '%1 отчет сдается с %2 по %3'"),
			?(ЭтоПервыйОтчет, НСтр("ru = 'Первый'"), НСтр("ru = 'Следующий'")),
			Формат(НачалоВыполнения, НСтр("ru = 'ДФ=''d MMMM'''")),
			Формат(Срок, НСтр("ru = 'ДФ=''d MMMM yyyy ""года""'''")));
	
	ЧастиОписания = Новый Массив;
	
	ЧастиОписания.Добавить(ПорядокСдачи);
	ЧастиОписания.Добавить(СтрШаблон("(%1)", НаименованиеОтчета));
	
	Описание.ПредставлениеОтчета = СтрСоединить(ЧастиОписания, Символы.ПС);
	
	Если Не ЭтоПервыйОтчет Тогда
		Возврат Описание;
	КонецЕсли;
	
	ПропущенныйПериод = ИнтерфейсыВзаимодействияБРО.ПропущенныйНалоговыйПериод(
		РеквизитыПравила.РасширенныйПервыйНалоговыйПериод,
		Организация,
		ДатаНачалаДеятельности);
	
	Если НЕ ЗначениеЗаполнено(ПропущенныйПериод) Тогда
		Возврат Описание;
	КонецЕсли;
	
	НачалоПропущенногоПериода = ДатаНачалаДеятельности;
	КонецПропущенногоПериода  = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(
		РеквизитыПравила.ФинансовыйПериод, ДатаНачалаДеятельности);
	
	ПериодПослеПропущенного = ИнтерфейсыВзаимодействияБРОКлиентСервер.ДобавитьПериод(ПропущенныйПериод,
		РеквизитыПравила.ФинансовыйПериод, 1);
	
	НачалоПервогоПериодаОтчета = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(
		РеквизитыПравила.ФинансовыйПериод, ПериодПослеПропущенного);
	КонецПервогоПериодаОтчета = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(
		РеквизитыПравила.ФинансовыйПериод, ПериодПослеПропущенного);
	
	ТекстПояснения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Период с даты регистрации %1 по %2 включается в отчет за %3'"),
			Формат(НачалоПропущенногоПериода, "ДЛФ=D"),
			Формат(КонецПропущенногоПериода, "ДЛФ=D"),
			ПредставлениеПериода(НачалоПервогоПериодаОтчета, КонецПервогоПериодаОтчета, "ФП = Истина"));
	
	Описание.ОтчетныйПериодРасширен = Истина;
	Описание.РасширенныйПериодТекстПояснения = ТекстПояснения;
	Описание.РасширенныйПериодТекстПодсказки = ТекстПодсказкиПоРасширенномуПервомуОтчетномуПериоду(
		РеквизитыПравила.ИдентификаторЗадачи);
	
	Возврат Описание;
	
КонецФункции

Функция ТекстПодсказкиПоРасширенномуПервомуОтчетномуПериоду(ИдентификаторЗадачи) Экспорт
	
	ТекстПодсказки = "";
	
	Если ИдентификаторЗадачи = "УСН" Тогда
		
		ТекстПодсказки = НСтр("ru = 'У организаций и ИП, зарегистрированных в декабре, первый налоговый период по УСН длится с даты регистрации до конца следующего года (п.2 ст. 55 НК РФ). Декларация сдается по истечении первого налогового периода.'");
		
	КонецЕсли;
	
	Возврат ТекстПодсказки;
	
КонецФункции

// Конструктор описания отчетов для вывода в помощник.
// 
// Возвращаемое значение:
//   - Структура - описание отчета
//
Функция НовыйОписаниеОтчета() Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("ПредставлениеОтчета", "");
	Результат.Вставить("ОтчетныйПериодРасширен", Ложь);
	Результат.Вставить("РасширенныйПериодТекстПояснения", "");
	Результат.Вставить("РасширенныйПериодТекстПодсказки", "");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ГосударственныеОрганы

Функция ОписаниеГосорганаПолучателяОтчетности(Госорган) Экспорт
	
	Если Госорган = Перечисления.ТипыКонтролирующихОрганов.ФНС Тогда
		Возврат НСтр("ru = 'В налоговую инспекцию:'");
	ИначеЕсли Госорган = Перечисления.ТипыКонтролирующихОрганов.ПФР Тогда
		Возврат НСтр("ru = 'В пенсионный фонд:'");
	ИначеЕсли Госорган = Перечисления.ТипыКонтролирующихОрганов.ФСС Тогда
		Возврат НСтр("ru = 'В фонд социального страхования:'");
	ИначеЕсли Госорган = Перечисления.ТипыКонтролирующихОрганов.ФСГС Тогда
		Возврат НСтр("ru = 'В Росстат:'");
	Иначе
		// Отчеты, предоставляемые в другие госорганы, не обслуживаются.
		Возврат "";
	КонецЕсли;
	
КонецФункции

#КонецОбласти

// Копирует и размещает в форме копию элемента вместе с подчиненными элементами.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Источник - ГруппаФормы, КнопкаФормы, ПолеФормы, ДекорацияФормы - элемент, используемый в качестве шаблона
//  Владелец - ГруппаФормы, ФормаКлиентскогоПриложения - элемент, в который необходимо поместить копию элемента
//  Номер - Число - порядковый номер в имени копии элемента
//  Местоположение - ГруппаФормы, КнопкаФормы, ПолеФормы, ДекорацияФормы, ТаблицаФормы - элемент, перед которым
//                   размещается копия элемента
//  ШаблонИменования - Строка - шаблон для именования подчиненных элементов, где %1 - имя владельца, %2 - порядковый номер копии. Например для шаблона "%1_%2" подчиненные элементы будут именоваться "ИмяВладельца_Номер"
//
Процедура КопироватьЭлементСПодчиненными(Форма, Источник, Владелец, Номер, Местоположение = Неопределено, ШаблонИменования = "%1%2") Экспорт
	
	ТипИсточника = ТипЗнч(Источник);
	Если Местоположение = Неопределено Тогда
		Приемник = Форма.Элементы.Добавить(СтрШаблон(ШаблонИменования, Источник.Имя, Номер), ТипИсточника, Владелец);
	Иначе
		Приемник = Форма.Элементы.Вставить(СтрШаблон(ШаблонИменования, Источник.Имя, Номер), ТипИсточника, Владелец, Местоположение);
	КонецЕсли;
	
	Если ТипИсточника = Тип("ГруппаФормы") Тогда
		Приемник.Вид = Источник.Вид;
		ЗаполнитьЗначенияСвойств(Приемник, Источник, , "Имя,ПутьКДаннымЗаголовка,ПодчиненныеЭлементы");
		Для Каждого ПодчиненныйЭлемент Из Источник.ПодчиненныеЭлементы Цикл
			КопироватьЭлементСПодчиненными(Форма, ПодчиненныйЭлемент, Приемник, Номер, , ШаблонИменования);
		КонецЦикла;
	ИначеЕсли ТипИсточника = Тип("КнопкаФормы") Тогда
		ИмяКоманды = Источник.ИмяКоманды + Номер;
		Если Форма.Команды.Найти(ИмяКоманды) = Неопределено Тогда
			НоваяКоманда = Форма.Команды.Добавить(ИмяКоманды);
			ЗаполнитьЗначенияСвойств(НоваяКоманда, Форма.Команды[Источник.ИмяКоманды]);
		КонецЕсли;
		Приемник.ИмяКоманды = ИмяКоманды;
		ЗаполнитьЗначенияСвойств(Приемник, Источник, , "Имя, ИмяКоманды");
	ИначеЕсли ТипИсточника = Тип("ДекорацияФормы") И Источник.Гиперссылка Тогда
		Приемник.Вид = Источник.Вид;
		ЗаполнитьЗначенияСвойств(Приемник, Источник, , "Имя");
		ДействиеДекорации = Источник.ПолучитьДействие("Нажатие");
		Приемник.УстановитьДействие("Нажатие", ДействиеДекорации);
	Иначе
		Приемник.Вид = Источник.Вид;
		ЗаполнитьЗначенияСвойств(Приемник, Источник, , "Имя");
	КонецЕсли;
	
КонецПроцедуры

Процедура УдалитьНеиспользуемыеЭлементы(Форма, ЭлементыКУдалению) Экспорт
	
	Элементы = Форма.Элементы;
	
	Для Каждого Элемент Из ЭлементыКУдалению Цикл
		Элементы.Удалить(Элемент);
	КонецЦикла;
	
КонецПроцедуры

Процедура УправлениеФормойБлокУведомление(Форма,
										  ТребуетсяУведомление = Истина,
										  ИнформацияУведомление = Неопределено,
										  Разрядность = 0, 
										  ПодаетсяЗаявлениеОЗачете = Ложь) Экспорт
	
	Элементы      = Форма.Элементы;
	ПлательщикЕНП = Форма.ПлательщикЕНП;
	
	Если Форма.Объект.Свойство("Организация") Тогда
		Организация = Форма.Объект.Организация;
	Иначе
		// В некоторых помощниках реквизит находится на форме
		РеквизитыФормы = Форма.ПолучитьРеквизиты();
		Для Каждого ТекущийРеквизит Из РеквизитыФормы Цикл
			Если ТекущийРеквизит.Имя = "Организация"
				И ТекущийРеквизит.ТипЗначения.СодержитТип(Тип("СправочникСсылка.Организации")) Тогда
				
				Организация = Форма.Организация;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Организация = Неопределено;
	КонецЕсли;
	
	Если Форма.Объект.Свойство("Период") Тогда
		ПериодСобытия = Форма.Объект.Период;
	ИначеЕсли Форма.Объект.Свойство("ПериодРегистрации") Тогда
		ПериодСобытия = Форма.Объект.ПериодРегистрации;
	Иначе
		ПериодСобытия = Форма.ПериодСобытия;
	КонецЕсли;
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "Срок") Тогда
		Срок = Форма.Срок;
	Иначе
		Срок = ПериодСобытия;
	КонецЕсли;
	
	Если Элементы.Найти("ГруппаЗаявлениеОЗачете") <> Неопределено Тогда
		Элементы.ГруппаЗаявлениеОЗачете.Видимость = ПодаетсяЗаявлениеОЗачете;
		Элементы.ГруппаОплата.ОтображатьЗаголовок = Не ПодаетсяЗаявлениеОЗачете;
	КонецЕсли;
	
	ОтображатьЗаявленияЗаПериодПослеУплаты = Ложь;
	Если СтрНайти(Форма.ИмяФормы, "РасчетСтраховыхВзносовИП") Тогда
		ОтображатьЗаявленияЗаПериодПослеУплаты = Форма.ВсегоВзносов = 0;
	КонецЕсли;
	
	ПодаетсяУведомлениеПоНалогуЗаПериод =
		Форма.ПодаетсяУведомлениеПоНалогуЗаПериод;
		
	Элементы.ГруппаУведомлениеОНалогах.Видимость = ПлательщикЕНП
		И Не ПодаетсяЗаявлениеОЗачете;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаУведомлениеОНалогахПустая",
		"Видимость",
		ПлательщикЕНП);
	
	Если Не ПлательщикЕНП Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПодаетсяУведомлениеПоНалогуЗаПериод Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы,
			"ГруппаУведомлениеОНалогах",
			"Заголовок",
			НСтр("ru = 'Отражение в ЕНС'"));
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы,
			"ДекорацияУведомление",
			"Заголовок",
			НСтр("ru = 'Отражение в ЕНС'"));
	КонецЕсли;
	
	Форма.УведомлениеОбИсчисленныхНалогахПодготовлено = Форма.УведомленияОбИсчисленныхНалогах.Количество() <> 0;
	Если ТребуетсяУведомление Тогда
		ТребуетсяУведомление = ПлательщикЕНП;
	КонецЕсли;
	
	КУдалению    = Новый Массив;
	Форма.ИтогПоУведомлениям = 0;
	
	ПростойУчетЕНС = Срок >= ЕдиныйНалоговыйСчет.НачалоПростогоУчета();
	
	Если ПодаетсяУведомлениеПоНалогуЗаПериод Тогда
		ТекстИнформацияУведомление = ТекстБлокаУведомления(Форма.СрокПодачиУведомления);
		Если ЗначениеЗаполнено(ИнформацияУведомление) Тогда
			ТекстИнформацияУведомление = ИнформацияУведомление;
		ИначеЕсли Не ТребуетсяУведомление Тогда
			ТекстИнформацияУведомление  = НСтр("ru = 'Нет сумм налога к уплате. Уведомление не требуется.'");
		КонецЕсли;
	ИначеЕсли ПростойУчетЕНС Тогда
		Если СтрНайти(Форма.ИмяФормы, "ПомощникУчетаЗарплаты") = 0 Тогда
			Если СтрНайти(Форма.ИмяФормы, "ПомощникУплатыНалога") <> 0
				И Форма.ИдентификаторЗадачи = "НалогНаПрибыль" Тогда
				ТекстИнформацияУведомление = НСтр("ru = 'По налогу на прибыль формировать уведомление по исчисленным налогам не нужно.'");
			Иначе
				ТекстИнформацияУведомление = НСтр(
					"ru = 'При отправке декларации формировать уведомление по исчисленным налогам не нужно.'");
			КонецЕсли;
		Иначе
			ТекстИнформацияУведомление = СтрШаблон(НСтр("ru = 'За %1 формировать уведомление по исчисленным налогам не нужно.'"),
				ПредставлениеПериода(НачалоМесяца(Форма.Объект.ПериодРегистрации), КонецМесяца(Форма.Объект.ПериодРегистрации)));
		КонецЕсли;
		Если Не ТребуетсяУведомление Тогда
			ТекстИнформацияУведомление  = НСтр("ru = 'Нет сумм налога к уплате.'");
		КонецЕсли;
	Иначе
		Если СтрНайти(Форма.ИмяФормы, "ПомощникУчетаЗарплаты") = 0 Тогда
			Если СтрНайти(Форма.ИмяФормы, "ПомощникУплатыНалога") <> 0
				И Форма.ИдентификаторЗадачи = "НалогНаПрибыль" Тогда
				ТекстИнформацияУведомление = НСтр("ru = 'По налогу на прибыль формировать уведомление по исчисленным налогам не нужно,
														|но для корректного ведения расчетов по налогам нужно отразить его на едином налоговом счете.'");
			Иначе
				ТекстИнформацияУведомление = НСтр(
					"ru = 'При отправке декларации формировать уведомление по исчисленным налогам не нужно, но для корректного ведения расчетов по налогам нужно отразить их на едином налоговом счете.'");
			КонецЕсли;
		Иначе
			ТекстИнформацияУведомление = СтрШаблон(НСтр("ru = 'За %1 формировать уведомление по исчисленным налогам не нужно,
														|но для корректного ведения расчетов по налогам нужно отразить их на едином налоговом счете.'"),
											ПредставлениеПериода(НачалоМесяца(Форма.Объект.ПериодРегистрации), КонецМесяца(Форма.Объект.ПериодРегистрации)));
		КонецЕсли;
		Если Не ТребуетсяУведомление Тогда
			ТекстИнформацияУведомление  = НСтр("ru = 'Нет сумм налога к уплате. Отражать в ЕНС не требуется.'");
		КонецЕсли;
	КонецЕсли;
	
	ВыполнитьДействиеСформироватьУведомление = Элементы.Найти("ВыполнитьДействиеСформироватьУведомление");
	Если ВыполнитьДействиеСформироватьУведомление <> Неопределено Тогда
		ВыполнитьДействиеСформироватьУведомление.Видимость = ПодаетсяУведомлениеПоНалогуЗаПериод;
	КонецЕсли;
	
	ВыполнитьДействиеСформироватьОперациюПоЕНС = Элементы.Найти("ВыполнитьДействиеСформироватьОперациюПоЕНС");
	Если ВыполнитьДействиеСформироватьОперациюПоЕНС <> Неопределено Тогда
		Элементы.ВыполнитьДействиеСформироватьОперациюПоЕНС.Видимость = Не ПростойУчетЕНС
			И Не ПодаетсяУведомлениеПоНалогуЗаПериод;
	КонецЕсли;
	
	ШрифтИнформацияУведомление = Новый Шрифт(,,,Истина);
	
	Элементы.ДекорацияИнформацияУведомление.Видимость = Истина;
	Элементы.ДекорацияИнформацияУведомление.Заголовок = Новый ФорматированнаяСтрока(ТекстИнформацияУведомление);
	
	Если ПодаетсяЗаявлениеОЗачете Тогда
		Элементы.СтраницыЗаявление.Видимость = ТребуетсяУведомление Или ОтображатьЗаявленияЗаПериодПослеУплаты;
	КонецЕсли;
	
	// Подсказка по уведомлению
	Если ПодаетсяУведомлениеПоНалогуЗаПериод Тогда
		Если Форма.УведомленияОбИсчисленныхНалогах.Количество() > 1 Тогда
			ТекстПодсказкиУведомления = ?(Форма.Подключена1СОтчетность,
				НСтр("ru = 'Перейдите по ссылкам и отправьте уведомления в ФНС:'"),
				НСтр("ru = 'Перейдите по ссылкам и установите статус ""Сдано"", если уведомление отправлено в ФНС:'"));
		Иначе
			ТекстПодсказкиУведомления = ?(Форма.Подключена1СОтчетность,
				НСтр("ru = 'Перейдите по ссылке и отправьте уведомление в ФНС:'"),
				НСтр("ru = 'Перейдите по ссылке и установите статус ""Сдано"", если уведомление отправлено в ФНС:'"));
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДекорацияПодсказкаУведомления",
			"Заголовок", ТекстПодсказкиУведомления);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПодсказкаУведомления",
			"Видимость", Ложь); // видимость подсказки определим ниже по коду
	КонецЕсли;
	
	ВсеУведомленияСданы = Истина;
	Если Не ТребуетсяУведомление И Не ОтображатьЗаявленияЗаПериодПослеУплаты Тогда
		Элементы.СтраницыУведомление.Видимость = Ложь;
	Иначе
		Элементы.СтраницыУведомление.Видимость = Истина;
		Если Форма.УведомлениеОбИсчисленныхНалогахПодготовлено Тогда
			
			Элементы.СтраницаРезультатВыполненияУведомление.Видимость = Истина;
			Элементы.СтраницаВыполнитьДействиеУведомление.Видимость   = Ложь;
			Если ПодаетсяЗаявлениеОЗачете Тогда
				Элементы.СтраницаРезультатВыполненияЗаявление.Видимость = Истина;
				Элементы.СтраницаВыполнитьДействиеЗаявление.Видимость   = Ложь;
			КонецЕсли;
			
			Для Каждого Уведомление Из Форма.УведомленияОбИсчисленныхНалогах Цикл
				Если ТипЗнч(Уведомление.Уведомление) = Тип("ДокументСсылка.ЗаявлениеОЗачетеВСчетПредстоящейОбязанности") Тогда
					НомерКонтейнера = ЗадачиБухгалтераКлиентСервер.НомерКонтейнера(Уведомление.ПолучитьИдентификатор());
					Если Элементы.Найти("ОписаниеЗаявление" + НомерКонтейнера) = Неопределено Тогда
						КопироватьЭлементСПодчиненными(Форма, Элементы.ОписаниеЗаявление, Элементы.СтраницаРезультатВыполненияЗаявление, НомерКонтейнера);
					КонецЕсли;
					ОформитьСобытиеУведомление(Форма, Уведомление, НомерКонтейнера, Разрядность);
					Элементы["ОписаниеЗаявление" + НомерКонтейнера].Видимость = Истина;
				Иначе
					НомерКонтейнера = ЗадачиБухгалтераКлиентСервер.НомерКонтейнера(Уведомление.ПолучитьИдентификатор());
					Если Элементы.Найти("ОписаниеУведомление" + НомерКонтейнера) = Неопределено Тогда
						КопироватьЭлементСПодчиненными(Форма, Элементы.ОписаниеУведомление, Элементы.СтраницаРезультатВыполненияУведомление, НомерКонтейнера);
					КонецЕсли;
					ОформитьСобытиеУведомление(Форма, Уведомление, НомерКонтейнера, Разрядность);
					Элементы["ОписаниеУведомление" + НомерКонтейнера].Видимость = Истина;
					
					Если ТипЗнч(Уведомление.Уведомление) = Тип("ДокументСсылка.УведомлениеОбИсчисленныхСуммахНалогов") Тогда 
						СтатусУведомления = Документы.УведомлениеОбИсчисленныхСуммахНалогов.СтатусДокумента(Уведомление.Уведомление);
						Элементы["УдалитьУведомление" + НомерКонтейнера].Видимость = СтатусУведомления.ВРаботе;
						
						Если СтатусУведомления.ВРаботе Тогда
							ВсеУведомленияСданы = Ложь;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		Иначе
			Элементы.СтраницаРезультатВыполненияУведомление.Видимость = Ложь;
			Элементы.СтраницаВыполнитьДействиеУведомление.Видимость   = Истина;
			Если ПодаетсяЗаявлениеОЗачете Тогда
				Элементы.СтраницаРезультатВыполненияЗаявление.Видимость = Ложь;
				Элементы.СтраницаВыполнитьДействиеЗаявление.Видимость   = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Видимость подсказки по уведомлению.
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПодсказкаУведомления",
		"Видимость", Не ВсеУведомленияСданы И ПодаетсяУведомлениеПоНалогуЗаПериод); // // не отображаем подсказку, если уведомления сданы
	
	//Удалим лишние контейнеры
	Если ПодаетсяЗаявлениеОЗачете Тогда
		СтраницаРезультата = Элементы.СтраницаРезультатВыполненияЗаявление;
	Иначе
		СтраницаРезультата = Элементы.СтраницаРезультатВыполненияУведомление;
	КонецЕсли;
	Для Каждого Контейнер Из СтраницаРезультата.ПодчиненныеЭлементы Цикл
		Идентификатор = ЗадачиБухгалтераКлиентСервер.Идентификатор(Контейнер.Имя);
		Если Идентификатор = Неопределено Тогда
			// Это шаблон
			Продолжить;
		КонецЕсли;
		Если Форма.УведомленияОбИсчисленныхНалогах.НайтиПоИдентификатору(Идентификатор) = Неопределено Тогда
			КУдалению.Добавить(Контейнер);
		КонецЕсли;
	КонецЦикла;
	УдалитьНеиспользуемыеЭлементы(Форма, КУдалению);
	
	ФорматнаяСтрока = "ЧЦ=15; ЧН=";
	Если Разрядность <> 0 Тогда
		ФорматнаяСтрока = ФорматнаяСтрока + ";ЧДЦ=" + Разрядность; 
	КонецЕсли;
	//Итог по уплате
	Если Форма.УведомленияОбИсчисленныхНалогах.Количество() > 1 Тогда
		// Обеспечим контейнер для вывода итога, для которго установим числовой идентификатор контейнера "0"
		Если ПодаетсяЗаявлениеОЗачете Тогда
			Если Элементы.Найти("ОписаниеЗаявленияИтог0") = Неопределено Тогда
				КопироватьЭлементСПодчиненными(Форма, Элементы.ОписаниеЗаявлениеИтог, Элементы.СтраницаРезультатВыполненияЗаявление, 0);
			КонецЕсли;
		Иначе
			Если Элементы.Найти("ОписаниеУведомлениеИтог0") = Неопределено Тогда
				КопироватьЭлементСПодчиненными(Форма, Элементы.ОписаниеУведомлениеИтог, Элементы.СтраницаРезультатВыполненияУведомление, 0);
			КонецЕсли;
		КонецЕсли;
		Элементы.ЗначениеУведомлениеИтог0.Заголовок = Формат(Форма.ИтогПоУведомлениям, ФорматнаяСтрока);
		Элементы.ОписаниеУведомлениеИтог0.Видимость = Истина;
	КонецЕсли;
	
	// Уберем шаблоны
	Элементы.ОписаниеУведомление.Видимость     = Ложь;
	Элементы.ОписаниеУведомлениеИтог.Видимость = Ложь;
	
	// Подсказка по заявлению
	Если ПодаетсяЗаявлениеОЗачете И ТребуетсяУведомление Тогда
		ДоступныйОстаток = ЕдиныйНалоговыйСчет.ОстатокНаЕдиномНалоговомСчете(Организация, ПериодСобытия);
		
		// Взносы ФСС платим отдельной платежкой не через ЕНС
		Если ДоступныйОстаток >= (Форма.ВсегоВзносов - Форма.СуммаВзносаФССНачислено) Тогда
			ТекстИнформацияЗаявление = СтрШаблон(
				НСтр("ru = 'Доступный остаток по ЕНС составляет %1. Данная сумма позволяет произвести зачет взносов в счет предстоящей обязанности.'"),
				Формат(ДоступныйОстаток, ФорматнаяСтрока));
			Элементы.СформироватьЗаявление.Доступность = Истина;
			Элементы.СформироватьЗаявление.Видимость = Истина;
		Иначе
			ТекстИнформацияЗаявление = СтрШаблон(
				НСтр("ru = 'Доступный остаток по ЕНС составляет %1. Данная сумма не позволяет произвести зачет взносов в счет предстоящей обязанности.'"),
				Формат(ДоступныйОстаток, ФорматнаяСтрока));
			Элементы.СформироватьЗаявление.Доступность = Ложь;
			Элементы.ДекорацияИнформацияЗаявление.Видимость = Не (Форма.ВсегоВзносов - Форма.СуммаВзносаФССНачислено) = 0;
		КонецЕсли;
		Элементы.ДекорацияИнформацияЗаявление.Заголовок = Новый ФорматированнаяСтрока(
			ТекстИнформацияЗаявление,
			ШрифтИнформацияУведомление);
		Элементы.ДекорацияИнформацияЗаявление.Видимость = Истина;
	ИначеЕсли ПодаетсяЗаявлениеОЗачете Тогда
		Элементы.ДекорацияИнформацияЗаявление.Видимость = Ложь;
		Элементы.СформироватьЗаявление.Доступность = Ложь;
		Элементы.СформироватьЗаявление.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

Функция РеквизитыЕдиногоНалоговогоПлатежа(Организация, Период) Экспорт
	
	ИспользуетсяЕдиныйНалоговыйПлатеж = Период >= НастройкиУчетаКлиентСервер.ДатаПереходаНаЕдиныйНалоговыйПлатеж()
		И УчетнаяПолитика.ПлательщикЕНП(Организация, Период);
	
	ВидНалога           = Перечисления.ВидыНалогов.ЕдиныйНалоговыйПлатеж;
	Налог               = Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(ВидНалога);
	КБК                 = Справочники.ВидыНалоговИПлатежейВБюджет.КБК(Налог, , Период);
	ГоловнаяОрганизация = БухгалтерскийУчетПереопределяемый.ГоловнаяОрганизация(Организация);
	ВидИКодНалоговогоОргана = ДанныеГосударственныхОрганов.ВидИКодГосударственногоОрганаПоНалогу(
		Налог, ГоловнаяОрганизация, Период);
	КодНалоговогоОргана = ВидИКодНалоговогоОргана.Код;
	
	РегистрацияВНалоговомОргане = Справочники.Организации.РегистрацияВНалоговомОрганеНаДату(ГоловнаяОрганизация, Период);
	РеквизитыОрганизации = ЕдиныйНалоговыйСчет.КППОКТМООрганизацииДляУплатыВБюджет(Организация, Налог, Период);
	ОКТМО = РеквизитыОрганизации.ОКТМО;
	ПлатежиВБюджетКлиентСервер.ПривестиКодТерриторииКФорматуПриказа58н(ОКТМО, Период);
	
	РеквизитыЕдиногоНалоговогоПлатежа = СтруктураРеквизитовЕдиногоНалоговогоПлатежа();
	РеквизитыЕдиногоНалоговогоПлатежа.Налог                       = Налог;
	РеквизитыЕдиногоНалоговогоПлатежа.ВидНалога                   = ВидНалога;
	РеквизитыЕдиногоНалоговогоПлатежа.КБК                         = КБК;
	РеквизитыЕдиногоНалоговогоПлатежа.НалоговыйПериод             = ПлатежиВБюджетКлиентСервер.НезаполненноеЗначение();
	РеквизитыЕдиногоНалоговогоПлатежа.ПоказательПериода           = ПлатежиВБюджетКлиентСервер.НезаполненноеЗначение();
	РеквизитыЕдиногоНалоговогоПлатежа.РегистрацияВНалоговомОргане = РегистрацияВНалоговомОргане;
	РеквизитыЕдиногоНалоговогоПлатежа.КодНалоговогоОргана         = КодНалоговогоОргана;
	РеквизитыЕдиногоНалоговогоПлатежа.КПП                         = РеквизитыОрганизации.КПП;
	РеквизитыЕдиногоНалоговогоПлатежа.ОКТМО                       = ОКТМО;
	РеквизитыЕдиногоНалоговогоПлатежа.ОКАТО                       = ОКТМО;
	РеквизитыЕдиногоНалоговогоПлатежа.Наименование                = Строка(Налог);
	РеквизитыЕдиногоНалоговогоПлатежа.Описание                    = Строка(Налог);
	
	Возврат РеквизитыЕдиногоНалоговогоПлатежа;
	
КонецФункции

Функция СтруктураРеквизитовЕдиногоНалоговогоПлатежа() Экспорт
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("Налог",                       Справочники.ВидыНалоговИПлатежейВБюджет.ПустаяСсылка());
	Реквизиты.Вставить("ВидНалога",                   Перечисления.ВидыНалогов.ПустаяСсылка());
	Реквизиты.Вставить("КБК",                         "");
	Реквизиты.Вставить("НалоговыйПериод",             "");
	Реквизиты.Вставить("ПоказательПериода",           "");
	Реквизиты.Вставить("РегистрацияВНалоговомОргане", Справочники.РегистрацииВНалоговомОргане.ПустаяСсылка());
	Реквизиты.Вставить("КодНалоговогоОргана",         "");
	Реквизиты.Вставить("КПП",                         "");
	Реквизиты.Вставить("ОКТМО",                       "");
	Реквизиты.Вставить("ОКАТО",                       "");
	Реквизиты.Вставить("Наименование",                "");
	Реквизиты.Вставить("Описание",                    "");
	
	Возврат Реквизиты;
	
КонецФункции

Функция ПрименяетсяОсобыйПорядокУплатыНалога(Организация, ТекущийПериод, Периодичность = Неопределено) Экспорт
	
	ПрименяетсяОсобыйПорядокУплатыНалога = УчетнаяПолитика.ПлательщикЕНП(Организация, ТекущийПериод); // в периоде запроса
	
	Если Не ПрименяетсяОсобыйПорядокУплатыНалога И Периодичность <> Неопределено Тогда
		// в периоде
		Если Периодичность = Перечисления.Периодичность.Год Тогда
			СледующийПериод = КонецГода(ТекущийПериод) + 1
		ИначеЕсли Периодичность = Перечисления.Периодичность.Квартал Тогда
			СледующийПериод = КонецКвартала(ТекущийПериод) + 1
		Иначе // Периодичность = Перечисления.Периодичность.Месяц
			СледующийПериод = КонецМесяца(ТекущийПериод) + 1
		КонецЕсли;
		
		ПрименяетсяОсобыйПорядокУплатыНалога = УчетнаяПолитика.ПлательщикЕНП(Организация, СледующийПериод);
		
	КонецЕсли;
	
	Возврат ПрименяетсяОсобыйПорядокУплатыНалога;
	
КонецФункции

// Возвращает ссылку на личный кабинет налогоплательщика.
//
// Параметры:
//   ЭтоЮрЛицо - Булево
//
// Возвращаемое значение:
//   Строка
//
Функция СсылкаНаЛичныйКабинетНалогоплательщика(ЭтоЮрЛицо) Экспорт
	
	Если ЭтоЮрЛицо Тогда
		Возврат "https://lkul.nalog.ru/";
	Иначе
		Возврат "https://lkip2.nalog.ru/";
	КонецЕсли;
	
КонецФункции

// Отбирает из списка платежных документов отправляемые напрямую через 1С:ДиректБанк
//
// Параметры:
//  Организация - СправочникСсылка.Организации - организация для настройки обмена
//  ДокументыОплаты - Массив - список документов
//
// Возвращаемое значение:
//  Массив - отобранные документы.
Функция ПлатежныеДокументыПоДиректБанку(Организация, ДокументыОплаты) Экспорт
	
	ДокументыОплатыПоБанку = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("ПлатежныеПоручения", ДокументыОплаты);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПлатежноеПоручение.СчетОрганизации.Банк КАК Банк,
	|	ПлатежноеПоручение.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПлатежноеПоручение КАК ПлатежноеПоручение
	|ГДЕ
	|	ПлатежноеПоручение.Ссылка В(&ПлатежныеПоручения)
	|ИТОГИ ПО
	|	Банк";
	Если ОбщегоНазначенияБП.ИнтеграцияСБанкомДоступна() Тогда
		СхемаЗапроса = Новый СхемаЗапроса;
		СхемаЗапроса.УстановитьТекстЗапроса(Запрос.Текст);
		Запрос.Параметры.Вставить("СчетаБезИнеграцииСБанком", Справочники.НастройкиИнтеграцииСБанками.БанковскиеСчетаОрганизацииБезИнтеграции(Организация));
		СхемаЗапроса.ПакетЗапросов[0].Операторы[0].Отбор.Добавить("ПлатежноеПоручение.СчетОрганизации В(&СчетаБезИнеграцииСБанком)");
		Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	КонецЕсли;
	
	ВыборкаБанк = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаБанк.Следующий() Цикл
		Если ЗначениеЗаполнено(ВыборкаБанк.Банк) Тогда
			НастройкаОбмена = ОбменСБанками.НастройкаОбмена(Организация, ВыборкаБанк.Банк);
			Если ЗначениеЗаполнено(НастройкаОбмена) Тогда
				Выборка = ВыборкаБанк.Выбрать();
				Пока Выборка.Следующий() Цикл
					ДокументыОплатыПоБанку.Добавить(Выборка.Ссылка);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ДокументыОплатыПоБанку;
	
КонецФункции

// Отбирает из списка платежных документов выгружаемые в файл
//
// Параметры:
//  Организация - СправочникСсылка.Организации - организация для отбора настроек обмена
//  ДокументыОплаты - Массив - список документов
//
// Возвращаемое значение:
//  Массив - отобранные документы.
Функция ПлатежныеДокументыДляВыгрузкиВФайл(Организация, ДокументыОплаты) Экспорт
	
	ДокументыОплатыПоБанку = ПлатежныеДокументыПоДиректБанку(Организация, ДокументыОплаты);
	
	Возврат ОбщегоНазначенияКлиентСервер.РазностьМассивов(ДокументыОплаты, ДокументыОплатыПоБанку);
	
КонецФункции

// Рассчитывает и возвращает статус оплаты на основании переданного массива статусов.
// Если все переданные статусы совпадают, возвращается этот общий статус.
// Если не совпадают, возвращается количество максимальных статусов и общее количество статусов.
// В случае, если общее количество статусов отличается от количества переданных статусов,
// его можно передать отдельно во втором параметре функции.
//
// Параметры:
//  Статусы - Массив - Массив строковых представлений статуса
//  КоличествоПлатежейКОплате = Число - Общее количество платежей. Необязательный. Если не передан,
//  количество платежей определяется как количество элементов в параметре "Статусы".
//
// Возвращаемое значение:
//  Строка - Рассчитанное представление статуса.
//
Функция СтатусОплаты(Статусы, КоличествоПлатежейКОплате = Неопределено) Экспорт
	
	Если Статусы.Количество() = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	// Выясним максимальный статус среди всех платежей
	МаксимальныйСтатус = Перечисления.СостоянияБанковскихДокументов.ПустаяСсылка();
	Для Каждого Статус Из Статусы Цикл
		Если Не ЗначениеЗаполнено(Статус) Тогда
			Продолжить;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(МаксимальныйСтатус)
			Или Перечисления.СостоянияБанковскихДокументов.Индекс(Статус) > Перечисления.СостоянияБанковскихДокументов.Индекс(МаксимальныйСтатус) Тогда
			МаксимальныйСтатус = Статус;
		КонецЕсли;
	КонецЦикла;
	
	// Подсчитаем количество платежей с максимальным статусом
	КоличествоПлатежейСМаксимальнымСтатусом = 0;
	Для Каждого Статус Из Статусы Цикл
		Если Статус = МаксимальныйСтатус Тогда
			КоличествоПлатежейСМаксимальнымСтатусом = КоличествоПлатежейСМаксимальнымСтатусом + 1;
		КонецЕсли;
	КонецЦикла;
	
	ОбщееКоличествоПлатежей = ?(КоличествоПлатежейКОплате <> Неопределено, КоличествоПлатежейКОплате, Статусы.Количество());
	
	Если ОбщееКоличествоПлатежей = 1
		Или ОбщееКоличествоПлатежей = КоличествоПлатежейСМаксимальнымСтатусом Тогда
		Возврат Строка(МаксимальныйСтатус);
	Иначе // статусы платежей различаются, отразим это в статусе
		Возврат СтрШаблон(НСтр("ru = '%1 %2 из %3'"),
			МаксимальныйСтатус, Формат(КоличествоПлатежейСМаксимальнымСтатусом, "ЧГ="), Формат(ОбщееКоличествоПлатежей, "ЧГ="));
	КонецЕсли;
	
КонецФункции

// Рассчитывает и возвращает статус оплаты на основании переданного массива платежных поручений.
//
// Параметры:
//  ПлатежныеПоручения - Массив - Массив ДокументСсылка.ПлатежноеПоручение
//
// Возвращаемое значение:
//  Строка - Рассчитанное представление статуса. См. ПомощникиПоУплатеНалоговИВзносов.СтатусОплаты()
//
Функция СтатусОплатыПлатежныхПоручений(ПлатежныеПоручения) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПлатежныеПоручения", ПлатежныеПоручения);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СостоянияБанковскихДокументов.Состояние КАК Состояние
	|ИЗ
	|	РегистрСведений.СостоянияБанковскихДокументов КАК СостоянияБанковскихДокументов
	|ГДЕ
	|	СостоянияБанковскихДокументов.СсылкаНаОбъект В(&ПлатежныеПоручения)";
	Статусы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Состояние");
	
	Возврат СтатусОплаты(Статусы);
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ВыгрузитьПлатежныйДокумент(ПлатежноеПоручение, УникальныйИдентификатор) Экспорт
	
	ДвоичныеДанные = Обработки.КлиентБанк.ВыгрузитьДокумент(ПлатежноеПоручение);
	Если ДвоичныеДанные = Неопределено Тогда
		Возврат ДвоичныеДанные;
	Иначе
		Возврат ПоместитьВоВременноеХранилище(ДвоичныеДанные, УникальныйИдентификатор);
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Добавляет декорацию на форму
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения
//   Индекс - Число - индекс декорации
//   ПрефиксИмени - Строка - префикс имени декорации (например, "ДекорацияПлатеж")
//
Процедура ДобавитьПредставление(Форма, Индекс, ПрефиксИмени)
	
	Элементы = Форма.Элементы;
	
	Родитель = Элементы["Группа" + ПрефиксИмени];
	
	Декорация = Элементы.Добавить(ИмяЭлементаПредставления(Индекс, ПрефиксИмени), Тип("ДекорацияФормы"), Родитель);
	Декорация.УстановитьДействие("ОбработкаНавигационнойСсылки", СтрШаблон("Подключаемый_%1ОбработкаНавигационнойСсылки", ПрефиксИмени));
	Декорация.РастягиватьПоГоризонтали = Истина;
	Декорация.АвтомаксимальнаяШирина   = Ложь;
	Декорация.Высота = 1;
	
КонецПроцедуры

// Добавляет группу элементов для отображения платежа с суммой в отдельной колонке.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения
//   Индекс - Число - индекс декорации
//   ПрефиксИмени - Строка - префикс имени декорации (например, "ДекорацияПлатеж")
//
Процедура ДобавитьПредставлениеПлатежаССуммойВОтдельнойКолонке(Форма, Индекс, ПрефиксИмени)
	
	Элементы = Форма.Элементы;
	
	Родитель = Элементы["Группа" + ПрефиксИмени];
	
	ГруппаПлатежа = Элементы.Добавить(ИмяЭлементаПредставлениеГруппыПлатежа(Индекс, ПрефиксИмени), Тип("ГруппаФормы"), Родитель);
	ГруппаПлатежа.Вид          = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаПлатежа.Группировка  = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	ГруппаПлатежа.ОтображатьЗаголовок = Ложь;
	ГруппаПлатежа.Отображение  = ОтображениеОбычнойГруппы.Нет;
	ГруппаПлатежа.Заголовок    = СтрШаблон(ПрефиксИмени + " " + "%1", Индекс);
	ГруппаПлатежа.Объединенная = Ложь;
	
	Декорация = Элементы.Добавить(ИмяЭлементаПредставления(Индекс, ПрефиксИмени), Тип("ДекорацияФормы"), ГруппаПлатежа);
	Декорация.УстановитьДействие("ОбработкаНавигационнойСсылки", СтрШаблон("Подключаемый_%1ОбработкаНавигационнойСсылки", ПрефиксИмени));
	Декорация.РастягиватьПоГоризонтали = Истина;
	Декорация.АвтомаксимальнаяШирина   = Ложь;
	Декорация.Высота = 1;
	
	Декорация = Элементы.Добавить(ИмяЭлементаПредставлениеСуммыПлатежа(Индекс, ПрефиксИмени), Тип("ДекорацияФормы"), ГруппаПлатежа);
	Декорация.Высота = 1;
	Декорация.РастягиватьПоГоризонтали = Истина;
	Декорация.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	
КонецПроцедуры

// Возвращает представление гиперссылки
//
Функция ПредставлениеПлатежа(СтрокаКоллекцииПлатежей, ВключатьСостояниеДокумента)
	
	СвойстваПлатежа = Новый Структура("Ссылка, Дата, Номер, Сумма, Оплачено, Состояние, ПредставлениеДокумента");
	ЗаполнитьЗначенияСвойств(СвойстваПлатежа, СтрокаКоллекцииПлатежей);
	
	Если Не ЗначениеЗаполнено(СвойстваПлатежа.Ссылка) Тогда
		Возврат "";
	КонецЕсли;
	
	СтатусДокумента = "";
	Если ВключатьСостояниеДокумента Тогда
	
		ТипДокумента = ТипЗнч(СвойстваПлатежа.Ссылка);
		Если ТипДокумента = Тип("ДокументСсылка.ПлатежноеПоручение") Тогда
			СостояниеДокумента = СвойстваПлатежа.Состояние;
		Иначе
			СостояниеДокумента = ?(СвойстваПлатежа.Оплачено, Перечисления.СостоянияБанковскихДокументов.Оплачено,
				Перечисления.СостоянияБанковскихДокументов.Подготовлено);
		КонецЕсли;
		СтатусДокумента = СтрШаблон(" (%1)", Строка(СостояниеДокумента));
		
	КонецЕсли;
	
	ПредставлениеПлатежа = Новый Массив;
	
	ПредставлениеПлатежа.Добавить(Новый ФорматированнаяСтрока(СтрШаблон("%1%2",
		СвойстваПлатежа.ПредставлениеДокумента, СтатусДокумента), , , , "ОткрытьДокумент"));
	
	Если СвойстваПлатежа.Состояние <> Перечисления.СостоянияБанковскихДокументов.Оплачено
		И СвойстваПлатежа.Состояние <> Перечисления.СостоянияБанковскихДокументов.Отправлено
		И ПравоДоступа("Изменение", СвойстваПлатежа.Ссылка.Метаданные()) Тогда
		ПредставлениеПлатежа.Добавить("  ");
		ПредставлениеПлатежа.Добавить(Новый ФорматированнаяСтрока(
			БиблиотекаКартинок.УдалитьДокументОплатыИзПомощника, , , , "УдалитьДокумент"));
	КонецЕсли;
	
	// Обход ошибки платформы отображения картинки в конце форматированной строки.
	ПредставлениеПлатежа.Добавить(" ");
	
	Возврат Новый ФорматированнаяСтрока(ПредставлениеПлатежа);
	
КонецФункции

Функция ПредставлениеУведомления(Реквизиты)
	
	Если Не ЗначениеЗаполнено(Реквизиты.Ссылка) Тогда
		Возврат "";
	КонецЕсли;
	
	ПредставлениеСтатуса = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Реквизиты, "СтатусПредставление", "");
	Если Не ПустаяСтрока(ПредставлениеСтатуса) Тогда
		ПредставлениеСтатуса = СтрШаблон(НСтр("ru = ' (%1)'"), ПредставлениеСтатуса);
	КонецЕсли;
	
	СтрокаПредставления = Новый Массив;
	
	СтрокаПредставления.Добавить(Новый ФорматированнаяСтрока(СтрШаблон(НСтр("ru = '%1%2'"),
		Реквизиты.ПредставлениеДокумента, ПредставлениеСтатуса),,,, "ОткрытьДокумент"));
	
	Сдано = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Реквизиты, "Сдано", Истина);
	Если Не Сдано И ПравоДоступа("Изменение", Реквизиты.Ссылка.Метаданные()) Тогда
		СтрокаПредставления.Добавить("  ");
		СтрокаПредставления.Добавить(Новый ФорматированнаяСтрока(
			БиблиотекаКартинок.УдалитьДокументОплатыИзПомощника,,,, "УдалитьДокумент"));
	КонецЕсли;
	
	// Обход ошибки платформы отображения картинки в конце форматированной строки.
	СтрокаПредставления.Добавить(" ");
	
	Возврат Новый ФорматированнаяСтрока(СтрокаПредставления);
	
КонецФункции

// Возвращает имя группы формы для платежи, соответствующей индексу
//
Функция ИмяЭлементаПредставлениеГруппыПлатежа(Индекс, ПрефиксИмени)
	
	Возврат СтрШаблон("Группа" + ПрефиксИмени + "%1", Формат(Индекс, "ЧН=0; ЧГ=0"));
	
КонецФункции

// Возвращает имя элемента формы, соответствующего индексу
//
Функция ИмяЭлементаПредставления(Индекс, ПрефиксИмени)
	
	Возврат СтрШаблон("Декорация" + ПрефиксИмени + "%1", Формат(Индекс, "ЧН=0; ЧГ=0"));
	
КонецФункции

// Возвращает имя элемента формы, соответствующего индексу
//
Функция ИмяЭлементаПредставлениеСуммыПлатежа(Индекс, ПрефиксИмени)
	
	Возврат СтрШаблон("Декорация" + ПрефиксИмени + "%1Сумма", Формат(Индекс, "ЧН=0; ЧГ=0"));
	
КонецФункции

// Возвращает количество декораций для отображения платежей заданных в конфигураторе
//
Функция КоличествоПредопределенныхЭлементов(Форма, ПрефиксИмени)
	
	Элементы = Форма.Элементы;
	
	Количество = 0;
	Пока Истина Цикл
		Если Элементы.Найти(ИмяЭлементаПредставления(Количество, ПрефиксИмени)) <> Неопределено Тогда
			Количество = Количество + 1;
		Иначе
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Количество;
	
КонецФункции

#Область УведомленияОбИсчисленныхНалогах

Функция ТекстБлокаУведомления(Срок) Экспорт
	
	ЖирныйШрифт = Новый Шрифт(,, Истина);
	ЧастиУведомления = Новый Массив;
	ЧастиУведомления.Добавить(НСтр("ru = 'Чтобы суммы налоговых платежей были правильно распределены по конкретным налогам и взносам, необходимо отправить в ФНС уведомление. Сделайте это до'"));
	ЧастиУведомления.Добавить(" ");
	ЧастиУведомления.Добавить(Новый ФорматированнаяСтрока(Формат(Срок, "ДЛФ=DD"), ЖирныйШрифт));
	
	Возврат Новый ФорматированнаяСтрока(ЧастиУведомления);
	
КонецФункции

#КонецОбласти

Процедура ОформитьСобытиеУведомление(Форма, Событие, Номер, Разрядность) Экспорт
	
	Элементы = Форма.Элементы;
	
	Если ЗначениеЗаполнено(Событие.Уведомление) Тогда
		Если ТипЗнч(Событие.Уведомление) = Тип("ДокументСсылка.ОперацияПоЕдиномуНалоговомуСчету") Тогда
			ПредставлениеУведомления = Документы.ОперацияПоЕдиномуНалоговомуСчету.ОписаниеДокумента(Событие.Уведомление);
		ИначеЕсли ТипЗнч(Событие.Уведомление) = Тип("ДокументСсылка.ЗаявлениеОЗачетеВСчетПредстоящейОбязанности") Тогда
			ПредставлениеУведомления = Документы.ЗаявлениеОЗачетеВСчетПредстоящейОбязанности.ОписаниеДокумента(Событие.Уведомление);
		Иначе
			ПредставлениеУведомления = Документы.УведомлениеОбИсчисленныхСуммахНалогов.ОписаниеДокумента(Событие.Уведомление);
		КонецЕсли;
		ЗаголовокУведомления     = ПредставлениеУведомления.Наименование;
		СуммаУведомления         = ПредставлениеУведомления.Сумма;
	Иначе
		ЗаголовокУведомления = Событие.Наименование;
		СуммаУведомления     = Событие.Сумма;
	КонецЕсли;
	
	Форма.ИтогПоУведомлениям = Форма.ИтогПоУведомлениям + СуммаУведомления;
	// Платежный документ
	Если ТипЗнч(Событие.Уведомление) = Тип("ДокументСсылка.ЗаявлениеОЗачетеВСчетПредстоящейОбязанности") Тогда
		Элемент = Элементы["ОткрытьЗаявление" + Номер];
	Иначе
		Элемент = Элементы["ОткрытьУведомление" + Номер];
	КонецЕсли;
	Элемент.Заголовок = ЗаголовокУведомления;
	
КонецПроцедуры

#КонецОбласти
