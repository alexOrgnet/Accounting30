#Область СлужебныйПрограммныйИнтерфейс

// Выводит оповещения о новых сообщениях от банков по заявкам на кредит
Процедура Подключаемый_ОповеститьОНовыхСообщенияхПоЗаявкамНаОткрытиеСчета() Экспорт
	
	// Если администратор сервиса вышел из области, но сеанс не закрыл,
	// то обработчик ожидания может быть вызван снова.
	// Поэтому проверим возможность обращения к разделенным данным области.
	ПараметрыРаботыКлиента = СтандартныеПодсистемыКлиент.ПараметрыРаботыКлиента();
	Если НЕ ПараметрыРаботыКлиента.ДоступноИспользованиеРазделенныхДанных Тогда
		Возврат;
	КонецЕсли;

	ПовторятьЗапрос = Истина;
	
	НовыеСообщенияОтБанков = ЗаявкиНаОткрытиеСчетаВызовСервера.НовыеСообщенияОтБанков(ПовторятьЗапрос);
	
	Если НовыеСообщенияОтБанков.Количество() > 0 Тогда
		ЗаявкиНаОткрытиеСчетаКлиент.ОповеститьОНовыхСообщенияхОтБанков(НовыеСообщенияОтБанков);
	КонецЕсли;
	
	Если ПовторятьЗапрос Тогда
		
		// Рег.задание по проверке новых сообщений от банков запускается с "затухающим" расписанием,
		// сообщения же пользователю о том, что есть не прочитанные сообщения от банков показываем раз в 30 мин.
		ИнтервалВыполненияПроверки = 1800;
		
		ПодключитьОбработчикОжидания("Подключаемый_ОповеститьОНовыхСообщенияхПоЗаявкамНаОткрытиеСчета",
			ИнтервалВыполненияПроверки, Истина);
	КонецЕсли; 
	
КонецПроцедуры

Процедура Подключаемый_ОповеститьОбОткрытииСчета() Экспорт
	
	// Если администратор сервиса вышел из области, но сеанс не закрыл,
	// то обработчик ожидания может быть вызван снова.
	// Поэтому проверим возможность обращения к разделенным данным области.
	ПараметрыРаботыКлиента = СтандартныеПодсистемыКлиент.ПараметрыРаботыКлиента();
	Если НЕ ПараметрыРаботыКлиента.ДоступноИспользованиеРазделенныхДанных Тогда
		Возврат;
	КонецЕсли;
	
	ПовторятьЗапрос = Истина;

	НовыеСообщенияОтБанков = ЗаявкиНаОткрытиеСчетаВызовСервера.НовыеСообщенияОтБанковОбОткрытии(ПовторятьЗапрос);
	
	Если НовыеСообщенияОтБанков.Количество() > 0 Тогда
		ЗаявкиНаОткрытиеСчетаКлиент.ОповеститьОбОткрытииНовыхСчетов(НовыеСообщенияОтБанков);
	КонецЕсли;
	
	Если ПовторятьЗапрос Тогда

		// Рег.задание по проверке новых сообщений от банков запускается с "затухающим" расписанием,
		// сообщения же пользователю о том, что есть не прочитанные сообщения от банков показываем раз в 30 мин.
		ИнтервалВыполненияПроверки = 1800;
	
		ПодключитьОбработчикОжидания("Подключаемый_ОповеститьОбОткрытииСчета", 
			ИнтервалВыполненияПроверки, Истина);
	КонецЕсли; 

КонецПроцедуры

// Выводит окно для подключения к интернет-поддержке
Процедура Подключаемый_НачатьПодключениеИнтернетПоддержкиИзЗаявкиНаОткрытиеСчета() Экспорт
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПодключенияИнтернетПоддержки", ЗаявкиНаОткрытиеСчетаКлиент);	

	ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти
