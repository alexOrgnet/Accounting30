
////////////////////////////////////////////////////////////////////////////////
// Универсальные методы для формы записи регистра и формы настройки налогов
//
// Серверные методы формы записи регистра сведений НастройкиУчетаНДС
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьФормуНаСервере(Форма) Экспорт
	
	Запись   = Форма.НастройкиУчетаНДС;
	Элементы = Форма.Элементы;
	
	Если НЕ ЗначениеЗаполнено(Запись.Организация) Тогда
		Запись.Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;
	
	РазрешенПолныйИнтерфейс = ТарификацияБП.РазрешенПолныйИнтерфейс();
	
	Элементы.ГруппаОграниченияТарифаНДС.Видимость = Не РазрешенПолныйИнтерфейс;
	Элементы.СложныйУчетНДС.Доступность = РазрешенПолныйИнтерфейс;
	
	ДобавитьБаннерПолныйИнтерфейсДляЭлементаФункциональности(Элементы.СложныйУчетНДС);
	
	НастройкиУчетаНДСФормыКлиентСервер.УправлениеФормой(Форма);
	
КонецПроцедуры

Процедура ДобавитьБаннерПолныйИнтерфейсДляЭлементаФункциональности(Элемент)
	
	ТекстБаннераЭлемента = НСтр("ru = 'Полный интерфейс'");
	ЦветБаннера = ЦветаСтиля.СобытиеОтказ;
	ДобавитьПодсказкуБаннерКЭлементу(Элемент, ТекстБаннераЭлемента, ЦветБаннера);
	
КонецПроцедуры

Процедура ДобавитьПодсказкуБаннерКЭлементу(Элемент, ТекстБаннераЭлемента, ЦветБаннера)
	
	ЗаголовокПодсказки = Новый ФорматированнаяСтрока(
		ТекстБаннераЭлемента,
		Новый Шрифт(ШрифтыСтиля.ОбычныйШрифтТекста, , 8),
		ЦветаСтиля.ЦветТекстаФормы,
		ЦветБаннера);
	
	Элемент.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСправа;
	Элемент.РасширеннаяПодсказка.Заголовок = ЗаголовокПодсказки;
	Элемент.РасширеннаяПодсказка.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Центр;
	
КонецПроцедуры

#КонецОбласти