#Область ПрограммныйИнтерфейс

// Возвращает параметры для отправки сообщения в СЭДО.
// Реквизиты РегистрационныйНомерФСС и СвойстваДляОбмена заполняются автоматически при вызове с сервера,
// при вызове с клиента эти реквизиты нужно заполнять самостоятельно.
//
// Параметры:
//   ТипСообщения                 - Число     - тип сообщения СЭДО из спецификации типов документов,
//   СодержимоеИлиАдресСообщения  - Строка    - текст выгрузки сообщения СЭДО по спецификации типов документов или
//                                - Строка    - адрес во временном хранилище, по которому содержится строка или двоичные
//                                              данные в кодировке utf-8 текста выгрузки сообщения СЭДО по спецификации
//                                              типов документов
//   Организация                  - СправочникСсылка.Организации - организация отправитель,
//   ОписаниеОшибки               - Строка    - шаблон текста сообщения, возвращаемого в ключе "ОписаниеОшибки"
//                                              результата оповещения обратного вызова при неудаче отправки,
//                                              с подстановкой текста ошибки вместо %1, например:
//                                              НСтр("ru = 'Не удалось подписать организацию на оповещения об изменении состояний ЭЛН сотрудников.'") + Символы.ПС + "%1",
//   РегистрационныйНомерФСС      - Строка    - регистрационный номер ФСС (дополнительный код ФСС в случае филиала),
//                                              при пустом значении подставляется из организации если метод вызывается с сервера.
//   ТипВзаимодействия            - Число     - Для отпрвки от имени страхователя передается значение 2, для отправки МЧД 3.
//                                              Если значение не заполнено, то тип определяется по типу сообщения.
//   СвойстваДляОбмена            - Структура - при значении Неопределено заполняется автоматически при вызове с сервера, при типе
//                                              взаимодействия 3 (МЧД) можно передать структуру с реквизитами
//                                              "ОГРН", "ИНН", "КПП", "СНИЛС"
// Возвращаемое значение:
//   Структура - переданные параметры отправки сообщения (незаполненные могут вычисляться) для передачи в процедуру
//               "ЭлектронныйДокументооборотСФССКлиент.ОтправитьСообщениеСЭДО", параметр "ПараметрыСообщения".
//
Функция ПараметрыОтправитьСообщениеСЭДО(
		ТипСообщения = 0,
		СодержимоеИлиАдресСообщения = "",
		Организация = Неопределено,
		ОписаниеОшибки = "",
		РегистрационныйНомерФСС = "",
		ТипВзаимодействия = Неопределено,
		СвойстваДляОбмена = Неопределено) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ТипСообщения", 					ТипСообщения);
	Результат.Вставить("СодержимоеИлиАдресСообщения", 	СодержимоеИлиАдресСообщения);
	Результат.Вставить("Организация", 					Организация);
	Результат.Вставить("ОписаниеОшибки", 				ОписаниеОшибки);
	Результат.Вставить("РегистрационныйНомерФСС", 		РегистрационныйНомерФСС);
	Результат.Вставить("ТипВзаимодействия", 			ТипВзаимодействия);
	Результат.Вставить("СвойстваДляОбмена", 			СвойстваДляОбмена);
	
	Если НЕ ЗначениеЗаполнено(Результат.РегистрационныйНомерФСС) И Результат.СвойстваДляОбмена <> Неопределено
		И Результат.СвойстваДляОбмена.Свойство("РегистрационныйНомерФСС") Тогда
		
		Результат.РегистрационныйНомерФСС = Результат.СвойстваДляОбмена.РегистрационныйНомерФСС;
	КонецЕсли;
	
	Если Результат.ТипВзаимодействия = Неопределено Тогда
		Результат.ТипВзаимодействия = ДокументооборотСФССКлиентСервер.ТипВзаимодействияСЭДО(ТипСообщения);
	КонецЕсли;
	
	ЭтоТипВзаимодействияОрганизацияСЭДО = (Результат.ТипВзаимодействия =
		ДокументооборотСФССКлиентСервер.ТипВзаимодействияОрганизацияСЭДО());
		
	#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	Если ЗначениеЗаполнено(Результат.Организация) И (НЕ ЗначениеЗаполнено(Результат.РегистрационныйНомерФСС)
		ИЛИ ЭтоТипВзаимодействияОрганизацияСЭДО И Результат.СвойстваДляОбмена = Неопределено) Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		КонтекстЭДО = ДокументооборотСКО.ПолучитьОбработкуЭДО();
		ПолучитьОГРН 			= ЭтоТипВзаимодействияОрганизацияСЭДО;
		ПолучитьИННКППиСНИЛС 	= ЭтоТипВзаимодействияОрганизацияСЭДО;
		Результат.СвойстваДляОбмена = КонтекстЭДО.СвойстваОрганизацииДляОбмена(
			Результат.Организация, ,
			ПолучитьОГРН, ,
			ПолучитьИННКППиСНИЛС);
		УстановитьПривилегированныйРежим(Ложь);
		Результат.РегистрационныйНомерФСС = ?(ЗначениеЗаполнено(Результат.СвойстваДляОбмена.РегистрационныйНомерФСС),
			СокрЛП(Результат.СвойстваДляОбмена.РегистрационныйНомерФСС), Неопределено);
	КонецЕсли;
	#КонецЕсли
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ПараметрыСохраненияЗапросаСЭДОФСС(Организация, Операция) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Организация", 				Организация);
	Результат.Вставить("ОписаниеОшибки", 			"");
	Результат.Вставить("АдресЗапросаSOAP", 			Неопределено);
	Результат.Вставить("АдресОтветаSOAP", 			Неопределено);
	Результат.Вставить("Операция", 					Операция);
	Результат.Вставить("АдресРесурса", 				"");
	Результат.Вставить("АдресСервера", 				"");
	Результат.Вставить("КодСостояния", 				200);
	Результат.Вставить("Дата", 						Неопределено);
	Результат.Вставить("УдалятьИзХранилища", 		Ложь);
	Результат.Вставить("ТипСообщенияСЭДО", 			0);
	Результат.Вставить("ТипВзаимодействияСЭДО", 	0);
	Результат.Вставить("ИдентификаторФСС", 			"");
	Результат.Вставить("ИдентификаторПакетаФСС", 	"");
	Результат.Вставить("ОбменЧерезОператора", 		Ложь);
	Результат.Вставить("ЭтоПользовательскийЗапрос", Ложь);
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьТекстОшибкиПриПроверкеРегистрационногоНомера(РегистрационныйНомерФСС, Отправка, СвойстваОрганизации, ЕстьНеЦифры)
	
	Шаблон = НСтр("ru='При %1 сообщения СЭДО ФСС произошла ошибка:'") + Символы.ПС + "%2";
	ТекстОперацииОбмена = ?(Отправка, НСтр("ru='отправке'"), НСтр("ru='получении'"));
	
	Если НЕ СвойстваОрганизации = Неопределено 
		И (СвойстваОрганизации.Свойство("ЭтоДополнительныйКодФСС")
			И СвойстваОрганизации.ЭтоДополнительныйКодФСС) Тогда
			
		ТекстОшибкиОбменаПриПроверкеРегистрационногоНомера = НСтр("ru='Указан недопустимый дополнительный код обособленного подразделения'");
		ТекстОшибкиОбменаРеквизитПроверки = НСтр("ru='Дополнительный код'");
		ТекстОшибкиОбменаДлинаРеквизита = НСтр("ru='длина кода %1'");
	Иначе
		ТекстОшибкиОбменаПриПроверкеРегистрационногоНомера = НСтр("ru='Указан недопустимый регистрационный номер'");
		ТекстОшибкиОбменаРеквизитПроверки = НСтр("ru='Регистрационный номер'");
		ТекстОшибкиОбменаДлинаРеквизита = НСтр("ru='длина номера %1'");
	КонецЕсли;
	ТекстОшибкиОбменаДлинаРеквизита = СтрШаблон(ТекстОшибкиОбменаДлинаРеквизита, СтрДлина(РегистрационныйНомерФСС));
	
	Если ЕстьНеЦифры Тогда
		ШаблонОписаниеОшибки = СтрШаблон(Шаблон,
			ТекстОперацииОбмена,
			НСтр("ru='%1 страхователя %2, %3. %4 должен состоять минимум из 10 цифр.'"));
	Иначе
		ТекстОшибкиОбменаРеквизитПроверки = "";
		ШаблонОписаниеОшибки = СтрШаблон(Шаблон,
			ТекстОперацииОбмена,
			НСтр("ru='%1 страхователя %2. Текущая %3,%4 минимальная длина 10 цифр.'"));
	КонецЕсли;
	
	ОписаниеОшибки = СтрШаблон(
		ШаблонОписаниеОшибки,
		ТекстОшибкиОбменаПриПроверкеРегистрационногоНомера,
		РегистрационныйНомерФСС,
		ТекстОшибкиОбменаДлинаРеквизита,
		ТекстОшибкиОбменаРеквизитПроверки);
	
	Возврат ОписаниеОшибки;
	
КонецФункции

Процедура ЗаписатьСообщениеОбОшибкеПроверкиРегистрационногоНомераФССВЖурналРегистрации(
	Результат,
	РегистрационныйНомерФСС,
	СвойстваОрганизации,
	Отправка,
	ЕстьНеЦифры = Ложь)
	
	ОписаниеОшибки = ПолучитьТекстОшибкиПриПроверкеРегистрационногоНомера(РегистрационныйНомерФСС, Отправка, СвойстваОрганизации, ЕстьНеЦифры);
	
	ШаблонДляЖурналаРегистрации = НСтр("ru='Электронный документооборот с контролирующими органами. %1'");
	Если Отправка Тогда
		ТекстОшибки = НСтр("ru='Отправка данных в СЭДО ФСС'");
		ОписаниеОшибкиДляЖурналаРегистрации =
			СтрШаблон(ШаблонДляЖурналаРегистрации, ТекстОшибки);
	Иначе
		ТекстОшибки = НСтр("ru='Получение сообщения СЭДО ФСС'");
		ОписаниеОшибкиДляЖурналаРегистрации =
			СтрШаблон(ШаблонДляЖурналаРегистрации, ТекстОшибки);
	КонецЕсли;
	#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
		ЖурналРегистрации.ДобавитьСообщениеДляЖурналаРегистрации(
			ОписаниеОшибкиДляЖурналаРегистрации,
			УровеньЖурналаРегистрации.Ошибка, , ,
			ОписаниеОшибки);
	#Иначе
		ЖурналРегистрацииКлиент.ДобавитьСообщениеДляЖурналаРегистрации(
			ОписаниеОшибкиДляЖурналаРегистрации,
			"Ошибка",
			ОписаниеОшибки, ,
			Истина);
	#КонецЕсли
	
	Результат.Выполнено			= Ложь;
	Результат.ОписаниеОшибки	= ОписаниеОшибки;
	
КонецПроцедуры

Функция ПроверитьДлинуРегистрационныйНомерФСС(
	СвойстваОрганизации,
	РегистрационныйНомерФСС,
	Отправка = Истина,
	Результат) Экспорт
	
	Если НЕ СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(РегистрационныйНомерФСС) Тогда
		// Ошибка формата регистрационного номера СФР.
		ЗаписатьСообщениеОбОшибкеПроверкиРегистрационногоНомераФССВЖурналРегистрации(Результат, РегистрационныйНомерФСС, СвойстваОрганизации, Отправка, Истина);
		Возврат Ложь;
	КонецЕсли;
	
	Если СтрДлина(РегистрационныйНомерФСС) < 10 Тогда
		// Ошибка длины дополнительного кода СФР.
		ЗаписатьСообщениеОбОшибкеПроверкиРегистрационногоНомераФССВЖурналРегистрации(Результат, РегистрационныйНомерФСС, СвойстваОрганизации, Отправка);
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;

КонецФункции

Функция ПолучитьСтрокуДатВСтруктуре(
	Дата = Неопределено,
	ВремяНачала = Неопределено,
	ВремяКонца = Неопределено) Экспорт
	
	Если Дата = Неопределено Тогда
		Дата		= Дата(1,1,1);
		ВремяНачала	= Дата(1,1,1);
		ВремяКонца	= Дата(1,1,1);
	КонецЕсли;
	
	СтрокаДат = Новый Структура;
	СтрокаДат.Вставить("Дата",			Дата);
	СтрокаДат.Вставить("ВремяНачала",	ВремяНачала);
	СтрокаДат.Вставить("ВремяКонца",	ВремяКонца);
	
	Возврат СтрокаДат;
	
КонецФункции

Функция ПолучитьДатуПолученияСообщений(ДатыСообщений) Экспорт
	
	ДатаСообщений = Неопределено;
	
	Если НЕ ДатыСообщений = Неопределено
		И ЗначениеЗаполнено(ДатыСообщений.Дата) Тогда
		
		ДатаСообщений = Дата(Формат(ДатыСообщений.Дата, "ДФ=yyyyMMdd") +
			Формат(ДатыСообщений.ВремяКонца, "ДФ=HHmmss"));
	КонецЕсли;
	
	Возврат ДатаСообщений;
	
КонецФункции

Функция КлючОбъектаНастроекЗапросовКСерверуСЭДО() Экспорт
	
	Возврат "ТаймаутМеждуHTTPЗапросамиСЭДО";
	
КонецФункции

Функция КлючНастроекИнтервалаМеждуЗапросамиКСерверуСЭДО() Экспорт
	
	Возврат "Таймаут";
	
КонецФункции

Функция ИнтервалМеждуЗапросамиСпискаВходящихСообщенийСЭДОПоУмолчанию() Экспорт
	
	Возврат 300;
	
КонецФункции

#КонецОбласти