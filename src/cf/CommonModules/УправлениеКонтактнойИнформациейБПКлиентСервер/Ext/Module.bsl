#Область ПрограммныйИнтерфейс

// Возвращает строку дополнительных значений по имени реквизита.
//
// Параметры:
//    Форма   - ФормаКлиентскогоПриложения - передаваемая форма.
//    Элемент - ДанныеФормыСтруктураСКоллекцией - данные формы.
//
// Возвращаемое значение:
//    СтрокаКоллекции - найденные данные.
//    Неопределено    - при отсутствии данных.
//
Функция ЗначениеКонтактнойИнформацииФормы(Форма, ИмяЭлемента) Экспорт
	
	Отбор = Новый Структура("ИмяРеквизита", ИмяЭлемента);
	Строки = Форма.КонтактнаяИнформацияОписаниеДополнительныхРеквизитов.НайтиСтроки(Отбор);
	ДанныеСтроки = ?(Строки.Количество() = 0, Неопределено, Строки[0]);
	
	Возврат ДанныеСтроки;
	
КонецФункции

Процедура СкопироватьПолеКонтактнойИнформации(Форма, ИмяПриемника, ИмяИсточника) Экспорт
	
	Форма.Модифицированность = Истина;
	
	ИмяОписания = "КонтактнаяИнформацияОписаниеДополнительныхРеквизитов";
	
	Отбор = Новый Структура("ИмяРеквизита");
	
	Отбор.ИмяРеквизита = ИмяИсточника;
	ДанныеИсточника = Форма[ИмяОписания].НайтиСтроки(Отбор);
	Если ДанныеИсточника.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Отбор.ИмяРеквизита = ИмяПриемника;
	ДанныеПриемника = Форма[ИмяОписания].НайтиСтроки(Отбор);
	Если ДанныеПриемника.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаИсточника = ДанныеИсточника[0];
	СтрокаПриемника =  ДанныеПриемника[0];
	
	Если СтрокаИсточника.Тип <> СтрокаПриемника.Тип Тогда
		Возврат;
	КонецЕсли;
	
	// Поле источника может быть адресом - гиперссылкой, а поле приемника - нет.
	// Типичный пример - юридический адрес открывается по ссылке, а почтовый - редактируется в поле.
	// В этом случае при пустом юридическом адресе в представлении поля записано "Заполнить" для того,
	// чтобы гиперссылка показывалась. Хотя реально Представление в данных контактной информации пустое.
	// Поэтому проверим, что поле источника - гиперссылка, а поле приемника нет.
	Если СтрокаИсточника.Тип = ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Адрес")
		И ПолеАдресаГиперссылка(Форма, СтрокаИсточника)
		И Не ПолеАдресаГиперссылка(Форма, СтрокаПриемника)
		И Форма[ИмяИсточника] = УправлениеКонтактнойИнформациейКлиентСервер.ТекстПустогоАдресаВВидеГиперссылки() Тогда
		Форма[ИмяПриемника] = "";
	Иначе
		Форма[ИмяПриемника] = Форма[ИмяИсточника];
	КонецЕсли;
	
	СтрокаПриемника.Значение = СтрокаИсточника.Значение;
	СтрокаПриемника.Представление = СтрокаИсточника.Представление;
	СтрокаПриемника.Комментарий = СтрокаИсточника.Комментарий;
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

Функция ПолеАдресаГиперссылка(Форма, ДанныеПоля)
	
	ЭлементАдреса = Форма.Элементы.Найти(ДанныеПоля.ИмяРеквизита);
	Если ЭлементАдреса = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ТипЗнч(ЭлементАдреса) <> Тип("ПолеФормы") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ЭлементАдреса.Вид <> ВидПоляФормы.ПолеНадписи Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат ЭлементАдреса.Гиперссылка;
	
КонецФункции

#КонецОбласти