#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, , "ЗакрыватьПриВыборе,ЗакрыватьПриЗакрытииВладельца,ТолькоПросмотр");
	ЭтоТребованиеНакладная = Параметры.ЭтоТребованиеНакладная;
	Элементы.ПорядокОтраженияВПрослеживаемости.Видимость = Параметры.ОтражатьВыборОтраженияВПрослеживаемости;
	Если ЭтоТребованиеНакладная И Параметры.ОтражатьВыборОтраженияВПрослеживаемости Тогда
		ТоварыПередаютсяВСоставеРабот = Параметры.ТоварыПередаютсяВСоставеРабот;
		ЭтотОбъект.Заголовок = НСтр("ru = 'Порядок отражения в прослеживаемости'");
		Если ТоварыПередаютсяВСоставеРабот Тогда
			ПорядокОтраженияВПрослеживаемости = "ПередаютсяВСоставеРабот";
			НастройкаВидимостиЭлементов();
		Иначе
			ПорядокОтраженияВПрослеживаемости = "ВыбываютИзПрослеживаемости";
		КонецЕсли;
	КонецЕсли;
	Если ЗначениеЗаполнено(Параметры.КодОперацииПрослеживаемости) Тогда
		КодОперацииПрослеживаемости = Параметры.КодОперацииПрослеживаемости;
		Элементы.НадписьКодОперацииПрослеживаемости.Заголовок = КодОперацииПрослеживаемостиНаименованиеНаСервере();
	КонецЕсли;
	ЭтоОС = Параметры.ЭтоОС;
	ЗаполнитьСписокВыбораКодаОперацииПрослеживаемости();

	ЕстьПравоИзменения = Истина;
	Если ЗначениеЗаполнено(Параметры.ТекущийДокумент) Тогда
		ЕстьПравоИзменения = ПравоДоступа("Изменение", Параметры.ТекущийДокумент.Метаданные());
	КонецЕсли;
	
	ЭтотОбъект.Доступность = ЕстьПравоИзменения;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ПеренестиВДокумент Тогда
		Если ЭтоТребованиеНакладная Тогда
			СтруктураРезультат = Новый Структура("КодОперацииПрослеживаемости, ТоварыПередаютсяВСоставеРабот",
			КодОперацииПрослеживаемости, ТоварыПередаютсяВСоставеРабот);
			ОповеститьОВыборе(СтруктураРезультат);
		Иначе
			ОповеститьОВыборе(КодОперацииПрослеживаемости);
		Конецесли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы И Модифицированность Тогда
		
		Отказ = Истина;
	
	ИначеЕсли Модифицированность И Не ПеренестиВДокумент Тогда
		
		Отказ = Истина;
		
		Оповещение = Новый ОписаниеОповещения("ВопросСохраненияДанныхЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да);
		
	ИначеЕсли ПеренестиВДокумент Тогда
		
		ОбработкаПроверкиЗаполненияНаКлиенте(Отказ);
		Если Отказ Тогда
			Модифицированность = Истина;
			ПеренестиВДокумент = Ложь;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КодОперацииПрослеживаемостиПриИзменении(Элемент)
	Модифицированность = Истина;
	Элементы.НадписьКодОперацииПрослеживаемости.Заголовок = КодОперацииПрослеживаемостиНаименованиеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПорядокОтражениВПрослеживаемостиПриИзменении(Элемент)
	
	Если ПорядокОтраженияВПрослеживаемости = "ВыбываютИзПрослеживаемости" Тогда
		ТоварыПередаютсяВСоставеРабот = Ложь;
	Иначе
		ТоварыПередаютсяВСоставеРабот = Истина;
		КодОперацииПрослеживаемости = ПредопределенноеЗначение("Справочник.КодыОперацийПрослеживаемости.ПустаяСсылка");
		Элементы.НадписьКодОперацииПрослеживаемости.Заголовок = "";
	КонецЕсли;
	
	НастройкаВидимостиЭлементов();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ПеренестиВДокумент = Истина;
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Модифицированность = Ложь;
	ПеренестиВДокумент = Ложь;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере 
Процедура ЗаполнитьСписокВыбораКодаОперацииПрослеживаемости()
	
	Элементы.КодОперацииПрослеживаемости.СписокВыбора.Очистить();
	
	МассивКодов = Новый Массив();
	Если Не ЭтоОС Тогда
		МассивКодов.Добавить(Справочники.КодыОперацийПрослеживаемости.ПередачаВПроизводство);
	КонецЕсли;
	МассивКодов.Добавить(Справочники.КодыОперацийПрослеживаемости.ЗахоронениеОбезвреживаниеУтилизацияТовара);
	МассивКодов.Добавить(Справочники.КодыОперацийПрослеживаемости.УничтожениеУтратаВследствиеДействияНепреодолимойСилы);
	МассивКодов.Добавить(Справочники.КодыОперацийПрослеживаемости.КонфискацияГосударством);
	МассивКодов.Добавить(Справочники.КодыОперацийПрослеживаемости.НедостачаВыявленнаяВХодеИнвентаризации);
	МассивКодов.Добавить(Справочники.КодыОперацийПрослеживаемости.ПередачаНеСвязаннаяСРеализацией);
	
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КодыОперацийПрослеживаемости.Код + "" - "" + КодыОперацийПрослеживаемости.Наименование КАК Представление,
	|	КодыОперацийПрослеживаемости.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.КодыОперацийПрослеживаемости КАК КодыОперацийПрослеживаемости
	|ГДЕ
	|	КодыОперацийПрослеживаемости.Ссылка В(&МассивКодов)";
	Запрос.УстановитьПараметр("МассивКодов", МассивКодов);
	Результат = Запрос.Выполнить().Выгрузить();
	Индекс = 0;
	Для каждого СтрокаКода Из Результат Цикл
		Элементы.КодОперацииПрослеживаемости.СписокВыбора.Вставить(Индекс,СтрокаКода.Ссылка, СтрокаКода.Представление);
		Индекс = Индекс + 1;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаПроверкиЗаполненияНаКлиенте(Отказ)

	Если Не ЗначениеЗаполнено(КодОперацииПрослеживаемости) И Не (ЭтоТребованиеНакладная И ТоварыПередаютсяВСоставеРабот) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
			"Поле", "Заполнение", НСтр("ru = 'Причина списания в системе прослеживаемости'"));
		Поле = "КодОперацииПрослеживаемости";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , Поле, "", Отказ);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция КодОперацииПрослеживаемостиНаименованиеНаСервере()
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КодОперацииПрослеживаемости, "Наименование");
КонецФункции

&НаКлиенте
Процедура ВопросСохраненияДанныхЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Модифицированность = Ложь;
		ПеренестиВДокумент = Истина;
		Закрыть();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		ПеренестиВДокумент = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

Процедура НастройкаВидимостиЭлементов()
	
	Элементы.КодОперацииПрослеживаемости.Видимость                 = Не ТоварыПередаютсяВСоставеРабот;
	Элементы.НадписьКодОперацииПрослеживаемости.Видимость          = Не ТоварыПередаютсяВСоставеРабот;
	Элементы.НадписьПодсказка.Видимость                            = Не ТоварыПередаютсяВСоставеРабот;
	Элементы.НадписьПодсказкаРНПТПередаютсяВСоставеРабот.Видимость = ТоварыПередаютсяВСоставеРабот;

КонецПроцедуры
#КонецОбласти
