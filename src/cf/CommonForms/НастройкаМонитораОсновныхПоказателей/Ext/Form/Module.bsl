#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Получаем настройки
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, 
		"СписокРазделов, 
		|ВариантОкругления, 
		|СравниватьСПрошлымПериодом,
		|СрокХраненияТоваров,
		|ПоказыватьОборотыЗаТекущийМесяц,
		|ПоказыватьОборотыЗаПрошлыйМесяц,
		|ПоказыватьОборотыСНачалаГодаПоТекущийМесяц,
		|ПоказыватьОборотыСНачалаГодаПоПрошлыйМесяц");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьИЗакрыть(Команда)
	
	НеВыбранПоказательПоказыватьОбороты =  Не ПоказыватьОборотыЗаТекущийМесяц
		И Не ПоказыватьОборотыЗаПрошлыйМесяц
		И Не ПоказыватьОборотыСНачалаГодаПоТекущийМесяц
		И Не ПоказыватьОборотыСНачалаГодаПоПрошлыйМесяц;
	
	Если НеВыбранПоказательПоказыватьОбороты Тогда
		
		ТекстОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
			"Поле", "Корректность", НСтр("ru='Показывать оброты за период'"),,, 
			НСтр("ru='Должен быть отмечен хотя бы один показатель.'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "ПоказыватьОборотыЗаТекущийМесяц");
		
		Возврат;
	КонецЕсли;
	
	// Сохраняем настройки
	СохранитьНастройкиНаСервере(СписокРазделов.Скопировать());
	
	Закрыть(Модифицированность);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	// Просто закрываем форму
	Закрыть(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтандартныеНастройки(Команда)
	
	ЗаполнитьСписокРазделовПоУмолчанию();
	ВариантОкругления                          = 1000;
	СрокХраненияТоваров                        = 6;
	СравниватьСПрошлымПериодом                 = Ложь;
	Модифицированность                         = Истина;
	ПоказыватьОборотыЗаТекущийМесяц            = Истина;
	ПоказыватьОборотыЗаПрошлыйМесяц            = Ложь;
	ПоказыватьОборотыСНачалаГодаПоТекущийМесяц = Истина;
	ПоказыватьОборотыСНачалаГодаПоПрошлыйМесяц = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция СписокРазделов()
	
	Список = Новый СписокЗначений;
	Список.Добавить("ДенежныеСредства", "Денежные средства", Истина);
	Список.Добавить("ВзаимоотношенияСКонтрагентами", "Взаимоотношения с контрагентами", Истина);
	Список.Добавить("ПродажиИСклад", "Продажи и склад", Истина);
	Список.Добавить("ДоходыРасходыИПрибыль", "Доходы, расходы и прибыль", Истина);
	
	Возврат Список;
	
КонецФункции

&НаСервере
Процедура ЗагрузитьСписокРазделов(Список)
	
	СписокРазделов.Очистить();
	
	Для каждого Раздел Из Список Цикл
		
		СписокРазделов.Добавить(Раздел.Значение, Раздел.Представление, Раздел.Пометка);
		
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокРазделовПоУмолчанию()
	
	СписокРазделовПоУмолчанию = СписокРазделов();
	ЗагрузитьСписокРазделов(СписокРазделовПоУмолчанию);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиНаСервере(СписокРазделовМонитора)
	
	// Записываем настройки в хранилище
	ХранилищеОбщихНастроек.Сохранить("МониторОсновныхПоказателей", 
		"СписокРазделовМонитораРуководителя", 
		СписокРазделовМонитора);
		
	ХранилищеОбщихНастроек.Сохранить("МониторОсновныхПоказателей", 
		"ВариантОкругленияМонитораРуководителя", 
		ВариантОкругления);
		
	ХранилищеОбщихНастроек.Сохранить("МониторОсновныхПоказателей", 
		"СрокХраненияТоваров", 
		СрокХраненияТоваров);
		
	ХранилищеОбщихНастроек.Сохранить("МониторОсновныхПоказателей", 
		"НовыйСравниватьСПрошлымПериодом", 
		СравниватьСПрошлымПериодом);
		
	ХранилищеОбщихНастроек.Сохранить("МониторОсновныхПоказателей", 
		"НовыйПоказыватьОборотыЗаТекущийМесяц", 
		ПоказыватьОборотыЗаТекущийМесяц);
		
	ХранилищеОбщихНастроек.Сохранить("МониторОсновныхПоказателей", 
		"НовыйПоказыватьОборотыЗаПрошлыйМесяц",
		ПоказыватьОборотыЗаПрошлыйМесяц);
		
	ХранилищеОбщихНастроек.Сохранить("МониторОсновныхПоказателей", 
		"НовыйПоказыватьОборотыСНачалаГодаПоТекущийМесяц", 
		ПоказыватьОборотыСНачалаГодаПоТекущийМесяц);
		
	ХранилищеОбщихНастроек.Сохранить("МониторОсновныхПоказателей", 
		"НовыйПоказыватьОборотыСНачалаГодаПоПрошлыйМесяц", 
		ПоказыватьОборотыСНачалаГодаПоПрошлыйМесяц);
		
	ХранилищеОбщихНастроек.Сохранить("МониторОсновныхПоказателей", 
		"НовыйПоказыватьУведомлениеОНастройке", 
		Ложь);
	
КонецПроцедуры

#КонецОбласти
