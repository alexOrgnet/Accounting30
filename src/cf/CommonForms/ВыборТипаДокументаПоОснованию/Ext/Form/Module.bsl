#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Параметры.Основание) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	МетаданныеОснование = Параметры.Основание.Метаданные();
	Для каждого ОбъектМетаданных Из Метаданные.Документы Цикл
		Если ОбъектМетаданных.ВводитсяНаОсновании.Содержит(МетаданныеОснование)
			И ПравоДоступа("Редактирование", ОбъектМетаданных)
			И ОбщегоНазначения.ОбъектМетаданныхДоступенПоФункциональнымОпциям(ОбъектМетаданных) Тогда
			
			ДобавитьДокумент(ОбъектМетаданных, Параметры.Основание);
		КонецЕсли;
	КонецЦикла;
	
	Если ТипыДокументов.Количество() = 0 Тогда
		Отказ = Истина;
	Иначе
		ТипыДокументов.СортироватьПоПредставлению();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТипыДокументов

&НаКлиенте
Процедура ТипыДокументовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ТипыДокументов.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ОповеститьОВыборе(ТекущиеДанные.Значение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ТекущиеДанные = Элементы.ТипыДокументов.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ОповеститьОВыборе(ТекущиеДанные.Значение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДобавитьДокумент(ОбъектМетаданных, Основание)

	Если НЕ ОбъектМетаданных = Метаданные.Документы.РозничнаяПродажа 
		ИЛИ НЕ Основание.Метаданные() = Метаданные.Документы.РозничнаяПродажа Тогда
	
		ТипыДокументов.Добавить(ОбъектМетаданных.Имя, ОбъектМетаданных.Синоним);
		Возврат;
	КонецЕсли;
	
	ВидОперации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Основание, "ВидОперации");
	Если ВидОперации = Перечисления.ВидыОперацийРозничнаяПродажа.Предоплата Тогда
		ТипыДокументов.Добавить("РозничнаяПродажа", НСтр("ru = 'Чек'"));
	КонецЕсли;
	Если ВидОперации <> Перечисления.ВидыОперацийРозничнаяПродажа.Возврат Тогда
		ТипыДокументов.Добавить("РозничныйВозврат", НСтр("ru = 'Возврат'"));
	КонецЕсли;

КонецПроцедуры
	
#КонецОбласти
