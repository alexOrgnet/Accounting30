
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДоступенОтборПоВидуОперации = ЭтоПоступлениеТоваровУслуг(Параметры.ИмяДокумента);
	ВидимостьКолонкиВидСчетаФактуры = ЭтоСчетФактураПолученный(Параметры.ИмяДокумента);
	
	УстановитьТекстЗапросаДинамическогоСписка(Параметры.ИмяДокумента);
	
	ЗаполнитьИУстановитьОтборы(Параметры.ИмяДокумента, Параметры.СпособОбработки);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОтборКонтрагентИспользованиеПриИзменении(Элемент)
	
	УстановитьОтборы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКонтрагентПриИзменении(Элемент)
	
	ОтборКонтрагентИспользование = ЗначениеЗаполнено(ОтборКонтрагент);
	
	УстановитьОтборы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияИспользованиеПриИзменении(Элемент)
	
	УстановитьОтборы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	
	ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
	
	УстановитьОтборы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидОперацииИспользованиеПриИзменении(Элемент)
	
	УстановитьОтборы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидОперацииПриИзменении(Элемент)
	
	ОтборВидОперацииИспользование = ЗначениеЗаполнено(ОтборВидОперации);
	
	УстановитьОтборы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьТекстЗапросаДинамическогоСписка(ИмяОбъекта)
	
	ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ИмяОбъекта);
	
	ВыбираемыеПоля = Новый Массив;
	Если Метаданные.Справочники.Содержит(ОбъектМетаданных) Тогда
		ВыбираемыеПоля.Добавить("Ссылка");
	ИначеЕсли Метаданные.Документы.Содержит(ОбъектМетаданных) Тогда
		ВыбираемыеПоля.Добавить("Ссылка");
		ВыбираемыеПоля.Добавить("Номер");
		ВыбираемыеПоля.Добавить("Дата");
		
		Поля = Новый Массив;
		Поля.Добавить("НомерВходящегоДокумента");
		Поля.Добавить("ДатаВходящегоДокумента");
		Поля.Добавить("Организация");
		Поля.Добавить("Контрагент");
		Поля.Добавить("СуммаДокумента");
		Для Каждого Поле Из Поля Цикл
			Если ОбъектМетаданных.Реквизиты.Найти(Поле) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			ВыбираемыеПоля.Добавить(Поле);
		КонецЦикла;
		
		Если ЭтоПоступлениеТоваровУслуг(ИмяОбъекта) Тогда
			ВыбираемыеПоля.Добавить("ВидОперации");
		КонецЕсли;
		
		Если ЭтоСчетФактураПолученный(ИмяОбъекта) Тогда
			ВыбираемыеПоля.Добавить("ВидСчетаФактуры");
		КонецЕсли;
	КонецЕсли;
	
	Список.ТекстЗапроса = ТекстЗапроса(ИмяОбъекта, ВыбираемыеПоля);
	Список.ОсновнаяТаблица = ИмяОбъекта;
	Список.ДинамическоеСчитываниеДанных = Истина;
	
КонецПроцедуры

&НаСервере
Функция ТекстЗапроса(ИсточникДанных, ВыбираемыеПоля)
	
	КонструкторЗапроса = Новый СхемаЗапроса;
	ПакетЗапроса = КонструкторЗапроса.ПакетЗапросов[0];
	ОператорЗапроса = ПакетЗапроса.Операторы[0];
	ОператорЗапроса.Источники.Добавить(ИсточникДанных, "Объект");
	Для Каждого Поле Из ВыбираемыеПоля Цикл
		ВыражениеЗапроса = ОператорЗапроса.ВыбираемыеПоля.Добавить("Объект." + Поле);
		ПолеЗапроса = ПакетЗапроса.Колонки.Найти(ВыражениеЗапроса);
		ПолеЗапроса.Псевдоним = Поле;
	КонецЦикла;
	
	Возврат КонструкторЗапроса.ПолучитьТекстЗапроса();
	
КонецФункции

&НаСервере
Процедура ЗаполнитьИУстановитьОтборы(ИмяДокумента, СпособОбработки)
	
	ОтборКонтрагент = Параметры.Контрагент;
	ОтборКонтрагентИспользование = ЗначениеЗаполнено(ОтборКонтрагент);
	
	ОтборОрганизация = Параметры.Организация;
	ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
	
	ОтборВидОперации = ВидОперацииПоСпособуОбработки(ИмяДокумента, СпособОбработки);
	ОтборВидОперацииИспользование = ЗначениеЗаполнено(ОтборВидОперации);
	
	УстановитьОтборы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборы(Форма)
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(Форма, "Контрагент");
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(Форма, "Организация");
	Если Форма.ДоступенОтборПоВидуОперации Тогда
		ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(Форма, "ВидОперации");
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЭтоПоступлениеТоваровУслуг(ИмяДокумента)
	
	Возврат (ИмяДокумента = Метаданные.Документы.ПоступлениеТоваровУслуг.ПолноеИмя());
	
КонецФункции

&НаСервереБезКонтекста
Функция ЭтоСчетФактураПолученный(ИмяДокумента)
	
	Возврат (ИмяДокумента = Метаданные.Документы.СчетФактураПолученный.ПолноеИмя());
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.ГруппаОтборВидОперации.Видимость = Форма.ДоступенОтборПоВидуОперации;
	Элементы.ВидСчетаФактуры.Видимость        = Форма.ВидимостьКолонкиВидСчетаФактуры;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВидОперацииПоСпособуОбработки(ИмяДокумента, СпособОбработки)
	
	Если Не ЭтоПоступлениеТоваровУслуг(ИмяДокумента) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ОбменСКонтрагентамиБП.ВидОперацииДокументаПоступленияПоСпособуОбработки(СпособОбработки);
	
КонецФункции

#КонецОбласти