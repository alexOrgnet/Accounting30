
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекстОшибки = Параметры.ТекстОшибки;
	
	Если ЗначениеЗаполнено(Параметры.Документы) Тогда
		Для Каждого Документ Из Параметры.Документы Цикл
			Строка = ИдентификаторыДокументов.Добавить();
			Строка.Идентификатор = Документ;
		КонецЦикла;
		Элементы.ДекорацияФайлы.Заголовок = 
			СтрШаблон(НСтр("ru = 'Прикрепленные файлы (%1)'"), ИдентификаторыДокументов.Количество());
	Иначе
		Элементы.ЗаголовокФайлы.Видимость = Ложь;
		Элементы.ДекорацияФайлы.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияФайлыНажатие(Элемент)
	
	ДанныеФайлов = ДанныеФайлов();
	СохраняемыеФайлы = Новый Массив;
	Для Каждого Данные Из ДанныеФайлов Цикл
		СохраняемыеФайлы.Добавить(Новый ОписаниеПередаваемогоФайла(Данные.Ключ, Данные.Значение));
	КонецЦикла;
	
	ПараметрыЗаписи = ФайловаяСистемаКлиент.ПараметрыСохраненияФайла();
	ПараметрыЗаписи.Интерактивно = Истина;
	ФайловаяСистемаКлиент.СохранитьФайлы(Неопределено, СохраняемыеФайлы, ПараметрыЗаписи);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ДанныеФайлов()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДокументыАУСН.Организация КАК Организация,
		|	ДокументыАУСН.Тип КАК Тип,
		|	ДокументыАУСН.Идентификатор КАК Идентификатор,
		|	ДокументыАУСН.ИдентификаторБанка КАК ИдентификаторБанка
		|ИЗ
		|	РегистрСведений.ДокументыАУСН КАК ДокументыАУСН
		|ГДЕ
		|	ДокументыАУСН.Идентификатор В (&Идентификаторы)";
	
	Идентификаторы = Новый Массив;
	Для Каждого Строка Из ИдентификаторыДокументов Цикл
		Идентификаторы.Добавить(Строка.Идентификатор);
	КонецЦикла;
	Запрос.УстановитьПараметр("Идентификаторы", Идентификаторы);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Данные = Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		
		МенеджерЗаписи = РегистрыСведений.ДокументыАУСН.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
		МенеджерЗаписи.Прочитать();
	
		ДвоичныеДанные = Base64Значение(МенеджерЗаписи.Данные.Получить());
		Данные.Вставить(
			СтрШаблон("%1.zip", Выборка.Идентификатор),
			ПоместитьВоВременноеХранилище(ДвоичныеДанные, УникальныйИдентификатор));
		
	КонецЦикла;
	
	Возврат Данные;
	
КонецФункции

#КонецОбласти