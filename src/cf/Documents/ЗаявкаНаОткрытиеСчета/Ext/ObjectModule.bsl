#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	Дата = НачалоДня(ТекущаяДатаСеанса()); // Заявку заполняем всегда по состоянию "на сегодня".
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения); // Общие для документов реквизиты (организация и т.п.)
	
	Документы.ЗаявкаНаОткрытиеСчета.ЗаполнитьПоУмолчанию(ЭтотОбъект);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьПоСтруктуре(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Ответственный = Пользователи.ТекущийПользователь();
	
	Документы.ЗаявкаНаОткрытиеСчета.ЗаполнитьПоУмолчанию(ЭтотОбъект);

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		ДополнительныеСвойства.Вставить("СинхронизироватьПрисоединенныеФайлы", Ложь);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	РегистрыСведений.СостояниеЗаявокНаОткрытиеСчета.УстановитьСостояниеНеотправленнойЗаявки(Ссылка);
	
	Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "СинхронизироватьПрисоединенныеФайлы", Истина) Тогда
		// Если присоед. файлы из справочника "ЗаявкаНаОткрытиеСчетаПрисоединенныеФайлы" выбраны в строках в текущем документе,
		// то у них не должно быть пометки удаления, если есть - то ее снять. И наоборот - поставить для удаленных.

		ТипПрисоединенногоФайлаПакета = Тип("СправочникСсылка.ЗаявкаНаОткрытиеСчетаПрисоединенныеФайлы");
		СписокФайлов = Новый Массив;
		Для Каждого ДопФайл Из ДополнительныеФайлы Цикл

			Если ЗначениеЗаполнено(ДопФайл.Значение)
			   И ТипЗнч(ДопФайл.Значение) = ТипПрисоединенногоФайлаПакета Тогда
				СписокФайлов.Добавить(ДопФайл.Значение);
			КонецЕсли;
			
		КонецЦикла;

		Если СписокФайлов.Количество() <> 0 Тогда
			
			СвойстваФайлов = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(СписокФайлов, "ПометкаУдаления", Истина);
		
			ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
			ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Синхронизация присоединенных файлов'");
			ПараметрыВыполнения.ОжидатьЗавершение           = 0;
			ПараметрыВыполнения.КлючФоновогоЗадания         = Строка(Ссылка.УникальныйИдентификатор());
			ПараметрыВыполнения.ЗапуститьВФоне              = Истина;

			ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения,
				"УниверсальныйОбменСБанкамиФормы.СинхронизироватьПрисоединенныеФайлы", Ссылка, СвойстваФайлов,
				Перечисления.СервисыОбменаСБанками.ЗаявкиНаОткрытиеСчета);
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПоСтруктуре(ДанныеЗаполнения)
	
	Если ДанныеЗаполнения.Свойство("Банки") Тогда
		СтрокаБанки = Банки.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаБанки, ДанныеЗаполнения.Банки);
	КонецЕсли;
	
	ТребуетсяИнтеграцияССервисомАУСН = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДанныеЗаполнения, "ТребуетсяИнтеграцияССервисомАУСН", Ложь);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли