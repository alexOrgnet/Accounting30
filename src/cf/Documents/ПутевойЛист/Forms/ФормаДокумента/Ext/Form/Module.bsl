
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Если Параметры.Ключ.Пустая() Тогда
		ПодготовитьФормуНаСервере();
		
		Если ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
			ОбновитьПоказанияОдометра();
		КонецЕсли;
		
		АктуализироватьСотрудника();
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
	УчетКассовыхЧековПодотчетныхЛиц.НастроитьФорму(
		ЭтотОбъект,
		ОписаниеДокументаДляЗагрузкиКассовыхЧековПодотчетныхЛиц());
	
	УправлениеФормойПриСозданииНаСервере();
	УстановитьУсловноеОформление();

	// Подсистема "ОбменСКонтрагентами".
	ОбменСКонтрагентамиБП.КомандыЭДО_ФормаДокумента(ЭтотОбъект);
	// Конец подсистема "ОбменСКонтрагентами".

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	УчетКассовыхЧековПодотчетныхЛицКлиент.ПодключитьСканерШтрихкода(ЭтотОбъект);
	УчетКассовыхЧековПодотчетныхЛицКлиент.ПодключитьОбновлениеЧековКЗагрузке(
		ЭтотОбъект,
		ОписаниеФормыДляЗагрузкиКассовыхЧековПодотчетныхЛиц());
		
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ОбменСКонтрагентамиКлиент.ПриОткрытии(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
	Если ЗначениеЗаполнено(Параметры.ДобавитьДокументПодотчетногоЛица) Тогда
		
		Модифицированность = Истина;
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ЗаполнитьСодержимымКассовыхЧеков", 
			ЭтотОбъект, 
			Параметры.ДобавитьДокументПодотчетногоЛица.ИмяСписка);
		
		УчетКассовыхЧековПодотчетныхЛицКлиент.ДобавитьЧекиВДокумент(
			ЭтотОбъект,
			ОписаниеФормыДляЗагрузкиКассовыхЧековПодотчетныхЛиц(),
			ОписаниеОповещения,
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Параметры.ДобавитьДокументПодотчетногоЛица.Ссылка));
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		
		Для Каждого ТекущийПараметр Из ВыбранноеЗначение Цикл
			Если Объект.Свойство(ТекущийПараметр.Ключ) Тогда
				Объект[ТекущийПараметр.Ключ] = ТекущийПараметр.Значение;
			КонецЕсли;
		КонецЦикла;
		
		ОбновитьПредставлениеПорядокУчетаЗатрат();
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененоТранспортноеСредство"
		И Параметр = Объект.ТранспортноеСредство
		И ТипЗнч(Источник) = Тип("ФормаКлиентскогоПриложения")
		И Источник.ВладелецФормы = Элементы.ТранспортноеСредство Тогда
		
		ОбработкаОповещенияИзмененоТранспортноеСредство();
		
	ИначеЕсли ИмяСобытия = "Запись_Организации" И Объект.Организация = Источник Тогда
		
		ОрганизацияПриИзмененииНаСервере();
		
	ИначеЕсли Источник = "ПодключаемоеОборудование" Тогда
		
		УчетКассовыхЧековПодотчетныхЛицКлиент.ОбработатьОтветСканера(
			ЭтотОбъект,
			ОписаниеФормыДляЗагрузкиКассовыхЧековПодотчетныхЛиц(),
			ИмяСобытия,
			Параметр,
			"Топливо",
			Объект.СуммаВключаетНДС);
			
	ИначеЕсли ИмяСобытия = "ПолучениеДанныхЧековЗагруженныхАппаратнымСканером" И Источник = ЭтотОбъект Тогда
		
		ПолучениеДанныхЧековЗагруженныхАппаратнымСканером(Параметр);
		
	Иначе
		
		ОбщегоНазначенияБПКлиент.ОбработкаОповещенияФормыДокумента(
			ЭтотОбъект, Объект.Ссылка, ИмяСобытия, Параметр, Источник);
		
	КонецЕсли;
		
	// Подсистема "ОбменСКонтрагентами".
	ПараметрыОповещения = ОбменСКонтрагентамиКлиент.ПараметрыОповещенияЭДО_ФормаДокумента();
	ПараметрыОповещения.Форма = ЭтотОбъект;
	ПараметрыОповещения.ДокументСсылка = Объект.Ссылка;
	ПараметрыОповещения.КонтроллерСостояниеЭДО = Элементы.СостояниеЭДО;
	ПараметрыОповещения.ГруппаСостояниеЭДО = Элементы.ГруппаСостояниеЭДО;
	
	ОбменСКонтрагентамиКлиент.ОбработкаОповещения_ФормаДокумента(ИмяСобытия, Параметр, Источник, ПараметрыОповещения);
	// Конец подсистема "ОбменСКонтрагентами".
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ПодготовитьФормуНаСервере();
	УправлениеФормой(ЭтотОбъект);
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ПараметрыЭДОПриСоздании = ОбменСКонтрагентами.ПараметрыПриЧтенииНаСервере_ФормаДокумента();
	ПараметрыЭДОПриСоздании.Форма = ЭтотОбъект;
	ПараметрыЭДОПриСоздании.ДокументСсылка = Объект.Ссылка;
	ПараметрыЭДОПриСоздании.МестоРазмещенияКоманд = Элементы.КомандыЭДО;
	ПараметрыЭДОПриСоздании.КонтроллерСостояниеЭДО = Элементы.СостояниеЭДО;
	ПараметрыЭДОПриСоздании.ГруппаСостояниеЭДО = Элементы.ГруппаСостояниеЭДО;
	ОбменСКонтрагентами.ПриЧтенииНаСервере_ФормаДокумента(ПараметрыЭДОПриСоздании);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПроведениеСервер.УстановитьПризнакПроверкиРеквизитов(Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами	
	ОбменСКонтрагентамиКлиент.ПослеЗаписи_ФормаДокумента(ЭтаФорма, ПараметрыЗаписи);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	УчетКассовыхЧековПодотчетныхЛиц.ПрочитатьКассовыеЧекиПодотчетныхЛиц(
		ЭтотОбъект,
		ОписаниеФормыДляЗагрузкиКассовыхЧековПодотчетныхЛиц());
	
	РассчитатьВсегоПоДокументу();
	
	СостояниеДокумента = ОбщегоНазначенияБП.СостояниеДокумента(Объект);
	УстановитьВремяМаршрута();
	
	// Подсистема "ОбменСКонтрагентами".
	ПараметрыПослеЗаписи = ОбменСКонтрагентами.ПараметрыПослеЗаписиНаСервере();
	ПараметрыПослеЗаписи.Форма = ЭтотОбъект;
	ПараметрыПослеЗаписи.ДокументСсылка = Объект.Ссылка;
	ПараметрыПослеЗаписи.КонтроллерСостояниеЭДО = Элементы.СостояниеЭДО;
	ПараметрыПослеЗаписи.ГруппаСостояниеЭДО = Элементы.ГруппаСостояниеЭДО;
	
	ОбменСКонтрагентами.ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи, ПараметрыПослеЗаписи);
	// Конец подсистема "ОбменСКонтрагентами".

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	УчетКассовыхЧековПодотчетныхЛицКлиент.ОтключитьСканерШтрихкода(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	ДатаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеОрганизацииПриИзменении(Элемент)
	
	ЗаполнитьРеквизитыОтветственногоЛица();
	
КонецПроцедуры

&НаКлиенте
Процедура СообщениеТребуютсяРеквизитыОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", Объект.Организация);
	ПараметрыФормы.Вставить("Контекст", Новый Структура);
	ПараметрыФормы.Вставить("Назначение", "ДляОтчетности");
	ПараметрыФормы.Вставить("ПроверяемыеРеквизиты",   ПроверяемыеРеквизитыОрганизации());
	ПараметрыФормы.Контекст.Вставить("Период",        Объект.Дата);
	ПараметрыФормы.Контекст.Вставить("ИмяРеглОтчета", "");
	
	ОткрытьФорму("Справочник.Организации.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ТранспортноеСредствоПриИзменении(Элемент)
	
	ТранспортноеСредствоПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПорядокУчетаЗатратНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ТолькоПросмотр",      ТолькоПросмотр);
	ПараметрыФормы.Вставить("Дата",                Объект.Дата);
	ПараметрыФормы.Вставить("Организация",         Объект.Организация);
	ПараметрыФормы.Вставить("ОтражениеВУСН",       Объект.ОтражениеВУСН);
	
	ПараметрыФормы.Вставить("СчетЗатрат",          Объект.СчетЗатрат);
	ПараметрыФормы.Вставить("ПодразделениеЗатрат", Объект.ПодразделениеЗатрат);
	ПараметрыФормы.Вставить("Субконто1",           Объект.Субконто1);
	ПараметрыФормы.Вставить("Субконто2",           Объект.Субконто2);
	ПараметрыФормы.Вставить("Субконто3",           Объект.Субконто3);
	ПараметрыФормы.Вставить("НДСВключенВСтоимость",Истина);
	ПараметрыФормы.Вставить("УчетАгентскогоНДС",   Ложь);
	
	ОткрытьФорму("ОбщаяФорма.РедактированиеСчетаЗатрат", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаВключаетНДСНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли;
	
	СписокКоманд = Новый СписокЗначений;
	СписокКоманд.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыРасчетаНДС.НДСНеВыделять"));
	СписокКоманд.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыРасчетаНДС.НДСВСумме"));
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("СуммаВключаетНДСНажатиеЗавершение", ЭтотОбъект);
	ПоказатьВыборИзМеню(ОповещениеОЗакрытии, СписокКоманд, Элементы.СуммаВключаетНДС);
	
КонецПроцедуры

&НаКлиенте
Процедура НормаРасходаПриИзменении(Элемент)
	
	ПересчитатьРасходМаршрута();
	
	Оповещение   = Новый ОписаниеОповещения("НормаРасходаПослеЗакрытияВопроса", ЭтотОбъект, Параметры);
	ТекстВопроса = НСтр("ru = 'Запомнить норму расхода для транспортного средства?'");
	
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,,, НСтр("ru = 'Норма расхода изменена'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументПодписалПриИзменении(Элемент)
	
	Объект.ДолжностьПодписавшегоЛица = ДолжностьОтветственногоЛица(
		Объект.Организация,
		Объект.ДокументПодписал,
		Объект.Дата,
		Объект.ПодразделениеОрганизации);
	
КонецПроцедуры

&НаКлиенте
Процедура КартинкаЗакрытьБаннерЧекиПодотчетныхЛицНажатие(Элемент)
	
	УчетКассовыхЧековПодотчетныхЛицКлиент.СкрытьБаннер(ЭтотОбъект, ОписаниеФормыДляЗагрузкиКассовыхЧековПодотчетныхЛиц());
	
КонецПроцедуры

&НаКлиенте
Процедура БаннерПодключенАппаратныйСканерЗакрытьНажатие(Элемент)
	
	УчетКассовыхЧековПодотчетныхЛицКлиент.СкрытьБаннерПодключенАппаратныйСканер(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьЧеки(Команда)
	
	УчетКассовыхЧековПодотчетныхЛицКлиент.НачатьЗагрузкуВДокумент(
		ЭтотОбъект,
		Объект.ФизЛицо,
		ОписаниеФормыДляЗагрузкиКассовыхЧековПодотчетныхЛиц(),
		Новый ОписаниеОповещения("ЗаполнитьСодержимымКассовыхЧеков", ЭтотОбъект, "Топливо"));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТопливо

&НаКлиенте
Процедура ТопливоВидВходящегоДокументаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Топливо.ТекущиеДанные;
	
	Если ТекущиеДанные.ВидВходящегоДокумента
		= ПредопределенноеЗначение("Перечисление.ВидыДокументовПриобретенияГСМ.ТопливнаяКарта") Тогда
		
		ТекущиеДанные.Цена  = 0;
		ТекущиеДанные.Сумма = 0;
		ТекущиеДанные.СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС");
		
	ИначеЕсли ПлательщикНДС Тогда
		
		ТекущиеДанные.СтавкаНДС = СтавкаНДСТопливаАвтомобиля(
			Объект.ТранспортноеСредство,
			Объект.Дата);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТопливоКоличествоПриИзменении(Элемент)
	
	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииКоличествоЦена(ЭтотОбъект, "Топливо");
	ОбновитьИтоги();
	
КонецПроцедуры

&НаКлиенте
Процедура ТопливоЦенаПриИзменении(Элемент)
	
	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииКоличествоЦена(ЭтотОбъект, "Топливо");
	ОбновитьИтоги();
	
КонецПроцедуры

&НаКлиенте
Процедура ТопливоСуммаПриИзменении(Элемент)
	
	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииСумма(ЭтотОбъект, "Топливо");
	ОбновитьИтоги();
	
КонецПроцедуры

&НаКлиенте
Процедура ТопливоСтавкаНДСПриИзменении(Элемент)
	
	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииКоличествоЦена(ЭтотОбъект, "Топливо");
	ВсегоПоДокументу = Объект.Топливо.Итог("Всего");
	
КонецПроцедуры

&НаКлиенте
Процедура ТопливоСуммаНДСПриИзменении(Элемент)
	
	ВсегоПоДокументу = Объект.Топливо.Итог("Всего");
	
КонецПроцедуры

&НаКлиенте
Процедура ТопливоПослеУдаления(Элемент)
	
	ОбновитьИтоги();
	
КонецПроцедуры

&НаКлиенте
Процедура ТопливоПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	УчетКассовыхЧековПодотчетныхЛицКлиент.ПриНачалеРедактированияОчиститьЧек(Элемент, Копирование);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыМаршрут

&НаКлиенте
Процедура МаршрутПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если Не НоваяСтрока Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	КоличествоСтрок = Объект.Маршрут.Количество();
	
	Если КоличествоСтрок = 1 Тогда
		
		ТекущиеДанные.ПоказаниеОдометраДо = ПоказаниеОдометра;
		ТекущиеДанные.ВидСообщения = ВидСообщенияПоУмолчанию();
		
	Иначе
		
		Если Не Копирование Тогда
			// По умолчанию в новой строке пункт отправления - последний пункт прибытия машины
			ТекущиеДанные.ПунктОтправления = Объект.Маршрут[КоличествоСтрок - 2].ПунктНазначения;
			ТекущиеДанные.ВидСообщения = Объект.Маршрут[КоличествоСтрок - 2].ВидСообщения;
		КонецЕсли;
		
		МаксимальноеЗначение = 0;
		Для Каждого ТекущаяСтрока Из Объект.Маршрут Цикл
			МаксимальноеЗначение = Макс(МаксимальноеЗначение, ТекущаяСтрока.ПоказаниеОдометраПосле);
		КонецЦикла;
		
		ТекущиеДанные.ПоказаниеОдометраДо = МаксимальноеЗначение;
		ТекущиеДанные.ПоказаниеОдометраПосле = ТекущиеДанные.ПоказаниеОдометраДо + ТекущиеДанные.Расстояние;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МаршрутПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ОбновитьИтоги();
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.ДатаОтправления = ПолнаяДатаМаршрута(ТекущиеДанные.ОтправлениеДата, ТекущиеДанные.ОтправлениеВремя);
	ТекущиеДанные.ДатаВозвращения = ПолнаяДатаМаршрута(ТекущиеДанные.ВозвращениеДата, ТекущиеДанные.ВозвращениеВремя);
	
КонецПроцедуры

&НаКлиенте
Процедура МаршрутПослеУдаления(Элемент)
	
	ОбновитьИтоги();
	
КонецПроцедуры

&НаКлиенте
Процедура МаршрутПунктОтправленияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	МаршрутВыборИзСписка(Элемент, "ПунктОтправления");
	
КонецПроцедуры

&НаКлиенте
Процедура МаршрутПунктНазначенияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	МаршрутВыборИзСписка(Элемент, "ПунктНазначения");
	
КонецПроцедуры

&НаКлиенте
Процедура МаршрутПоказаниеОдометраДоПриИзменении(Элемент)
	
	ПересчитатьПоказателиМаршрута();
	
КонецПроцедуры

&НаКлиенте
Процедура МаршрутПоказаниеОдометраПослеПриИзменении(Элемент)
	
	ПересчитатьПоказателиМаршрута();
	
КонецПроцедуры

&НаКлиенте
Процедура МаршрутРасстояниеПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Маршрут.ТекущиеДанные;
	ТекущиеДанные.ПоказаниеОдометраПосле = ТекущиеДанные.ПоказаниеОдометраДо + ТекущиеДанные.Расстояние;
	
	ПересчитатьРасходМаршрутаПоСтроке(ТекущиеДанные, Объект.НормаРасхода, ПробегАвтотранспортаПоНормативу);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция АналитикаЗатратЗаполнена(Субконто)
	
	Если ЗначениеЗаполнено(Субконто) И ТипЗнч(Субконто) = Тип("СправочникСсылка.НоменклатурныеГруппы")
		И БухгалтерскийУчетВызовСервераПовтИсп.ИспользоватьОднуНоменклатурнуюГруппу() Тогда
		Значение = Ложь;
	ИначеЕсли ЗначениеЗаполнено(Субконто) Тогда
		Значение = Истина;
	Иначе
		Значение = Ложь;
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

&НаСервере
Процедура ДатаПриИзмененииНаСервере()
	
	Если Не ЗначениеЗаполнено(Объект.Маршрут) Тогда
		РеквизитыТС = РеквизитыТранспортногоСредства(Объект.ТранспортноеСредство, Объект.Ссылка, Объект.Дата);
		ПоказаниеОдометра = РеквизитыТС.ПоказаниеОдометра;
	КонецЕсли;
	
	АктуализироватьСотрудника();
	ОбновитьПредставлениеПорядокУчетаЗатрат();
	
	УстановитьФункциональныеОпцииФормы();
	Если ПлательщикНДС Тогда
		Объект.НДСНеВыделять = Ложь;
	Иначе
		Объект.НДСНеВыделять = Истина;
		ОчиститьСуммыНДС();
	КонецЕсли;
	
	Элементы.МаршрутВидСообщения.Видимость = ОтображатьВидСообщения(Объект.Дата);
	
	ЗаполнитьЗависимыеСчета("Дата");
	ОбновитьИтоги();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДолжностьОтветственногоЛица(Организация, ФизическоеЛицо, ДатаСреза, Подразделение)
	
	Если Не ЗначениеЗаполнено(ФизическоеЛицо) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// По умолчанию в качестве подписанта устанавливается руководитель организации
	ОтветственноеЛицо = Перечисления.ОтветственныеЛицаОрганизаций.Руководитель;
	Возврат ОтветственныеЛицаБП.ДолжностьОтветственногоЛица(Организация, ФизическоеЛицо, ОтветственноеЛицо)
	
КонецФункции

&НаСервере
Процедура ЗаполнитьРеквизитыОтветственногоЛица()
	
	РеквизитыОтветственногоЛица = Документы.ПутевойЛист.ОтветственноеЛицоПоДокументу(
		Объект.Организация, Объект.Дата, Объект.ПодразделениеОрганизации);
	ЗаполнитьЗначенияСвойств(Объект, РеквизитыОтветственногоЛица);
	
КонецПроцедуры

&НаКлиенте
Процедура МаршрутВыборИзСписка(Элемент, ИмяРеквизита)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("МаршрутВыборИзСпискаЗавершение", ЭтотОбъект, ИмяРеквизита);
	
	АдресаОрганизации = СписокАдресовОрганизации(Объект.Организация, Объект.Дата);
	
	ЭлементСписка = АдресаОрганизации.НайтиПоЗначению(Элементы.Маршрут.ТекущиеДанные[ИмяРеквизита]);
	ПоказатьВыборИзСписка(ОписаниеОповещения, АдресаОрганизации, Элемент, ЭлементСписка);
	
КонецПроцедуры

&НаКлиенте
Процедура МаршрутВыборИзСпискаЗавершение(Результат, ИмяРеквизита) Экспорт
	
	Если ТипЗнч(Результат) = Тип("ЭлементСпискаЗначений") Тогда
		Элементы.Маршрут.ТекущиеДанные[ИмяРеквизита] = Результат.Значение;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИспользуетсяТрудНаемныхРаботников(Организация)
	
	Если Не ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Организация) Тогда
		ИспользуетсяТрудНаемныхРаботников = УчетЗарплаты.ИПИспользуетТрудНаемныхРаботников(Организация);
	Иначе
		ИспользуетсяТрудНаемныхРаботников = Истина;
	КонецЕсли;
	
	Возврат ИспользуетсяТрудНаемныхРаботников;
	
КонецФункции

&НаКлиенте
Процедура НормаРасходаПослеЗакрытияВопроса(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьНормуРасходаТоплива(Объект.ТранспортноеСредство, Объект.НормаРасхода);
	
КонецПроцедуры

// Выполняет обновление реквизитов и полей формы, исходя из данных транспортного средства
//
// Параметры:
//    РеквизитыТранспортногоСредства - Структура (см. РеквизитыТранспортногоСредства())
//
&НаСервере
Процедура ОбновитьПоляИРеквизитыФормы(РеквизитыТранспортногоСредства)
	
	ПоказаниеОдометра = РеквизитыТранспортногоСредства.ПоказаниеОдометра;
	
	Если РеквизитыТранспортногоСредства.Свойство("ЕдиницаИзмеренияНаименование", ЕдиницаИзмеренияНаименование)
		И Не ПустаяСтрока(ЕдиницаИзмеренияНаименование) Тогда
		
		Элементы.ТопливоКоличество.Заголовок = СтрШаблон(НСтр("ru = 'Количество, %1'"), ЕдиницаИзмеренияНаименование);
		Элементы.МаршрутРасход.Заголовок     = СтрШаблон(НСтр("ru = 'Расход, %1'"), ЕдиницаИзмеренияНаименование);
		
	Иначе
		
		Элементы.ТопливоКоличество.Заголовок = НСтр("ru = 'Количество'");
		Элементы.МаршрутРасход.Заголовок     = НСтр("ru = 'Расход'");
		
	КонецЕсли;
	
	ПодсказкаНормаРасхода = РеквизитыТранспортногоСредства.Подсказка;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИтоги()
	
	Если Объект.Ссылка.Пустая() Тогда
		ДатаДокумента = КонецДня(Объект.Дата);
	Иначе
		ДатаДокумента = Новый Граница(Объект.Ссылка.МоментВремени(), ВидГраницы.Исключая);
	КонецЕсли;
	
	ОстатокНачало  = Справочники.ТранспортныеСредства.ОстатокТопливаВБаке(
		Объект.Организация, Объект.ТранспортноеСредство, ДатаДокумента);
	ПокупкаТоплива = Объект.Топливо.Итог("Количество");
	РасходТоплива  = Объект.Маршрут.Итог("Расход");
	ОстатокНаКонец = ОстатокНачало + ПокупкаТоплива - РасходТоплива;
	ПереносСтроки  = Символы.ПС;
	
	ИтогОстатокНачало  = СтрШаблон(НСтр("ru = 'Топливо в баке
		|%1 %2'"), ОстатокНачало, ЕдиницаИзмеренияНаименование);
	
	ИтогПокупкаТоплива = СтрШаблон(НСтр("ru = 'Куплено
		|%1 %2'"), ПокупкаТоплива, ЕдиницаИзмеренияНаименование);
	
	ИтогРасходТоплива  = СтрШаблон(НСтр("ru = 'Израсходовано
		|%1 %2'"), РасходТоплива, ЕдиницаИзмеренияНаименование);
	
	ЭтоПерерасходТоплива = ОстатокНаКонец < 0;
	Если ЭтоПерерасходТоплива Тогда
		ИтогОстатокКонец = СтрШаблон(НСтр("ru = 'Перерасход
			|%1 %2'"), - ОстатокНаКонец, ЕдиницаИзмеренияНаименование);
		Элементы.ОстатокВБакеНаКонец.ЦветФона = ЦветаСтиля.НекорректныйКонтрагентЦветФонаВСписке;
	Иначе
		ИтогОстатокКонец = СтрШаблон(НСтр("ru = 'Остаток
			|%1 %2'"), ОстатокНаКонец, ЕдиницаИзмеренияНаименование);
		Элементы.ОстатокВБакеНаКонец.ЦветФона = ЦветаСтиля.ЦветФонаПодсказки;
	КонецЕсли;
	
	ВсегоПоДокументу = Объект.Топливо.Итог("Всего");
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПоказанияОдометра()
	
	// Обновить показания одометра - за начальные показания одометра в табличной части принимаются данные первой строки
	Если ЗначениеЗаполнено(Объект.Маршрут) И ПоказаниеОдометра <> Объект.Маршрут[0].ПоказаниеОдометраДо Тогда
		
		ПоказаниеОдометраТекущие = ПоказаниеОдометра;
		Для Каждого ТекущаяСтрока Из Объект.Маршрут Цикл
			
			ТекущаяСтрока.ПоказаниеОдометраДо    = ПоказаниеОдометраТекущие;
			ТекущаяСтрока.ПоказаниеОдометраПосле = ТекущаяСтрока.ПоказаниеОдометраДо + ТекущаяСтрока.Расстояние;
			ПоказаниеОдометраТекущие = ТекущаяСтрока.ПоказаниеОдометраПосле;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПредставлениеПорядокУчетаЗатрат()
	
	ПорядокУчетаЗатрат = "";
	УчетПоПодразделениям = Ложь;
	
	АналитикаПредставления = Новый Массив;
	
	Если ЗначениеЗаполнено(Объект.СчетЗатрат) Тогда
		АналитикаПредставления.Добавить(Объект.СчетЗатрат);
		УчетПоПодразделениям = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(
			Объект.СчетЗатрат).УчетПоПодразделениям;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ПодразделениеЗатрат) И УчетПоПодразделениям Тогда
		АналитикаПредставления.Добавить(Объект.ПодразделениеЗатрат);
	КонецЕсли;
	
	Если АналитикаЗатратЗаполнена(Объект.Субконто1) Тогда
		АналитикаПредставления.Добавить(Объект.Субконто1);
	КонецЕсли;
	
	Если АналитикаЗатратЗаполнена(Объект.Субконто2) Тогда
		АналитикаПредставления.Добавить(Объект.Субконто2);
	КонецЕсли;
	
	Если АналитикаЗатратЗаполнена(Объект.Субконто3) Тогда
		АналитикаПредставления.Добавить(Объект.Субконто3);
	КонецЕсли;
	
	Если УчетнаяПолитика.ПрименяетсяУСНДоходыМинусРасходы(Объект.Организация, Объект.Дата)
		И ЗначениеЗаполнено(Объект.ОтражениеВУСН) Тогда
		АналитикаПредставления.Добавить(СтрШаблон(НСтр("ru = 'расходы %1'"), Нрег(Объект.ОтражениеВУСН)));
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(АналитикаПредставления) Тогда
		ПорядокУчетаЗатрат = НСтр("ru = 'Заполнить'");
	Иначе
		ПорядокУчетаЗатрат = СтрСоединить(АналитикаПредставления, ", ");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаОповещенияИзмененоТранспортноеСредство()
	
	РеквизитыТС = РеквизитыТранспортногоСредства(Объект.ТранспортноеСредство, Объект.Ссылка, Объект.Дата);
	ОбновитьПоляИРеквизитыФормы(РеквизитыТС);
	
	Модифицированность = Объект.НормаРасхода <> РеквизитыТС.НормаРасхода;
	Объект.НормаРасхода = РеквизитыТС.НормаРасхода;
	
	ПересчитатьРасходМаршрута();
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	ЗаполнитьЗависимыеСчета("Организация");
	
	ИспользуетсяТрудНаемныхРаботников = ИспользуетсяТрудНаемныхРаботников(Объект.Организация);
	
	АктуализироватьСотрудника();
	ЗаполнитьРеквизитыОтветственногоЛица();
	ОбновитьПредставлениеПорядокУчетаЗатрат();
	ПроверитьРеквизитыОрганизацииНаСервере();
	
	УстановитьФункциональныеОпцииФормы();
	Если ПлательщикНДС Тогда
		Объект.НДСНеВыделять = Ложь;
	Иначе
		Объект.НДСНеВыделять = Истина;
		ОчиститьСуммыНДС();
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура СуммаВключаетНДСПриИзменении(ЭтоНДСВСумме)
	
	Для Каждого ТекущаяСтрока Из Объект.Топливо Цикл
		
		ТекущаяСтрока.Цена = УчетНДСКлиентСервер.ПересчитатьЦенуПриИзмененииФлаговНалогов(
			ТекущаяСтрока.Цена,
			ЭтоНДСВСумме,
			Объект.СуммаВключаетНДС,
			УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(ТекущаяСтрока.СтавкаНДС));
		
		ТекущаяСтрока.Сумма = ТекущаяСтрока.Цена * ТекущаяСтрока.Количество;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьРасходМаршрута()
	
	Для Каждого ТекущийМаршрут Из Объект.Маршрут Цикл
		
		ПересчитатьРасходМаршрутаПоСтроке(ТекущийМаршрут, Объект.НормаРасхода, ПробегАвтотранспортаПоНормативу);
		
	КонецЦикла;
	
	ОбновитьИтоги();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПересчитатьРасходМаршрутаПоСтроке(ТекущаяСтрока, НормаРасхода, ПробегАвтотранспортаПоНормативу)
	
	ТекущаяСтрока.Расход = ТекущаяСтрока.Расстояние * НормаРасхода / ПробегАвтотранспортаПоНормативу;
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьПоказателиМаршрута()
	
	РассчитатьРасстояниеТекущегоМаршрута();
	
	ПересчитатьРасходМаршрутаПоСтроке(
		Элементы.Маршрут.ТекущиеДанные,
		Объект.НормаРасхода,
		ПробегАвтотранспортаПоНормативу);
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	УчетКассовыхЧековПодотчетныхЛиц.НастроитьОтображениеЗагруженныхЧеков(
		ЭтотОбъект,
		ОписаниеФормыДляЗагрузкиКассовыхЧековПодотчетныхЛиц());
	
	ПробегАвтотранспортаПоНормативу = Справочники.ТранспортныеСредства.ПробегАвтотранспортаПоНормативу();
	
	СостояниеДокумента = ОбщегоНазначенияБП.СостояниеДокумента(Объект);
	
	Элементы.ГруппаНормаРасхода.РасширеннаяПодсказка.Заголовок =
		Справочники.ТранспортныеСредства.ПодсказкаОписаниеНормыМинтранса();
	
	ИспользуетсяТрудНаемныхРаботников = ИспользуетсяТрудНаемныхРаботников(Объект.Организация);
	
	РеквизитыТС = РеквизитыТранспортногоСредства(Объект.ТранспортноеСредство, Объект.Ссылка, Объект.Дата);
	ОбновитьПоляИРеквизитыФормы(РеквизитыТС);
	
	РассчитатьВсегоПоДокументу();
	
	УстановитьФункциональныеОпцииФормы();
	
	УстановитьВремяМаршрута();
	ОбновитьПредставлениеПорядокУчетаЗатрат();
	ОбновитьИтоги();
	ПроверитьРеквизитыОрганизацииНаСервере();
	
	Элементы.МаршрутВидСообщения.Видимость = ОтображатьВидСообщения(Объект.Дата);
	
КонецПроцедуры

&НаКлиенте
Функция ПолнаяДатаМаршрута(Дата, Время)
	
	Возврат Дата + (Время - НачалоДня(Время));
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПроверяемыеРеквизитыОрганизации()
	
	ПроверяемыеРеквизиты = Новый Массив;
	ПроверяемыеРеквизиты.Добавить("ИНН");
	ПроверяемыеРеквизиты.Добавить("ОГРН");
	ПроверяемыеРеквизиты.Добавить("Телефон");
	ПроверяемыеРеквизиты.Добавить("Адрес");
	
	Возврат ПроверяемыеРеквизиты;
	
КонецФункции

&НаСервере
Процедура ПроверитьРеквизитыОрганизацииНаСервере()
	
	НезаполненныеРеквизиты = Неопределено;
	ПроверяемыеРеквизиты   = ПроверяемыеРеквизитыОрганизации();
	
	РеквизитыОрганизацииЗаполнены = ОрганизацииФормыДляОтчетности.РеквизитыЗаполнены(
		Объект.Организация,
		ПроверяемыеРеквизиты,
		НезаполненныеРеквизиты);
	
	Если Не РеквизитыОрганизацииЗаполнены Тогда
		
		СообщениеТребуютсяРеквизиты   = ПроверкаРеквизитовОрганизации.СтрокаСообщенияНеЗаполненыРеквизитыДляОтчетности(
			Объект.Организация, НСтр("ru = 'подготовить путевой лист'"));
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьРасстояниеТекущегоМаршрута()
	
	ТекущаяСтрока = Элементы.Маршрут.ТекущиеДанные;
	Если ТекущаяСтрока.ПоказаниеОдометраПосле = 0 Тогда
		ТекущаяСтрока.Расстояние = 0;
	Иначе
		ТекущаяСтрока.Расстояние = ТекущаяСтрока.ПоказаниеОдометраПосле - ТекущаяСтрока.ПоказаниеОдометраДо;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьВсегоПоДокументу()
	
	ВсегоПоДокументу = 0;
	Для Каждого ТекущаяСтрока Из Объект.Топливо Цикл
		ТекущаяСтрока.Всего = ТекущаяСтрока.Сумма + ?(Объект.СуммаВключаетНДС, 0, ТекущаяСтрока.СуммаНДС);
		ВсегоПоДокументу = ВсегоПоДокументу + ТекущаяСтрока.Всего;
	КонецЦикла;
	
КонецПроцедуры

// Возвращает реквизиты транспортного средства: норма расхода и свойства топлива транспортного средства.
//
// Параметры:
//    ТранспортноеСредство  - СправочникСсылка.ТранспортныеСредства
//    СсылкаНаДокумент      - ДокументСсылка.ПутевойЛист
//    ДатаДокумента         - Дата
//
// Возвращаемое значение:
//    Структура - см. Справочники.ТранспортныеСредства.РеквизитыТранспортногоСредства()
//
&НаСервереБезКонтекста
Функция РеквизитыТранспортногоСредства(ТранспортноеСредство, СсылкаНаДокумент, ДатаДокумента)
	
	СтруктураРезультат = Справочники.ТранспортныеСредства.РеквизитыТранспортногоСредства(
		ТранспортноеСредство, ДатаДокумента);
	
	// Если транспортное средство то же самое, что и было указано в документе,
	// тогда показание нужно оставить такими, какие были введены в записанном документе
	Если ЗначениеЗаполнено(СсылкаНаДокумент)
		И ТранспортноеСредство = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаДокумент, "ТранспортноеСредство") Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	МИНИМУМ(ПутевойЛистМаршрут.ПоказаниеОдометраДо) КАК ПоказаниеОдометраДо
		|ИЗ
		|	Документ.ПутевойЛист.Маршрут КАК ПутевойЛистМаршрут
		|ГДЕ
		|	ПутевойЛистМаршрут.Ссылка = &СсылкаНаДокумент
		|	И ПутевойЛистМаршрут.ПоказаниеОдометраДо > 0");
		Запрос.УстановитьПараметр("СсылкаНаДокумент", СсылкаНаДокумент);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			СтруктураРезультат.Вставить("ПоказаниеОдометра", Выборка.ПоказаниеОдометраДо);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СтруктураРезультат;
	
КонецФункции

&НаСервереБезКонтекста
Функция СписокАдресовОрганизации(Организация, Дата)
	
	Возврат УправлениеКонтактнойИнформациейБП.СписокАдресовОбъекта(Организация, Дата);
	
КонецФункции

&НаСервереБезКонтекста
Функция СтавкаНДСТопливаАвтомобиля(ТранспортноеСредство, Дата)
	
	СтавкаПоУмолчанию = Перечисления.СтавкиНДС.БезНДС;
	
	Номенклатура = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТранспортноеСредство, "Топливо");
	Если Не ЗначениеЗаполнено(Номенклатура) Тогда
		Возврат СтавкаПоУмолчанию;
	КонецЕсли;
	
	ВидСтавкиНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ВидСтавкиНДС");
	Если Не ЗначениеЗаполнено(ВидСтавкиНДС) Тогда
		Возврат СтавкаПоУмолчанию;
	КонецЕсли;
	
	Возврат Перечисления.СтавкиНДС.СтавкаНДС(ВидСтавкиНДС, Дата);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПрименитьСтавкуНДСТопливаАвтомобиля(СтавкаАвтомобиля, СтавкаДокумента)
	
	// На основании чека с АЗС принять НДС к вычету, как правило, нельзя - см. письма Минфина
	// от 3 августа 2010 г. N 03-07-11/335, от 9 марта 2010 г. N 03-07-11/51, от 25 июня 2020 № 03-07-09/54634.
	// С учетом этого, заданная в программе ставка НДС может "отменить" вычет.
	
	Возврат Не ЗначениеЗаполнено(СтавкаДокумента) Или СтавкаАвтомобиля = Перечисления.СтавкиНДС.БезНДС;
	
КонецФункции

&НаКлиенте
Процедура СуммаВключаетНДСНажатиеЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатЗакрытия) = Тип("ЭлементСпискаЗначений") Тогда
		
		СуммаВключаетНДСПредыдущееЗначение = Объект.СуммаВключаетНДС;
		Объект.СуммаВключаетНДС =
			(РезультатЗакрытия.Значение = ПредопределенноеЗначение("Перечисление.ВариантыРасчетаНДС.НДСВСумме"));
		
		Если Не Объект.СуммаВключаетНДС Тогда
			
			Для Каждого ТекущаяСтрока Из Объект.Топливо Цикл
				ТекущаяСтрока.СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС");
				ТекущаяСтрока.СуммаНДС = 0;
			КонецЦикла;
			
		Иначе
			
			СуммаВключаетНДСПриИзменении(СуммаВключаетНДСПредыдущееЗначение);
			СформироватьНадписьСуммаВключаетНДС(ЭтотОбъект);
			
		КонецЕсли;
		
		Объект.НДСНеВыделять = Не Объект.СуммаВключаетНДС;
		Модифицированность = Истина;
		УправлениеФормой(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СформироватьНадписьСуммаВключаетНДС(Форма)
	
	Объект = Форма.Объект;
	
	СтруктураНадписи = Новый Структура;
	СтруктураНадписи.Вставить("СуммаВключаетНДС", Объект.СуммаВключаетНДС);
	СтруктураНадписи.Вставить("НДСНеВыделять", Не Объект.СуммаВключаетНДС);
	
	Форма.СуммаВключаетНДС = ОбщегоНазначенияБПКлиентСервер.СформироватьНадписьЦеныИВалюта(СтруктураНадписи);
	
КонецПроцедуры

&НаСервере
Процедура ТранспортноеСредствоПриИзмененииНаСервере()
	
	РеквизитыТС = РеквизитыТранспортногоСредства(Объект.ТранспортноеСредство, Объект.Ссылка, Объект.Дата);
	ОбновитьПоляИРеквизитыФормы(РеквизитыТС);
	Объект.НормаРасхода = РеквизитыТС.НормаРасхода;
	
	ОбновитьПоказанияОдометра();
	ПересчитатьРасходМаршрута();
	
	Если ПлательщикНДС Тогда
		
		ВсегоПоДокументу = 0;
		СтавкаНДС = СтавкаНДСТопливаАвтомобиля(Объект.ТранспортноеСредство, Объект.Дата);
		Для Каждого ТекущаяСтрока Из Объект.Топливо Цикл
			Если ПрименитьСтавкуНДСТопливаАвтомобиля(СтавкаНДС, ТекущаяСтрока.СтавкаНДС) Тогда
				ТекущаяСтрока.СтавкаНДС = СтавкаНДС;
				ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(ТекущаяСтрока, Объект.СуммаВключаетНДС);
				ТекущаяСтрока.Всего = ТекущаяСтрока.Сумма + ?(Объект.СуммаВключаетНДС, 0, ТекущаяСтрока.СуммаНДС);
			КонецЕсли;
			ВсегоПоДокументу = ВсегоПоДокументу + ТекущаяСтрока.Всего;
		КонецЦикла;
		
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВремяМаршрута()
	
	Для Каждого ТекущаяСтрока Из Объект.Маршрут Цикл
		ТекущаяСтрока.ОтправлениеДата  = ТекущаяСтрока.ДатаОтправления;
		ТекущаяСтрока.ОтправлениеВремя = ТекущаяСтрока.ДатаОтправления;
		ТекущаяСтрока.ВозвращениеДата  = ТекущаяСтрока.ДатаВозвращения;
		ТекущаяСтрока.ВозвращениеВремя = ТекущаяСтрока.ДатаВозвращения;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьНормуРасходаТоплива(ТранспортноеСредство, НормаРасхода)
	
	Справочники.ТранспортныеСредства.ИзменитьНормуРасходаТоплива(ТранспортноеСредство, НормаРасхода);
	
КонецПроцедуры

&НаСервере
Процедура АктуализироватьСотрудника()
	
	Если Не ИспользуетсяТрудНаемныхРаботников Тогда
		Объект.ФизЛицо = ОтветственныеЛицаБП.ОтветственныеЛица(Объект.Организация, Объект.Дата).Руководитель;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьСуммыНДС()
	
	СтавкаБезНДС = Перечисления.СтавкиНДС.БезНДС;
	
	Для Каждого ТекущаяСтрока Из Объект.Топливо Цикл
		ТекущаяСтрока.СтавкаНДС = СтавкаБезНДС;
		ТекущаяСтрока.СуммаНДС = 0;
		ТекущаяСтрока.Всего = 0;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеФормойПриСозданииНаСервере()
	
	Элементы.ПорядокУчетаЗатрат.Видимость = СчетаУчетаВДокументахВызовСервераПовтИсп.ПользовательУправляетСчетамиУчета();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	Элементы.ФонТребуютсяРеквизиты.Видимость = Не Форма.РеквизитыОрганизацииЗаполнены
		И ЗначениеЗаполнено(Объект.Организация);
	
	Элементы.ПодсказкаНормаРасхода.Видимость = Не ПустаяСтрока(Форма.ПодсказкаНормаРасхода);
	Элементы.Сотрудник.Видимость = Форма.ИспользуетсяТрудНаемныхРаботников;
	Элементы.ГруппаНДС.Видимость = Форма.ПлательщикНДС И Объект.СуммаВключаетНДС;
	Элементы.СуммаВключаетНДС.Видимость = Форма.ПлательщикНДС;
	
	СформироватьНадписьСуммаВключаетНДС(Форма);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	ТопливнаяКарта = Перечисления.ВидыДокументовПриобретенияГСМ.ТопливнаяКарта;
	Равно = ВидСравненияКомпоновкиДанных.Равно;
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТопливоЦена");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТопливоСумма");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТопливоСтавкаНДС");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТопливоСуммаНДС");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТопливоВсего");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Топливо.ВидВходящегоДокумента", Равно, ТопливнаяКарта);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не требуется>'"));
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТопливоЦена");
	НоваяГруппа = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Топливо.ВидВходящегоДокумента",
		ВидСравненияКомпоновкиДанных.НеРавно,
		ТопливнаяКарта);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(НоваяГруппа,
		"Объект.Топливо.Цена",
		ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТопливоСтавкаНДС");
	НоваяГруппа = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Топливо.ВидВходящегоДокумента",
		ВидСравненияКомпоновкиДанных.НеРавно,
		ТопливнаяКарта);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(НоваяГруппа,
		"Объект.Топливо.СтавкаНДС",
		ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	СтавкиБезНДС = Новый СписокЗначений;
	СтавкиБезНДС.Добавить(Перечисления.СтавкиНДС.БезНДС);
	СтавкиБезНДС.Добавить(Перечисления.СтавкиНДС.НДС0);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТопливоСуммаНДС");
	НоваяГруппа = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Топливо.ВидВходящегоДокумента",
		ВидСравненияКомпоновкиДанных.НеРавно,
		ТопливнаяКарта);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(НоваяГруппа,
		"Объект.Топливо.СуммаНДС",
		ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(НоваяГруппа,
		"Объект.Топливо.СтавкаНДС",
		ВидСравненияКомпоновкиДанных.НеВСписке,
		СтавкиБезНДС);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	ШрифтЗаголовкаСтроки = Новый Шрифт(ШрифтыСтиля.ОбычныйШрифтТекста,,,, Истина);
	ЦветТекста = ЦветаСтиля.ЗаголовкиСтрокЦветТекста;
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "МаршрутЗаголовокОтправление");
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Отправление'"));
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтЗаголовкаСтроки);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветТекста);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "МаршрутЗаголовокНазначение");
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Назначение'"));
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтЗаголовкаСтроки);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветТекста);
	
	УчетКассовыхЧековПодотчетныхЛиц.УстановитьУсловноеОформлениеНаименованияИзЧека(УсловноеОформление, "ТопливоОписание");
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()
	
	ПлательщикНДС = Документы.ПутевойЛист.ВыделятьНДС(Объект.Организация, Объект.Дата);
	
КонецПроцедуры

&НаКлиенте
Функция ВидСообщенияПоУмолчанию()
	
	Возврат ПредопределенноеЗначение("Перечисление.ВидыСообщенияПеревозки.Городское");
	
КонецФункции

&НаСервереБезКонтекста
Функция ОтображатьВидСообщения(Период)
	
	Возврат Документы.ПутевойЛист.ОтображатьВидСообщения(Период);
	
КонецФункции

#Область СчетаУчета

&НаСервере
Процедура ЗаполнитьЗависимыеСчета(ИмяРеквизита)
	
	ПараметрыЗаполнения = НачатьЗаполнениеСчетовУчета(ИмяРеквизита, Объект);
	СчетаУчетаВДокументах.ЗаполнитьОбъектПриИзменении(ПараметрыЗаполнения);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция НачатьЗаполнениеСчетовУчета(ПричиныИзменения, Объект = Неопределено, СтрокаСписка = Неопределено, КонтейнерОбъект = Неопределено, КонтейнерСтрокаСписка = Неопределено)

	// Код этой функции сформирован автоматически с помощью СчетаУчетаВДокументах.КодФункцииНачатьЗаполнениеСчетовУчета()

	ПараметрыЗаполнения = СчетаУчетаВДокументахКлиентСервер.НовыйПараметрыЗаполнения(
		"ПутевойЛист",
		ПричиныИзменения,
		Объект,
		СтрокаСписка,
		КонтейнерОбъект,
		КонтейнерСтрокаСписка);

	// 1. Заполняемые реквизиты
	// Организация
	Если ПараметрыЗаполнения.ПричиныИзменения.Найти("Организация") <> Неопределено Тогда
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "СчетЗатрат");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "Субконто1");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "Субконто2");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "Субконто3");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "ПодразделениеЗатрат");
	КонецЕсли;

	// СчетЗатрат
	Если ПараметрыЗаполнения.ПричиныИзменения.Найти("СчетЗатрат") <> Неопределено Тогда
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "Субконто1");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "Субконто2");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "Субконто3");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "ПодразделениеЗатрат");
	КонецЕсли;

	// ПодразделениеОрганизации
	Если ПараметрыЗаполнения.ПричиныИзменения.Найти("ПодразделениеОрганизации") <> Неопределено Тогда
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "ПодразделениеЗатрат");
	КонецЕсли;

	// 2. (если требуется) Передадим на сервер данные, необходимые для заполнения
	Если ПараметрыЗаполнения.Свойство("Контейнер") Тогда
		// Организация
		Если ПараметрыЗаполнения.ПричиныИзменения.Найти("Организация") <> Неопределено Тогда
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Организация");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "СчетЗатрат");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Субконто1");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Субконто2");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Субконто3");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "ПодразделениеЗатрат");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "ПодразделениеОрганизации");
		КонецЕсли;

		// СчетЗатрат
		Если ПараметрыЗаполнения.ПричиныИзменения.Найти("СчетЗатрат") <> Неопределено Тогда
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "СчетЗатрат");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Субконто1");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Организация");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Субконто2");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Субконто3");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "ПодразделениеЗатрат");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "ПодразделениеОрганизации");
		КонецЕсли;

		// ПодразделениеОрганизации
		Если ПараметрыЗаполнения.ПричиныИзменения.Найти("ПодразделениеОрганизации") <> Неопределено Тогда
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "ПодразделениеОрганизации");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "ПодразделениеЗатрат");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "СчетЗатрат");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Организация");
		КонецЕсли;

	КонецЕсли; // Нужно передавать на сервер данные заполнения

	Возврат ПараметрыЗаполнения;

КонецФункции

#КонецОбласти

#Область КассовыеЧекиПодотчетныхЛиц

&НаСервереБезКонтекста
Функция ОписаниеДокументаДляЗагрузкиКассовыхЧековПодотчетныхЛиц()
	Возврат УчетКассовыхЧековПодотчетныхЛиц.НовыйОписаниеДокумента(ОписаниеФормыДляЗагрузкиКассовыхЧековПодотчетныхЛиц());
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеФормыДляЗагрузкиКассовыхЧековПодотчетныхЛиц()
	
	ОписаниеФормы = УчетКассовыхЧековПодотчетныхЛицКлиентСервер.НовыйОписаниеФормы();
	ОписаниеФормы.КнопкиЗагрузитьЧеки.Добавить("ТопливоЗагрузитьЧеки");
	Возврат ОписаниеФормы;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ОбновитьЧекиКЗагрузке()
	
	// Описан в ОписаниеФормыДляЗагрузкиКассовыхЧековПодотчетныхЛиц
	// Подключается в УчетКассовыхЧековПодотчетныхЛицКлиент.ПодключитьОбновлениеЧековКЗагрузке
	
	УчетКассовыхЧековПодотчетныхЛицКлиент.ОбновитьЧекиКЗагрузке(
		ЭтотОбъект,
		ОписаниеФормыДляЗагрузкиКассовыхЧековПодотчетныхЛиц());
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСодержимымКассовыхЧеков(АдресСодержимоеЧеков, ИмяСписка) Экспорт // обработчик оповещения
	
	ЗаполнитьСодержимымКассовыхЧековНаСервере(АдресСодержимоеЧеков, ИмяСписка);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСодержимымКассовыхЧековНаСервере(АдресСодержимоеЧеков, ИмяСписка)
	
	ДобавленныеСтроки = УчетКассовыхЧековПодотчетныхЛиц.ДействияПоЗаполнениюНаСервере(
		ЭтотОбъект,
		АдресСодержимоеЧеков,
		ИмяСписка,
		Истина,
		ОписаниеДокументаДляЗагрузкиКассовыхЧековПодотчетныхЛиц());
		
	ОбновитьИтоги();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучениеДанныхЧековЗагруженныхАппаратнымСканером(Результат)
	
	Если Результат.АдресХранилища <> Неопределено Тогда
		АдресСодержимоеЧеков = Результат.АдресХранилища;
		ЗаполнитьСодержимымКассовыхЧековНаСервере(АдресСодержимоеЧеков, Результат.ИмяСписка);
		Элементы.Страницы.ТекущаяСтраница = Элементы.ГруппаТопливо;
		Если Элементы.ГруппаБаннерПодключенАппаратныйСканер.Видимость = Истина Тогда
			УчетКассовыхЧековПодотчетныхЛицКлиент.СкрытьБаннерПодключенАппаратныйСканер(ЭтотОбъект);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииБСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область БЭД

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуЭДО(Команда)
	
	ЭлектронноеВзаимодействиеКлиент.ВыполнитьПодключаемуюКомандуЭДО(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработчикОжиданияЭДО()

	ОбменСКонтрагентамиКлиент.ОбработчикОжиданияЭДО(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура СостояниеЭДОНажатие(Элемент, СтандартнаяОбработка)
	ОбменСКонтрагентамиКлиент.СостояниеЭДОНажатие_ФормаДокумента(ЭтотОбъект, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#КонецОбласти
