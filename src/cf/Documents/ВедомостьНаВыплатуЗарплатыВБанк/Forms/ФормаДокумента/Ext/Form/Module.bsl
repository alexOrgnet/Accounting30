#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Объект") Тогда
		КопироватьДанныеФормы(Параметры.Объект, Объект);
	КонецЕсли;
	
	// ЗарплатаКадрыПодсистемы.ПодписиДокументов
	ПодписиДокументов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов
	
	ВедомостьНаВыплатуЗарплатыФормы.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	ВидыДоходовИсполнительногоПроизводстваКлиентСервер.НастроитьПолеВидДоходаДляВыплатыЗарплаты(ЭтотОбъект,Объект.Дата,, Ложь);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность = ОбщегоНазначения.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональность");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность.ПриСозданииНаСервере(ЭтотОбъект);
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
	// ЭлектронноеВзаимодействие.ОбменСБанками
 	ОбменСБанками.ПриСозданииФормыДокументаНаСервере(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ОбменСБанками
 	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьНачисленияПоДоговорам") Тогда
		ВидВыплатыДоговорПодряда = Элементы.СпособВыплатыЧислом.СписокВыбора.НайтиПоЗначению(2);
		Если ВидВыплатыДоговорПодряда <> Неопределено Тогда
			Элементы.СпособВыплатыЧислом.СписокВыбора.Удалить(ВидВыплатыДоговорПодряда);
		КонецЕсли;
	КонецЕсли;
	
	УстановитьСтатьюРасходов();
	
	СменитьФорматФайла();
	
	УстановитьУсловноеОформление();
	
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	ВедомостьНаВыплатуЗарплатыФормы.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		ОценкаПроизводительностиКлиент.ЗамерВремени("ПроведениеДокументаВедомостьНаВыплатуЗарплатыВБанк");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность = ОбщегоНазначения.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональность");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность.ПередЗаписьюНаСервере(
			ЭтотОбъект,
			ТекущийОбъект,
			ПараметрыЗаписи);
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.СостоянияДокументовЗачисленияЗарплаты"));
	Оповестить("Запись_ВедомостьНаВыплатуЗарплатыВБанк", ПараметрыЗаписи, Объект.Ссылка);
	
	// ЭлектронноеВзаимодействие.ОбменСБанками
	ОбменСБанкамиКлиент.ПослеЗаписи(ЭтотОбъект, ПараметрыЗаписи);
	// Конец ЭлектронноеВзаимодействие.ОбменСБанками
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	ВедомостьНаВыплатуЗарплатыКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	Если ИмяСобытия = "ЗагруженоПодтверждениеЗачисленияЗарплаты" И Параметр = Объект.Ссылка Тогда
		ГруппаПодтверждениеИзБанкаДополнитьФорму();
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	// ЭлектронноеВзаимодействие.ОбменСБанками
 	ОбменСБанкамиКлиент.ОбработатьОповещениеФормыДокумента(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ЭлектронноеВзаимодействие.ОбменСБанками
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)

	ЗаполнитьДанныеОВыплатеЗначениямиПоУмолчанию();
	
	ВедомостьНаВыплатуЗарплатыФормы.ОбработкаПроверкиЗаполненияНаСервере(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// ЗарплатаКадрыПодсистемы.ПодписиДокументов
	ПодписиДокументов.ПослеЗаписиНаСервере(ЭтотОбъект);
	// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов
	
	ВедомостьНаВыплатуЗарплатыФормы.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи); 
	
	// ЭлектронноеВзаимодействие.ОбменСБанками
	ОбменСБанками.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец ЭлектронноеВзаимодействие.ОбменСБанками
	
	УстановитьСостояниеДокумента();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	ВедомостьНаВыплатуЗарплатыКлиентСервер.НастроитьПолеВидДоходаИсполнительногоПроизводства(ЭтотОбъект);
	ВидыДоходовИсполнительногоПроизводстваКлиентСервер.НастроитьПолеВидДоходаДляВыплатыЗарплаты(ЭтотОбъект,Объект.Дата,, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ЗарплатаКадрыКлиент.КлючевыеРеквизитыЗаполненияФормыОчиститьТаблицы(ЭтотОбъект);
	ОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеОрганизацииПриИзменении(Элемент)
	
	ЗарплатаКадрыКлиент.КлючевыеРеквизитыЗаполненияФормыОчиститьТаблицы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура СпособВыплатыПриИзмененииНаСервере()
	ВедомостьНаВыплатуЗарплатыФормы.СпособВыплатыПриИзмененииНаСервере(ЭтотОбъект);
	Если Объект.СпособВыплаты = Справочники.СпособыВыплатыЗарплаты.Аванс()
		И Не ИспользоватьРасчетПервойПоловиныМесяца Тогда
		Для Каждого СтрокаТаблицы ИЗ Объект.Зарплата Цикл
			СтрокаТаблицы.ДокументОснование = "";
			СтрокаТаблицы.ПериодВзаиморасчетов = Объект.ПериодРегистрации;
		КонецЦикла;
		Если Объект.НДФЛ.Количество() <> 0 Тогда
			Объект.НДФЛ.Очистить();
			Для Каждого СтрокаСостава ИЗ Объект.Состав Цикл
				ПриПолученииДанныхСтрокиСостава(СтрокаСостава);
			КонецЦикла;
		КонецЕсли;
		Объект.ВидДоходаИсполнительногоПроизводства = Перечисления.ВидыДоходовИсполнительногоПроизводства.ЗарплатаВознаграждения;
	ИначеЕсли Объект.СпособВыплаты = Справочники.СпособыВыплатыЗарплаты.ДоговорПодряда() Тогда
		Объект.Подразделение = "";
		Объект.ВидДоходаИсполнительногоПроизводства = Перечисления.ВидыДоходовИсполнительногоПроизводства.ЗарплатаВознаграждения;
	КонецЕсли;
	СкорректироватьЗаполнениеВзысканнойСуммы();
КонецПроцедуры

&НаКлиенте
Процедура ЗарплатныйПроектПриИзменении(Элемент)
	
	ЗарплатныйПроектПриИзмененииНаСервере();
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗарплатныйПроектПриИзмененииНаСервере()
	СменитьФорматФайла();
КонецПроцедуры

&НаКлиенте
Процедура ВидДоходаИсполнительногоПроизводстваПриИзменении(Элемент)
	
	ВидДоходаИсполнительногоПроизводстваПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидДоходаИсполнительногоПроизводстваОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура СкорректироватьЗаполнениеВзысканнойСуммы()
	
	Если Объект.СпособВыплаты = Справочники.СпособыВыплатыЗарплаты.Аванс() Тогда
		Для Каждого СтрокаТаблицы ИЗ Объект.Состав Цикл
			СтрокаТаблицы.ВзысканнаяСумма = 0;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВидДоходаИсполнительногоПроизводстваПриИзмененииНаСервере()
	
	СкорректироватьЗаполнениеВзысканнойСуммы();
	УправлениеФормой();
	
КонецПроцедуры

#Область РедактированиеМесяцаСтрокой

&НаКлиенте
Процедура ПериодРегистрацииПриИзменении(Элемент)
	ЗарплатаКадрыКлиент.ВводМесяцаПриИзменении(ЭтотОбъект, "Объект.ПериодРегистрации", "ПериодРегистрацииСтрокой", Модифицированность);
КонецПроцедуры

&НаКлиенте
Процедура ПериодРегистрацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаНачалоВыбора(ЭтотОбъект, ЭтотОбъект, "Объект.ПериодРегистрации", "ПериодРегистрацииСтрокой");
КонецПроцедуры

&НаКлиенте
Процедура ПериодРегистрацииРегулирование(Элемент, Направление, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаРегулирование(ЭтотОбъект, "Объект.ПериодРегистрации", "ПериодРегистрацииСтрокой", Направление, Модифицированность);
КонецПроцедуры

&НаКлиенте
Процедура ПериодРегистрацииАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ПериодРегистрацииОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ЗарплатныйПроектОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура СпособВыплатыЧисломПриИзменении(Элемент)
	
	ИзменитьСпособВыплатыНаСервере();
	
КонецПроцедуры

// Обработчик подсистемы "ПодписиДокументов".
&НаКлиенте
Процедура Подключаемый_ПодписиДокументовЭлементПриИзменении(Элемент) 
	ПодписиДокументовКлиент.ПриИзмененииПодписывающегоЛица(ЭтотОбъект, Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПодписиДокументовЭлементНажатие(Элемент) 
	ПодписиДокументовКлиент.РасширеннаяПодсказкаНажатие(ЭтотОбъект, Элемент.Имя);
КонецПроцедуры
// Конец Обработчик подсистемы "ПодписиДокументов".

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.КомментарийНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка)
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСостав

&НаКлиенте
Процедура СоставВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ВедомостьНаВыплатуЗарплатыКлиент.СоставВыбор(ЭтотОбъект, Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)	
КонецПроцедуры

&НаКлиенте
Процедура СоставОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СоставОбработкаВыбораНаСервере(ВыбранноеЗначение, СтандартнаяОбработка);
КонецПроцедуры

&НаСервере
Процедура СоставОбработкаВыбораНаСервере(ВыбранноеЗначение, СтандартнаяОбработка)
	ВедомостьНаВыплатуЗарплатыФормы.СоставОбработкаВыбораНаСервере(ЭтотОбъект, ВыбранноеЗначение, СтандартнаяОбработка)
КонецПроцедуры

&НаКлиенте
Процедура СоставПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Если Не Копирование Тогда
		ВедомостьНаВыплатуЗарплатыКлиент.Подобрать(ЭтотОбъект);
	КонецЕсли;	
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СоставПередУдалением(Элемент, Отказ)
	ВедомостьНаВыплатуЗарплатыКлиент.СоставПередУдалением(ЭтотОбъект, Элемент, Отказ) 
КонецПроцедуры

&НаКлиенте
Процедура СоставПослеУдаления(Элемент)
	СоставПослеУдаленияНаСервере()
КонецПроцедуры

&НаСервере
Процедура СоставПослеУдаленияНаСервере()
	ВедомостьНаВыплатуЗарплатыФормы.СоставПослеУдаленияНаСервере(ЭтотОбъект)
КонецПроцедуры

&НаСервере
Процедура СоставКВыплатеПриИзмененииНаСервере()
	ВедомостьНаВыплатуЗарплатыФормы.СоставКВыплатеПриИзмененииНаСервере(ЭтотОбъект);
	ОбновитьНДФЛНаСервере(Новый ФиксированныйМассив(ЭтотОбъект.Элементы.Состав.ВыделенныеСтроки));
КонецПроцедуры

&НаКлиенте
Процедура СоставКВыплатеПриИзменении(Элемент)
	СоставКВыплатеПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СоставКВыплатеОткрытие(Элемент, СтандартнаяОбработка)
	ВедомостьНаВыплатуЗарплатыКлиент.СоставКВыплатеОткрытие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура СоставСпособЗачисленияВыплатыПриИзменении(Элемент)
	
	ОчиститьСпособыЗачисленияНаСервере(Элементы.Состав.ТекущаяСтрока);
	ЗаполнитьПоСпособуЗачисленияНаСервере(Элементы.Состав.ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура СоставНомерЛицевогоСчетаПриИзменении(Элемент)
	СоставПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СоставБанковскаяКартаПриИзменении(Элемент)
	СоставБанковскаяКартаПриИзмененииНаСервере(Элементы.Состав.ТекущаяСтрока);
КонецПроцедуры

&НаСервере
Процедура СоставТелефонПриИзмененииНаСервере(Идентификатор)
	
	Строка = Объект.Состав.НайтиПоИдентификатору(Идентификатор);
	
	КонтактнаяИнформация = Новый ТаблицаЗначений;
	
	Если ЗначениеЗаполнено(Строка.Телефон) Тогда
		КонтактнаяИнформация = КонтактнаяИнформацияБЗК.КонтактнаяИнформацияОбъектов(
			Строка.ФизическоеЛицо, 
			,
			Строка.Телефон);
	КонецЕсли;
		
	Если КонтактнаяИнформация.Количество()> 0 Тогда
		Строка.НомерТелефонаПредставление = КонтактнаяИнформация[0].Представление;
		Строка.НомерТелефона = КонтактнаяИнформация[0].Значение;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоставТелефонПриИзменении(Элемент)
	СоставТелефонПриИзмененииНаСервере(Элементы.Состав.ТекущаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура СоставНомерТелефонаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыЭлемента = Новый Структура;
	ПараметрыЭлемента.Вставить("ВидКонтактнойИнформации", ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ТелефонМобильныйФизическиеЛица"));
	ПараметрыЭлемента.Вставить("ИмяКИ", "НомерТелефона");
	ПараметрыЭлемента.Вставить("ИмяКИПредставление", "НомерТелефонаПредставление");
	
	ЭлементКонтактнойИнформацииНомерТелефонаНачалоВыбора(ПараметрыЭлемента, СтандартнаяОбработка, Ложь);

КонецПроцедуры

&НаСервере
Процедура СоставБанковскаяКартаПриИзмененииНаСервере(Идентификатор)
	
	Строка = Объект.Состав.НайтиПоИдентификатору(Идентификатор);
	Строка.НомерБанковскойКарты = БанковскиеКарты.МаскированныйНомерКарты(Строка.БанковскаяКарта);

КонецПроцедуры

&НаСервере
Процедура СоставСчетВБанкеПриИзмененииНаСервере(Идентификатор)
	
	Строка = Объект.Состав.НайтиПоИдентификатору(Идентификатор);
	
	РеквизитыСчета = Неопределено;
	ВедомостьНаВыплатуЗарплаты.ПриИзмененииБанковскогоСчета(Строка.БанковскийСчет, РеквизитыСчета);
	
	Строка.БИКБанкаСчета = РеквизитыСчета.БИК;
	Строка.НомерЛицевогоСчета = РеквизитыСчета.НомерСчета;
	
КонецПроцедуры

&НаКлиенте
Процедура СоставСчетВБанкеПриИзменении(Элемент)
	
	СоставСчетВБанкеПриИзмененииНаСервере(Элементы.Состав.ТекущаяСтрока);
	
КонецПроцедуры

&НаСервере
Процедура СоставПриИзмененииНаСервере()
	ВедомостьНаВыплатуЗарплатыФормы.СоставПриИзмененииНаСервере(ЭтотОбъект)
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец ЭлектронноеВзаимодействие.ОбменСБанками

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Заполнить(Команда)
	
	Если ЗарплатныйПроект
		И НЕ ЗначениеЗаполнено(Объект.ЗарплатныйПроект) Тогда
		ТекстСообщения = НСтр("ru='Не указан зарплатный проект'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ЗарплатныйПроект", "Объект");
		Возврат;
	КонецЕсли;
		
	ОценкаПроизводительностиКлиент.ЗамерВремени("ЗаполнениеДокументаВедомостьНаВыплатуЗарплатыВБанк");
	ВедомостьНаВыплатуЗарплатыКлиент.Заполнить(ЭтотОбъект)
	
КонецПроцедуры

&НаКлиенте
Процедура Подобрать(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("ПодборСотрудникаВФормеДокументаВедомостьНаВыплатуЗарплатыВБанк");
	
	ВедомостьНаВыплатуЗарплатыКлиент.Подобрать(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьЗарплату(Команда)
	РедактироватьЗарплатуСтроки(Элементы.Состав.ТекущиеДанные);	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНДФЛ(Команда)
	ВедомостьНаВыплатуЗарплатыКлиент.ОбновитьНДФЛ(ЭтотОбъект);	
КонецПроцедуры

&НаКлиенте
Процедура ОплатыПредставлениеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	ВедомостьНаВыплатуЗарплатыКлиент.ОплатаПоказать(ЭтотОбъект, Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОткрытьДокументПодтверждение(Команда)
	ОбменСБанкамиПоЗарплатнымПроектамКлиент.ОткрытьДокументПодтверждение(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗагрузитьДокументПодтверждение(Команда)
	
	ОткрытьФорму("Документ.ПодтверждениеЗачисленияЗарплаты.ФормаОбъекта");
	
КонецПроцедуры

// ЭлектронноеВзаимодействие.ОбменСБанками
&НаКлиенте
Процедура Подключаемый_ПриНажатииНаСсылкуДиректБанк()
     ОбменСБанкамиКлиент.ОткрытьАктуальныйЭД(Объект.Ссылка, ЭтотОбъект);
 КонецПроцедуры

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль(
		"ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент");
	МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(
		Команда,
		ЭтотОбъект,
		Объект);
	
КонецПроцедуры
// Конец ИнтеграцияС1СДокументооборотом

&НаКлиенте
Процедура Оплатить(Команда)
	
	Если Объект.ПометкаУдаления Тогда
		ПоказатьПредупреждение( , НСтр("ru = 'Нельзя оформить выплату на основании документа, помеченного на удаление.'"));
		Возврат;
	Иначе
		Если НЕ Объект.Проведен ИЛИ Модифицированность Тогда
			Кнопки = Новый СписокЗначений;
			Кнопки.Вставить(0, КодВозвратаДиалога.Да,     "Провести");
			Кнопки.Вставить(1, КодВозвратаДиалога.Отмена, "Отмена");
			
			Оповещение = Новый ОписаниеОповещения("ВопросПередВыплатойСледуетПровестиЗавершение", ЭтотОбъект);
			ПоказатьВопрос(Оповещение, НСтр("ru = 'Перед выплатой документ следует провести'"), Кнопки,, КодВозвратаДиалога.Да);
		Иначе
			ОплатитьВедомость();
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЛицевойСчетВСтроке(Строка)
	
	ЛицевойСчет = ОбменСБанкамиПоЗарплатнымПроектам.ЛицевыеСчетаФизическихЛиц(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Строка.ФизическоеЛицо),
		Истина, Объект.Организация, Объект.Дата, Объект.ЗарплатныйПроект);
	Если ЛицевойСчет.Количество() > 0 Тогда
		Строка.НомерЛицевогоСчета = ЛицевойСчет[0].НомерЛицевогоСчета;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСНИЛСВСтроке(Строка)
	
	ФизическоеЛицо = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Строка.ФизическоеЛицо);
	КадровыеДанные = "СтраховойНомерПФР";
	ТЗКадровыеДанныеФизическогоЛица = КадровыйУчет.КадровыеДанныеФизическихЛиц(Истина, ФизическоеЛицо, КадровыеДанные, Объект.Дата);
	Строка.СНИЛС = ТЗКадровыеДанныеФизическогоЛица[0].СтраховойНомерПФР;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНомерТелефонаВСтроке(Строка)
	
	ФизическоеЛицо = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Строка.ФизическоеЛицо);
	КадровыеДанные = "ТелефонМобильный,ТелефонМобильныйПредставление";
	ТЗКадровыеДанныеФизическогоЛица = КадровыйУчет.КадровыеДанныеФизическихЛиц(Истина, ФизическоеЛицо, КадровыеДанные, Объект.Дата);
	Строка.НомерТелефона = ТЗКадровыеДанныеФизическогоЛица[0].ТелефонМобильный;
	Строка.НомерТелефонаПредставление = ТЗКадровыеДанныеФизическогоЛица[0].ТелефонМобильныйПредставление;
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область КлючевыеРеквизитыЗаполненияФормы

// Функция возвращает описание таблиц формы подключенных к механизму ключевых реквизитов формы.
&НаСервере
Функция КлючевыеРеквизитыЗаполненияФормыТаблицыОчищаемыеПриИзменении() Экспорт
	Возврат ВедомостьНаВыплатуЗарплатыФормы.КлючевыеРеквизитыЗаполненияФормыТаблицыОчищаемыеПриИзменении()
КонецФункции 

// Функция возвращает массив реквизитов формы подключенных к механизму ключевых реквизитов формы.
&НаСервере
Функция КлючевыеРеквизитыЗаполненияФормыОписаниеКлючевыхРеквизитов() Экспорт
	Возврат ВедомостьНаВыплатуЗарплатыФормы.КлючевыеРеквизитыЗаполненияФормыОписаниеКлючевыхРеквизитов()
КонецФункции

#КонецОбласти

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#Область ОбратныеВызовы

&НаСервере
Процедура ЗаполнитьПервоначальныеЗначения() Экспорт
	
	// ЗарплатаКадрыПодсистемы.ПодписиДокументов
	ПодписиДокументов.ЗаполнитьПодписиПоОрганизации(ЭтотОбъект);
	// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов
	
	ВедомостьНаВыплатуЗарплатыФормы.ЗаполнитьПервоначальныеЗначения(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриПолученииДанныхНаСервере(ТекущийОбъект) Экспорт
	
	ИспользоватьРасчетПервойПоловиныМесяца =
		УчетЗарплаты.ИспользоватьРасчетПервойПоловиныМесяца(ТекущийОбъект.Организация, ТекущийОбъект.ПериодРегистрации);
	
	ВедомостьНаВыплатуЗарплатыФормы.ПриПолученииДанныхНаСервере(ЭтотОбъект, ТекущийОбъект);
	
	ВедомостьНаВыплатуЗарплатыКлиентСервер.НастроитьПолеВидДоходаИсполнительногоПроизводства(ЭтотОбъект);
	
	ЗарплатныйПроект = ТекущийОбъект.ВидМестаВыплаты = Перечисления.ВидыМестВыплатыЗарплаты.ЗарплатныйПроект;
	
	Если Параметры.Ключ.Пустая() Тогда
		Если ЗарплатныйПроект Тогда
			ПодобратьЗарплатныйПроект();
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Объект.СпособВыплаты) Тогда
			Объект.СпособВыплаты = Справочники.СпособыВыплатыЗарплаты.ПоУмолчанию();
		КонецЕсли;
	КонецЕсли;
	
	УстановитьВидимостьДанныхЗарплатногоПроекта();
	ГруппаПодтверждениеИзБанкаДополнитьФорму();
	
	Если Объект.СпособВыплаты = Справочники.СпособыВыплатыЗарплаты.Аванс() Тогда
		СпособВыплатыЧислом = 1;
	ИначеЕсли Объект.СпособВыплаты = Справочники.СпособыВыплатыЗарплаты.ДоговорПодряда() Тогда
		СпособВыплатыЧислом = 2;
	Иначе
		СпособВыплатыЧислом = 0;
	КонецЕсли;
	
	УстановитьСостояниеДокумента();
	
КонецПроцедуры

&НаСервере
Процедура ГруппаПодтверждениеИзБанкаДополнитьФорму()
	ОбменСБанкамиПоЗарплатнымПроектам.ГруппаПодтверждениеИзБанкаДополнитьФорму(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ПриПолученииДанныхСтрокиСостава(СтрокаСостава) Экспорт
	ВедомостьНаВыплатуЗарплатыФормы.ПриПолученииДанныхСтрокиСостава(ЭтотОбъект, СтрокаСостава)
КонецПроцедуры

&НаСервере
Процедура ОбработатьСообщенияПользователю() Экспорт
	ВедомостьНаВыплатуЗарплатыФормы.ОбработатьСообщенияПользователю(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьЭлементов() Экспорт

	ВедомостьНаВыплатуЗарплатыФормы.УстановитьДоступностьЭлементов(ЭтотОбъект);
	
	ЕстьОплатаПоВедомости = ВзаиморасчетыССотрудниками.ЕстьОплатаПоВедомости(Объект.Ссылка);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ГруппаОплатыКнопка",
		"Видимость",
		НЕ ЕстьОплатаПоВедомости);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ГруппаОплатыСсылка",
		"Видимость",
		ЕстьОплатаПоВедомости);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"Оплатить",
		"Доступность",
		НЕ ТолькоПросмотр);
		
КонецПроцедуры

&НаСервере
Процедура УстановитьПредставлениеОплаты() Экспорт
	ВедомостьНаВыплатуЗарплатыФормы.УстановитьПредставлениеОплаты(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере() Экспорт

	Если НЕ ЗначениеЗаполнено(Объект.Округление) Тогда
		Объект.Округление = Справочники.СпособыОкругленияПриРасчетеЗарплаты.ПоУмолчанию();
	КонецЕсли;
	
	ВедомостьНаВыплатуЗарплатыФормы.ЗаполнитьНаСервере(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция АдресСпискаПодобранныхСотрудников() Экспорт
	Возврат ВедомостьНаВыплатуЗарплатыФормы.АдресСпискаПодобранныхСотрудников(ЭтотОбъект)
КонецФункции

&НаКлиенте
Процедура РедактироватьЗарплатуСтроки(ДанныеСтроки) Экспорт
	ВедомостьНаВыплатуЗарплатыКлиент.РедактироватьЗарплатуСтроки(ЭтотОбъект, ДанныеСтроки);	
КонецПроцедуры

&НаСервере
Процедура РедактироватьЗарплатуСтрокиЗавершениеНаСервере(РезультатыРедактирования) Экспорт
	
	ВедомостьНаВыплатуЗарплатыФормы.РедактироватьЗарплатуСтрокиЗавершениеНаСервере(ЭтотОбъект, РезультатыРедактирования);
	ОбновитьНДФЛНаСервере(Новый ФиксированныйМассив(ЭтотОбъект.Элементы.Состав.ВыделенныеСтроки));
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьНДФЛСтроки(ДанныеСтроки) Экспорт
	ВедомостьНаВыплатуЗарплатыКлиент.РедактироватьНДФЛСтроки(ЭтотОбъект, ДанныеСтроки);	
КонецПроцедуры

&НаСервере
Процедура РедактироватьНДФЛСтрокиЗавершениеНаСервере(РезультатыРедактирования) Экспорт
	ВедомостьНаВыплатуЗарплатыФормы.РедактироватьНДФЛСтрокиЗавершениеНаСервере(ЭтотОбъект, РезультатыРедактирования) 
КонецПроцедуры

&НаСервере
Функция АдресВХранилищеЗарплатыПоСтроке(ИдентификаторСтроки) Экспорт
	Возврат ВедомостьНаВыплатуЗарплатыФормы.АдресВХранилищеЗарплатыПоСтроке(ЭтотОбъект, ИдентификаторСтроки)
КонецФункции	

&НаСервере
Функция АдресВХранилищеНДФЛПоСтроке(ИдентификаторСтроки) Экспорт
	Возврат ВедомостьНаВыплатуЗарплатыФормы.АдресВХранилищеНДФЛПоСтроке(ЭтотОбъект, ИдентификаторСтроки)
КонецФункции	

&НаСервере
Процедура ОбновитьНДФЛНаСервере(ИдентификаторыСтрок) Экспорт
	ВедомостьНаВыплатуЗарплатыФормы.ОбновитьНДФЛНаСервере(ЭтотОбъект, ИдентификаторыСтрок)
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СоставНомерЛицевогоСчета");
	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ЭлементУО.Отбор.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ЗарплатныйПроектПоддерживаетСпособыЗачисленияВыплат",
		ВидСравненияКомпоновкиДанных.Равно,
		Истина);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора1,
		"Объект.Состав.СпособЗачисления",
		ВидСравненияКомпоновкиДанных.НеРавно,
		Перечисления.СпособыЗачисленияВыплат.ПоЛицевомуСчету);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СоставБанковскаяКарта");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"Объект.Состав.СпособЗачисления",
		ВидСравненияКомпоновкиДанных.НеРавно,
		Перечисления.СпособыЗачисленияВыплат.ПоНомеруКарты);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СоставСНИЛС");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"Объект.Состав.СпособЗачисления",
		ВидСравненияКомпоновкиДанных.НеРавно,
		Перечисления.СпособыЗачисленияВыплат.ПоСНИЛС);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СоставНомерТелефона");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"Объект.Состав.СпособЗачисления",
		ВидСравненияКомпоновкиДанных.НеРавно,
		Перечисления.СпособыЗачисленияВыплат.ПоНомеруТелефона);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СоставТелефон");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"Объект.Состав.СпособЗачисления",
		ВидСравненияКомпоновкиДанных.НеРавно,
		Перечисления.СпособыЗачисленияВыплат.ПоНомеруТелефона);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СоставСчетВБанке");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"Объект.Состав.СпособЗачисления",
		ВидСравненияКомпоновкиДанных.НеРавно,
		Перечисления.СпособыЗачисленияВыплат.ПоНомеруСчетаВБанк);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СоставСпособЗачисленияВыплаты");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ЗарплатныйПроектПоддерживаетСпособыЗачисленияВыплат",
		ВидСравненияКомпоновкиДанных.Равно,
		Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьСпособыЗачисленияНаСервере(Идентификатор)
	
	Строка = Объект.Состав.НайтиПоИдентификатору(Идентификатор);
	Строка.НомерЛицевогоСчета = "";
	Строка.БанковскаяКарта = Справочники.БанковскиеКартыКонтрагентов.ПустаяСсылка();
	Строка.СНИЛС = "";
	Строка.НомерТелефона = "";
	Строка.БанковскийСчет = "";
	Строка.БИКБанкаСчета = "";
	
КонецПроцедуры

&НаКлиенте
Процедура ЭлементКонтактнойИнформацииНомерТелефонаНачалоВыбора(ПараметрыЭлемента, СтандартнаяОбработка, ФиксироватьЗначения = Истина)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.Состав.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = УправлениеКонтактнойИнформациейКлиент.ПараметрыФормыКонтактнойИнформации(
		ПараметрыЭлемента.ВидКонтактнойИнформации,
		ТекущиеДанные[ПараметрыЭлемента.ИмяКИ]);
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("ТекущиеДанные", ТекущиеДанные);
	ПараметрыОповещения.Вставить("ПараметрыЭлемента", ПараметрыЭлемента);
	
	Оповещение = Новый ОписаниеОповещения("ЗавершениеВыбораКИ", ЭтотОбъект, ПараметрыОповещения);
	УправлениеКонтактнойИнформациейКлиент.ОткрытьФормуКонтактнойИнформации(ПараметрыОткрытия, ЭтотОбъект, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеВыбораКИ(РезультатОткрытияФормы, ПараметрыОповещения) Экспорт 
	
	Если ТипЗнч(РезультатОткрытияФормы) <> Тип("Структура") Тогда
		// не было изменений в данных
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = ПараметрыОповещения.ТекущиеДанные;
	ПараметрыЭлемента = ПараметрыОповещения.ПараметрыЭлемента;
	
	ТекущиеДанные[ПараметрыЭлемента.ИмяКИ] = РезультатОткрытияФормы.Значение;
	ТекущиеДанные[ПараметрыЭлемента.ИмяКИПредставление] = РезультатОткрытияФормы.Представление;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоСпособуЗачисленияНаСервере(Идентификатор)
	
	Строка = Объект.Состав.НайтиПоИдентификатору(Идентификатор);
	
	Если Строка.СпособЗачисления = Перечисления.СпособыЗачисленияВыплат.ПоЛицевомуСчету 
		Или Не ЗначениеЗаполнено(Строка.СпособЗачисления) Тогда
		ЗаполнитьЛицевойСчетВСтроке(Строка);
	ИначеЕсли Строка.СпособЗачисления = Перечисления.СпособыЗачисленияВыплат.ПоСНИЛС Тогда
		ЗаполнитьСНИЛСВСтроке(Строка);
	ИначеЕсли Строка.СпособЗачисления = Перечисления.СпособыЗачисленияВыплат.ПоНомеруТелефона Тогда
		ЗаполнитьНомерТелефонаВСтроке(Строка);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура СменитьФорматФайла()
	
	ЗарплатныйПроектПоддерживаетСпособыЗачисленияВыплат = 
		ОбменСБанкамиПоЗарплатнымПроектам.ЗарплатныйПроектПоддерживаетСпособыЗачисленияВыплат(
			Объект.ЗарплатныйПроект);
			
	ОбменСБанкамиПоЗарплатнымПроектам.ЗаполнитьСписокСпособовЗачисления(Элементы.СоставСпособЗачисленияВыплаты, 
		Объект.ЗарплатныйПроект);
		
	Если Не ЗарплатныйПроектПоддерживаетСпособыЗачисленияВыплат Тогда
			Для Каждого СтрокаСостава Из Объект.Состав Цикл
				СтрокаСостава.СпособЗачисления = Перечисления.СпособыЗачисленияВыплат.ПоЛицевомуСчету;
				СтрокаСостава.СНИЛС = "";
				СтрокаСостава.НомерТелефонаПредставление = "";
				СтрокаСостава.НомерТелефона = "";
				СтрокаСостава.БанковскийСчет = Неопределено;
				СтрокаСостава.БИКБанкаСчета = Неопределено;
				СтрокаСостава.БанковскаяКарта = Неопределено;
				СтрокаСостава.НомерБанковскойКарты = "";
			КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеФормой()
	
	Элементы.СоставНДФЛГруппа.Видимость = СпособВыплатыЧислом <> 1
		Или ИспользоватьРасчетПервойПоловиныМесяца;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ПодразделениеОрганизации",
		"Видимость",
		СпособВыплатыЧислом <> 2);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ВидДоходаИсполнительногоПроизводства",
		"Видимость",
		СпособВыплатыЧислом = 0
		Или (СпособВыплатыЧислом = 1
			И ИспользоватьРасчетПервойПоловиныМесяца));
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"СоставВзысканнаяСумма",
		"Видимость",
		СпособВыплатыЧислом <> 1
		Или ИспользоватьРасчетПервойПоловиныМесяца);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"СоставКонтекстноеМенюОбновитьНДФЛ",
		"Видимость",
		СпособВыплатыЧислом <> 1
		Или ИспользоватьРасчетПервойПоловиныМесяца);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"Организация",
		"Доступность",
		Не Параметры.КонтекстныйВызов);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ПериодРегистрации",
		"Доступность",
		Не Параметры.КонтекстныйВызов);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"СпособВыплаты",
		"Доступность",
		Не Параметры.КонтекстныйВызов
		Или (ПолучитьФункциональнуюОпцию("ИспользоватьНачисленияПоДоговорам")
			И СпособВыплатыЧислом <> 1));
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"СпособВыплатыЧислом1",
		"Доступность",
		Не Параметры.КонтекстныйВызов);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"СпособВыплатыЧислом2",
		"Видимость",
		ПолучитьФункциональнуюОпцию("ИспользоватьНачисленияПоДоговорам"));
		
КонецПроцедуры

&НаСервере
Процедура ИзменитьСпособВыплатыНаСервере()
	
	Если СпособВыплатыЧислом = 1 Тогда
		Объект.СпособВыплаты = Справочники.СпособыВыплатыЗарплаты.Аванс();
	ИначеЕсли СпособВыплатыЧислом = 2 Тогда
		Объект.СпособВыплаты = Справочники.СпособыВыплатыЗарплаты.ДоговорПодряда();
	Иначе
		Объект.СпособВыплаты = Справочники.СпособыВыплатыЗарплаты.ПоУмолчанию();
	КонецЕсли;
	Объект.Округление = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.СпособВыплаты, "Округление");
	СпособВыплатыПриИзмененииНаСервере();
	УстановитьСтатьюРасходов();
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура ПодобратьЗарплатныйПроект()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗарплатныеПроекты.Ссылка КАК ЗарплатныйПроект
	|ИЗ
	|	Справочник.ЗарплатныеПроекты КАК ЗарплатныеПроекты
	|ГДЕ
	|	ЗарплатныеПроекты.Организация = &Организация
	|	И НЕ ЗарплатныеПроекты.ПометкаУдаления";
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	Если РезультатЗапроса.Количество() = 1 Тогда
		Объект.ЗарплатныйПроект = РезультатЗапроса[0].ЗарплатныйПроект;
	КонецЕсли;
	
	УстановитьВидимостьДанныхЗарплатногоПроекта();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДанныхЗарплатногоПроекта()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ГруппаЗарплатныйПроект",
		"Видимость",
		ЗарплатныйПроект);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"СпособЗачисленияГруппа",
		"Видимость",
		ЗарплатныйПроект);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ГруппаОбменСБанками",
		"Видимость",
		ЗарплатныйПроект);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ГруппаПодтверждениеИзБанка",
		"Видимость",
		ЗарплатныйПроект);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ГруппаПодтверждениеИзБанка",
		"Видимость",
		ЗарплатныйПроект);
		
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = 0;
	ЗарплатаКадрыПереопределяемый.СостояниеДокумента(Объект, СостояниеДокумента);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередВыплатойСледуетПровестиЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ДокументПроведен = Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
	Если НЕ ДокументПроведен Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не удалось провести документ'"));
		Возврат;
	КонецЕсли;
	
	ОплатитьВедомость();
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатитьВедомость()
	
	Если СпособВыплатыЧислом = 2 Тогда
		ОткрытьФорму("Обработка.ВыплатаЗарплатыПлатежнымиПоручениями.Форма", Новый Структура("ПлатежнаяВедомость", Объект.Ссылка));
	Иначе
		ОплатитьНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОплатитьНаСервере()
	
	ВедомостьНаВыплатуЗарплатыФормыПереопределяемый.СформироватьДокументыОплаты(ЭтотОбъект);
	УстановитьПредставлениеОплаты();
	УстановитьДоступностьЭлементов();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСтатьюРасходов()
	
	СтатьяРасходов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.СпособВыплаты, "СтатьяРасходов");
	
КонецПроцедуры

#КонецОбласти

#Область ВызовыСервера

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере() Экспорт
	
	ИспользоватьРасчетПервойПоловиныМесяца =
		УчетЗарплаты.ИспользоватьРасчетПервойПоловиныМесяца(Объект.Организация, Объект.ПериодРегистрации);
	
	УправлениеФормой();
	
	// ЗарплатаКадрыПодсистемы.ПодписиДокументов
	ПодписиДокументов.ЗаполнитьПодписиПоОрганизации(ЭтотОбъект);
	// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеОВыплатеЗначениямиПоУмолчанию()
	
	Если НЕ ЗначениеЗаполнено(Объект.ВидМестаВыплаты) Тогда
		Объект.ВидМестаВыплаты = Перечисления.ВидыМестВыплатыЗарплаты.БанковскийСчет;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Объект.СпособВыплаты) Тогда
		Объект.СпособВыплаты = Справочники.СпособыВыплатыЗарплаты.ПоУмолчанию();
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Объект.Округление) Тогда
		Объект.Округление = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.СпособВыплаты, "Округление");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
