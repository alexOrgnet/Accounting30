
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры,
		"КлючСтроки,АдресТаблицыКодыОперацийНалоговаяБаза,
		|ДокументОтгрузкиПродажиСНДС0,ДокументОтгрузкиСуммаНДС,
		|ОтредактированыСтрокиКодовОпераций,Событие,Дата,Организация,
		|ВерсияПостановления1137,Покупатель,ДокументОтгрузки");

	ИсключитьПустоеЗначение = Новый Массив;
	ИсключитьПустоеЗначение.Добавить("");
	ТаблицаВыбора.Загрузить(УчетНДС.КлассификаторКодовОперацийПоСтавке0(Параметры.Дата,,ИсключитьПустоеЗначение));
	
	ТаблицаКодыОперацийНалоговаяБаза = ПолучитьИзВременногоХранилища(АдресТаблицыКодыОперацийНалоговаяБаза);
	
	Объект.КодыОперацийНалоговаяБаза.Очистить();
	Объект.КодыОперацийНалоговаяБаза.Загрузить(ТаблицаКодыОперацийНалоговаяБаза);
	
	Элементы.КодыОперацийНалоговаяБаза.ТолькоПросмотр = НЕ Параметры.ОтредактированыСтрокиКодовОпераций;
	
	Если Событие = Перечисления.СобытияПоНДСПродажи.ПодтвержденаСтавка0 Тогда
		Элементы.КодыОперацийНалоговаяБазаСуммаНДС.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	ОбновитьПодвал(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗавершениеРаботы И (Модифицированность ИЛИ ПеренестиВДокумент) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Модифицированность И НЕ ПеренестиВДокумент Тогда
		Отказ = Истина;
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемФормыЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да);
	КонецЕсли;
	
	Если ПеренестиВДокумент И НЕ Отказ Тогда
		Отказ = НЕ ПроверитьЗаполнениеНаКлиенте();
	КонецЕсли;
	
	Если Отказ Тогда
		ПеренестиВДокумент = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ПеренестиВДокумент Тогда
		
		СтруктураРезультат = ПриЗакрытииНаСервере();
		
		Оповестить("РедактированиеСпискаКодовОпераций", СтруктураРезультат, ЭтотОбъект.ВладелецФормы);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицы

&НаКлиенте
Процедура КодыОперацийПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ДанныеСтроки = Элементы.КодыОперацийНалоговаяБаза.ТекущиеДанные;
	
	Если НоваяСтрока Тогда
		ДанныеСтроки.КлючСтроки = КлючСтроки;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ПеренестиВДокумент = Истина;
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Модифицированность = Ложь;
	ПеренестиВДокумент = Ложь;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВопросПередЗакрытиемФормыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПеренестиВДокумент = Истина;
		Закрыть();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		ПеренестиВДокумент = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьЗаполнениеНаКлиенте()
	
	Отказ = Ложь;
	
	#Область ПроверкаКодовОперацийНалоговаяБаза

		ИтогоПродажиСНДС0 = Объект.КодыОперацийНалоговаяБаза.Итог("ПродажиСНДС0");
		ИтогоСуммаНДС = Объект.КодыОперацийНалоговаяБаза.Итог("СуммаНДС");
		
		Если ДокументОтгрузкиПродажиСНДС0 <> ИтогоПродажиСНДС0 Тогда
			
			Текст = НСтр("ru = 'Итоговая сумма по кодам операций в колонке ""Продажи с НДС 0%"" '") +
			СтрШаблон(НСтр("ru = '%1 не совпадает со значением %2 по документу отгрузки.'"),
			ИтогоПродажиСНДС0,
			ДокументОтгрузкиПродажиСНДС0);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					Текст,
					,
					"ДокументОтгрузкиПродажиСНДС0",
					,
					Отказ);
			
		КонецЕсли;
		
		Если ДокументОтгрузкиСуммаНДС <> ИтогоСуммаНДС Тогда
			
			Текст = СтрШаблон(НСтр
			("ru = 'Итоговая сумма по кодам операций в колонке ""НДС"" %1 не совпадает со значением %2 по документу отгрузки.'"),
			ИтогоСуммаНДС,
			ДокументОтгрузкиСуммаНДС);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					Текст,
					,
					"ДокументОтгрузкиСуммаНДС",
					,
					Отказ);

		КонецЕсли;

		ИмяСписка = НСтр("ru = 'Налоговая база'");
		
		МассивКодовОпераций = Новый Массив;
		
		Для Индекс = 0 По Объект.КодыОперацийНалоговаяБаза.Количество() - 1 Цикл
			
			СтрокаКодыОпераций = Объект.КодыОперацийНалоговаяБаза[Индекс];
			Префикс = "Объект.КодыОперацийНалоговаяБаза[%1]";
			Префикс = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				Префикс, Формат(Индекс, "ЧН=0; ЧГ="));
			
			Если Не ЗначениеЗаполнено(СтрокаКодыОпераций.КодОперации) Тогда
				Поле = Префикс + ".КодОперации";
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка",, НСтр("ru = 'Код операции'"),
					Индекс + 1, ИмяСписка);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , Поле, , Отказ);
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(СтрокаКодыОпераций.ПродажиСНДС0) Тогда
				Поле = Префикс + ".ПродажиСНДС0";
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка",, НСтр("ru = 'Продажи с НДС 0%'"),
					Индекс + 1, ИмяСписка);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , Поле, , Отказ);
			КонецЕсли;
			
			Отбор = Новый Структура();
			Отбор.Вставить("КодОперации", СтрокаКодыОпераций.КодОперации);
			Отбор.Вставить("НесырьевыеТовары", СтрокаКодыОпераций.НесырьевыеТовары);
			СтрокиТаблицы = Объект.КодыОперацийНалоговаяБаза.НайтиСтроки(Отбор);

			Если СтрокиТаблицы.Количество() > 1 И ЗначениеЗаполнено(СтрокаКодыОпераций.КодОперации) Тогда
				Текст = СтрШаблон(НСтр("ru = 'В строке %1 повторно указана комбинация кода операции %2 и признака ""Несырьевые товары "" %3.'"),
					Индекс + 1,
					СтрокаКодыОпераций.КодОперации,
					СтрокаКодыОпераций.НесырьевыеТовары);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					Текст,
					,
					"КодыОперацийНалоговаяБаза["+Индекс+"].КодОперации",
					,
					Отказ);
			КонецЕсли;
			
			МассивКодовОпераций.Добавить(СтрокаКодыОпераций.КодОперации);
			
		КонецЦикла;
	
	#КонецОбласти
	
	Возврат Не Отказ;
	
КонецФункции

Функция ПриЗакрытииНаСервере()
	
	ТаблицаКодыОперацийНалоговаяБаза = Объект.КодыОперацийНалоговаяБаза.Выгрузить();
	АдресТаблицыКодыОперацийНалоговаяБаза = ПоместитьВоВременноеХранилище(ТаблицаКодыОперацийНалоговаяБаза, УникальныйИдентификатор);
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("КлючСтроки", КлючСтроки);
	СтруктураРезультат.Вставить("АдресТаблицыКодыОперацийНалоговаяБаза", АдресТаблицыКодыОперацийНалоговаяБаза);
	СтруктураРезультат.Вставить("ОтредактированыСтрокиКодовОпераций", ОтредактированыСтрокиКодовОпераций);
	
	Возврат СтруктураРезультат;
	
КонецФункции

&НаКлиенте
Процедура КодыОперацийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "КодыОперацийНалоговаяБазаКодОперации" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Заголовок",          "Выбор кода операции");
		ПараметрыФормы.Вставить("ТаблицаЗначений",    ТаблицаВыбора);
		ПараметрыФормы.Вставить("СтруктураДляПоиска", Новый Структура("Код", Элементы.КодыОперацийНалоговаяБаза.ТекущиеДанные.КодОперации));
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ВвестиКодОперацииНалоговаяБазаЗавершение", ЭтотОбъект, Элементы.КодыОперацийНалоговаяБаза.ТолькоПросмотр);
		
		ОткрытьФорму("Обработка.ОбщиеОбъектыРеглОтчетности.Форма.ФормаВыбораЗначенияИзТаблицы",
		ПараметрыФормы, ЭтотОбъект,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиКодОперацииНалоговаяБазаЗавершение(РезультатВыбора, ТолькоПросмотр) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ТолькоПросмотр Тогда
		Элементы.КодыОперацийНалоговаяБаза.ТекущиеДанные.КодОперации = РезультатВыбора.Код;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьПодвал(Форма)
	
	Объект = Форма.Объект;
	Форма.НадписьВсегоНалоговаяБаза = Объект.КодыОперацийНалоговаяБаза.Итог("ПродажиСНДС0");
	Форма.НадписьВсегоНДС = Объект.КодыОперацийНалоговаяБаза.Итог("СуммаНДС");
	
КонецПроцедуры

&НаКлиенте
Процедура КодыОперацийПриИзменении(Элемент)
	
	ОбновитьПодвал(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтредактированыСтрокиКодовОперацийПриИзменении(Элемент)
	
	Элементы.КодыОперацийНалоговаяБаза.ТолькоПросмотр = НЕ ОтредактированыСтрокиКодовОпераций;
	
	Если НЕ ОтредактированыСтрокиКодовОпераций Тогда
		
		Если Строка(Событие) = "Не подтверждена ставка 0%" Тогда
			
			Если ВерсияПостановления1137 >= 3 Тогда
				
				ПерерасчетКодовОпераций();
				
			КонецЕсли;
			
		Иначе
			
			Если ВерсияПостановления1137 >= 3 Тогда
				
				ПерерасчетКодовОпераций();
				
			Иначе
				
				Для Каждого СтрокаКодаОперации Из Объект.КодыОперацийНалоговаяБаза Цикл
					
					СтрокаКодаОперации.СуммаНДС = 0;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПерерасчетКодовОпераций()
	
	Данные = Новый Структура;
	Данные.Вставить("КлючСтроки", КлючСтроки);
	Данные.Вставить("ДокументОтгрузки", ДокументОтгрузки);
	Данные.Вставить("Покупатель", Покупатель);
	Данные.Вставить("Событие", Событие);
	Если ТипЗнч(ДокументОтгрузки) = Тип("ДокументСсылка.ДокументРасчетовСКонтрагентом") Тогда
		Данные.Вставить("ПродажиСНДС0", ДокументОтгрузкиПродажиСНДС0);
	КонецЕсли;
	
	ВладелецФормы.ПолучитьСуммыПоДокументуОтгрузки(Данные);
	
	Отбор = Новый Структура ("КлючСтроки", КлючСтроки);
	НайденныеСтроки = ВладелецФормы.Объект.КодыОперацийНалоговаяБаза.НайтиСтроки(Отбор);
	
	Если НайденныеСтроки.Количество() > 0 Тогда
		
		Объект.КодыОперацийНалоговаяБаза.Очистить();
			
		Для Каждого Строка Из НайденныеСтроки Цикл
			
			НоваяСтрока = Объект.КодыОперацийНалоговаяБаза.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			
		КонецЦикла;
		
	Иначе
		
		Объект.КодыОперацийНалоговаяБаза.Очистить();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
