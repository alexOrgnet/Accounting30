#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("Товары") Тогда
		
		Для Каждого Строка Из ДанныеЗаполнения.Товары Цикл
			
			НоваяСтрока = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Строка") Тогда
		
		QRКод = ДанныеЗаполнения;
		
		РеквизитыЧека = КассовыеЧекиQRКоды.РеквизитыЧека(QRКод);
		Если КассовыеЧекиQRКоды.РеквизитыЧекаЗаполнены(РеквизитыЧека) Тогда
			
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыЧека);
			СуммаДокумента = РеквизитыЧека.Сумма;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	РегистрыСведений.КассовыеЧекиПодотчетныхЛицЗапросыДетальныхДанных.ВключитьОбъект(ЭтотОбъект);
	РегистрыСведений.СканированиеКассовыхЧековПодотчетныхЛиц.ВключитьОбъект(ЭтотОбъект);
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
