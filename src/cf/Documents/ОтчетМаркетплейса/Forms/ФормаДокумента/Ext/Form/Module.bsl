
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Маркетплейс = Перечисления.ВидыМаркетплейсов.ЯндексМаркет Тогда
		ЭтотОбъект.Заголовок = НСтр("ru = 'Отчет о продажах Яндекс.Маркет '") + Формат(Объект.Дата, "ДФ='ММММ ггг'");
	КонецЕсли;
		
	ЗаполнитьДеревоДокументов();
	УстановитьУсловноеОформление();
	
	Если Объект.Ссылка.Пустая() Тогда
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Отчет о продажах маркетплейса создается автоматически, при загрузке. Ручное заполнение не предусмотрено.'"));
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
    Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
         МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
         МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
    КонецЕсли;
    // Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_РеализацияТоваровУслуг"
		ИЛИ ИмяСобытия = "Запись_ВозвратТоваровОтПокупателя"
		ИЛИ ИмяСобытия = "Запись_РеализацияОтгруженныхТоваров" Тогда
		
		ОбновитьДеревоДокументов();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДеревоДокументовПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	ТекущиеДанные = Элементы.ДеревоДокументов.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.Документ) Тогда
		ПараметрыФормы = Новый Структура("Ключ", ТекущиеДанные.Документ);
		Если ТипЗнч(ТекущиеДанные.Документ) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
			ОткрытьФорму("Документ.ВозвратТоваровОтПокупателя.ФормаОбъекта", ПараметрыФормы, , , , , );
		ИначеЕсли ТипЗнч(ТекущиеДанные.Документ) = Тип("ДокументСсылка.РеализацияОтгруженныхТоваров") Тогда
			ОткрытьФорму("Документ.РеализацияОтгруженныхТоваров.ФормаОбъекта", ПараметрыФормы, , , , , );
		ИначеЕсли ТипЗнч(ТекущиеДанные.Документ) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
			ОткрытьФорму("Документ.РеализацияТоваровУслуг.ФормаОбъекта", ПараметрыФормы, , , , , );
		КонецЕсли;		
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДеревоДокументов()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОтчетМаркетплейса", Объект.Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Документ,
	|	РеализацияТоваровУслуг.СуммаДокумента КАК Сумма,
	|	ВЫБОР КОГДА РеализацияТоваровУслуг.Проведен ТОГДА 2 ИНАЧЕ 1 КОНЕЦ КАК Картинка,
	|	""Товары, переданные в доставку"" КАК РазделОтчета,
	|	"""" КАК Отступ,
	|	1 КАК Порядок
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.ОтчетМаркетплейса = &ОтчетМаркетплейса
	|	И НЕ РеализацияТоваровУслуг.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеализацияОтгруженныхТоваров.Ссылка,
	|	РеализацияОтгруженныхТоваров.СуммаДокумента,
	|	ВЫБОР КОГДА РеализацияОтгруженныхТоваров.Проведен ТОГДА 2 ИНАЧЕ 1 КОНЕЦ,
	|	""Доставленные товары"",
	|	"""",
	|	2
	|ИЗ
	|	Документ.РеализацияОтгруженныхТоваров КАК РеализацияОтгруженныхТоваров
	|ГДЕ
	|	РеализацияОтгруженныхТоваров.ОтчетМаркетплейса = &ОтчетМаркетплейса
	|	И НЕ РеализацияОтгруженныхТоваров.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВозвратТоваровОтПокупателя.Ссылка,
	|	ВозвратТоваровОтПокупателя.СуммаДокумента,
	|	ВЫБОР КОГДА ВозвратТоваровОтПокупателя.Проведен ТОГДА 2 ИНАЧЕ 1 КОНЕЦ,
	|	""Невыкупленные товары"",
	|	"""",
	|	3
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|ГДЕ
	|	ВозвратТоваровОтПокупателя.ОтчетМаркетплейса = &ОтчетМаркетплейса
	|	И ВозвратТоваровОтПокупателя.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВозвратТоваровОтПокупателя.ОтгруженныеТовары)
	|	И НЕ ВозвратТоваровОтПокупателя.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВозвратТоваровОтПокупателя.Ссылка,
	|	ВозвратТоваровОтПокупателя.СуммаДокумента,
	|	ВЫБОР КОГДА ВозвратТоваровОтПокупателя.Проведен ТОГДА 2 ИНАЧЕ 1 КОНЕЦ,
	|	""Возвращённые товары"",
	|	"""",
	|	4
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|ГДЕ
	|	ВозвратТоваровОтПокупателя.ОтчетМаркетплейса = &ОтчетМаркетплейса
	|	И ВозвратТоваровОтПокупателя.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВозвратТоваровОтПокупателя.Товары)
	|	И НЕ ВозвратТоваровОтПокупателя.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок
	|ИТОГИ
	|	СУММА(Сумма)
	|ПО
	|	РазделОтчета";
	
	Дерево = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	ЗначениеВРеквизитФормы(Дерево, "ДеревоДокументов");
		
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	//Оформление группы
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДокументовДокумент");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ДеревоДокументов.Документ", ВидСравненияКомпоновкиДанных.НеЗаполнено, "");
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", Новый ПолеКомпоновкиДанных("ДеревоДокументов.РазделОтчета"));
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДокументовДокумент");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДокументовСумма");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДокументовОтступ");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ДеревоДокументов.Документ", ВидСравненияКомпоновкиДанных.НеЗаполнено, "");
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЦветаСтиля.ИтогиФонГруппы);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(,,Истина,Ложь));
	
	//Оформление элемента
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДокументовДокумент");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ДеревоДокументов.Документ", ВидСравненияКомпоновкиДанных.Заполнено, "");
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветГиперссылки);
		
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДеревоДокументов()
	
	ЗаполнитьДеревоДокументов();
	Для Каждого СтрокаДерева Из ДеревоДокументов.ПолучитьЭлементы() Цикл 
		Элементы.ДеревоДокументов.Развернуть(СтрокаДерева.ПолучитьИдентификатор() ,Истина);
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти
