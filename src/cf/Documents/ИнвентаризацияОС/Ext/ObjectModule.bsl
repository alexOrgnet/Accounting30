#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	Если ДанныеЗаполнения <> Неопределено
			И ТипДанныхЗаполнения <> Тип("Структура")
			И Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Метаданные()) Тогда
		ЗаполнитьПоДокументуОснованию(ДанныеЗаполнения);
	КонецЕсли;
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);

	Если ТипДанныхЗаполнения = Тип("Структура") 
		И ДанныеЗаполнения.Свойство("СписокОС") Тогда
		
		ЗаполнитьИнвентаризациюСОтборомПоОС(ДанныеЗаполнения);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	УправлениеВнеоборотнымиАктивами.ПроверитьОтсутствиеДублейВТабличнойЧасти(ЭтотОбъект, "ОС", Новый Структура("ОсновноеСредство"), Отказ);
	ПроверитьЗаполнениеНаличияВСпискеОС(Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	
	ПараметрыПроведения = Документы.ИнвентаризацияОС.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПрослеживаемостьБП.ЗарегистрироватьПрослеживаемыйОС(ПараметрыПроведения.Реквизиты);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();
	
	ПрослеживаемостьБП.УдалениеПроведенияПервичногоДокумента(Ссылка);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь();
	
	Для каждого ОсновноеСредство Из ОС Цикл
		
		ОсновноеСредство.СтоимостьПоДаннымУчета = 0;
		ОсновноеСредство.НаличиеПоДаннымУчета = Ложь;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПоДокументуОснованию(Основание)

	// Заполним реквизиты из стандартного набора по документу основанию.
	ЗаполнениеДокументов.ЗаполнитьПоОснованию(ЭтотОбъект, Основание);

КонецПроцедуры

Процедура ПроверитьЗаполнениеНаличияВСпискеОС(Отказ)
	
	НомераНекорректныхСтрок = Новый Массив;
	 
	Для каждого СтрокаТЧ Из ЭтотОбъект.ОС Цикл
		Если ПолучитьФункциональнуюОпцию("ВедетсяУчетГрупповыхОС") Тогда
			Если СтрокаТЧ.КоличествоПоДаннымУчета = 0 И СтрокаТЧ.КоличествоФактическое = 0 Тогда
				НомераНекорректныхСтрок.Добавить(СтрокаТЧ.НомерСтроки);
			КонецЕсли;
		Иначе
			Если СтрокаТЧ.НаличиеПоДаннымУчета = Ложь И СтрокаТЧ.НаличиеФактическое = Ложь Тогда
				НомераНекорректныхСтрок.Добавить(СтрокаТЧ.НомерСтроки);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если НомераНекорректныхСтрок.Количество() > 0 Тогда	
		Для каждого НомерСтроки из НомераНекорректныхСтрок Цикл
			ТекстСообщения = НСтр("ru = 'Не указана информация о наличии основного средства в строке %1 списка ""ОсновныеСредства"".
									|Укажите наличие по данным учета или фактическое.'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, НомерСтроки);
			Если ПолучитьФункциональнуюОпцию("ВедетсяУчетГрупповыхОС") Тогда
				Поле = "ОС[" + Формат(НомерСтроки - 1, "ЧН=0; ЧГ=") + "].КоличествоПоДаннымУчета";	
			Иначе
				Поле = "ОС[" + Формат(НомерСтроки - 1, "ЧН=0; ЧГ=") + "].НаличиеПоДаннымУчета";
			КонецЕсли;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле);
		КонецЦикла;	
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьИнвентаризациюСОтборомПоОС(ДанныеЗаполнения)
	
	АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено);
	ПараметрыЗаполнения = Новый Структура();
	
	ЭтотОбъект.Дата = ДанныеЗаполнения.Дата;
	
	ТаблицаОС = Документы.ИнвентаризацияОС.ОстаткиОС(ЭтотОбъект, ДанныеЗаполнения.СписокОС);
	
	Для Каждого СтрокаОС Из ТаблицаОС Цикл
		
		НоваяСтрока = ЭтотОбъект.ОС.Добавить();
		НоваяСтрока.ОсновноеСредство        = СтрокаОС.ОсновноеСредство;
		НоваяСтрока.СтоимостьПоДаннымУчета  = СтрокаОС.ВосстановительнаяСтоимость;
		
		Если НоваяСтрока.СтоимостьПоДаннымУчета <> Неопределено Тогда
			НоваяСтрока.СтоимостьПоРезультатамИнвентаризации = НоваяСтрока.СтоимостьПоДаннымУчета;
		КонецЕсли;
		
		НоваяСтрока.НаличиеПоДаннымУчета    = Истина;
		НоваяСтрока.СтоимостьФактическая    = НоваяСтрока.СтоимостьПоДаннымУчета;
		НоваяСтрока.НаличиеФактическое      = Истина;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли