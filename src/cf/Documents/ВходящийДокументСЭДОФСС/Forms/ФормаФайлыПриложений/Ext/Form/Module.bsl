
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Основание") Тогда
		ДокументСсылка = Параметры.Основание;
		
		Если ЗначениеЗаполнено(ДокументСсылка) Тогда
			Для каждого Файл из ДокументСсылка.ФайлыПриложений Цикл
				Если НЕ СтрНачинаетсяС(Файл.ИмяФайла, "Печатная форма")
					И НЕ Файл.ВидФайла = ДокументСсылка.ВидДокументаФСС Тогда
					НовыйФайл						= Файлы.Добавить();
					НовыйФайл.ИмяФайлаПриложения	= Файл.ИмяФайла;
					НовыйФайл.РазмерФайлаПриложения	= Строка(Окр(Файл.ФайлПриложения.Размер / 1024, 2)) + " (Кб)";
					НовыйФайл.ФайлПриложенияСсылка	= Файл.ФайлПриложения
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ФайлыИмяФайлаПриложения" Тогда
		ТекущиеДанные = Элементы.Файлы.ТекущиеДанные;
		
		Если ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Если НЕ ТекущиеДанные.ФайлПриложенияСсылка.Пустая() Тогда
			ДанныеФайла = ДанныеФайла(ТекущиеДанные.ФайлПриложенияСсылка, ЭтотОбъект.УникальныйИдентификатор);
			
			ОписаниеОшибки = НСтр("ru = 'Открыть файл требования невозможно. С сервера СФР получен некорректный файл pdf.'");
			
			Если ПроверитьКорректностьФайлаPDF(ДанныеФайла) Тогда
				РаботаСФайламиКлиент.ОткрытьФайл(ДанныеФайла, Ложь);
			Иначе
				ПоказатьОшибку(ОписаниеОшибки);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ДанныеФайла(СсылкаНаФайл, Идентификатор)
	
	ПараметрыФайла = РаботаСФайламиКлиентСервер.ПараметрыДанныхФайла();
	ПараметрыФайла.ИдентификаторФормы = Идентификатор;
	Возврат РаботаСФайлами.ДанныеФайла(СсылкаНаФайл, ПараметрыФайла);
	
КонецФункции

&НаКлиенте
Функция ПроверитьКорректностьФайлаPDF(ДанныеФайла)
	
	ПроверкаФайлаПройдена = Истина;
	
	//необходимо проверить, что вместо файла не пришла ошибка от сервера СФР
	//пример ошибки:
	//{
	//  "msg": "502 Bad Gateway",
	//  "type": "org.springframework.web.client.HttpServerErrorException$BadGateway",
	//  "reference": 1709184112514,
	//  "errorCode": null,
	//  "errorMessage": null,
	//  "procedureCallInfo": null,
	//  "newId": null,
	//  "success": false
	//}
	ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(ДанныеФайла.СсылкаНаДвоичныеДанныеФайла);
	
	Поток = ДвоичныеДанныеФайла.ОткрытьПотокДляЧтения();
	ЧтениеPDF = Новый ЧтениеPDF();
	Попытка
		ЧтениеPDF.Открыть(Поток);
		СписокВложений = ЧтениеPDF.ПолучитьВложения();
	Исключение
		ЧтениеPDF.Закрыть();
		ПроверкаФайлаПройдена = Ложь;
	КонецПопытки;
	
	Поток.Закрыть();
	
	Возврат ПроверкаФайлаПройдена;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьОшибку(ОписаниеОшибки)
	
	ДлительнаяОтправкаКлиентСервер.ВывестиОшибку(ОписаниеОшибки);
	
	// Записываем ошибку в журнал регистрации.
	ЖурналРегистрацииКлиент.ДобавитьСообщениеДляЖурналаРегистрации(
		НСтр("ru = 'Электронный документооборот с контролирующими органами. Открытие файла вложения требования СФР'"),
		"Ошибка",
		ОписаниеОшибки,,
		Истина);
		
КонецПроцедуры

#КонецОбласти
