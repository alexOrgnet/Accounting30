#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ДокументОснование = Неопределено;
	
	Если ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		
		ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
		Если ТипДанныхЗаполнения <> Тип("Структура")
			И Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Метаданные()) Тогда
			ДокументОснование = ДанныеЗаполнения;
		ИначеЕсли ТипДанныхЗаполнения = Тип("Структура")
			И ДанныеЗаполнения.Свойство("Основание")
			И Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Основание.Метаданные()) Тогда
			ДокументОснование = ДанныеЗаполнения.Основание;
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнитьПоПоступлениюВАренду(ДокументОснование);
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
	Если ЗначениеЗаполнено(ДокументОснование) Тогда
		Возврат;
	КонецЕсли;
	
	ПрименяетсяФСБУ25 = УчетнаяПолитика.ПрименяетсяФСБУ25(Организация, Дата);
	
	Если Не ЗначениеЗаполнено(ВидОперации) Тогда
		Если Не ПрименяетсяФСБУ25 Тогда
			ВидОперации = Перечисления.ВидыОперацийИзменениеУсловийАренды.ИзменениеУсловийЛизинга;
		Иначе
			ВидОперации = ?(ПолучитьФункциональнуюОпцию("ВедетсяУчетЛизинговогоИмущества"),
				Перечисления.ВидыОперацийИзменениеУсловийАренды.ИзменениеУсловийЛизинга,
				Перечисления.ВидыОперацийИзменениеУсловийАренды.ИзменениеУсловийАренды);
		КонецЕсли;
	КонецЕсли;
	
	Если Не ПрименяетсяФСБУ25
		Или Не УчетнаяПолитика.ПоддерживаетсяДисконтированиеОбязательств()
		Или Не УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Организация, Дата) Тогда
		СпособОценкиАрендыБУ = Перечисления.СпособыОценкиБУ.РавнаСуммеДоговора;
	Иначе
		СпособОценкиАрендыБУ = Перечисления.СпособыОценкиБУ.РассчитываетсяПоСтавке;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ДоговорКонтрагента, "Валютный,РасчетыВУсловныхЕдиницах");
	Иначе
		РеквизитыДоговора = Новый Структура("Валютный,РасчетыВУсловныхЕдиницах", Ложь, Ложь);
	КонецЕсли;
	Если РеквизитыДоговора.РасчетыВУсловныхЕдиницах Тогда
		СчетУчетаРасчетовСКонтрагентом = ПланыСчетов.Хозрасчетный.АрендныеОбязательстваУЕ;
	ИначеЕсли РеквизитыДоговора.Валютный Тогда
		СчетУчетаРасчетовСКонтрагентом = ПланыСчетов.Хозрасчетный.АрендныеОбязательстваВал;
	Иначе
		СчетУчетаРасчетовСКонтрагентом = ПланыСчетов.Хозрасчетный.АрендныеОбязательства;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь();
	
	СтруктураКурсаВзаиморасчетов = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаДокумента, Дата);
	
	КурсВзаиморасчетов      = СтруктураКурсаВзаиморасчетов.Курс;
	КратностьВзаиморасчетов = СтруктураКурсаВзаиморасчетов.Кратность;
	
	ЗаполнениеДокументов.ПриКопировании(ЭтотОбъект, ОбъектКопирования);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	МассивНепроверяемыхРеквизитов = Новый Массив();
	
	Если НЕ ПолучитьФункциональнуюОпцию("ВестиУчетПоДоговорам") Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ДоговорКонтрагента");
	КонецЕсли;
	
	Если СпособОценкиАрендыБУ = Перечисления.СпособыОценкиБУ.РавнаСуммеДоговора Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СтавкаДисконтирования");
		МассивНепроверяемыхРеквизитов.Добавить("ПредметыАренды.ОценкаБУ");
			МассивНепроверяемыхРеквизитов.Добавить("ГрафикПлатежей");
			МассивНепроверяемыхРеквизитов.Добавить("ГрафикПлатежей.ДатаПлатежа");
			МассивНепроверяемыхРеквизитов.Добавить("ГрафикПлатежей.СуммаПлатежа");
	ИначеЕсли СпособОценкиАрендыБУ = Перечисления.СпособыОценкиБУ.РассчитываетсяПоСтавке Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ПредметыАренды.ОценкаБУ");
	ИначеЕсли СпособОценкиАрендыБУ = Перечисления.СпособыОценкиБУ.УказываетсяВручную Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СтавкаДисконтирования");
	КонецЕсли;
	
	УправлениеВнеоборотнымиАктивами.ПроверитьОтсутствиеДублейВТабличнойЧасти(
		ЭтотОбъект, "ПредметыАренды", Новый Структура("ПредметАренды"), Отказ);
	
	МассивНепроверяемыхРеквизитов.Добавить("ПредметыАренды.СчетУчетаНДС");
	Для Каждого СтрокаТаблицы Из ПредметыАренды Цикл 
		Префикс = "ПредметыАренды[%1].";
		Префикс = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Префикс, Формат(СтрокаТаблицы.НомерСтроки - 1, "ЧН=0; ЧГ="));
		ИмяСписка = НСтр("ru = 'Предметы аренды'");
		
		Если СтрокаТаблицы.СуммаНДС <> 0 
			И НЕ ЗначениеЗаполнено(СтрокаТаблицы.СчетУчетаНДС) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка",, НСтр("ru = 'Счет НДС'"),
				СтрокаТаблицы.НомерСтроки, ИмяСписка);
			Поле = Префикс + "СчетУчетаНДС";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
		КонецЕсли;
	КонецЦикла;
	
	ПроверятьОценкуБУ = МассивНепроверяемыхРеквизитов.Найти("ПредметыАренды.ОценкаБУ") = Неопределено;
	Если ПроверятьОценкуБУ Тогда
		Для каждого СтрокаТаблицы Из ПредметыАренды Цикл
			
			Префикс = "ПредметыАренды[%1].";
			Префикс = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				Префикс, Формат(СтрокаТаблицы.НомерСтроки - 1, "ЧН=0; ЧГ="));
			ИмяСписка = НСтр("ru = 'Предметы аренды'");
			
			Если СуммаВключаетНДС И Не НДСВключенВСтоимость Тогда
				СуммаДоговора = СтрокаТаблицы.Сумма - СтрокаТаблицы.СуммаНДС;
			ИначеЕсли Не СуммаВключаетНДС И НДСВключенВСтоимость Тогда
				СуммаДоговора = СтрокаТаблицы.Сумма + СтрокаТаблицы.СуммаНДС;
			Иначе
				СуммаДоговора = СтрокаТаблицы.Сумма;
			КонецЕсли;
			
			Если СуммаДоговора <= СтрокаТаблицы.ОценкаБУ Тогда
				Поле = Префикс + "ОценкаБУ";
				ИмяПоля = НСтр("ru = 'Оценка в БУ'");
				ТекстОшибки = НСтр("ru='Оценка в БУ должна быть меньше суммы остатка по договору.'");
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
					"Колонка", "Корректность", ИмяПоля, СтрокаТаблицы.НомерСтроки, ИмяСписка, ТекстОшибки);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаОкончанияАренды) И НачалоМесяца(ДатаОкончанияАренды) <= НачалоМесяца(Дата) Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru='Дата окончания аренды должна быть не ранее %1.'"),
			Формат(КонецМесяца(Дата) + 1, "ДЛФ=D"));
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ДатаОкончанияАренды", "Объект", Отказ);
	КонецЕсли;
	
	Если СпособОценкиАрендыБУ <> Перечисления.СпособыОценкиБУ.РавнаСуммеДоговора Тогда
		ИтогСуммаПлатежа = ГрафикПлатежей.Итог("СуммаПлатежа");
		ИтогСуммаДокумента = УчетНДСПереопределяемый.ПолучитьСуммуДокументаСНДС(ЭтотОбъект);
		Если ИтогСуммаПлатежа <> 0 И ИтогСуммаДокумента <> 0 И ИтогСуммаПлатежа <> ИтогСуммаДокумента Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru='Сумма по графику платежей (%1) отличается от суммы документа (%2).'"),
				Формат(ИтогСуммаПлатежа, "ЧДЦ=2"), Формат(ИтогСуммаДокумента, "ЧДЦ=2"));
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, , "Объект", Отказ);
		КонецЕсли;
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	// При групповом перепроведении реквизиты документов не меняются,
	// поэтому обновление связанных данных выполнять не требуется.
	Если ПроведениеСервер.ГрупповоеПерепроведение(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	РаботаСДоговорамиКонтрагентовБП.ЗаполнитьДоговорПередЗаписью(ЭтотОбъект);
	
	// Посчитать суммы документа и записать ее в соответствующий реквизит шапки для показа в журналах
	СуммаДокумента = УчетНДСПереопределяемый.ПолучитьСуммуДокументаСНДС(ЭтотОбъект);
	
	Если Не УчетнаяПолитика.ПоддерживаетсяДисконтированиеОбязательств() Тогда
		СпособОценкиАрендыБУ = Перечисления.СпособыОценкиБУ.РавнаСуммеДоговора;
	КонецЕсли;
	
	УчетПроцентовПоОбязательствам.ЗаполнитьДанныеДисконтированияОбязательстваПоАренде(ЭтотОбъект, Отказ, РежимЗаписи, Истина);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПроведения = Документы.ИзменениеУсловийАренды.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ИНФОРМАЦИОННОЙ БАЗЫ (АНАЛИЗ ОСТАТКОВ И Т.П.)
	
	ТаблицаРеквизиты = ПараметрыПроведения.Реквизиты;
	ТаблицаПредметыАренды = ПараметрыПроведения.ТаблицаПредметыАренды;
	
	УчетОС.ПроверитьСостояниеОСПринятоКУчету(
		ТаблицаПредметыАренды, ТаблицаРеквизиты, Отказ);
	
	УчетОС.ПроверитьДоговорПредметовАренды(
		ТаблицаПредметыАренды, ТаблицаРеквизиты, Отказ);
	
	ТаблицаНовыйГрафик = Документы.ИзменениеУсловийАренды.НовыйГрафикПроцентовПоАренде(
		ПараметрыПроведения.ТаблицаГрафикПлатежей, ТаблицаПредметыАренды, ТаблицаРеквизиты);
		
	ПараметрыНачисленияАмортизации = УчетОС.ПодготовитьТаблицыАмортизацииОСИСуммАмортизационнойПремии(
		ТаблицаПредметыАренды, ТаблицаРеквизиты, Отказ);
		
	ТаблицыДанныеДвижений = Документы.ИзменениеУсловийАренды.ТаблицыДанныеДвижений(
		ТаблицаПредметыАренды, ПараметрыНачисленияАмортизации, ТаблицаРеквизиты);
	ТаблицаДанныеПроводок = ТаблицыДанныеДвижений.ДанныеПроводок;
	ТаблицаПараметрыАмортизации = ТаблицыДанныеДвижений.ПараметрыАмортизации;
	ТаблицаДанныеПредметовАренды = ТаблицыДанныеДвижений.ДанныеПредметовАренды;
	
	Документы.ИзменениеУсловийАренды.ДобавитьКолонкуСодержание(ТаблицаДанныеПроводок, ТаблицаРеквизиты);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// ФОРМИРОВАНИЕ ДВИЖЕНИЙ
	
	УчетОС.СформироватьДвиженияНачислениеАмортизацииИАмортизационнойПремии(
		ПараметрыНачисленияАмортизации, Движения, Отказ);
	
	// Изменение стоимости предметов аренды - проводки
	УчетОС.СформироватьДвиженияИзменениеУсловийПредметыАренды(
		ТаблицаДанныеПроводок, ТаблицаРеквизиты, Движения, Отказ);
	
	УчетОС.СформироватьДвиженияДанныеОПоступленииПредметыАренды(
		ТаблицаДанныеПредметовАренды, ТаблицаРеквизиты, Движения, Отказ);
		
	// Изменение процентов по аренде - проводки
	УчетПроцентовПоОбязательствам.СформироватьДвиженияИзменениеУсловийПроцентыПоАренде(
		ТаблицаДанныеПроводок, ТаблицаРеквизиты, Движения, Отказ);
		
	// Новый график процентных расходов
	УчетПроцентовПоОбязательствам.СформироватьДвиженияГрафикиПроцентныхРасходов(
		ТаблицаНовыйГрафик, ТаблицаРеквизиты, Движения, Отказ);
	
	// Учет НДС - проводки
	УчетНДС.СформироватьДвиженияИзменениеУсловийАренды(
		ТаблицаДанныеПроводок, ТаблицаРеквизиты, Движения, Отказ);
	
	УчетОС.СформироватьДвиженияИзмененияПараметровАмортизацииОСБУ(
		ТаблицаПараметрыАмортизации, ТаблицаРеквизиты, Движения, Отказ);
	
	// Переоценка валютных остатков - после формирования проводок всеми другими механизмами
	
	ТаблицаПереоценка = УчетДоходовРасходов.ПодготовитьТаблицуПереоценкаВалютныхОстатковПоПроводкамДокумента(
		ПараметрыПроведения.ПереоценкаВалютныхОстатковРеквизиты, Движения, Отказ);
	
	УчетДоходовРасходов.СформироватьДвиженияПереоценкаВалютныхОстатков(ТаблицаПереоценка,
		ПараметрыПроведения.ПереоценкаВалютныхОстатковРеквизиты, Движения, Отказ);
	
	УчетДоходовРасходов.СформироватьДвиженияРасчетПереоценкиВалютныхСредств(ТаблицаПереоценка,
		ПараметрыПроведения.ПереоценкаВалютныхОстатковРеквизиты, Движения, Отказ);
	
	УчетУСН.СформироватьДвиженияПереоценкаВалютныхОстатков(ТаблицаПереоценка,
		ПараметрыПроведения.ПереоценкаВалютныхОстатковРеквизиты, Движения, Отказ);
	
	Движения.Хозрасчетный.ЗаполнитьСуммыВременныхРазниц();
	
	ПроведениеСервер.УстановитьЗаписьОчищаемыхНаборовЗаписей(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)

	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПоПоступлениюВАренду(Основание)
	
	Если Не ЗначениеЗаполнено(Основание)
		Или ТипЗнч(Основание) <> Тип("ДокументСсылка.ПоступлениеВАренду") Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание, "ЛизингДоФСБУ25") Тогда
		ТекстОшибки = НСтр("ru='""Изменение условий аренды (лизинга)"" нельзя создать на основании старых документов ""Поступление в лизинг"".'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	ЗаполнениеДокументов.ЗаполнитьПоОснованию(ЭтотОбъект, Основание);
	
	ДанныеОснования = Документы.ИзменениеУсловийАренды.ДанныеПоступления(Основание, Дата);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеОснования);
	ПредметыАренды.Загрузить(ДанныеОснования.ПредметыАренды);
	ГрафикПлатежей.Загрузить(ДанныеОснования.ГрафикПлатежей);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли