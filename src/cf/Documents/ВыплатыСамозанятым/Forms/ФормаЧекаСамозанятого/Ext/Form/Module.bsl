#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ПравоДоступа("Изменение", Метаданные.Документы.ВыплатыСамозанятым) Тогда
		ТолькоПросмотр = Истина;
		Элементы.ГруппаОчиститьЗагрузить.Видимость = Ложь;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "НомерЧека, СсылкаНаЧек, ЧекСамозанятого, РеестрВыплатСамозанятым");
	
	ПредыдущееЗначениеСсылкиНаЧек = СсылкаНаЧек;
	
	ВывестиКартинку(АдресКартинки, ЧекСамозанятого);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СсылкаНаЧекПриИзменении(Элемент)
	
	Если СсылкаНаЧек = ПредыдущееЗначениеСсылкиНаЧек Тогда
		Возврат;
	КонецЕсли;
	
	ПредыдущееЗначениеСсылкиНаЧек = СсылкаНаЧек;
	
	Если СтрНайти(СсылкаНаЧек, ВыплатыСамозанятымВызовСервераПовтИсп.АдресСервисаФНС()) = 0 Тогда
		// Это не ссылка на чек, поэтому ничего не делаем
		Возврат;
	КонецЕсли;
	
	ПолучитьДанныеПоСсылкеНаЧек();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура АдресКартинкиНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если РеестрВыплатСамозанятым.Пустая() Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru='Для загрузки чека запишите документ ""Выплаты самозанятым""'"));
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(АдресКартинки) Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ДобавлениеЧекаСамозанятогоЗавершение", ЭтотОбъект);
		РаботаСФайламиКлиент.ДобавитьФайл(ОписаниеОповещения, РеестрВыплатСамозанятым, ЭтотОбъект, 2);
	Иначе
		ДанныеФайла = ДанныеФайла(ЧекСамозанятого, УникальныйИдентификатор);
		РаботаСФайламиКлиент.ОткрытьФайл(ДанныеФайла);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьЧекНажатие(Элемент)
	
	СсылкаНаЧек = "";
	НомерЧека = "";
	АдресКартинки = "";
	ПредыдущееЗначениеСсылкиНаЧек = "";
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьЧекНажатие(Элемент)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ДобавлениеЧекаСамозанятогоЗавершение", ЭтотОбъект);
	РаботаСФайламиКлиент.ДобавитьФайл(ОписаниеОповещения, РеестрВыплатСамозанятым, ЭтотОбъект, 2);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПерейтиПоСсылке(Команда)
	Если ЗначениеЗаполнено(СсылкаНаЧек) Тогда
		ПерейтиПоНавигационнойСсылке(СсылкаНаЧек);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Структура = Новый Структура;
	Структура.Вставить("СсылкаНаЧек", СсылкаНаЧек);
	Структура.Вставить("НомерЧека", НомерЧека);
	Если Не ЗначениеЗаполнено(АдресКартинки) Тогда
		Структура.Вставить("ЧекСамозанятого", ПредопределенноеЗначение("Справочник.ВыплатыСамозанятымПрисоединенныеФайлы.ПустаяСсылка"));
	Иначе
		Структура.Вставить("ЧекСамозанятого", ЧекСамозанятого);
	КонецЕсли;
		
	Закрыть(Структура);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ДобавлениеЧекаСамозанятогоЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЧекСамозанятого = Результат.ФайлСсылка;
	ВывестиКартинку(АдресКартинки, ЧекСамозанятого);

	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВывестиКартинку(АдресКартинки, Знач ЧекСамозанятого)
	
	Если ЗначениеЗаполнено(ЧекСамозанятого) Тогда
		АдресКартинки = РаботаСФайлами.ДанныеФайла(ЧекСамозанятого).СсылкаНаДвоичныеДанныеФайла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеФайла(ФайлКартинки, УникальныйИдентификатор)
	
	ДополнительныеПараметры = РаботаСФайламиКлиентСервер.ПараметрыДанныхФайла();
	ДополнительныеПараметры.ИдентификаторФормы = УникальныйИдентификатор;
	
	Возврат РаботаСФайлами.ДанныеФайла(ФайлКартинки, ДополнительныеПараметры);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Элементы.ЗагрузитьЧек.Видимость = Не ЗначениеЗаполнено(Форма.АдресКартинки);
	Элементы.ОчиститьЧек.Видимость = ЗначениеЗаполнено(Форма.АдресКартинки);
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьДанныеПоСсылкеНаЧек()
	
	НомерЧека = ВыплатыСамозанятым.НомерЧекаИзСсылки(СсылкаНаЧек);
	ЧекСамозанятого = ВыплатыСамозанятым.ЧекСамозанятогоИзИнтернета(СсылкаНаЧек, ЧекСамозанятого, РеестрВыплатСамозанятым);
	
	Если ЗначениеЗаполнено(ЧекСамозанятого) Тогда
		ВывестиКартинку(АдресКартинки, ЧекСамозанятого);
	Иначе
		ТекстСообщения = НСтр("ru='Не удалось скачать чек с сайта ФНС. 
			|Проверьте ссылку или попробуйте загрузить чек позднее'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "СсылкаНаЧек");
	КонецЕсли;
	
	СтатистикаПоПоказателямКлиентСервер.ДобавитьСобытие("ВыплатыСамозанятым.ЗагрузкаЧеков.ПоСсылке");
	
КонецПроцедуры

#КонецОбласти