#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура")
		ИЛИ ДанныеЗаполнения = Неопределено Тогда
		
		ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
		
	Иначе
		
		Документы.УведомлениеОбОстаткахПрослеживаемыхТоваров.ЗаполнитьКорректировочноеУведомлениеОбОстаткахПрослеживаемыхТоваров(
			ЭтотОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
		
		
	КонецЕсли;
	
	ПрослеживаемостьПереопределяемый.ОбработкаЗаполненияДокумента(
		ЭтотОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);

	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПрослеживаемостьПереопределяемый.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПроведения = Документы.УведомлениеОбОстаткахПрослеживаемыхТоваров.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПрослеживаемостьБРУ.ОбработкаПроведенияУведомление(ПараметрыПроведения, Движения, Отказ);
	
	ПроведениеСервер.УстановитьЗаписьОчищаемыхНаборовЗаписей(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();
	
	ПрослеживаемостьБРУ.ОбработкаУдаленияПроведенияУведомление(Ссылка, ДополнительныеСвойства.Основание);
		
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтотОбъект.НомерКорректировки = 0
		И ЭтотОбъект.ПервичныйДокумент = Неопределено Тогда
		
		ЭтотОбъект.ПервичныйДокумент = Ссылка;
		
		Если ЭтоНовый() Тогда
			СсылкаНового = Документы.УведомлениеОбОстаткахПрослеживаемыхТоваров.ПолучитьСсылку();
			УстановитьСсылкуНового(СсылкаНового);
			ЭтотОбъект.ПервичныйДокумент = СсылкаНового;
		КонецЕсли;
		
	КонецЕсли;	
	
	ПрослеживаемостьБРУ.ПроверитьДублированиеНомераКорректировкиВУведомлениях(ЭтотОбъект);
	
	ДополнительныеСвойства.Вставить("Основание", 
		?(Ссылка.НомерКорректировки = 0, Ссылка.ПервичныйДокумент, Ссылка.ОснованиеКорректировки));
	
	ПрослеживаемостьПереопределяемый.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	МассивНепроверяемыхРеквизитов.Добавить("Товары.ОсновноеСредство");
		
	Если ЭтотОбъект.ВидОперации = Перечисления.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.ОсновныеСредства
		ИЛИ
		ЭтотОбъект.ВидОперации = Перечисления.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.КомплектующиеОС Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Номенклатура");
		
	КонецЕсли;
	
	Если ЭтотОбъект.НомерКорректировки = 0 Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("КодТНВЭДПослеИзменения");
		МассивНепроверяемыхРеквизитов.Добавить("КодОКПД2ПослеИзменения");
		МассивНепроверяемыхРеквизитов.Добавить("ЕдиницаИзмеренияПослеИзменения");
		МассивНепроверяемыхРеквизитов.Добавить("ЕдиницаПрослеживаемостиПослеИзменения");
		МассивНепроверяемыхРеквизитов.Добавить("ДатаСоздания");
		МассивНепроверяемыхРеквизитов.Добавить("Товары.СтранаПроисхожденияПослеИзменения");
		МассивНепроверяемыхРеквизитов.Добавить("ПервичныйДокумент");
		
	Иначе
		
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Количество");
		МассивНепроверяемыхРеквизитов.Добавить("Товары.КоличествоПрослеживаемости");
		
	КонецЕсли;
	
	ПрослеживаемостьПереопределяемый.ОбработкаПроверкиЗаполнения(
		ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
		
	Для каждого ТекущаяСтрокаТовары Из Товары Цикл
	
		Если ЗначениеЗаполнено(РНПТ) И НЕ ЗначениеЗаполнено(ТекущаяСтрокаТовары.СтранаПроисхождения) Тогда
			
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"Поле", "Заполнение", НСтр("ru = 'Страна происхождения'"),,,);
			
			ПутьКДанным = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
			"Объект.Товары", ТекущаяСтрокаТовары.НомерСтроки, "СтранаПроисхождения");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, ПутьКДанным,, Отказ);
				
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ ЗначениеЗаполнено(ПервичныйДокумент) И НЕ ЗначениеЗаполнено(НаименованиеПервичногоДокумента) Тогда
		
		ТекстСообщения = НСтр("ru = 'Не выбран первичный документ'");
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"НадписьПервичногоДокумента",, Отказ);
		
	КонецЕсли;
		
	// Удаляем из проверяемых реквизитов все, по которым автоматическая проверка не нужна:
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
		
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ПервичныйДокумент = Неопределено И ЗначениеЗаполнено(НаименованиеПервичногоДокумента) Тогда
		
		ПервичныйДокумент = Ссылка;
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли


