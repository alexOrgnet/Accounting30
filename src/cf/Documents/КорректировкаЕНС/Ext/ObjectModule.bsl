#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	НачалоПростогоУчета = ЕдиныйНалоговыйСчет.НачалоПростогоУчета();
	Если Дата < НачалоПростогоУчета Тогда
		 Дата = НачалоПростогоУчета;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	Если Не (ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.НачислениеНалога
		Или ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.ВозмещениеНалога) Тогда
		НепроверяемыеРеквизиты.Добавить("Налог");
		НепроверяемыеРеквизиты.Добавить("ВидПлатежаВГосБюджет");
	КонецЕсли;
	
	Если Не (ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.ПереводСредствТретьемуЛицу
		Или ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.ПоступлениеСредствОтТретьегоЛица) Тогда
		НепроверяемыеРеквизиты.Добавить("Контрагент");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПроведениеСервер.ГрупповоеПерепроведение(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	СвойстваСчета = Документы.КорректировкаЕНС.СвойстваСчета(СчетУчета);
	
	Если ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.НачислениеНалога 
		Или ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.ВозмещениеНалога Тогда
		Если Не СвойстваСчета.ЕстьУровниБюджетов Тогда
			УровеньБюджета = Неопределено;
		КонецЕсли;
		Если Не СвойстваСчета.ЕстьРегистрацияВНалоговомОргане Тогда
			РегистрацияВНалоговомОргане = Неопределено;
		КонецЕсли;
	Иначе
		Налог = Неопределено;
		ВидПлатежаВГосБюджет = Неопределено;
		УровеньБюджета = Неопределено;
		РегистрацияВНалоговомОргане = Неопределено;
	КонецЕсли;
	
	Если Не (ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.ПоступлениеСредствОтТретьегоЛица 
		Или ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.ПереводСредствТретьемуЛицу) Тогда
		Контрагент = Неопределено;
	КонецЕсли;
	
	Если Не (ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.ПрочееУвеличениеСальдо
		Или ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.ПрочееУменьшениеСальдо) Тогда
		Субконто1 = Неопределено;
		Субконто2 = Неопределено;
		Субконто3 = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	
	ПараметрыПроведения = Документы.КорректировкаЕНС.ПодготовитьПараметрыПроведения(
		Ссылка,
		Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// ФОРМИРОВАНИЕ ДВИЖЕНИЙ
	
	Документы.КорректировкаЕНС.СформироватьПроводки(ПараметрыПроведения.Реквизиты, Движения, Отказ);
	
	ПроведениеСервер.УстановитьЗаписьОчищаемыхНаборовЗаписей(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	НачалоПростогоУчета = ЕдиныйНалоговыйСчет.НачалоПростогоУчета();
	Если Дата < НачалоПростогоУчета Тогда
		 Дата = НачалоПростогоУчета;
	КонецЕсли;
	Ответственный = Пользователи.ТекущийПользователь();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли