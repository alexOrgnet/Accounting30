#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	Если ДанныеЗаполнения <> Неопределено И ОбщегоНазначения.ЗначениеСсылочногоТипа(ДанныеЗаполнения) Тогда
		Если Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Метаданные()) Тогда
			ЗаполнитьПоДокументуОснованию(ДанныеЗаполнения);
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);

	// Специфические для конкретного документа действия
	СтруктураКурсаДокумента = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаДокумента, Дата);
	КурсВзаиморасчетов      = СтруктураКурсаДокумента.Курс;
	КратностьВзаиморасчетов = СтруктураКурсаДокумента.Кратность;

	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхДокументов.ПоступлениеОтПоставщика Тогда
		Контрагент = Справочники.Контрагенты.ПустаяСсылка();
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхДокументов.ПоступлениеОтПодотчетногоЛица Тогда
		Контрагент = Справочники.ФизическиеЛица.ПустаяСсылка();
	КонецЕсли;

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь();

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	РаботаСДоговорамиКонтрагентовБП.ЗаполнитьДоговорПередЗаписью(ЭтотОбъект);
	// Посчитать суммы документа и записать ее в соответствующий реквизит шапки для показа в журналах
	СуммаДокумента = УчетНДСПереопределяемый.ПолучитьСуммуДокументаСНДС(ЭтотОбъект);

	// Обновление реквизитов УСН выполняем всегда для учета возможных изменений в учетной политике.
	ПараметрыУСН = УчетУСН.СтруктураПараметровОбъектаДляУСН(ЭтотОбъект); 
	НалоговыйУчетУСН.ЗаполнитьОтражениеДокументаВУСН(ЭтотОбъект, ПараметрыУСН); 
	Содержание_УСН = НалоговыйУчетУСН.СодержаниеОперацииДляКУДиР(ПараметрыУСН);

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если НЕ ПолучитьФункциональнуюОпцию("ВестиУчетПоДоговорам") Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ДоговорКонтрагента");
	КонецЕсли;
	
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхДокументов.ПоступлениеОтПодотчетногоЛица Тогда

		МассивНепроверяемыхРеквизитов.Добавить("Контрагент");
		МассивНепроверяемыхРеквизитов.Добавить("ДоговорКонтрагента");
		МассивНепроверяемыхРеквизитов.Добавить("СчетУчетаРасчетовСКонтрагентом");
		// Заменить стандартное сообщение о невыбранном поле
		Если НЕ ЗначениеЗаполнено(Контрагент) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, НСтр("ru = 'Подотчетное лицо'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,
				"Контрагент", "Объект", Отказ);
		КонецЕсли;

	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхДокументов.ПоступлениеОтПоставщика Тогда

		// Заменить стандартное сообщение о невыбранном поле
		МассивНепроверяемыхРеквизитов.Добавить("СчетУчетаРасчетовСКонтрагентом");
		Если НЕ ЗначениеЗаполнено(СчетУчетаРасчетовСКонтрагентом) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, НСтр("ru = 'Счет расчетов'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,
				"СчетУчетаРасчетовСКонтрагентом", "Объект", Отказ);
		КонецЕсли;

		Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
			Если НЕ УчетВзаиморасчетов.ПроверитьВозможностьПроведенияВРеглУчете(ЭтотОбъект,
				ДоговорКонтрагента, ТекстСообщения) Тогда
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, "Корректность",
					НСтр("ru = 'Договор'"),,ТекстСообщения);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ДоговорКонтрагента", "Объект", Отказ);
			КонецЕсли;
		КонецЕсли;

	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхДокументов.ПрочееПоступление Тогда

		МассивНепроверяемыхРеквизитов.Добавить("Контрагент");
		МассивНепроверяемыхРеквизитов.Добавить("ДоговорКонтрагента");
		МассивНепроверяемыхРеквизитов.Добавить("СчетУчетаРасчетовСКонтрагентом");
		// Заменить стандартное сообщение о невыбранном поле
		Если НЕ ЗначениеЗаполнено(СчетУчетаРасчетовСКонтрагентом) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, НСтр("ru = 'Счет кредита'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,
				"СчетУчетаРасчетовСКонтрагентом", "Объект", Отказ);
		КонецЕсли;

	КонецЕсли;

	Если НЕ СчетУчетаДенежныхДокументов.Валютный Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ВалютаДокумента");
	КонецЕсли;

	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА

	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;

	ПараметрыПроведения = Документы.ПоступлениеДенежныхДокументов.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ИНФОРМАЦИОННОЙ БАЗЫ

	// Способ зачета всегда "Автоматически"
	ТаблицаВзаиморасчетов = УчетВзаиморасчетов.ПодготовитьТаблицуВзаиморасчетовЗачетАвансов(
		ПараметрыПроведения.ЗачетАвансов, Неопределено, ПараметрыПроведения.Реквизиты, Отказ);

	// ФОРМИРОВАНИЕ ДВИЖЕНИЙ

	УчетВзаиморасчетов.СформироватьДвиженияЗачетАвансов(ТаблицаВзаиморасчетов,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);

	Документы.ПоступлениеДенежныхДокументов.СформироватьДвиженияПоступлениеДенежныхДокументов(
		ПараметрыПроведения.ПоступлениеДенежныхДокументов, ПараметрыПроведения.Реквизиты, Движения, Отказ);

	НалоговыйУчетУСН.СформироватьДвиженияУСН(ЭтотОбъект);

	// Переоценка валютных остатков - после формирования проводок всеми другими механизмами
	ТаблицаПереоценка = УчетДоходовРасходов.ПодготовитьТаблицуПереоценкаВалютныхОстатковПоПроводкамДокумента(
		ПараметрыПроведения.Реквизиты, Движения, Отказ);

	УчетДоходовРасходов.СформироватьДвиженияПереоценкаВалютныхОстатков(ТаблицаПереоценка,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
	
	УчетДоходовРасходов.СформироватьДвиженияРасчетПереоценкиВалютныхСредств(ТаблицаПереоценка,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
	
	УчетУСН.СформироватьДвиженияПереоценкаВалютныхОстатков(ТаблицаПереоценка,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	// Отложенные расчеты с контрагентами.
	УчетВзаиморасчетовОтложенноеПроведение.ЗарегистрироватьОтложенныеРасчетыСКонтрагентами(
		ЭтотОбъект, Отказ, ПараметрыПроведения.РасчетыСКонтрагентамиОтложенноеПроведение);
		
	// Регистрация в последовательности
	РаботаСПоследовательностями.ЗарегистрироватьОтложенныеРасчетыВПоследовательности(
		ЭтотОбъект, Отказ, ПараметрыПроведения.РасчетыСКонтрагентамиОтложенноеПроведение);
		
	ПроведениеСервер.УстановитьЗаписьОчищаемыхНаборовЗаписей(ЭтотОбъект);

КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)

	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();
	
	РаботаСПоследовательностями.ОтменитьРегистрациюВПоследовательности(ЭтотОбъект, Отказ);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПоДокументуОснованию(Основание)
	
	// Заполнение реквизитов из стандартного набора по документу-основанию.
	ЗаполнениеДокументов.ЗаполнитьПоОснованию(ЭтотОбъект, Основание);
	
	ДокументОснование = Основание;
	
	ТипЗначенияОснования = ТипЗнч(Основание);
	
	Если ТипЗначенияОснования = Тип("ДокументСсылка.ИнвентаризацияКассы") Тогда
		
		ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхДокументов.ПрочееПоступление;
		
		СчетУчетаРасчетовСКонтрагентом = ПланыСчетов.Хозрасчетный.ПрочиеДоходы;
		СубконтоКт1 = Справочники.ПрочиеДоходыИРасходы.ПредопределенныйЭлемент("ПрочиеВнереализационныеДоходыРасходы");
		
		// Количество в строках документа "Инвентаризация кассы" не указывается. Считаем, что на каждый денежный 
		// документ заполняется отдельная строка.
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИнвентаризацияКассыЦенности.СчетУчета КАК СчетУчетаДенежныхДокументов,
		|	СУММА(ИнвентаризацияКассыЦенности.ВалютнаяСумма - ИнвентаризацияКассыЦенности.ВалютнаяСуммаУчет) КАК Сумма,
		|	ИнвентаризацияКассыЦенности.Валюта КАК ВалютаДокумента,
		|	ИнвентаризацияКассыЦенности.Ссылка.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
		|	ИнвентаризацияКассыЦенности.ВидЦенностей КАК ДенежныйДокумент,
		|	СУММА(1) КАК Количество
		|ИЗ
		|	Документ.ИнвентаризацияКассы.Ценности КАК ИнвентаризацияКассыЦенности
		|ГДЕ
		|	ИнвентаризацияКассыЦенности.СчетУчета В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДенежныеДокументы), ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДенежныеДокументыВал))
		|	И ИнвентаризацияКассыЦенности.Ссылка = &Основание
		|
		|СГРУППИРОВАТЬ ПО
		|	ИнвентаризацияКассыЦенности.СчетУчета,
		|	ИнвентаризацияКассыЦенности.Валюта,
		|	ИнвентаризацияКассыЦенности.Ссылка.ПодразделениеОрганизации,
		|	ИнвентаризацияКассыЦенности.ВидЦенностей
		|
		|ИМЕЮЩИЕ
		|	СУММА(ИнвентаризацияКассыЦенности.ВалютнаяСумма - ИнвентаризацияКассыЦенности.ВалютнаяСуммаУчет) > 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВалютаДокумента,
		|	СчетУчетаДенежныхДокументов,
		|	ДенежныйДокумент
		|ИТОГИ
		|	МАКСИМУМ(ПодразделениеОрганизации)
		|ПО
		|	ВалютаДокумента,
		|	СчетУчетаДенежныхДокументов";
		
		Запрос.УстановитьПараметр("Основание", Основание);
		
		РезультатРасхождений = Запрос.Выполнить();
		
		// Валюта и счет учета денежных документов заполняются в шапке, обычно это рубль и счет 50.03.
		// Если есть расхождение по нескольким счетам и/или валютам, то в "Поступлении денежных документов"
		// отражаем только первое из них.
		
		ВыборкаВалюта = РезультатРасхождений.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Если ВыборкаВалюта.Следующий() Тогда
			ВыборкаСчетУчета = ВыборкаВалюта.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Если ВыборкаСчетУчета.Следующий() Тогда
				ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаСчетУчета);
				ВыборкаРасхождений = ВыборкаСчетУчета.Выбрать();
				Пока ВыборкаРасхождений.Следующий() Цикл
					СтрокаДенежныхСредств = ДенежныеДокументы.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаДенежныхСредств, ВыборкаРасхождений);
					ВалютаДокумента = ВыборкаРасхождений.ВалютаДокумента;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли