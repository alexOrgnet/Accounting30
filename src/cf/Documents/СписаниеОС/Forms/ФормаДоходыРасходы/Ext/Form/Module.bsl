#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры,
		"Субконто,СубконтоДоходов,СчетСписания,СчетУчетаДоходов,Организация,ПоказыватьДоходы");
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка) 

	Если ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		Отказ = Истина;
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемФормыЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да);
	КонецЕсли;
	
	Если Не Отказ И Модифицированность Тогда
		Отказ = НЕ ПроверитьЗаполнение();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СчетСписанияПриИзменении(Элемент)
	
	БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоШапкиПриИзмененииСчета(
		ЭтотОбъект, ЭтотОбъект, ПараметрыУстановкиСвойствСубконтоРасходов(ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура СчетУчетаДоходовПриИзменении(Элемент)
	
	БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоШапкиПриИзмененииСчета(
		ЭтотОбъект, ЭтотОбъект, ПараметрыУстановкиСвойствСубконтоДоходов(ЭтотОбъект));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ЗакрытьФормуИВернутьРезультат(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	ЗакрытьФормуИВернутьРезультат(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	НастроитьОтображениеДоходовРасходов();
	
	УстановитьОграниченияЭлементовФормыИзМетаданных();
	
	БухгалтерскийУчетКлиентСервер.УстановитьНачальныеСвойстваСубконтоШапки(
		ЭтотОбъект, ЭтотОбъект, ПараметрыУстановкиСвойствСубконтоРасходов(ЭтотОбъект));
	
	Если ПоказыватьДоходы Тогда
		БухгалтерскийУчетКлиентСервер.УстановитьНачальныеСвойстваСубконтоШапки(
			ЭтотОбъект, ЭтотОбъект, ПараметрыУстановкиСвойствСубконтоДоходов(ЭтотОбъект));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОграниченияЭлементовФормыИзМетаданных()
	
	ЭлементыИМетаданные = Новый Соответствие;
	
	ЭлементыИМетаданные.Вставить(Элементы.СчетСписания,
		Метаданные.Документы.СписаниеОС.Реквизиты.СчетСписания);
	
	ЭлементыИМетаданные.Вставить(Элементы.Субконто,
		Метаданные.Документы.СписаниеОС.Реквизиты.Субконто);
	
	Если ПоказыватьДоходы Тогда
		ЭлементыИМетаданные.Вставить(Элементы.СчетУчетаДоходов,
			Метаданные.Документы.СписаниеОС.Реквизиты.СчетУчетаДоходов);
		
		ЭлементыИМетаданные.Вставить(Элементы.СубконтоДоходов,
			Метаданные.Документы.СписаниеОС.Реквизиты.СубконтоДоходов);
	КонецЕсли;
	
	РедактированиеВПодчиненныхФормах.УстановитьСвойстваЭлементовПоРеквизитамМетаданных(ЭлементыИМетаданные);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьОтображениеДоходовРасходов()
	
	Заголовок = ?(ПоказыватьДоходы, НСтр("ru = 'Доходы и расходы'"), НСтр("ru = 'Расходы'"));
	
	Элементы.СчетУчетаДоходов.Видимость = ПоказыватьДоходы;
	Элементы.СубконтоДоходов.Видимость = ПоказыватьДоходы;
	КлючСохраненияПоложенияОкна = ?(ПоказыватьДоходы, "ДоходыИРасходы", "Расходы");
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПараметрыУстановкиСвойствСубконтоРасходов(Форма)
	
	Результат = БухгалтерскийУчетКлиентСервер.НовыеПараметрыУстановкиСвойствСубконто();
	
	Результат.ПоляФормы.Субконто1   = "Субконто";
	Результат.ПоляОбъекта.Субконто1 = "Субконто";
	Результат.ПоляОбъекта.СчетУчета = "СчетСписания";
	
	Результат.ДопРеквизиты.Вставить("Организация", Форма.Организация);
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПараметрыУстановкиСвойствСубконтоДоходов(Форма)
	
	Результат = БухгалтерскийУчетКлиентСервер.НовыеПараметрыУстановкиСвойствСубконто();
	
	Результат.ПоляФормы.Субконто1   = "СубконтоДоходов";
	Результат.ПоляОбъекта.Субконто1 = "СубконтоДоходов";
	Результат.ПоляОбъекта.СчетУчета = "СчетУчетаДоходов";
	
	Результат.ДопРеквизиты.Вставить("Организация", Форма.Организация);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ВопросПередЗакрытиемФормыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗакрытьФормуИВернутьРезультат(Истина);
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		ЗакрытьФормуИВернутьРезультат(Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФормуИВернутьРезультат(ПередатьВладельцуРезультат)
	
	РезультатЗаполнения = Неопределено;
	Если ПередатьВладельцуРезультат Тогда
		
		Если НЕ ПроверитьЗаполнение() Тогда
			Возврат;
		КонецЕсли;
		
		РезультатЗаполнения = Новый Структура("Субконто,СубконтоДоходов,СчетСписания,СчетУчетаДоходов");
		ЗаполнитьЗначенияСвойств(РезультатЗаполнения, ЭтотОбъект);
	КонецЕсли;
	
	Модифицированность = Ложь;
	Закрыть(РезультатЗаполнения);
	
КонецПроцедуры


#КонецОбласти
