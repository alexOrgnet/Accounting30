#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает список документов, реквизиты которых входят в состав критерия отбора.
// Из списка исключаются документы отключенные функциональными опциями.
//
// Возвращаемое значение:
// Массив объектов метаданных
//
Функция СоставДокументов() Экспорт
	
	СписокДокументов = Новый Массив;
	
	МетаданныеКритерия = Метаданные.КритерииОтбора.ДокументыПоДоговоруКонтрагента;
	
	Для Каждого МетаданныеРеквизита Из МетаданныеКритерия.Состав Цикл
		Родитель = МетаданныеРеквизита.Родитель();
		Если Метаданные.Документы.Содержит(Родитель) Тогда
			Если СписокДокументов.Найти(Родитель) = Неопределено Тогда
				СписокДокументов.Добавить(Родитель);
			КонецЕсли;
		ИначеЕсли ОбщегоНазначения.ОбъектМетаданныхДоступенПоФункциональнымОпциям(Родитель) Тогда
			РодительРодителя = Родитель.Родитель();
			Если Метаданные.Документы.Содержит(РодительРодителя) Тогда
				Если СписокДокументов.Найти(РодительРодителя) = Неопределено Тогда
					СписокДокументов.Добавить(РодительРодителя);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	СписокДокументовРазрешенные = Новый Массив;
	Для Каждого Документ Из СписокДокументов Цикл
		Если ОбщегоНазначения.ОбъектМетаданныхДоступенПоФункциональнымОпциям(Документ) Тогда
			СписокДокументовРазрешенные.Добавить(Документ);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СписокДокументовРазрешенные;
	
КонецФункции

// Отбирает из списка документов те, в которые входит реквизит с видом договора.
//
// Параметры:
//   СписокДокументов - Массив типов ОбъектМетаданных: Документ - список метаданных документов
//   ВидДоговора - ПеречислениеСсылка.ВидыДоговоровКонтрагентов - вид договора
//
// Возвращаемое значение:
//   Массив - список метаданных документов
Функция ОтобратьДокументыПоВидуДоговора(СписокДокументов, ВидДоговора) Экспорт
	
	Если Не ЗначениеЗаполнено(СписокДокументов) Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	ОтобранныйСписок = Новый Массив;
	
	СписокДокументовСРеквизитами = СоставДокументовСРеквизитами();
	
	Для Каждого Документ Из СписокДокументов Цикл
		
		ТипДокумента = Тип("ДокументСсылка." + Документ.Имя);
		Если РаботаСДоговорамиКонтрагентовБПВызовСервера.ЭтоДокументСДоговоромПоВидуОперации(ТипДокумента) Тогда
			// В некоторых документах вид договора зависит от вида операции.
			// При этом, не все виды операций доступны по ФО
			ВидыОпераций = РаботаСДоговорамиКонтрагентовБПВызовСервера.ВидыОперацийДокумента(ВидДоговора, ТипДокумента);
			Если ВидыОпераций.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
		Иначе
			// Отбираем по параметрам выбора в метаданных реквизитов документа.
			// Если хотя бы один реквизит соответствует по параметрам выбора, то отбираем документ в список
			СписокРеквизитов = СписокДокументовСРеквизитами[Документ];
			Для Каждого МетаданныеРеквизита Из СписокРеквизитов Цикл
				ОтобранПоВидуДоговора = РеквизитОтобранПоВидуДоговора(МетаданныеРеквизита, ВидДоговора);
				Если ОтобранПоВидуДоговора Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если Не ОтобранПоВидуДоговора Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		ОтобранныйСписок.Добавить(Документ);
		
	КонецЦикла;
	
	Возврат ОтобранныйСписок;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СоставДокументовСРеквизитами()
	
	СписокДокументов = Новый Соответствие;
	
	МетаданныеКритерия = Метаданные.КритерииОтбора.ДокументыПоДоговоруКонтрагента;
	
	Для Каждого МетаданныеРеквизита Из МетаданныеКритерия.Состав Цикл
		Родитель = МетаданныеРеквизита.Родитель();
		Если Метаданные.Документы.Содержит(Родитель) Тогда
			СписокРеквизитов = СписокДокументов[Родитель];
			Если СписокРеквизитов = Неопределено Тогда
				СписокРеквизитов = Новый Массив;
				СписокДокументов[Родитель] = СписокРеквизитов;
			КонецЕсли;
			СписокРеквизитов.Добавить(МетаданныеРеквизита);
		ИначеЕсли ОбщегоНазначения.ОбъектМетаданныхДоступенПоФункциональнымОпциям(Родитель) Тогда
			РодительРодителя = Родитель.Родитель();
			Если Метаданные.Документы.Содержит(РодительРодителя) Тогда
				СписокРеквизитов = СписокДокументов[РодительРодителя];
				Если СписокРеквизитов = Неопределено Тогда
					СписокРеквизитов = Новый Массив;
					СписокДокументов[РодительРодителя] = СписокРеквизитов;
				КонецЕсли;
				СписокРеквизитов.Добавить(МетаданныеРеквизита);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СписокДокументов;
	
КонецФункции

Функция РеквизитОтобранПоВидуДоговора(Реквизит, ВидДоговора)
	
	Результат = Истина; // По умолчанию реквизит отобран
	
	Для Каждого ПараметрВыбораРеквизита Из Реквизит.ПараметрыВыбора Цикл
		Если ПараметрВыбораРеквизита.Имя = "Отбор.ВидДоговора" Тогда
			Если ТипЗнч(ПараметрВыбораРеквизита.Значение) = Тип("ФиксированныйМассив") Тогда
				Если ПараметрВыбораРеквизита.Значение.Найти(ВидДоговора) = Неопределено Тогда
					Результат = Ложь;
				КонецЕсли;
			ИначеЕсли ВидДоговора <> ПараметрВыбораРеквизита.Значение Тогда
				Результат = Ложь;
			КонецЕсли;
			Возврат Результат; // Есть отбор по виду договора: возвращаем результат
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли