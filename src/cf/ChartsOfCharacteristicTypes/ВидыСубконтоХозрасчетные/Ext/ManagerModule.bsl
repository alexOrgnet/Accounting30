#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Возвращает порядковый номер субконто по виду
//
// Возвращаемое значение:
//   Число - порядковый номер вида субконто. Если вид субконто не найден, возвращается 0.
//
Функция НомерСубконто(Счет, ВидСубконто) Экспорт
	
	Результат = 0;
	
	СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Счет);
	
	Для НомерСубконто = 1 По СвойстваСчета.КоличествоСубконто Цикл
		
		Если СвойстваСчета["ВидСубконто" + НомерСубконто] = ВидСубконто Тогда
			Результат = НомерСубконто;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура ВосстановитьТипыСубконтоСФПолученные(Параметры) Экспорт

	Параметры.ОбработкаЗавершена = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыСубконтоХозрасчетные.Ссылка КАК Ссылка,
	|	ВидыСубконтоХозрасчетные.ТипЗначения КАК ТипЗначения
	|ИЗ
	|	ПланВидовХарактеристик.ВидыСубконтоХозрасчетные КАК ВидыСубконтоХозрасчетные
	|ГДЕ
	|	ВидыСубконтоХозрасчетные.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СФПолученные)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	ДобавитьСчетФактураВыданный = Истина;
	ДобавитьПутевойЛист = Истина;
	
	Если Выборка.Следующий() Тогда
		
		ТекущиеТипыЗначения = Выборка.ТипЗначения.Типы();

		Для каждого ТипСубконто Из ТекущиеТипыЗначения Цикл
			Если ТипСубконто = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
				ДобавитьСчетФактураВыданный = Ложь;
			ИначеЕсли ТипСубконто = Тип("ДокументСсылка.ПутевойЛист") Тогда 
				ДобавитьПутевойЛист = Ложь;
			КонецЕсли;
		КонецЦикла;
		
		ПередатьЦентруМониторингаВыявленныеПроблемыСТипомСубконтоСФПолученные(ДобавитьСчетФактураВыданный, ДобавитьПутевойЛист);
		
		Если Не ДобавитьСчетФактураВыданный И Не ДобавитьПутевойЛист Тогда
			Возврат;
		КонецЕсли;
		
		Если ДобавитьСчетФактураВыданный Тогда
			ТекущиеТипыЗначения.Добавить(Тип("ДокументСсылка.СчетФактураВыданный"));
		КонецЕсли;
		
		Если ДобавитьПутевойЛист Тогда
			ТекущиеТипыЗначения.Добавить(Тип("ДокументСсылка.ПутевойЛист"));
		КонецЕсли;
		
		НовыеТипыЗначения = Новый ОписаниеТипов(ТекущиеТипыЗначения);

		СФПолученныеОбъект = Выборка.Ссылка.ПолучитьОбъект();
		СФПолученныеОбъект.ТипЗначения = НовыеТипыЗначения;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(СФПолученныеОбъект);
	
	КонецЕсли;

КонецПроцедуры

Процедура ВосстановитьТипыСубконтоПартииМатериаловВЭксплуатации(Параметры) Экспорт

	Параметры.ОбработкаЗавершена = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыСубконтоХозрасчетные.Ссылка КАК Ссылка,
	|	ВидыСубконтоХозрасчетные.ТипЗначения КАК ТипЗначения
	|ИЗ
	|	ПланВидовХарактеристик.ВидыСубконтоХозрасчетные КАК ВидыСубконтоХозрасчетные
	|ГДЕ
	|	ВидыСубконтоХозрасчетные.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПартииМатериаловВЭксплуатации)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	ДобавитьТребованиеНакладная = Истина;
	ДобавитьПереводОСВМалоценноеОборудование = Истина;
	
	Если Выборка.Следующий() Тогда
		
		ТекущиеТипыЗначения = Выборка.ТипЗначения.Типы();

		Для каждого ТипСубконто Из ТекущиеТипыЗначения Цикл
			Если ТипСубконто = Тип("ДокументСсылка.ТребованиеНакладная") Тогда
				ДобавитьТребованиеНакладная = Ложь;
			ИначеЕсли ТипСубконто = Тип("ДокументСсылка.ПереводОСВМалоценноеОборудование") Тогда 
				ДобавитьПереводОСВМалоценноеОборудование = Ложь;
			КонецЕсли;
		КонецЦикла;
		
		Если Не ДобавитьТребованиеНакладная И Не ДобавитьПереводОСВМалоценноеОборудование Тогда
			Возврат;
		КонецЕсли;
		
		Если ДобавитьТребованиеНакладная Тогда
			ТекущиеТипыЗначения.Добавить(Тип("ДокументСсылка.ТребованиеНакладная"));
		КонецЕсли;
		
		Если ДобавитьПереводОСВМалоценноеОборудование Тогда
			ТекущиеТипыЗначения.Добавить(Тип("ДокументСсылка.ПереводОСВМалоценноеОборудование"));
		КонецЕсли;
		
		НовыеТипыЗначения = Новый ОписаниеТипов(ТекущиеТипыЗначения);

		ВидСубконтоОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ВидСубконтоОбъект.ТипЗначения = НовыеТипыЗначения;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ВидСубконтоОбъект);
	
	КонецЕсли;

КонецПроцедуры

Процедура ВосстановитьТипыСубконтоПартии(Параметры) Экспорт

	Параметры.ОбработкаЗавершена = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыСубконтоХозрасчетные.Ссылка КАК Ссылка,
	|	ВидыСубконтоХозрасчетные.ТипЗначения КАК ТипЗначения
	|ИЗ
	|	ПланВидовХарактеристик.ВидыСубконтоХозрасчетные КАК ВидыСубконтоХозрасчетные
	|ГДЕ
	|	ВидыСубконтоХозрасчетные.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	ДобавитьРеализацияТоваровУслуг = Истина;
	
	Если Выборка.Следующий() Тогда
		
		ТекущиеТипыЗначения = Выборка.ТипЗначения.Типы();

		Для каждого ТипСубконто Из ТекущиеТипыЗначения Цикл
			Если ТипСубконто = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
				ДобавитьРеализацияТоваровУслуг = Ложь;
			КонецЕсли;
		КонецЦикла;
		
		Если Не ДобавитьРеализацияТоваровУслуг Тогда
			Возврат;
		КонецЕсли;
		
		Если ДобавитьРеализацияТоваровУслуг Тогда
			ТекущиеТипыЗначения.Добавить(Тип("ДокументСсылка.РеализацияТоваровУслуг"));
		КонецЕсли;
		
		НовыеТипыЗначения = Новый ОписаниеТипов(ТекущиеТипыЗначения);

		ВидСубконтоОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ВидСубконтоОбъект.ТипЗначения = НовыеТипыЗначения;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ВидСубконтоОбъект);
	
	КонецЕсли;

КонецПроцедуры

Процедура ПередатьЦентруМониторингаВыявленныеПроблемыСТипомСубконтоСФПолученные(ОтсутствуетСчетФактураВыданный, ОтсутствуетПутевойЛист)
	
	Если Не ЦентрМониторинга.ЗаписыватьОперацииБизнесСтатистики() Тогда
		Возврат;
	КонецЕсли;
	
	ЦентрМониторинга.ЗаписатьОперациюБизнесСтатистики(
		"СтатистикаБП.ПроблемыСТипомСубконтоСФПолученные.ОтсутствуетСчетФактураВыданный",
		Число(ОтсутствуетСчетФактураВыданный));
		
	ЦентрМониторинга.ЗаписатьОперациюБизнесСтатистики(
		"СтатистикаБП.ПроблемыСТипомСубконтоСФПолученные.ОтсутствуетПутевойЛист",
		Число(ОтсутствуетПутевойЛист));
	
КонецПроцедуры

#КонецЕсли
