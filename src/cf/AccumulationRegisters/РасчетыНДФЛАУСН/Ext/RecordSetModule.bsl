#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)

	Если ОбменДанными.Загрузка Тогда
		Возврат;	
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ОтправленоВБанк", Ложь) = Истина Тогда
		Возврат;
	КонецЕсли;

	Регистратор = Отбор.Регистратор.Значение;
	
	НаборЗаписей = РегистрыНакопления.РасчетыНДФЛАУСН.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
	НаборЗаписей.Прочитать(); 
	
	Если НаборЗаписей.Количество() = 0 И ЭтотОбъект.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СравниваемыеЗначения = Новый Массив;
	МетаданныеРегистра = Метаданные();
	Для Каждого Измерение Из МетаданныеРегистра.Измерения Цикл
		СравниваемыеЗначения.Добавить(Измерение.Имя);
	КонецЦикла;
	Для Каждого Ресурс Из МетаданныеРегистра.Ресурсы Цикл
		СравниваемыеЗначения.Добавить(Ресурс.Имя);
	КонецЦикла;
	Для Каждого Реквизит Из МетаданныеРегистра.Реквизиты Цикл
		СравниваемыеЗначения.Добавить(Реквизит.Имя);
	КонецЦикла;
	
	ИменаСравниваемыхРеквизитов = СтрСоединить(СравниваемыеЗначения, ",");
	
	Если ОбщегоНазначения.КоллекцииИдентичны(НаборЗаписей, ЭтотОбъект, ИменаСравниваемыхРеквизитов) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		Запись.ОтправитьВБанк = Истина;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли