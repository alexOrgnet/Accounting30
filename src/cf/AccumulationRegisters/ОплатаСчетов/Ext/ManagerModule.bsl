
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс 

Функция ОплатаПоСчету(Организация, СчетНаОплату) Экспорт
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СчетНаОплату", СчетНаОплату);
	Запрос.УстановитьПараметр("Организация",  Организация);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СчетНаОплатуПокупателю.СуммаДокумента КАК СуммаДокумента,
	|	ЕСТЬNULL(ОплатаСчетовОбороты.СуммаОборот, 0) КАК СуммаОплаты
	|ИЗ
	|	Документ.СчетНаОплатуПокупателю КАК СчетНаОплатуПокупателю
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОплатаСчетов.Обороты(, , , СчетНаОплату = &СчетНаОплату И Организация = &Организация) КАК ОплатаСчетовОбороты
	|		ПО СчетНаОплатуПокупателю.Ссылка = ОплатаСчетовОбороты.СчетНаОплату
	|ГДЕ
	|	СчетНаОплатуПокупателю.Ссылка = &СчетНаОплату";
	
	Результат = Новый Структура("СуммаДокумента, СуммаОплаты", 0, 0);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции 

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли
