#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	МетаданныеРегистра = Метаданные();
	
	НаборЗаписей = ПрошлыйНаборЗаписей(МетаданныеРегистра);
	
	СравниваемыеЗначения = Новый Массив;
	Для Каждого Измерение Из МетаданныеРегистра.Измерения Цикл
		СравниваемыеЗначения.Добавить(Измерение.Имя);
	КонецЦикла;
	Для Каждого Ресурс Из МетаданныеРегистра.Ресурсы Цикл
		СравниваемыеЗначения.Добавить(Ресурс.Имя);
	КонецЦикла;
	
	ИменаСравниваемыхРеквизитов = СтрСоединить(СравниваемыеЗначения, ",");
	
	Если ОбщегоНазначения.КоллекцииИдентичны(НаборЗаписей, ЭтотОбъект, ИменаСравниваемыхРеквизитов) Тогда
		Возврат;
	КонецЕсли;
	
	БанковскиеОперации = ОбщегоНазначения.ВыгрузитьКолонку(ЭтотОбъект, "БанковскаяОперация", Истина);
	БанковскиеОперацииПрошлогоНабораЗаписей = ОбщегоНазначения.ВыгрузитьКолонку(НаборЗаписей, "БанковскаяОперация", Истина);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(БанковскиеОперации, БанковскиеОперацииПрошлогоНабораЗаписей, Истина);
	
	ИнтеграцияАУСНБП.ИзменитьОтправкуВБанк(БанковскиеОперации, Истина);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПрошлыйНаборЗаписей(МетаданныеРегистра)
	
	Регистратор = Отбор.Регистратор.Значение;
	
	НаборЗаписей = РегистрыНакопления.РазметкаАУСН.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
	НаборЗаписей.Прочитать();
	
	БанковскиеОперации = ОбщегоНазначения.ВыгрузитьКолонку(НаборЗаписей, "БанковскаяОперация", Истина);
	
	Если Не ЗначениеЗаполнено(БанковскиеОперации) Тогда
		Возврат НаборЗаписей;
	КонецЕсли;
	
	ЗаписиВведеныПользователем = ОбщегоНазначения.ЕстьРеквизитОбъекта("РучнаяКорректировка", МетаданныеРегистра)
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Регистратор, "РучнаяКорректировка");
	
	Если Не ЗаписиВведеныПользователем
		И Не ЭтоБанковскаяОперация(Регистратор, МетаданныеРегистра) Тогда
		// Если проводятся прочие документы (а не сама банковская операция), то перед сравнением
		// уберем из прошлого набора те банковские операции, в которых Порядок расчета АУСН = "По данным банка".
		// Поскольку разметка по таким операциям не отражается прочими документами.
		ИсточникРазметкиОпераций = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(
			БанковскиеОперации, "ИсточникРазметкиАУСН");
		
		ИндексЗаписи = 0;
		Пока ИндексЗаписи < НаборЗаписей.Количество() Цикл
			Запись = НаборЗаписей[ИндексЗаписи];
			ИсточникРазметкиДокумента = ИсточникРазметкиОпераций[Запись.БанковскаяОперация];
			Если ИсточникРазметкиДокумента = Перечисления.ИсточникиРазметкиАУСН.Банк Тогда
				НаборЗаписей.Удалить(Запись);
			Иначе
				ИндексЗаписи = ИндексЗаписи + 1;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат НаборЗаписей;
	
КонецФункции

Функция ЭтоБанковскаяОперация(ДокументОперации, МетаданныеРегистра)
	
	ТипыБанковскойОперации = МетаданныеРегистра.Измерения.БанковскаяОперация.Тип;
	
	Возврат ТипыБанковскойОперации.СодержитТип(ТипЗнч(ДокументОперации));
	
КонецФункции

#КонецОбласти

#КонецЕсли